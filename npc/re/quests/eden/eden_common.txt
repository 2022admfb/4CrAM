//===== rAthena Script ======================================= 
//= Eden Group Quests - Common NPCs
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 2.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Eden Group Headquarter NPC's.
//===== Additional Comments: ================================= 
//= 1.0 First Version.
//= 1.1 Removed showevent use, and use of duplicates.
//=     Commented out warps that should not be active.
//= 1.2 Reduced 300+ copy-paste lines to 1 duplicated NPC. [Brian]
//= 1.3 Some little optimization here and there. [Masao]
//= 1.4 Updated to match the new Izlude Map. [Masao]
//= 1.5 Added some missing Eden Group Teleport Officers. [Masao]
//= 1.6 Added Izlude RE coordinates. [Euphy]
//= 1.7 Added Malaya teleporter. [Euphy]
//= 1.8 Updated to match the latest official script. [Euphy]
//= 1.9 Added support to Para_Team_Mark_ in Eden Group. [AtlantisRO/Ragno]
//= 2.0 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

// Common NPCs :: lv_tre
//============================================================
moc_para01,27,35,5	script	Secretary Lime Evenor	4_F_EDEN_OFFICER,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 2000) {
		mes "- เดี๋ยวก่อน!! -";
		mes "- ขณะนี้คุณกำลังดำเนินการอยู่ -";
		mes "- มีรายการที่อยู่กับคุณมากเกินไป. -";
		mes "- กรุณาลองอีกครั้ง -";
		mes "- หลังจากที่คุณลดน้ำหนัก -";
		close;
	}
	if (countitem(6219) > 0) {
		mes "[Lime Evenor]";
		mes "ขอบคุณนักผจญภัยจำนวนมากที่มาเยี่ยมชม Eden Group ของเราเมื่อเร็ว ๆ นี้เราได้เตรียม Eden Group Mark เวอร์ชันใหม่สำหรับผู้เยี่ยมชมของเรา.";
		mes "และยังสามารถแลกเปลี่ยน Eden Group Mark เวอร์ชันเก่าเป็นรุ่นใหม่ได้.";
		next;
		mes "[Lime Evenor]";
		mes "แม้ว่าจะมีการหน่วงเวลาเล็กน้อย แต่ Eden Group Mark เวอร์ชันใหม่นี้จะส่งคุณกลับไปยังตำแหน่งที่บันทึกไว้ในปัจจุบัน ";
		mes "คุณต้องการแลกเปลี่ยนของคุณหรือไม่ ";
		next;
		if (select("แน่นอน:ไม่ล่ะ.") == 2){
			mes "[Lime Evenor]";
			mes "มีเหตุผลอะไรที่เป็นไปได้ที่คุณจะ ...";
			close;
		}
		delitem 6219,1;
		getitem 22508,1;
		mes "[Lime Evenor]";
		mes "ไปเลย กลับมาอีกครั้ง ~ ";
		close;
	}
	mes "[Lime Evenor]";
	mes "คนที่ทำตามความฝันและความรักของพวกเขาฟังฉัน เราเป็นตัวแทนของสวรรค์ที่เรียกว่า 'The garden of Eden' ที่เรียกว่า Eden group ไม่มีแบบนี้ที่ไหน";
	mes "สวัสดี. มีอะไรให้ช่วยไหม ";
	next;
	while (1) {
		switch (select("Eden Group คืออะไร?:เข้าร่วม Eden Group.:ลงทะเบียนภารกิจใหม่.:ค้นหาภารกิจ.:ยกเลิก.")) {
		case 1:
			mes "[Lime Evenor]";
			mes "Eden Group เป็นกลุ่มที่ช่วยเหลือผู้คนที่ทุกข์ทรมานจากความทุกข์ในมิดการ์ด ";
			next;
			mes "[Lime Evenor]";
			mes "หากผู้คนต้องการความช่วยเหลือเราจะนำเรื่องนี้ขึ้นบนกระดานข่าวและแนะนำให้สมาชิกของเราที่ต้องการช่วยเหลือผู้ที่ต้องการความช่วยเหลือ";
			next;
			mes "[Lime Evenor]";
			mes "สมาชิก Eden จะตรวจสอบภารกิจและช่วยเหลือผู้ที่โพสต์ขึ้น ";
			mes "สมาชิกทำภารกิจเหล่านี้เพื่อรับรางวัล ";
			next;
			mes "[Lime Evenor]";
			mes "หากคุณไม่คุ้นเคยกับ Midgard มากนักนี่เป็นวิธีที่ยอดเยี่ยมในการสำรวจโลก ";
			mes "หรือถ้าคุณโตพอคุณสามารถท่องโลกและหาเพื่อนใหม่ขณะทำภารกิจได้ ชื่อเสียงอันทรงเกียรติของท่านจะถูกเผยแพร่ไปในดินแดนต่างๆ ";
			next;
			break;
		case 2:
			if (!callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "คุณสามารถเป็นสมาชิกของ Eden ได้โดยเพียงแค่ลงทะเบียนกับฉัน ";
				mes "คุณต้องการเข้าร่วม Eden Group หรือไม่ ";
				next;
				switch (select("ใช่ฉันต้องการเข้าร่วม:ไม่ฉันไม่ต้องการเข้าร่วม.")) {
				case 1:
					mes "[Lime Evenor]";
					mes "ใช่. ยอดเยี่ยม! กรุณาเขียนชื่อของคุณที่นี่ ";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "คุณจะทำ?";
					mes "ให้ฉันดู.";
					next;
					mes "[Lime Evenor]";
					mes "ชื่อของคุณคือ ^3131FF "+.@input$+" ^000000 ... ? นั่นถูกต้องใช่ไหม?";
					mes "ฮะ? ไม่ใช่เหรอ??";
					next;
					emotion ET_SWEAT;
					mes "[Lime Evenor]";
					mes "อืมนั่นไม่ใช่สิ่งที่คุณเขียน? ";
					mes "โอเคอืมอ่านยากไปหน่อย ";
					next;
					mes "[Lime Evenor]";
					mes "กรุณาเขียนชื่อของคุณอีกครั้งได้ไหม กรุณาเขียนมัน ^3131FF ชัดเจน ^000000 ";
					input .@input$;
					next;
					mes "[Lime Evenor]";
					mes "อ่า ~ ฮ่า คุณคือ ^3131FF "+ strcharinfo (0) +" ^000000. ";
					mes "คราวนี้ฉันเข้าใจถูกแล้ว";
					next;
					emotion ET_BEST;
					mes "[Lime Evenor]";
					mes "เราใส่ชื่อของคุณในรายการแล้ว ";
					mes "ยินดีต้อนรับสู่สมาชิกใหม่ของกลุ่ม Eden! ";
					next;
					mes "[Lime Evenor]";
					mes "หวังว่าคุณจะทำผลงานได้อย่างยอดเยี่ยมในฐานะสมาชิกของ Eden ";
					getitem 22508,1; //Para_Team_Mark_
					next;
					break;
				case 2:
					mes "[Lime Evenor]";
					mes "คุณยังมีคำถามเกี่ยวกับ Eden Group หรือไม่ ";
					next;
					break;
				}
			} else {
				mes "[Lime Evenor]";
				mes "คุณเป็นสมาชิกของ Eden Group แล้ว ";
				next;
			}
			break;
		case 3:
			mes "[Lime Evenor]";
			mes "คุณต้องการลงทะเบียนบางภารกิจที่คุณต้องการให้เราทำหรือไม่ ";
			next;
			if(select("ใช่ฉันต้องการลงทะเบียน:ไม่ฉันไม่ทำ.") == 1) {
				mes "[Lime Evenor]";
				mes "ได้เลย กรุณาจดชื่อของคุณไว้ ";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FF "+ strcharinfo (0) +" ^000000 .. ชื่อคุณหรือเปล่า ";
				mes "อืมลายมือคุณไม่ชัด ฉันอ่านไม่ออก มัน";
				next;
				mes "[Lime Evenor]";
				mes "^3131FF "+ strcharinfo (0) +" ^000000 บอกฉันทีว่าคุณต้องการลงทะเบียนเพื่ออะไร ";
				mes "เราควรไปแผนที่ไหน ";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "^3131FFMission แผนที่: "+.@inputstr$ +" ^000000 ";
				mes "จะสังเกตว่า";
				next;
				mes "[Lime Evenor]";
				mes "โปรดแจ้งให้เราทราบว่าคุณกำลังมองหาภารกิจประเภทใด ";
				mes "มันควรจะสั้น ๆ เหมือน 'ล่า 10 Porings' ";
				input .@input$;
				next;
				mes "[Lime Evenor]";
				mes "คุณจะทำ?";
				mes "ให้ฉันตรวจสอบก่อน.";
				next;
				mes "[Lime Evenor]";
				mes "ฮะ...........";
				next;
				mes "[Lime Evenor]";
				mes "ฮึบ .. ห๊ะ ?? .................. ";
				next;
				emotion ET_THINK;
				mes "[Lime Evenor]";
				mes "ฉันคิดว่าคุณควรทำหนึ่งในภารกิจของเราให้สำเร็จก่อนที่คุณจะทำภารกิจใหม่ ";
				next;
				emotion ET_HNG;
				mes "[Lime Evenor]";
				mes "^3131FFClient: Lime Evenor ^000000 ";
				mes "^3131FF ภารกิจ: ฝึกคัดลายมือเป็นเวลา 1 เดือน ^000000";
				mes "คุณมีฝีไม้ลายมือแย่มาก! ";
				next;
				mes "[Lime Evenor]";
				mes "^3131FF ภารกิจ: "+.@input$ +" ^000000";
				mes "อย่างไรก็ตามคุณได้ลงทะเบียนสำหรับภารกิจใหม่เรียบร้อยแล้ว ";
				next;
				mes "[Lime Evenor]";
				mes "ภารกิจถูกจองเต็มแล้ว คุณต้องรอให้จุดที่ชัดเจนขึ้น.";
				mes "กรุณารอจนกว่าจะถึงตาคุณ ";
				next;
			}
			break;
		case 4:
			if (callfunc("F_HasEdenGroupMark")) {
				mes "[Lime Evenor]";
				mes "อยากลองทำภารกิจในฐานะสมาชิก Eden ดูไหม ";
				next;
				mes "[Lime Evenor]";
				mes "ตรวจสอบด้านขวาของกระดานข่าวมีภารกิจหลายประเภทรอคุณอยู่ ";
				mes "โปรดอ่านรายชื่อภารกิจและคุณสมบัติอย่างละเอียดแล้วเลือกหนึ่งในนั้น ";
				next;
			}
			else {
				mes "[Lime Evenor]";
				mes "คุณต้องเข้าร่วม Eden Group ก่อนถ้าคุณต้องการทำภารกิจบางอย่าง";
				close;
			}
			break;
		case 5:
			mes "[Lime Evenor]";
			mes "หากคุณมีคำถามใด ๆ โปรดกลับมาอีกครั้ง ";
			close;
		}
	}
	close;
}

-	script	Eden Teleport Officer#0::eto	-1,{
	mes "[Eden Teleport Officer] ";
	mes "บรรดาผู้ที่กำลังเดินไปรอบ ๆ โลกที่แปลกประหลาดนี้ด้วยสายตาที่ไร้จุดหมายฟังและมาหาฉัน! ";
	mes "Eden group อยู่ที่นี่เพื่อช่วยคุณและจะแสดงให้คุณเห็นอนาคตที่ยิ่งใหญ่ ";
	next;
	mes "[Eden Teleport Officer]";
	mes "คุณไม่มีอะไรทำนอกจากเสียเวลา";
	mes "คุณมีความกระตือรือร้นที่จะทำสิ่งที่ดี แต่ไม่มีใครเสนอให้คุณทำงาน? ";
	mes "คุณต้องการเป็นผู้แก้ปัญหาหรือไม่ ";
	mes "Eden group อยู่ที่นี่เพื่อแก้ปัญหาของคุณ ";
	next;
	switch (select("เคลื่อนย้ายไปยัง Eden Group.:ไม่ต้องการคุยต่อ.")) {
	case 1:
		mes "[Eden Teleport Officer]";
		mes "ไปที่ฐานลับของเรากันเถอะ! ";
		set nak_warp,strnpcinfo(2);
		close2;
		warp "moc_para01",31,14;
		end;
	case 2:
		mes "[Eden Teleport Officer]";
		mes "หากคุณไม่รู้ว่าจะทำอย่างไรเพื่ออนาคตของคุณอย่าลังเลที่จะบอกฉัน แวะมาคุยกับฉันได้เลย! ตกลง?";
		close;
	}
}
prontera,124,76,3	duplicate(eto)	Eden Teleport Officer#1	729
moc_ruins,68,164,3	duplicate(eto)	Eden Teleport Officer#2	729
geffen,132,66,3	duplicate(eto)	Eden Teleport Officer#3	729
alberta,124,67,3	duplicate(eto)	Eden Teleport Officer#4	729
aldebaran,133,119,5	duplicate(eto)	Eden Teleport Officer#5	729
izlude_in,68,162,1	duplicate(eto)	Eden Teleport Officer#6	729
prt_church,103,78,3	duplicate(eto)	Eden Teleport Officer#7	729
geffen_in,160,104,5	duplicate(eto)	Eden Teleport Officer#8	729
moc_prydb1,53,126,3	duplicate(eto)	Eden Teleport Officer#9	729
alberta_in,75,39,3	duplicate(eto)	Eden Teleport Officer#10	729
payon_in02,58,58,1	duplicate(eto)	Eden Teleport Officer#11	729
payon,177,111,3	duplicate(eto)	Eden Teleport Officer#12	729
que_ng,33,73,3	duplicate(eto)	Eden Teleport Officer#13	729
que_ng,144,161,5	duplicate(eto)	Eden Teleport Officer#14	729
yuno,144,189,5	duplicate(eto)	Eden Teleport Officer#15	729
rachel,125,144,3	duplicate(eto)	Eden Teleport Officer#16	729
comodo,202,151,4	duplicate(eto)	Eden Teleport Officer#17	729
hugel,93,153,4	duplicate(eto)	Eden Teleport Officer#18	729
veins,220,109,4	duplicate(eto)	Eden Teleport Officer#19	729
einbroch,250,211,4	duplicate(eto)	Eden Teleport Officer#20	729
lighthalzen,164,86,4	duplicate(eto)	Eden Teleport Officer#21	729
amatsu,100,145,5	duplicate(eto)	Eden Teleport Officer#22	729
ayothaya,221,191,3	duplicate(eto)	Eden Teleport Officer#23	729
louyang,224,107,3	duplicate(eto)	Eden Teleport Officer#24	729
gonryun,162,122,5	duplicate(eto)	Eden Teleport Officer#25	729
moscovia,209,197,5	duplicate(eto)	Eden Teleport Officer#26	729
brasilis,191,224,3	duplicate(eto)	Eden Teleport Officer#27	729
dewata,192,193,5	duplicate(eto)	Eden Teleport Officer#28	729
morocc,161,97,5	duplicate(eto)	Eden Teleport Officer#29	729
izlude,131,148,4	duplicate(eto)	Eden Teleport Officer#30	729
izlude_a,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_a	729
izlude_b,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_b	729
izlude_c,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_c	729
izlude_d,131,148,4	duplicate(eto)	Eden Teleport Officer#30::eto_iz_d	729
umbala,105,158,3	duplicate(eto)	Eden Teleport Officer#31	729
malaya,225,218,1	duplicate(eto)	Eden Teleport Officer#32	729
verus04,116,243,0	duplicate(eto)	Eden Teleport Officer#33	729

moc_para01,30,10,0	script	#eden_out	45,1,1,{
OnTouch:
	switch (nak_warp) {
		case 1: warp "prontera",116,72; end;
		case 2: warp "moc_ruins",64,161; end;
		case 3: warp "geffen",120,39; end;
		case 4: warp "alberta",117,56; end;
		case 5: warp "aldebaran",168,112; end;
		case 6: warp "izlude_in",73,165; end;
		case 7: warp "prt_church",99,78; end;
		case 8: warp "geffen_in",162,99; end;
		case 9: warp "moc_prydb1",51,118; end;
		case 10: warp "alberta_in",73,43; end;
		case 11: warp "payon_in02",64,60; end;
		case 12: warp "payon",161,58; end;
		case 13: warp "que_ng",33,63; end;
		case 14: warp "que_ng",144,166; end;
		case 15: warp "yuno",158,125; end;
		case 16: warp "rachel",115,125; end;
		case 17: warp "comodo",192,145; end;
		case 18: warp "hugel",88,148; end;
		case 19: warp "veins",216,104; end;
		case 20: warp "einbroch",246,204; end;
		case 21: warp "lighthalzen",159,95; end;
		case 22: warp "amatsu",110,150; end;
		case 23: warp "ayothaya",217,178; end;
		case 24: warp "louyang",217,103; end;
		case 25: warp "gonryun",155,120; end;
		case 26: warp "moscovia",218,198; end;
		case 27: warp "brasilis",190,220; end;
		case 28: warp "dewata",192,182; end;
		case 29: warp "morocc",161,97; end;
		case 30: warp "izlude",134,118; end;	// Old coordinates: (127,142)
		case 31: warp "umbala",94,154; end;
		case 32: warp "malaya",234,199; end;
		case 33: warp "verus04",115,243; end;
		default: warp "prontera",116,72; end;
	}
	end;
}
moc_para01,48,39,3	script	#warp_2_pass	45,1,1,{
//moc_para01,47,39,3	script	#warp_2_pass	111,{
	if (callfunc("F_HasEdenGroupMark")) {
		mes "[Lime Evenor]";
		mes "โอ้มันเป็นสถานที่พิเศษสำหรับสมาชิกของ Eden เท่านั้น ";
		mes "หากคุณเป็นสมาชิกคุณสามารถมาเมื่อใดก็ได้ที่คุณต้องการ! ";
		next;
		switch (select("เข้าไป.:ยกเลิก.")) {
		case 1:
			mes "ประตูนี้ได้รับการตกแต่งอย่างสวยงาม แต่ดูเหมือนจะหนักไปหน่อย ";
			close2;
			warp "moc_para01",106,14;
			end;
		case 2:
			mes "[Lime Evenor]";
			mes "ถ้าคุณไม่สนใจ ";
			close;
		}
	}
	mes "[Lime Evenor]";
	mes "โอ้นี่เป็นสถานที่พิเศษสำหรับสมาชิกของ Eden เท่านั้น ";
	mes "ถ้าคุณต้องการเข้าไปข้างในคุณต้องเข้าร่วม Eden Group ";
	close;
}

moc_para01,16,22,7	script	Old Adventurer#eden	900,{
	mes "[Old Adventurer]";
	mes "รุนแรงฉันแข็งแกร่งพอแม้ว่าฉันจะดูแก่! เธอชื่ออะไร? Lime Evenor? เธอไม่สนใจฉันเหมือนฉันเป็นไอ้แก่ ๆ ";
	next;
	mes "[Old Adventurer]";
	mes "ฉันมาที่นี่เพื่อเข้าร่วมกลุ่ม Eden! ฉันไม่สมควรได้รับสิ่งนี้! ";
	mes "คุณคิดว่าฉันเหมือนชราหรือเปล่า?";
	close;
}

moc_para01,182,48,3	script	Eden's Chief#eden	886,{
	set .@eggf,rand(1,118);
	set .@eggf_1,.@eggf + 1;
	set .@eggf_2,.@eggf + 2;
	set .@eggf_6,.@eggf + 6;
	mes "[Eden's Intern]";
	mes ""+.@eggf+".. "+.@eggf_1+"... "+.@eggf_2+".... "+.@eggf_6+"..?";
	next;
	emotion ET_CRY;
	mes "[Eden's Intern]";
	mes "โอ้ยิง! ฉันทำไข่ไปกี่ฟอง ";
	mes "เอ้า !!! ฉันลืมไปหมดแล้ว! ฉันต้องนับใหม่! ";
	next;
	emotion ET_CRY;
	mes "[Eden's Intern]";
	mes "ทำไมคุณถึงขอให้ฉันทำสิ่งนี้? ภารกิจแบบนั้นคืออะไร ";
	next;
	emotion ET_CRY;
	mes "[Eden's Intern]";
	mes "คุณจะเป็นอย่างไรกับไข่ดาวทั้งหมด? ฉันแน่ใจว่าคุณกินไม่หมดแน่ ๆ คุณแค่อยากรบกวนฉันใช่มั้ย ";
	close;
}

moc_para01,172,28,5	script	Eden's Intern#eden	883,{
	mes "[Eden's Intern]";
	mes "ที่จริงฉันก็รู้แล้ว ";
	next;
	mes "[Eden's Intern]";
	mes "เราต้องทำงานหนักจริงๆเว้นแต่คุณจะล้มเหลว ";
	next;
	mes "[Eden's Intern]";
	mes "แต่ฉันยังไม่เคยเรียนเกี่ยวกับเภสัชภัณฑ์เลย ";
	next;
	mes "[Eden's Intern]";
	mes "ฉันจะล้มเหลว ฉันทำไม่ได้ ";
	close;
}

moc_para01,20,35,5	script	Office Assistant Neede	814,{
	mes "[Neede]";
	mes "ฉันได้เป็นสมาชิกของ Eden เมื่อหลายปีก่อน แต่มันรู้สึกเหมือนเมื่อวานนี้ ";
	mes "ฉันทำงานมามากมายฉันจึงไม่ได้สังเกตว่าฉันใช้เวลาที่นี่กี่ปี ";
	next;
	mes "[Neede]";
	mes "มันเตือนฉันว่ามันเหมือนสงครามทุกวัน";
	next;
	mes "[Neede]";
	mes "หัวหน้าของ ^3131FF ร้านอาหารทางด้านขวา ^000000";
	mes "ใช้เครื่องปรุงรสมากเกินไปทุกครั้งที่ทำอาหารกลิ่นจะอบอวลไปทั่วล็อบบี้ เราต้องเปิดหน้าต่างเป็นประจำเพราะทนกลิ่นไม่ได้!";
	next;
	mes "[Neede]";
	mes "และคุณเห็น ^3131FF ยิมเนเซียม ^000000 บนไม้กางเขนของ ^3131FF ทางด้านทิศเหนือของล็อบบี้ ^000000 หรือไม่ พวกเขามักจะตะโกนเวลาออกกำลังกายฉันยังได้ยินจากที่นี่!";
	next;
	emotion ET_ANGER;
	mes "[Neede]";
	mes "นอกจากนี้สาวแปลกจาก ^3131FF ชั้นสอง ^000000 ร้องไห้ตลอดเวลา เสียงร้องของเธอทำให้ฉันแทบคลั่ง ";
	next;
	mes "[Neede]";
	mes "อาผู้หญิงประหลาดคนนั้นเป็นเจ้านายของพวกเรา ...";
	next;
	mes "[Neede]";
	mes "ฉันไม่รู้ว่าเธอทำอะไรอยู่ แต่เธอร้องไห้และตะโกนตลอดเวลา โอ้บางครั้งฉันก็ได้ยินเสียง 'ปัง!' ดังสนั่น ... ฉันไม่รู้ว่ามันมาจากไหน ";
	next;
	emotion ET_HUK;
	mes "[Neede]";
	mes "รอ!";
	mes "จุ๊ ๆ ! เป็นความลับที่บอกใครไม่ได้! ";
	next;
	mes "[Neede]";
	emotion ET_OHNO;
	mes "ฉันไม่ไว้ใจอีเนอร์เธอไม่เคยแสดงความรู้สึกของเธอ ... เธอจะมาหาเรื่องกับเจ้านายฉัน";
	next;
	emotion ET_HNG, getnpcid(0, "Secretary Lime Evenor");
	mes "[Lime Evenor]";
	mes "ฉันไม่ใช่ผู้หญิงแบบนั้น ";
	next;
	emotion ET_KEK;
	mes "[Neede]";
	mes "ฮะ? คุณได้ยินไหม เอ้ย ~! ";
	close;
}

function	script	F_HasEdenGroupMark	{
	return (countitem(6219) > 0 || countitem(22508) > 0);
}
