//===== rAthena Script =======================================
//= Eden Group Quests - Mission [26 - 40]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 26 - 40.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.4 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,38,38,3	script	Mission [26 - 40]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "คุณไม่ใช่สมาชิกที่มีคุณสมบัติเหมาะสม. คุณไม่สามารถใช้กระดานข่าวได้.";
		close;
	}
	mes "[26 ~ 40 Lv. กระดานภารกิจ]";
	mes "- มีภารกิจมากมาย -";
	mes "- บนกระดานข่าว. -";
	next;
	if ((BaseLevel < 26) || (BaseLevel > 40)) {
		mes "ระดับของคุณไม่ผ่านการรับรอง. คุณไม่สามารถใช้กระดานข่าวได้.";
		close;
	}
	switch(select("Hunting Mission:Delivery Mission:Gathering Mission:Escort Mission")) {
	case 1:
		mes "[กระดานภารกิจล่าสัตว์]";
		mes "- รายละเอียดภารกิจล่าสัตว์ -";
		mes "- อยู่บนกระดาน. -";
		next;
		switch(select("Hunting Poison Spore:Hunting Smokie:Hunting Elder Willow(Above Lv.30):Hunting Coco(Above Lv.35):Hunting Thara Frog(Above Lv.38)")) {
			case 1: callsub L_Quest,8266,0,"Hunting Poison Spore","Sponiac","Geffen","Hunt 10 Poison Spores";
			case 2: callsub L_Quest,8267,0,"Hunting Smokie","Sponiac","Geffen","Hunt 10 Smokies";
			case 3: callsub L_Quest,8268,30,"Hunting Elder Willow","Sponiac","Geffen","Hunt 10 Elder Willows";
			case 4: callsub L_Quest,8269,35,"Hunting Coco","Sponiac","Geffen","Hunt 10 Cocos";
			case 5: callsub L_Quest,8270,38,"Hunting Thara Frog","Sponiac","Geffen","Hunt 10 Thara Frogs";
		}
	case 2:
		mes "[บอร์ดภารกิจการจัดส่ง]";
		mes "- รายละเอียดภารกิจการจัดส่ง -";
		mes "- อยู่บนกระดาน. -";
		next;
		switch(select("Deliver an Insecticide:Deliver Honey:Deliver Blanket(Above Lv.35)")) {
			case 1: callsub L_Quest,8271,0,"Deliver an Insecticide","Dieshin Man","Morocc Ruins","รับ 'Insecticide' จาก Dieshin Man แล้วส่งไปทางตะวันออกเฉียงใต้ของ Payon";
			case 2: callsub L_Quest,8272,0,"Deliver Honey","Dieshin Man","Morocc Ruins","รับ 'Honey' จาก Dieshin Man และส่งไปทางตะวันตกของ St.Capitolina Abbey";
			case 3: callsub L_Quest,8273,35,"Deliver Blanket","Dieshin Man","Morocc Ruins","รับ Blankets จาก Dieshin Man และส่งไปยังสนามทางใต้ของ Lutie";
		}
	case 3:
		mes "[การรวบรวมบอร์ดภารกิจ]";
		mes "- การรวบรวมรายละเอียดภารกิจ -";
		mes "- อยู่บนกระดาน. -";
		next;
		switch(select("Gather Bones:Gather Animal Skins:Gather Peelings(Above Lv.30):Gather Tails(Above Lv.32):Gather Cookies(Above Lv.35):Gather Mustaches(Above Lv.35)")) {
			case 1: callsub L_Quest,8274,0,"Gather Bones","Dashia","Payon","Gather '10 Skel-Bones' และ มอบให้แก่ Dashia";
			case 2: callsub L_Quest,8275,0,"Gather Animal Skins","Dashia","Payon","Gather '10 Bear's Footskins' และ '5 Animal Skins' และ มอบให้แก่ Dashia";
			case 3: callsub L_Quest,8276,30,"Gather Peelings","Dashia","Payon","Gather '20 Worm Peelings' และ '20 Garlets' และ มอบให้แก่ Dashia";
			case 4: callsub L_Quest,8277,32,"Gather Tails","Dashia","Payon","Gather '10 Yoyo Tails', '10 Acorns' และ '10 Raccon Leafs' และ มอบให้แก่ Dashia";
			case 5: callsub L_Quest,8278,35,"Gather Cookies","Dashia","Payon","Gather '5 Well-baked Cookies' และ '1 Piece of Cake' และ มอบให้แก่ Dashia";
			case 6: callsub L_Quest,8279,35,"Gather Mustaches","Dashia","Payon","Gather '5 Mole Whiskers' และ '3 Mole Claws' และ มอบให้แก่ Dashia";
		}
	case 4:
		mes "[บอร์ดภารกิจคุ้มกัน]";
		mes "ขณะนี้ไม่มีภารกิจคุ้มกันที่ลงทะเบียนบนกระดาน.";
		close;
	}
	end;
L_Quest:
	//QuestID,ReqLvl,Quest,Client,Loc,Desc
	.@string$ = getarg(2);
	if (BaseLevel < getarg(1,26)) {
		mes "ระดับของคุณไม่เป็นไปตามข้อกำหนด. คุณไม่สามารถใช้ '"+ .@string$ +"' ภารกิจ.";
		close;
	}
	mes "["+ .@string$ +" !!]";
	mes "ลูกค้า : "+getarg(3);
	mes "ที่ตั้งของลูกค้า : "+getarg(4);
	mes "ภารกิจที่ร้องขอ : "+getarg(5);
	mes " ";
	mes "คุณจะรับภารกิจนี้ไหม?";
	next;
	if(select("ไม่ฉันจะไม่ยอมรับมัน:ใช่ฉันจะยอมรับมัน.") == 1) {
		mes "คุณได้ปฏิเสธ "+ .@string$ +" ภารกิจ.";
		close;
	}
	if (checkquest(getarg(0)) > -1) mes "คุณกำลังทำภารกิจนี้อยู่แล้ว.";
	else {
		setquest getarg(0);
		mes "คุณได้ยอมรับ "+ .@string$ +" ภารกิจ.";
	}
	mes "คุณสามารถรับรางวัลจากลูกค้า ได้ หลังจากเสร็จสิ้นภารกิจ.";
	next;
	mes "รายละเอียดเพิ่มเติมกรุณากด 'Alt + U', เพื่อรับข้อมูลเพิ่มเติมจากหน้าต่าง Quest.";
	close;
}

geffen,103,42,5	script	Sponiac	99,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[Sponiac]";
		mes "ทำไมคุณถึงถือของมากมาย?";
		mes "คุณอยู่ในหลักสูตรการฝึกอบรมหรืออะไร?";
		close;
	}
	mes "[Sponiac]";
	mes "ลู่~ลู่~ลู่~ลู่~";
	mes "ลู่ ~ หือ?";
	next;
	mes "[Sponiac]";
	mes "ว้าวคุณต้องเป็นมือใหม่ใช่ไหม?";
	mes "ช่างหน้าซื่อ! ฮา ~ ฮ่า ๆ ๆ.";
	emotion ET_KIK;
	next;
	mes "[Sponiac]";
	mes "ทำไมคุณไม่ออกไปล่าสัตว์ข้างนอกบ้างแทนที่จะเดินไปมาเสียเวลา?";
	next;
	if(select("ฉันกำลังจะออกไปแล้ว:ฉันมีภารกิจHunting.") == 1) {
		mes "[Sponiac]";
		mes "โอ้ดี จากนั้นไปคิด.";
		mes "บาย ~ ลาก่อน.";
		close;
	}
	mes "[Sponiac]";
	mes "คุณมาจาก Eden Group?";
	mes "คุณมีภารกิจอะไร?";
	next;
	switch(select("Hunting Poison Spore:Hunting Smokie:Hunting Elder Willow:Hunting Coco:Hunting Thara Frog")) {
		case 1: callsub L_Quest,8266,"Hunting Poison Spore",1000,0,100,"You can find Poison Spores on the Geffen Dungeon 1F or northeast of Prontera. There are other spots but I'll leave it up to you to find out.";
		case 2: callsub L_Quest,8267,"Hunting Smokie",1000,0,100,"You can find Smokies northeast and northwest of Geffen. Oh, also the field east of Ayothaya is good spot to find them.";
		case 3: callsub L_Quest,8268,"Hunting Elder Willow",1000,100,110,"You can find Elder Willows northeast of Geffen, east of payon and southwest of Prontera.";
		case 4: callsub L_Quest,8269,"Hunting Coco",1000,200,150,"You can find Cocos around the east of Payon, southwest of Prontera and the field east of Ayothaya.";
		case 5: callsub L_Quest,8270,"Hunting Thara Frog",1500,300,200,"You can find Thara Frogs in the Cave east of Comodo or on Turtle Island.";
	}
	end;
L_Quest:
	//QuestID,Quest,BExp,JExp,PotionAmt,Desc
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == 1) {
		if (checkquest(.@quest_id,HUNTING) == 2) {
			mes "[Sponiac]";
			mes getarg(1)+"!";
			mes "ว้าวฉันไม่ได้คาดหวังว่าคุณจะทำภารกิจนั้น. ฉันกำลังจะยอมแพ้ แต่คุณทำได้ดี ฉันรู้สึกทราบซึ้ง!";
			next;
			mes "[Sponiac]";
			mes "ฉันขอภารกิจและคุณก็ทำได้ดีมาก.";
			mes "ฉันจะให้รางวัลคุณตามที่สัญญาไว้.";
			next;
			mes "[Sponiac]";
			mes "นี่คือ. ขอบคุณมาก. ขอให้พระเจ้าคุ้มครอง!";
			getitem 569,getarg(4); //Novice_Potion
			getexp getarg(2),getarg(3);
			erasequest .@quest_id;
			close;
		}
		mes "[Sponiac]";
		mes "โอ้คุณพูดถูก.";
		next;
		mes "[Sponiac]";
		mes "แต่คุณยังไม่ได้ทำภารกิจ ทำไมคุณถึงมาหาฉันตอนนี้? คุณยังทำภารกิจไม่สำเร็จ!";
		mes "ฉันจะให้รางวัลคุณถ้าคุณทำภารกิจสำเร็จ. ตกลง? เราสบายดีไหม?";
		next;
		mes "[Sponiac]";
		mes getarg(5);
		mes "คุณสามารถตรวจสอบแผนที่โลกได้โดยกด 'ctrl + ~' key.";
		next;
		mes "[Sponiac]";
		mes "อย่างไรก็ตามฉันจะอยู่ที่นี่เพื่อรอคุณ โปรดกลับมาเมื่อคุณเสร็จสิ้นภารกิจ!";
		close;
	}
	mes "[Sponiac]";
	mes "ฮะ? คุณแน่ใจไหม? บางสิ่งบางอย่างที่ไม่ถูกต้อง...";
	next;
	mes "[Sponiac]";
	mes "มันแปลก.";
	mes "โปรดตรวจสอบหน้าต่าง Quest ของคุณโดยกด 'alt + u'.";
	close;
}

moc_ruins,173,55,3	script	Dieshin Man	89,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[ไดชินแมน]";
		mes "ทำไมคุณถึงถือของมากมาย?";
		mes "คุณอยู่ในหลักสูตรการฝึกอบรมหรืออะไร?";
		close;
	}
	mes "[ไดชินแมน]";
	mes "ฮิ้ววว ~~ เราเป็นคนส่งของ ~";
	mes "เราเร็วกว่าเที่ยวบินใด ๆ~!";
	next;
	mes "[ไดชินแมน]";
	mes "ยินดีต้อนรับ ~ ยินดีต้อนรับ!";
	mes "เราเป็น บริษัท จัดส่งที่เร็วที่สุด Daishin Corporation.";
	next;
	mes "[ไดชินแมน]";
	mes "ฉันจะช่วยคุณได้อย่างไร?";
	next;
	if(select("ฉันไม่ต้องการความช่วยเหลือ.:ฉันมีภารกิจส่งของ.") == 1) {
		mes "[ไดชินแมน]";
		mes "โอ้ดี จากนั้นไปคิด บาย~!";
		close;
	}
	mes "[ไดชินแมน]";
	mes "คุณมาจาก Eden Group?";
	mes "คุณมีภารกิจอะไร?";
	next;
	switch(select("Deliver an Insecticide.:Deliver Honey.:Deliver Blankets.")) {
		case 1: callsub L_Quest,8271;
		case 2: callsub L_Quest,8272;
		case 3: callsub L_Quest,8273;
	}
	end;
L_Quest:
	if (checkquest(getarg(0)) != 1) {
		mes "[ไดชินแมน]";
		mes "ฮะ? คุณแน่ใจไหม? บางสิ่งบางอย่างที่ไม่ถูกต้อง...";
		next;
		mes "[ไดชินแมน]";
		mes "มันแปลก.";
		mes "โปรดตรวจสอบหน้าต่าง Quest ของคุณโดยกด 'alt + u'.";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[ไดชินแมน]";
		mes "รอ แต่คุณมีกล่องส่งแล้ว.";
		next;
		mes "[ไดชินแมน]";
		mes "เราไม่สามารถให้คุณส่งสองกล่องพร้อมกันได้ ดังนั้นโปรดส่งของให้เสร็จก่อน.";
		next;
		mes "[ไดชินแมน]";
		mes "หากคุณลืมสถานที่จัดส่งโปรดตรวจสอบหน้าต่างเควสโดยกด 'alt + u'.";
		close;
	}
	switch(getarg(0)) {
	case 8271:
		mes "[ไดชินแมน]";
		mes "อะฮ่า! ฉันเข้าใจแล้ว!";
		mes "ยาฆ่าแมลง? ต้องเป็นนักสัตววิทยาทางตะวันออกเฉียงใต้ของ Payon ที่สั่งยาฆ่าแมลง.";
		break;
	case 8272:
		mes "[ไดชินแมน]";
		mes "อะฮ่า! ฉันเข้าใจแล้ว!";
		mes "น้ำผึ้ง? ต้องเป็นนักกีฏวิทยาที่อยู่ทางตะวันตกของ St. Capitolina Abbey ซึ่งอยู่ทางตะวันออกเฉียงเหนือของ Prontera ที่สั่งซื้อน้ำผึ้ง.";
		break;
	case 8273:
		mes "[ไดชินแมน]";
		mes "โอ้ใช่!";
		mes "ผ้าห่ม! ต้องเป็นนักชีววิทยาทางตอนใต้ของสนาม Lutie ที่สั่งผ้าห่ม.";
		next;
		mes "[ไดชินแมน]";
		mes "คุณสามารถไปที่ Lutie ได้โดยถาม Mr. Claus ทางตะวันออกเฉียงเหนือของ Al De Baran.";
		break;
	}
	next;
	mes "[ไดชินแมน]";
	mes "แรงจูงใจในการร่วมมือของ Dieshin คือการส่งมอบที่รวดเร็วและให้เครดิต ดังนั้นโปรดจัดส่งทันทีโดยไม่ชักช้า.";
	next;
	mes "[ไดชินแมน]";
	mes "หากคุณลืมสถานที่จัดส่งโปรดตรวจสอบหน้าต่างเควสโดยกด 'alt + u'.";
	getitem 6218,1; //Disin_Delivery_Box
	close;
}

pay_fild07,83,89,7	script	Zoologist	89,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[นักสัตววิทยา]";
		mes "ทำไมคุณถึงถือของมากมาย?";
		mes "คุณอยู่ในหลักสูตรการฝึกอบรมหรืออะไร?";
		close;
	}
	mes "[นักสัตววิทยา]";
	mes "คุณเคยได้ยินสัตว์ประหลาดชื่อบิ๊กฟุต?";
	mes "ดูเหมือนหมีที่มีขนสีแดงและมีแผลเป็นรูปตัว 'X' ที่ไหล่ของเขา.";
	next;
	mes "[นักสัตววิทยา]";
	mes "จากการวิจัยล่าสุดแมลงวันและสัตว์ประหลาดบิ๊กฟุตมีความสัมพันธ์ทางชีวภาพ.";
	next;
	mes "[นักสัตววิทยา]";
	mes "ฉันกำลังศึกษาเกี่ยวกับเรื่องนี้และพยายามพิสูจน์ทฤษฎี ดังนั้นฉันจึงวางแผนที่จะฆ่าแมลงวันรอบ ๆ บิ๊กฟุตและสังเกตปฏิกิริยาของมัน.";
	next;
	if(select("... ฉันไม่สนใจ:นี่คือยาฆ่าแมลง.") == 1) {
		mes "[นักสัตววิทยา]";
		mes "โอ้ฉันต้องการยาฆ่าแมลงเพื่อทำการศึกษาต่อ.";
		close;
	}
	if (checkquest(8271) != 1) {
		mes "[นักสัตววิทยา]";
		mes "ฮะ?";
		mes "คุณไม่ใช่คนส่งของ Dieshin?";
		next;
		mes "[นักสัตววิทยา]";
		mes "ฉันกำลังรอการจัดส่งแบบพิเศษ.";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[นักสัตววิทยา]";
		mes "โอ้โห! ใช่ ที่หนึ่ง!";
		next;
		mes "[นักสัตววิทยา]";
		mes "นั่นคือยาฆ่าแมลงที่ฉันสั่ง! คุณถูกส่งมาโดย Dieshin ถูกต้อง?";
		next;
		mes "[นักสัตววิทยา]";
		mes "ฉันรอคอยสิ่งนี้!";
		mes "ส่งมาให้ฉัน!";
		next;
		mes "[นักสัตววิทยา]";
		mes "มันมาถึงเร็วกว่าที่ฉันคิดไว้มาก ฉันต้องการทดสอบทันที!";
		next;
		mes "[นักสัตววิทยา]";
		mes "ทำได้ดี. งานดี! ขอบคุณมาก! นี่คือของขวัญเล็ก ๆ น้อย ๆ สำหรับคุณ.";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8271;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes "[นักสัตววิทยา]";
	mes "ขอขอบคุณในความกรุณาของคุณ.";
	mes "แต่คุณเอาอะไรมา?";
	next;
	mes "[นักสัตววิทยา]";
	mes "อืมฉันคิดว่า Dieshin เร็วและแม่นยำ แต่คุณเอากล่องเปล่ามาให้ฉัน น่าผิดหวังแค่ไหน.";
	next;
	mes "[นักสัตววิทยา]";
	mes "กรุณานำคำสั่งของฉัน Dieshin จะให้ผลิตภัณฑ์ของฉันแก่คุณ.";
	close;
}

prt_fild03,30,254,3	script	Entomologist	83,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[นักกีฏวิทยา]";
		mes "ทำไมคุณถึงถือของมากมาย?";
		mes "คุณอยู่ในหลักสูตรการฝึกอบรมหรืออะไร?";
		close;
	}
	mes "[นักกีฏวิทยา]";
	mes "คุณเคยได้ยินเกี่ยวกับสัตว์ประหลาดฮอร์น?";
	mes "มันมีรูปร่างเหมือนแมลงที่มีผิวมันสีดำและมีเขาขนาดยักษ์เป็นหัว.";
	next;
	mes "[นักกีฏวิทยา]";
	mes "การวิจัยล่าสุดออกมาและแสดงให้เห็นว่าสัตว์ประหลาดตัวนี้สามารถเชื่องและฝึกฝนโดยฮันนี่ได้.";
	next;
	mes "[นักกีฏวิทยา]";
	mes "ตอนนี้ฉันกำลังศึกษาเกี่ยวกับเรื่องนี้และพยายามพิสูจน์ทฤษฎี. ดังนั้นฉันจึงวางแผนที่จะวางกับดักน้ำผึ้งในสถานที่ที่เขามักจะผ่านไปและสังเกตปฏิกิริยาของเขา.";
	next;
	if(select("... ฉันไม่สนใจ:นี่คือ Honey.") == 1) {
		mes "[นักกีฏวิทยา]";
		mes "โอ้ฉันต้องการที่รักเพื่อศึกษาต่อ.";
		close;
	}
	if (checkquest(8272) != 1) {
		mes "[นักกีฏวิทยา]";
		mes "ฮะ?";
		mes "คุณไม่ใช่คนส่งของ Dieshin?";
		next;
		mes "[นักกีฏวิทยา]";
		mes "ฉันกำลังรอการจัดส่งแบบพิเศษ.";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[นักกีฏวิทยา]";
		mes "โอ้โห! ใช่ ที่หนึ่ง!";
		next;
		mes "[นักกีฏวิทยา]";
		mes "นั่นคือฮันนี่ที่ฉันสั่ง! คุณถูกส่งมาโดย Dieshin ถูกต้อง?";
		next;
		mes "[นักกีฏวิทยา]";
		mes "ฉันรอคอยสิ่งนี้!";
		mes "ส่งมาให้ฉัน!";
		next;
		mes "[นักกีฏวิทยา]";
		mes "มันมาถึงเร็วกว่าที่ฉันคิดไว้มาก ฉันต้องการทดสอบทันที!";
		next;
		mes "[นักกีฏวิทยา]";
		mes "ทำได้ดี. งานดี! ขอบคุณมาก! นี่คือของขวัญเล็ก ๆ น้อย ๆ สำหรับคุณ.";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8272;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes "[นักกีฏวิทยา]";
	mes "ขอขอบคุณในความกรุณาของคุณ.";
	mes "แต่คุณเอาอะไรมา?";
	next;
	mes "[นักกีฏวิทยา]";
	mes "อืมฉันคิดว่า Dieshin เร็วและแม่นยำ แต่คุณเอากล่องเปล่ามาให้ฉัน น่าผิดหวังแค่ไหน.";
	next;
	mes "[นักกีฏวิทยา]";
	mes "กรุณานำคำสั่งของฉัน Dieshin จะให้ผลิตภัณฑ์ของฉันแก่คุณ.";
	close;
}

xmas_fild01,92,57,3	script	Biologist	97,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[นักชีววิทยา]";
		mes "ทำไมคุณถึงถือของมากมาย?";
		mes "คุณอยู่ในหลักสูตรการฝึกอบรมหรืออะไร?";
		close;
	}
	mes "[นักชีววิทยา]";
	mes "คุณเคยได้ยินเกี่ยวกับสัตว์ประหลาดมาริน?";
	mes "ดูเหมือน poring ทั่วไป แต่มีลักษณะเฉพาะ.";
	next;
	mes "[นักชีววิทยา]";
	mes "Porings ขโมยสิ่งของถ้าเห็นบนพื้น อย่างไรก็ตาม Marin ไม่มีความสนใจในรายการที่ดรอป. ฉันเลยสงสัยว่าทำไมมารินถึงไม่ให้ความสนใจกับการรวบรวมไอเทมแม้ว่าพวกมันจะเป็นสัตว์ประหลาดชนิดเดียวกับ Porings ก็ตาม.";
	next;
	mes "[นักชีววิทยา]";
	mes "แต่สถานที่แห่งนี้หนาวจัด.";
	next;
	if(select("... ฉันไม่สนใจ:นี่คือผ้าห่ม.") == 1) {
		mes "[นักชีววิทยา]";
		mes "หืออะไรก็ได้ แต่ระวัง! มันลื่นมากที่นี่.";
		close;
	}
	if (checkquest(8273) != 1) {
		mes "[นักชีววิทยา]";
		mes "ฮะ?";
		mes "คุณไม่ใช่คนส่งของ Dieshin?";
		next;
		mes "[นักชีววิทยา]";
		mes "ฉันกำลังรอการจัดส่งแบบพิเศษ.";
		close;
	}
	if (countitem(6218) > 0) {
		mes "[นักชีววิทยา]";
		mes "โอ้โห! ใช่ ที่หนึ่ง!";
		next;
		mes "[นักชีววิทยา]";
		mes "นั่นคือผ้าห่มที่ฉันสั่ง! คุณถูกส่งมาโดย Dieshin ถูกต้อง?";
		next;
		mes "[นักชีววิทยา]";
		mes "ฉันรอคอยสิ่งนี้!";
		mes "ส่งมาให้ฉัน!";
		next;
		mes "[นักชีววิทยา]";
		mes "มันมาถึงเร็วกว่าที่ฉันคิดไว้มาก ฉันต้องการทดสอบทันที!";
		next;
		mes "[นักชีววิทยา]";
		mes "ทำได้ดี. งานดี! ขอบคุณมาก! นี่คือของขวัญเล็ก ๆ น้อย ๆ สำหรับคุณ.";
		delitem 6218,1; //Disin_Delivery_Box
		erasequest 8273;
		getitem 569,100; //Novice_Potion
		getexp 1000,0;
		close;
	}
	mes "[นักชีววิทยา]";
	mes "ขอขอบคุณในความกรุณาของคุณ.";
	mes "แต่คุณเอาอะไรมา?";
	next;
	mes "[นักชีววิทยา]";
	mes "อืมฉันคิดว่า Dieshin เร็วและแม่นยำ แต่คุณเอากล่องเปล่ามาให้ฉัน น่าผิดหวังแค่ไหน.";
	next;
	mes "[นักชีววิทยา]";
	mes "กรุณานำคำสั่งของฉัน Dieshin จะให้ผลิตภัณฑ์ของฉันแก่คุณ.";
	close;
}

payon,179,66,3	script	Dashia	931,{
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 3500) {
		mes "[Dashia]";
		mes "ทำไมคุณถึงถือของมากมาย?";
		mes "คุณอยู่ในหลักสูตรการฝึกอบรมหรืออะไร?";
		close;
	}
	mes "[Dashia]";
	mes "นี่คือผมบางส่วน ~ นาฬิกาเสีย~!";
	next;
	mes "[Dashia]";
	mes "นาฬิกาเสีย .. ? หือหือ..";
	next;
	mes "[Dashia]";
	mes "เฮ้ ~! เพื่อน! คุณดูเหมือนนักผจญภัยที่ร่ำรวยมาก.";
	next;
	if(select("ฉันเป็นนักผจญภัยที่น่าสงสาร:ฉันมีภารกิจรวบรวม.") == 1) {
		mes "[Dashia]";
		mes "จริงๆ? อืมใช่ คุณดูไม่ซับซ้อน.";
		next;
		mes "[Dashia]";
		mes "คุณเป็นนักผจญภัยที่น่าสงสาร ~! โชคดี!";
		close;
	}
	mes "[Dashia]";
	mes "คุณมาจาก Eden Group?";
	mes "คุณมีภารกิจอะไร?";
	next;
	switch(select("รวบรวม Bones:รวบรวม Animal Skin:รวบรวม Peelings:รวบรวม Tails:รวบรวม Cookies:รวบรวม Mustaches")) {
	case 1:
		callsub L_Quest,8274;
		if (countitem(932) > 9) {
			callsub L_Reward;
			delitem 932,10; //Skel_Bone
			getitem 569,100; //Novice_Potion
			getexp 1000,100;
			erasequest 8274;
			close;
		}
		mes "[Dashia]";
		mes "ฉันเห็นว่าคุณมีภารกิจในการรวบรวม.";
		mes "แต่ Skel-Bones อยู่ที่ไหน?";
		next;
		mes "[Dashia]";
		mes "คุณยังไม่ได้ทำภารกิจ ทำไมคุณถึงกลับมาที่นี่?";
		next;
		mes "[Dashia]";
		mes "คุณมี 'Skel-Bones ไม่เพียงพอ'... คุณจะพบมันได้เมื่อคุณล่าโครงกระดูก.";
		next;
		mes "[Dashia]";
		mes "หากต้องการค้นหา Skeletons คุณควรตรวจสอบ Payon Dungeon 1F, ซึ่งตั้งอยู่ใน Archer Village ทางเหนือของ Payon.";
		break;
	case 2:
		callsub L_Quest,8275;
		if ((countitem(948) > 9) && (countitem(919) > 4)) {
			callsub L_Reward;
			getitem 569,100; //Novice_Potion
			delitem 948,10; //Bear's_Foot
			delitem 919,5; //Animal's_Skin
			getexp 1000,100;
			erasequest 8275;
			close;
		}
		mes "[Dashia]";
		mes "ฉันเห็นว่าคุณมีภารกิจในการรวบรวม.";
		mes "แต่ตีนหมีและหนังสัตว์อยู่ที่ไหน?";
		next;
		mes "[Dashia]";
		mes "คุณยังไม่ได้ทำภารกิจ. ทำไมคุณถึงกลับมาที่นี่?";
		next;
		mes "[Dashia]";
		mes "คุณต้องนำ Bear's Footskins 'และ' Animal Skins 5 ชิ้นมาด้วย!";
		mes "คุณสามารถหาหนังเท้าหมีได้จากบิ๊กฟุตและหนังสัตว์จากสัตว์.";
		next;
		mes "[Dashia]";
		mes "บิ๊กฟุตอยู่รอบ ๆ ทางเหนือของสนาม Geffen หรือทางตะวันออกเฉียงใต้ของสนาม Payon.";
		break;
	case 3:
		callsub L_Quest,8276;
		if ((countitem(955) > 19) && (countitem(910) > 19)) {
			callsub L_Reward;
			delitem 955,20; //Worm_Peelings
			delitem 910,20; //Garlet
			getitem 569,100; //Novice_Potion
			getexp 1000,100;
			erasequest 8276;
			close;
		}
		mes "[Dashia]";
		mes "ฉันเห็นว่าคุณมีภารกิจในการรวบรวม.";
		mes "แต่เปลือกหนอนและการ์เล็ตอยู่ที่ไหน?";
		next;
		mes "[Dashia]";
		mes "คุณต้องนำ '20 Worm Peelings 'และ '20 Garlets' เพื่อทำภารกิจให้สำเร็จ.";
		next;
		mes "[Dashia]";
		mes "คุณสามารถรับไอเทมเหล่านั้นได้จาก 'Pierre', 'Andre', 'De Niro', 'Vitara', 'Thief Bug', 'Female Thief Bug' และ 'Male Thief Bug'.";
		next;
		mes "[Dashia]";
		mes "'Piere ',' Andre ',' Deniro 'และ' Vitata 'อยู่ใน Ant Hell ซึ่งตั้งอยู่ใกล้กับป้อม Saint Darmain ทางตะวันตกเฉียงใต้ของโมร็อกโก.";
		next;
		mes "[Dashia]";
		mes "'Thief Bug ',' Female Thief Bug 'และ' Male Thief Bug 'อยู่ใน Prontera Culvert ในการไปที่นั่นคุณต้องเป็นอาสาสมัครของกองกำลังลงโทษ Prontera Culvert.";
		break;
	case 4:
		callsub L_Quest,8277;
		if ((countitem(942) > 9) && (countitem(1026) > 9) && (countitem(945) > 9)) {
			callsub L_Reward;
			delitem 942,10; //Yoyo_Tail
			delitem 1026,10; //Acorn
			delitem 945,10; //Raccoon_Leaf
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8277;
			close;
		}
		mes "[Dashia]";
		mes "ฉันเห็นว่าคุณมีภารกิจในการรวบรวม.";
		mes "แต่ Yoyo Tails, Acorns และ Raccoon Leafs อยู่ที่ไหน?";
		next;
		mes "[Dashia]";
		mes "คุณยังไม่ได้ทำภารกิจ ทำไมคุณถึงกลับมาที่นี่?";
		mes "คุณต้องนำ '10 Yoyo Tails ''10 Acorns' และ '10 Raccoon Leafs 'เพื่อทำภารกิจให้สำเร็จ.";
		next;
		mes "[Dashia]";
		mes "คุณสามารถรับ Yoyo Tails จาก 'Yoyo', Acorn จาก 'Coco's' และ Raccoon Leaf จาก 'Smokies'.";
		next;
		mes "[Dashia]";
		mes "คุณสามารถพบพวกมันได้อย่างง่ายดายรอบ ๆ ทุ่ง แต่สนามทางตะวันออกของอโยธยาเป็นจุดที่ดีที่สุดสำหรับการล่าสัตว์ประหลาดทั้ง 3 ตัว.";
		next;
		mes "[Dashia]";
		mes "หากต้องการไปที่อโยธยาให้ไปที่ 'Aibakthing' ใน Alberta แล้วจ่ายค่าโดยสาร.";
		break;
	case 5:
		callsub L_Quest,8278;
		if ((countitem(538) > 4) && (countitem(539) > 0)) {
			callsub L_Reward;
			delitem 538,5; //Well_Baked_Cookie
			delitem 539,1; //Piece_Of_Cake
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8278;
			close;
		}
		mes "[Dashia]";
		mes "ฉันเห็นว่าคุณมีภารกิจในการรวบรวม.";
		mes "แต่คุกกี้ที่อบอย่างดีและชิ้นส่วนของเค้กอยู่ที่ไหน?";
		next;
		mes "[Dashia]";
		mes "คุณยังไม่ได้ทำภารกิจ ทำไมคุณถึงกลับมาที่นี่?";
		mes "คุณต้องนำ '5 Well-baked Cookies' และ 'Piece of Cake 1 ชิ้น' เพื่อทำภารกิจให้สำเร็จ.";
		next;
		mes "[Dashia]";
		mes "คุณสามารถรับ Well-baked Cookie จาก 'คุกกี้' ที่สวมเสื้อสีแดงหรือสีเขียว.";
		mes "หากต้องการรับ Piece of Cake คุณควรมองหา 'Mystcases'.";
		next;
		mes "[Dashia]";
		mes "มอนสเตอร์ทั้งสองสามารถพบได้ใน โกดังโรงงานของเล่นหรือห้องตรวจสอบของเล่น หากต้องการไปที่นั่นให้ถาม Mr. Claus ทางตะวันออกเฉียงเหนือของ Al De Baran.";
		break;
	case 6:
		callsub L_Quest,8279;
		if ((countitem(1017) > 4) && (countitem(1018) > 2)) {
			callsub L_Reward;
			delitem 1017,5; //Moustache_Of_Mole
			delitem 1018,3; //Nail_Of_Mole
			getitem 569,150; //Novice_Potion
			getexp 1000,200;
			erasequest 8279;
			close;
		}
		mes "[Dashia]";
		mes "ฉันเห็นว่าคุณมีภารกิจในการรวบรวม.";
		mes "แต่ Mole Whiskerss และ Mole Claws อยู่ที่ไหน??";
		next;
		mes "[Dashia]";
		mes "คุณยังไม่ได้ทำภารกิจ ทำไมคุณถึงกลับมาที่นี่?";
		mes "คุณต้องนำ '5 Mole Whiskers' และ '3 Mole Claws' เพื่อทำภารกิจให้สำเร็จ.";
		next;
		mes "[Dashia]";
		mes "คุณสามารถรับไอเทมเหล่านั้นได้จาก 'Martins'.";
		mes "Martins สามารถพบได้ใน Mjolnir Dead Pit แต่ชั้นสองนั้นอันตรายโปรดระวัง!";
		next;
		mes "[Dashia]";
		mes "ไปที่นั่นถามพนักงาน Kafra ใน Geffen พวกเขาสามารถวาปคุณไปที่นั่นได้.";
		break;
	}
	next;
	mes "[Dashia]";
	mes "อย่างไรก็ตามฉันจะอยู่ที่นี่เพื่อรอคุณ โปรดกลับมาเมื่อคุณเสร็จสิ้นภารกิจ!";
	close;
L_Quest:
	if (checkquest(getarg(0)) != 1) {
		mes "[Dashia]";
		mes "ฮะ? คุณแน่ใจไหม? บางสิ่งบางอย่างที่ไม่ถูกต้อง...";
		next;
		mes "[Dashia]";
		mes "มันแปลก.";
		mes "โปรดตรวจสอบหน้าต่าง Quest ของคุณโดยกด 'alt + u'.";
		close;
	}
	return;
L_Reward:
	mes "[Dashia]";
	mes "ว้าว ~ คุณทำได้ดีมาก!";
	next;
	mes "[Dashia]";
	mes "ขอบคุณมาก. ขอให้พระเจ้าคุ้มครอง!";
	emotion ET_SMILE;
	next;
	mes "[Dashia]";
	mes "ฮา ~! ฮ่า~!";
	mes "ฉันจะให้รางวัลตามสัญญา.";
	return;
}
