//===== rAthena Script =======================================
//= Eden Group Quests - Mission [111-120]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 111 - 120.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,17,95,5	script	Mingmin#1	4_F_GENETIC,{
	mes "[Mingmin]";
	if (!callfunc("F_HasEdenGroupMark")) {
		cutin "min02",0;
		mes "คนแปลกหน้า. สถานที่นี้เหมาะสำหรับสมาชิกกลุ่ม Eden เท่านั้น.";
		mes "ฉันต้องการระดับพื้นฐาน ^FF0000111 - 120 ^000000 นักผจญภัยเพื่อช่วยในการค้นคว้าของฉัน.";
	}
	else if (para_110lv01 == 0) {
		cutin "min02",0;
		mes "......";
		emotion ET_THINK;
		next;
		mes "[Mingmin]";
		mes "(ว้าวว...)";
		next;
		mes "นัก Mingmin กำลังพึมพำกับตัวเอง.";
		next;
		mes "[Mingmin]";
		mes "...";
		next;
		mes "[Mingmin]";
		mes "......";
		next;
		mes "[Mingmin]";
		mes "^0000ffคำถาม: คุณเป็นคนรับใช้ของฉันหรือไม่ ^000000";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "(ผู้หญิงคนนี้พูดถึงอะไร?)";
		emotion ET_THINK, playerattached();
		next;
		mes "[Mingmin]";
		mes "ตอบฉัน!";
		mes "เจ้าเป็นคนรับใช้ของฉัน?";
		emotion ET_THINK;
		next;
		select( "ไม่ใช่ฉัน.", "คุณกำลังพูดอะไร'?" );
		mes "[Mingmin]";
		mes "เจ้าไม่? ยอดเยี่ยม ... เพราะฉันมอบหมายให้ ผู้รับใช้ของฉันคนใด มีศักดิ์ศรีมากกว่ากัน.";
		emotion ET_OHNO;
		next;
		mes "[Mingmin]";
		mes "ไม่ใช่คนใช้ ... คุณกำลังมองหาอะไรที่นี่?";
		next;
		if (select( "ไม่มีอะไรเป็นพิเศษ.", "Quests!คุณคือใคร?" ) == 1) {
			mes "[Mingmin]";
			mes "เป็นที่ยอมรับฉันหวังว่าคุณจะพบสิ่งที่คุณต้องการ.";
			emotion ET_OK;
		}
		else {
			mes "[Mingmin]";
			mes "โอ้ฉัน? ฉันคือ ^E51BD8Busaner Mingmin ^000000 และฉัน กำลังวิจัยสัตว์ประหลาดตามคำขอของ Eden Group.";
			next;
			mes "[Mingmin]";
			mes "ฉันเคยเก็บเชื้อราและราเพียงอย่างเดียวที่ ห้องปฏิบัติการบนภูเขา ของฉัน ตั้งแต่นั้นมาฉันก็เข้าร่วมกลุ่ม เพื่อค้นหาสัตว์ประหลาดตัวใหม่...";
			next;
			mes "[Mingmin]";
			mes "จากประสบการณ์ทั้งหมด ของฉันพวกเขาทำ ให้ฉันเป็นศาสตราจารย์ที่ Eden Group จริงๆ.";
			next;
			mes "[Mingmin]";
			mes "อย่างไรก็ตามมีงานเร่งด่วน ที่ฉันต้องทำให้เสร็จ.";
			para_110lv01 = 1;
		}
	}
	else if (para_110lv01 == 1) {
		cutin "min02",0;
		mes "^0C0CF2คุณเป็นคนรับใช้ของฉันหรือเปล่า ^000000";
		next;
		select("ไม่ ~ ฉัน " + strcharinfo (0) + " จำได้ไหม");
		mes "[Mingmin]";
		mes "โอ้ฉันเคยพบคุณมาก่อน การอาศัยอยู่ในบ้าน ที่เต็มไปด้วยเชื้อรา ดูเหมือนจะทำให้ความจำเสื่อม.";
		emotion ET_SURPRISE;
		next;
		mes "[Mingmin]";
		mes "คุณเป็นใครอีกแล้ว?";
		emotion ET_QUESTION;
		next;
		select("การร้องขอ และ การวิจัยของ Eden Group");
		mes "[Mingmin]";
		mes "อาที่ฉันจำได้ ";
		next;
		mes "[Mingmin]";
		mes "ฉันกำลังรวบรวมข้อมูลเกี่ยวกับพืชและสัตว์ใน Midgard สำหรับ Eden Group ";
		next;
		mes "[Mingmin]";
		mes "ความตั้งใจคือการจัดทำรายการทางวิทยาศาสตร์และระบุถึงอันตรายของโลก ฉันไม่ชอบความคิดนี้เป็นการส่วนตัว";
		next;
		mes "[Mingmin]";
		mes "ทฤษฎีเท่านั้นที่จะพาคุณไปได้ไกลในโลกนี้ ";
		next;
		mes "[Mingmin]";
		mes "มอร์เวอร์นักผจญภัยที่เดินทางนอกรูน - มิดการ์ทน่าจะมีประสบการณ์มากพอที่จะรู้จักตัวเอง ";
		next;
		mes "[Mingmin]";
		mes "นี่คือ ^ff0000 ความคิดเห็นของฉัน ^000000 เท่านั้นดังนั้นอย่ากังวลกับตัวเอง";
		next;
		mes "[Mingmin]";
		mes "หลังจากที่พูดทั้งหมดนี้ฉันต้องพักผ่อน เท้าของฉันค่อนข้างล้า.";
		emotion ET_SLEEPY;
		para_110lv01 = 2;
	}
	else if (para_110lv01 == 2) {
		cutin "min02",0;
		mes "โอ้ "+ strcharinfo (0) +" มันคือคุณ";
		next;
		mes "[Mingmin]";
		mes "ฉันอยู่ที่ไหน? ฉันเพิ่งพูดถึงหน้าที่ของ Eden Group ในการปกป้องผู้อื่นจากอันตราย ";
		next;
		mes "[Mingmin]";
		mes "เพื่อปกป้องผู้คนมากขึ้นฉันคิดว่าพวกเขาต้องการนักผจญภัยที่มีประสบการณ์โดยตรง ";
		next;
		mes "[Mingmin]";
		mes "ดังนั้นเราจึงต้องการผู้ที่กล้าหาญหรือโง่เขลาพอที่จะออกไปรวบรวมข้อมูลในสนาม";
		next;
		select("ฟังดูอันตราย ... ฉันจะเริ่มได้เมื่อไหร่?");
		mes "[Mingmin]";
		mes "คุยกับฉันอีกสักครู่แล้วเราจะไปกัน ";
		emotion ET_SLEEPY;
		para_110lv01 = 3;
	}
	else if (para_110lv01 == 3) {
		cutin "min02",0;
		mes "คำถาม "+ strcharinfo (0) +"! ฉันกำลังฝึกเพราะคนรับใช้คนใหม่กำลังจะมาเร็ว ๆ นี้ ";
		next;
		select("Waddya หมายถึงคนรับใช้ใหม่?");
		mes "[Mingmin]";
		//mes "โอ ... ขอโทษฉันหมายถึงสไควร์ ... ";
		mes "ฉันหมายถึงนักผจญภัย ... ";
		emotion ET_SURPRISE;
		next;
		mes "[Mingmin]";
		mes "ขอโทษค่ะเชื้อราในบ้านของฉันเริ่มระบาดมากแล้ว ";
		next;
		mes "[Mingmin]";
		mes "ฉันพบว่า Rohtert ทำงานอยู่ที่นี่เมื่อฉันมาพร้อมกับคำขอจาก Eden Group ";
		next;
		if (para_100lv02 > 100) {
			mes "[Mingmin]";
			mes "คุณเคยพบเขาก่อนหน้านี้หรือไม่? ฉันมีประวัติกับเขา ";
		}
		else {
			mes "[Mingmin]";
			mes "ผู้ชายผมขาวข้างทางเข้า คุณควรแวะไปถ้าคุณสนใจเขากำลังจัดการนักผจญภัยระดับ 100-110 ";
		}
		next;
		mes "[Mingmin]";
		mes "อย่างไรก็ตามเขาทำงานที่นี่ดังนั้นเขาจึงรู้ดีกว่าฉัน ฉันแค่มาเพื่อรับคำแนะนำ ";
		next;
		mes "[Mingmin]";
		mes "นั่นก็ไม่ยากขนาดนั้น .. ";
		next;
		cutin "min01",0;
		mes "[Mingmin]";
		mes "นักผจญภัยโง่ ๆ กำลังจะมาถามฉันว่า ^ff0000 'ราชินีของฉันปฏิบัติ กับ ฉันเหมือนคนรับใช้ !' ^000000 ";
		next;
		select("......");
		cutin "min02",0;
		mes "[Mingmin]";
		mes " ด้วยเหตุผลบาง ประการจึงยัง ไม่มีคนมากมายที่จะ ช่วยเหลือฉัน ";
		emotion ET_THINK, playerattached();
		next;
		mes "[Mingmin]";
		mes "ฉันคิดว่าต้องใช้ ^ff0000bravery ^000000 ในการขอใหม่ ";
		next;
		mes "[Mingmin]";
		mes "ฉันอยากจะทำสัญญากับคนที่อยากผจญภัยจริงๆ ";
		next;
		mes "[Mingmin]";
		mes "บางที "+ strcharinfo (0) +" ฉันจะให้คุณเป็นคนรับใช้ ของฉัน ถ้าคุณสนใจที่จะ ทำตามคำขอของ Eden Group สำหรับการวิจัยของฉัน ";
		para_110lv01 = 4;
	}
	else if (para_110lv01 > 3) {
		cutin "min01",0;
		if (para_110lv01 == 5)
			mes "ยินดีต้อนรับ! "+ strcharinfo (0) +"...";
		else
			mes "สวัสดี "+ strcharinfo (0) +".";
			next;
		switch( select( "ขอภารกิจวิจัยสัตว์ประหลาด ","พูดคุยเล็ก ๆ น้อย ๆ" ) ) {
			mes "[Mingmin]";
		case 1:
			if (BaseLevel < 111) {
				cutin "min02",0;
				mes "ฉันคิดว่าฉันบอกคุณไปแล้วว่าฉันกำลังมองหานักผจญภัยที่มีประสบการณ์มากขึ้นสำหรับการวิจัยสัตว์ประหลาดของฉัน ";
				next;
				mes "[Mingmin]";
				mes "เป็นเพราะภารกิจนี้มีคำขอที่อันตรายมาก";
				next;
				mes "[Mingmin]";
				mes "มาดูกัน. หากคุณสามารถจัดการกับคำขอทั้งหมดจาก rohtert ที่ยืนอยู่ที่นั่นฉันจะพิจารณาใหม่";
				next;
				mes "[Mingmin]";
				mes "ฉันจะพิจารณาใหม่ถ้าคุณเปลี่ยนเป็นระดับพื้นฐาน 111 ";
			}
			else if (BaseLevel < 121) {
				if (para_110lv01 == 4) {
					cutin "min02",0;
					mes "โอ้คุณต้องการช่วยฉันค้นคว้าหรือไม่";
					next;
					mes "ดูเหมือนเธอจะรอสัญญาณ ^FF0000 พิเศษอยู่ ^000000 คุณต้องการทำอะไร?";
					next;
					while(select( "ทำหัวใจด้วยสองมือ ","เต้นไปรอบ ๆ เหมือนคนบ้า ","ขยิบตาให้เธอ ","เป่านกหวีดใส่เธอ." ) != 2) {
						mes "[Mingmin]";
						mes "เพื่ออะไร";
						emotion ET_SCRATCH;
						next;
						mes "อืมเธอไม่ได้ไปเพื่อสิ่งนั้น";
						next;
					}
					mes "[Mingmin]";
					mes "สัญญาณนี้!?";
					emotion ET_SPARK;
					next;
					cutin "min01",0;
					mes "[Mingmin]";
					mes "^FF0000 .................. ตอนนี้? ^000000";
					emotion ET_SPARK;
					next;
					select("ไม่พูดอะไรแล้วพยักหน้า.");
					mes "[Mingmin]";
					mes "......";
					next;
					mes "[Mingmin]";
					mes "คำถาม...! เจ้าเป็นคนรับใช้ของฉันหรือ ";
					next;
					select("ใช่ฉันโกหก..");
					cutin "min02",0;
					mes "[Mingmin]";
					mes "............";
					next;
					cutin "min01",0;
					mes "[Mingmin]";
					mes "นั่นคือทั้งหมดที่ตัดสินแล้ว! คุณต้องลงนามในแบบฟอร์มนี้ ";
					next;
					mes "[สัญญา]";
					mes "A - นักผจญภัยจะไม่พูดกลับ Busaner Mingmin และจะไม่บ่นไม่ว่าในกรณีใด ๆ ";
					next;
					mes "[สัญญา]";
					mes "B - นักผจญภัยจะดำเนินการ ตามคำขอของ Busaner Mingmin โดยเร็วที่สุดเท่าที่จะทำได้";
					next;
					mes "[สัญญา]";
					mes "C - สัญญานี้สิ้นสุดในวัน ที่คำขอของนักผจญภัยเสร็จสมบูรณ์";
					next;
					cutin "min02",0;
					mes "[Contract]";
					mes "D - สัญญานี้จะเป็น ความลับยกเว้น ระหว่างนักผจญภัยและ Mingmin ภายใต้ ^FF0000 บทลงโทษแห่งความตาย ^000000";
					next;
					mes "[Mingmin]";
					mes "อย่ากังวลมากสหภาพแรงงานต้องการให้คุณรู้เพียงเซ็นชื่อบนเส้นที่ขีดไว้";
					next;
					if (select( "เซ็นสัญญา","ใช้เวลาคิด." ) == 2) {
						mes "[Mingmin]";
						mes "คุณสามารถใช้เวลาของคุณ .... ";
					}
					else {
						cutin "min01",0;
						mes "[Mingmin]";
						mes "คุณเซ็นหรือเปล่า ";
						next;
						mes "[Mingmin]";
						mes "............";
						next;
						cutin "min03",0;
						mes "[Mingmin]";
						mes "ฮาฮา! ฉันรู้สึกดีขึ้นมากแล้ว ";
						emotion ET_BLABLA;
						next;
						mes "[Mingmin]";
						mes "ขอบคุณที่ยอมรับสัญญาแปลก ๆ รอคอยที่จะได้ร่วมงานกับคุณ ";
						next;
						mes "[Mingmin]";
						mes "ฉันระมัดระวังสำหรับการร้องขอจากสาธารณะเพราะฉันต้องสุภาพกับนักผจญภัย";
						next;
						mes "[Mingmin]";
						mes "ฉันพยายามทำตัวงี่เง่า แต่มันไม่เหมาะกับบุคลิกของฉัน";
						next;
						cutin "min02",0;
						mes "[Mingmin]";
						mes "คุณควรระวังให้มากขึ้น ... คุณสังเกตเห็นอะไรบางอย่าง เกี่ยวกับปากกาที่คุณเซ็นด้วย หรือไม่มีสปอร์เห็ดหอมสีส้ม ^FF0000 อยู่ ^000000 อยู่";
						next;
						mes "[Mingmin]";
						mes "หากสปอร์เข้าสู่กระแสเลือด คุณอาจจะปลูกเห็ดภายในร่างกายของคุณเอง ";
						next;
						mes "[Mingmin]";
						mes "ใช้เวลาค่อนข้างนานดังนั้นทำให้ดีที่สุด ฉันอาจให้ยาแก้พิษได้ถ้าคุณทำได้ดี ";
						next;
						cutin "min03",0;
						mes "[Mingmin]";
						mes "เอาล่ะตอนนี้เราค่อนข้างคุ้นเคย ดังนั้น. เราไปเริ่มงานกันเลยไหม ";
						para_110lv01 = 5;
					}
				}
				else if (para_110lv01 > 4) {
					cutin "min01",0;
					mes "เราต้องการข้อมูลเพิ่มเติมเกี่ยวกับสัตว์ประหลาดต่อไปนี้ เลือก.";
					next;
					switch( select( "คำขอ Glastheim", "คำขอ Thanatos tower related", "คำขอ Ash Vacuum related", "คำขอ Mingmin's personal" ) ) {
						mes "[Mingmin]";
					case 1:
						mes "ฉันได้ยินมาว่ามีบางอย่างเกิดขึ้นใน Glastheim สัตว์ประหลาดกำลังเคลื่อนไหว";
						next;
						switch( select( "Raydric Research", "Khalitzberg Research", "Wanderer Research" ) ) {
							mes "[Mingmin]";
						case 1:
							if (checkquest(13068,PLAYTIME) == 0)
								mes "การวิจัยของ Raydric เป็นไปได้ด้วยดี จะดีกว่าถ้ามีตัวอย่างให้ค้นคว้ามากขึ้น!";
							else if (checkquest(13067,HUNTING) == 1) {
								cutin "ทนภฬตๅธฏฤซตๅ",3;  //daydric_card
								mes "คุณสามารถพบ Raydric ได้ทางเข้าทิศตะวันตกหรือที่ชั้น 2 ของ Glastheim ";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการข้อมูลและเอกสารไม่ว่าจะเกิดอะไรขึ้น กลับมาหาฉันหลังจากจัดการกับ ^FF000030 Raydric ^000000 's.";
								next;
								cutin "min01",0;
								mes "[Mingmin]";
								mes "ประสบการณ์ที่คุณเรียนรู้จากการต่อสู้กับสัตว์ประหลาดจะเป็นประวัติที่ดีสำหรับการวิจัย! ";
							}
							else if (checkquest(13067,HUNTING) == 2) {
								cutin "min03",0;
								mes "คุณรู้สึกอย่างไรกับการรับมือกับสัตว์ประหลาดที่แข็งแกร่ง? เอกสารที่ดีและประสบการณ์ที่ดีสำหรับคุณเช่นกัน ";
								callsub S_Quest,13067,13068,700000,200000;
							}
							else {
								cutin "ทนภฬตๅธฏฤซตๅ",3;  //daydric_card
								mes "มีบางอย่างเกิดขึ้นใน Glastheim และเบาะแสนั้นอยู่ใน Raydrics นั่นคือข้อมูลที่ฉันมี ";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการข้อมูลและเอกสารไม่ว่าจะเกิดอะไรขึ้น กลับมาหาฉันหลังจากจัดการกับ ^FF000030 Raydric ^000000's ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "โปรดกลับมาหาฉันหลังจากจัดการกับมอนสเตอร์ ฉันต้องการเรื่องราวทั้งหมดของคุณเป็นเอกสาร ";
									callsub S_Quest,13068,13067;
								}
							}
							break;
						case 2:
							if (checkquest(13070,PLAYTIME) == 0)
								mes "การวิจัยของ Khalitzburg เป็นไปได้ด้วยดี จะดีกว่าถ้ามีตัวอย่างให้ค้นคว้ามากขึ้น! ";
							else if (checkquest(13069,HUNTING) == 1) {
								cutin "ฝบฤฬณชภฬฦฎฤซตๅ",3;  //skullknight_card
								mes "Khalitzburg จะพบทางเข้าทางทิศตะวันตกของ Glastheim";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการข้อมูลและเอกสารไม่ว่าจะเกิดอะไรขึ้น กลับมาหาฉันหลังจากจัดการกับ ^FF000030 Khalitzburg's ^000000 ";
								next;
								cutin "min01",0;
								mes "[Mingmin]";
								mes "ประสบการณ์ที่คุณเรียนรู้จากการต่อสู้กับสัตว์ประหลาดจะเป็นประวัติที่ดีสำหรับการวิจัย!";
							}
							else if (checkquest(13069,HUNTING) == 2) {
								cutin "min03",0;
								mes "คุณรู้สึกอย่างไรที่ต้องรับมือกับมอนสเตอร์ที่แข็งแกร่ง? เอกสารที่ดีและประสบการณ์ที่ดีสำหรับคุณเช่นกัน";
								callsub S_Quest,13069,13070,700000,200000;
							}
							else {
								cutin "ฝบฤฬณชภฬฦฎฤซตๅ",3;  //skullknight_card
								mes "บางทีคาลิทซ์เบิร์กอาจเชื่อมโยงกับสิ่งที่เกิดขึ้นในกลาสไฮม์ดังนั้นมาลองค้นคว้าดู ";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการข้อมูลและเอกสารไม่ว่าจะเกิดอะไรขึ้น กลับมาหาฉันหลังจากจัดการกับ ^FF000030 Khalitzburg's ^000000";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ช่วยเล่าเรื่องราวหลังจากจัดการกับพวกเขาทีละคน พวกเขาจะเป็นเอกสารสำคัญ ... ";
									callsub S_Quest,13070,13069;
								}
							}
							break;
						case 3:
							if (checkquest(13072,PLAYTIME) == 0)
								mes "การวิจัยของ Wanderer เป็นไปได้ด้วยดี จะดีกว่าถ้ามีตัวอย่างให้ค้นคว้ามากขึ้น! ";
							else if (checkquest(13071,HUNTING) == 1) {
								cutin "น่ศธวฯดยภฺฤซตๅ",3;  //wander_man_card
								mes "Wanderer จะพบถ้าคุณเข้าจากทางเข้าด้านเหนือและไปที่ทางเข้าใหญ่กลางถนนใน Glastheim ";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการข้อมูลและเอกสารไม่ว่าจะเกิดอะไรขึ้น วิธีจัดการกับ ^FF000030 Wanderers ^000000. ";
								next;
								cutin "min01",0;
								mes "[Mingmin]";
								mes "ประสบการณ์ที่คุณได้เรียนรู้จากการต่อสู้กับสัตว์ประหลาดจะเป็นประวัติที่ดีสำหรับการวิจัย! ";
							}
							else if (checkquest(13071,HUNTING) == 2) {
								cutin "min03",0;
								mes "คุณรู้สึกอย่างไรที่ต้องรับมือกับมอนสเตอร์ที่แข็งแกร่ง? เอกสารที่ดีและประสบการณ์ที่ดีสำหรับคุณเช่นกัน ";
								callsub S_Quest,13071,13072,700000,200000;
							}
							else {
								cutin "น่ศธวฯดยภฺฤซตๅ",3;  //wander_man_card
								mes "วันเดอเรอร์เป็นตัวแทนของกลาสไฮม์ มันอันตราย แต่ก็เจ๋ง! ";
								next;
								mes "[Mingmin]";
								mes "มันจะเป็นงานวิจัยที่ดี กลับมาหลังจากจัดการกับ ^FF000030 Wanderers ^000000. ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ช่วยเล่าเรื่องราวหลังจากจัดการกับพวกเขาทีละคน พวกเขาจะเป็นเอกสารสำคัญ ... ";
									emotion ET_BEST;
									callsub S_Quest,13072,13071;
								}
							}
						}
						break;
					case 2:
						mes "เราจำเป็นต้องวิจัยมอนสเตอร์ในหอคอยธานาทอส";
						next;
						switch( select( "Ancient Mimic research", "Deathword research", "Owl Baron research", "Bloody Page research" ) ) {
							mes "[Mingmin]";
						case 1:
							if (checkquest(13074,PLAYTIME) == 0)
								mes "การวิจัยเลียนแบบโบราณกำลังดำเนินไปได้ด้วยดี จะดีกว่าถ้ามีตัวอย่างสำหรับการวิจัยมากขึ้น! ";
							else if (checkquest(13073,HUNTING) == 1) {
								cutin "ฟกภฮผวฦฎนฬนอ",3;  //ancient_mimic_card
								mes "ไม่ไกลถ้าคุณย้ายจาก Juno หรือ Hugel ไปยัง Thanatos หรือ Abyss lake ";
								next;
								mes "[Mingmin]";
								mes "อย่าบ่น! มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ!";
								next;
								cutin "min01",0;
								mes "[Mingmin]";
								mes "ประสบการณ์ที่คุณเรียนรู้จากการต่อสู้กับสัตว์ประหลาดจะเป็นประวัติที่ดีในการค้นคว้า! ";
							}
							else if (checkquest(13073,HUNTING) == 2) {
								cutin "min02",0;
								mes "คุณทำงานได้ดีในวันนี้!";
								callsub S_Quest,13073,13074,700000,300000;
							}
							else {
								cutin "ฟกภฮผวฦฎนฬนอ",3;  //ancient_mimic_card
								mes "คุณจะเห็นพวกมันบ่อยครั้งที่หอคอย Thanatos หรือทะเลสาบเหวนรก พวกเขาไม่ได้ใจกว้างกับการหยอดของพวกเขา แต่นั่นไม่ใช่สิ่งสำคัญในการจบคำขอนี้";
								next;
								mes "[Mingmin]";
								mes "ฉันไม่พบการเชื่อมต่อใด ๆ ในสองด้าน แต่ยังคงเกิดขึ้นดังนั้นจึงควรค่าแก่การค้นคว้า กลับมาหลังจากดูแล ^FF000030 เลียนแบบโบราณ ^000000 ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ถ้าคุณอาจจะได้รับประโยชน์จากพวกเขาคุณรู้ใช่มั้ย? อย่าลืมฉัน!";
									emotion ET_SPARK;
									callsub S_Quest,13074,13073;
								}
							}
							break;
						case 2:
							if (checkquest(13076,PLAYTIME) == 0)
								mes "การวิจัยของ Deathword เป็นไปได้ด้วยดี จะดีกว่าถ้ามีตัวอย่างสำหรับการวิจัยมากขึ้น! ";
							else if (checkquest(13075,HUNTING) == 1) {
								mes "หอคอย ธ นาทอสจะอันตรายกว่าที่คิด คุณอาจต้องการจัดปาร์ตี้ก่อนไปที่นั่น ";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการค้นคว้าข้อมูลนี้ดังนั้นโปรดรับคำขอนี้ด้วย!";
								next;
								mes "[Mingmin]";
								mes "ฉันจะให้รางวัล ... ";
							}
							else if (checkquest(13075,HUNTING) == 2) {
								cutin "min03",0;
								mes "ขอบคุณมาก. นี่จะเป็นการวิจัยที่ยอดเยี่ยม ";
								callsub S_Quest,13075,13076,700000,300000;
							}
							else {
								mes "นั่นคือเหตุผลว่าทำไมฉันจึงเลือกหอคอย Thanatos เพื่อทำการวิจัย ไม่ต้องแปลกใจ. เล่มนี้เป็นหนังสือเก่าที่กลายเป็นสัตว์ประหลาด! ";
								next;
								mes "[Mingmin]";
								mes "ฉันต้องการค้นคว้าเบื้องหลังของสิ่งมีชีวิตเหล่านั้น โปรดตรวจสอบ ^FF000030 Deathwords ^000000 ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ช่วยดูแลทีละคน หนังสือเหล่านั้นมีประวัติอันล้ำค่าของ Midgard ดังนั้นฉันจึงต้องการค้นคว้า ";
									callsub S_Quest,13076,13075;
								}
							}
							break;
						case 3:
							if (checkquest(13078,PLAYTIME) == 0)
								mes "การวิจัยของ Owl Baron เป็นไปได้ด้วยดี จะดีกว่าถ้ามีตัวอย่างสำหรับการวิจัยมากขึ้น!";
							else if (checkquest(13077,HUNTING) == 1) {
								cutin "พฦฟ๏นูทะฤซตๅ",3;  //owl_baron_card
								mes "คุณสามารถพบได้หากคุณปีนหอคอยตามข่าวลือ สำหรับฉันฉันไม่เคยไปที่นั่นเลย ";
								next;
								mes "[Mingmin]";
								mes "ผู้ชายที่มีหมวกสีฟ้าเดินขึ้นไปด้านบน แต่ฉันไม่สนใจที่จะทำตาม ";
								next;
								mes "[Mingmin]";
								mes "ยังไงก็ตามจะมีรางวัลเพียงพอแน่นอน";
								cutin "min01",0;
							}
							else if (checkquest(13077,HUNTING) == 2) {
								cutin "min03",0;
								mes "กลับอย่างปลอดภัย!? เอเดนกรุ๊ปจะมีความสุข";
								callsub S_Quest,13077,13078,700000,300000;
							}
							else {
								cutin "พฦฟ๏นูทะฤซตๅ",3;  //owl_baron_card
								mes "โดยส่วนตัวแล้วฉันไม่ได้สนใจสัตว์ประหลาดตัวนี้ แต่ได้รับการร้องขอจาก Eden Group";
								next;
								mes "[Mingmin]";
								mes "โปรดดูแล ^FF000020 Owl Barons ^000000 มันจะช่วยนักผจญภัยคนอื่น ๆ ในการสำรวจหอคอยธานาทอส";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ไม่ได้หมายความว่าฉันจะไม่ค้นคว้าข้อมูลเหล่านี้ ยิ่งมีเอกสารการวิจัยที่ดี ";
									callsub S_Quest,13078,13077;
								}
							}
							break;
						case 4:
							if (checkquest(13080,PLAYTIME) == 0) {
								cutin "min03",0;
								mes "ฉันชอบหนังสือเก่า! โอเคฉันต้องวิเคราะห์สิ่งนี้ เพจเปื้อนเลือด! กรุณานำมาเพิ่มเติมในวันพรุ่งนี้หรือมะรืนนี้ ทุกเวลา! ";
							}
							else if (isbegin_quest(13079) == 1) {
								if (countitem(7449) > 19) {// Bloody_Page
									cutin "min03",0;
									mes "ว้าว. มันช่างน่าสงสัยเหลือเกิน เป็นเรื่องที่น่าตื่นเต้น!";
									delitem 7449,20;
									callsub S_Quest,13079,13080,700000,300000;
								}
								else {
									cutin "ตฅฝบฟ๖ตๅ",3;  //deathword_card
									mes "ชั้น 3 ของ Thanatos มีมอนสเตอร์ Deathworld ที่ดรอปหน้าเลือดเหล่านี้ ";
									next;
									mes "[Mingmin]";
									mes "ฉันคิดว่าถ้าคุณรวบรวม 20 สิ่งเหล่านี้ก็น่าจะเพียงพอสำหรับการค้นคว้าของฉัน มันป่าเถื่อน แต่มันคืองาน! ";
									next;
									mes "[Mingmin]";
									mes "จะมีรางวัลเพียงพออย่างแน่นอน";
									cutin "min01",0;
								}
							}
							else {
								cutin "ตฅฝบฟ๖ตๅ",3;  //deathword_card
								mes "การที่ฉันตัดสินใจเดินทางไป Thanatos นั้นมีเหตุผล ฮ่าฮ่าฉันได้ยินมาว่าฉันสามารถรับ Bloody Pages จากมอนสเตอร์ Deathword ได้ ";
								next;
								mes "[Mingmin]";
								mes "ฉันรู้ว่ามันเลวร้ายและเปื้อนเลือด แต่ลองนึกภาพเรื่องราวหลายศตวรรษที่เก็บรักษาไว้ในหน้าเว็บเหล่านั้น ... ";
								next;
								mes "[Mingmin]";
								mes "คุณช่วยรับ 20 ^FF0000Bloody Pages ^000000 ให้ฉันได้ไหมฉันจะมีความสุขถ้ามีชั้นวางหนังสือที่มีประโยชน์ที่เราสามารถใช้เมื่อคุณนำกลับมา";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ได้โปรดฉันต้องการค้นคว้าเรื่องนี้จริงๆ.";
									callsub S_Quest,13080,13079;
								}
							}
						}
						break;
					case 3:
						mes "การวิจัยเรื่อง Ash Vacuum เพิ่งเริ่มต้น ยังมีอีกหลายสิ่งที่ต้องศึกษา! ";
						next;
						switch( select( "Dark Pinguicula research", "Nepenthes research", "Naga research", "Centipede Larva research", "Cornus research", "We know the magic power of the horn we saw that day." ) ) {
							mes "[Mingmin]";
						case 1:
							if (checkquest(13082,PLAYTIME) == 0)
								mes "การวิจัยของ Pinguicula dark กำลังดำเนินอยู่ โปรดกลับมาหาฉันในครั้งต่อไป!";
							else if (checkquest(13081,HUNTING) == 1) {
								cutin "วษฑอลงถ๓ดูลฉฤซตๅ",3;  //pinguicula_dark_card
								mes "ฉันคิดว่างานวิจัยนี้เป็นเรื่องง่าย พวกเขาไม่ได้อันตรายเกินไปจากรายงานเบื้องต้นที่ฉันได้รับ";
								next;
								mes "[Mingmin]";
								mes "อยู่ทางตะวันตกเฉียงเหนือจากค่ายกองกำลังพันธมิตรไม่ไกลขนาดนั้นให้รีบขึ้นไป.";
								cutin "min01",0;
							}
							else if (checkquest(13081,HUNTING) == 2) {
								cutin "min03",0;
								mes "เก่งมาก! คอยช่วยงานวิจัยของฉัน ฉันยังมีอะไรให้ทำอีกมาก";
								callsub S_Quest,13081,13082,700000,180000;
							}
							else {
								cutin "วษฑอลงถ๓ดูลฉฤซตๅ",3;  //pinguicula_dark_card
								mes "มันคือสัตว์ประหลาดที่อาศัยอยู่ทางตะวันตกเฉียงเหนือของค่ายกองกำลังพันธมิตร ฉันไม่สนใจที่จะไปด้วยตัวเอง แต่ได้รับการชำระเงินตามคำขอไปแล้วและฉันอาจค้นคว้าข้อมูลเหล่านั้นด้วย";
								next;
								mes "[Mingmin]";
								mes "ล่า ^FF000030 Dark Pinguicula ^000000 การต่อสู้มันจะทำให้มีสื่อการวิจัยที่ดี ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ระมัดระวังและปลอดภัย";
									callsub S_Quest,13082,13081;
								}
							}
							break;
						case 2:
							if (checkquest(13084,PLAYTIME) == 0)
								mes "งานวิจัยของหม้อข้าวหม้อแกงลิงยังคงดำเนินอยู่ โปรดกลับมาหาฉันในครั้งต่อไป! ";
							else if (checkquest(13083,HUNTING) == 1) {
								cutin "ณืฦๆตฅฝบฤซตๅ",3;  //nepenthes_card
								mes "อาจเป็นคำของ่ายๆหรือคำขอที่ยากลำบาก พยายามหาเพื่อนถ้าคุณมีปัญหา นั่นคือการผจญภัยด้วย! ";
								next;
								mes "[Mingmin]";
								mes "มันอยู่ทางตะวันออกของค่ายกองกำลังพันธมิตรมันไม่ไกลขนาดนั้นให้รีบไป";
								cutin "min01",0;
							}
							else if (checkquest(13083,HUNTING) == 2) {
								cutin "min03",0;
								mes "เก่งมาก! ให้ความช่วยเหลือในการวิจัยของฉัน ฉันยังมีอะไรให้ทำอีกมาก";
								callsub S_Quest,13083,13084,700000,180000;
							}
							else {
								cutin "ณืฦๆตฅฝบฤซตๅ",3;  //nepenthes_card
								mes "คุณรู้จักสัตว์ประหลาดชื่อแมนดราโกร่าหรือไม่? มีสัตว์ประหลาดคล้าย ๆ กันอยู่ทางทิศตะวันออกของสถานีสำรวจ ฉันแน่ใจว่ามันจะเกี่ยวข้องกับการค้นคว้ามอนสเตอร์";
								next;
								mes "[Mingmin]";
								mes "ทำไมคุณไม่ล่า 30 ^FF0000Nepenthes ^000000? เถาวัลย์ของพวกเขาเลื้อยขึ้นไปทุกที่ดังนั้นโปรดระวัง";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "คำขอเช่นนี้สร้างความพึงพอใจให้กับฉันและกลุ่มเอเดน ระวังด้วยนะ ";
									callsub S_Quest,13084,13083;
								}
							}
							break;
						case 3:
							if (checkquest(13086,PLAYTIME) == 0)
								mes "งานวิจัยของนาคยังคงดำเนินอยู่ โปรดกลับมาหาฉันในครั้งต่อไป";
							else if (checkquest(13085,HUNTING) == 1) {
								cutin "ณชฐกฤซตๅ",3;  //naga_card
								mes "คุณดูแลพวกที่ติดพลังเวทฉันไม่สนใจ";
								next;
								mes "[Mingmin]";
								mes "โปรดดูแลสัตว์ประหลาดพญานาค 30 ตัวทางทิศตะวันตกเฉียงเหนือของสถานีสำรวจ ";
								cutin "min01",0;
							}
							else if (checkquest(13085,HUNTING) == 2) {
								cutin "min03",0;
								mes "ดี. ช่วยค้นคว้าต่อไป ฉันมีจำนวนมากเกินไปที่จะตรวจสอบ ";
								callsub S_Quest,13085,13086,700000,180000;
							}
							else {
								cutin "ณชฐกฤซตๅ",3;  //naga_card
								mes "โปรดดูแลสัตว์ประหลาด 30 ^FF0000Naga ^000000 ทางตะวันตกเฉียงเหนือของสถานีสำรวจ ";
								next;
								mes "[Mingmin]";
								mes "ฉันไม่ได้สนใจมัน แต่มันรบกวนเวลาของฉันในการค้นคว้าสัตว์ประหลาดตัวอื่นที่ฉันต้องการศึกษาจริงๆ ดังนั้นฉันต้องการให้จำนวนพวกเขาลดลง ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "กรุณาทำให้ดีที่สุด คำขอนี้มีความสำคัญเช่นกันในการปลดล็อกการวิจัยเพิ่มเติม ";
									callsub S_Quest,13086,13085;
								}
							}
							break;
						case 4:
							if (checkquest(13088,PLAYTIME) == 0)
								mes "ฉันยังคงค้นคว้าเกี่ยวกับ Centipede Larva อยู่ โปรดมาช่วยฉันในครั้งต่อไป";
							else if (checkquest(13087,HUNTING) == 1) {
								cutin "ผพฦผฦไตฅภฏรๆฤซตๅ",3;  //centipede_larva_card
								mes "โอ ... ไม่ให้หายใจเข้าลึก ๆ สำหรับฉันฉันต้องการกำจัดมากกว่า 20 เพื่อการวิจัย";
								next;
								emotion ET_CRY;
								mes "[Mingmin]";
								mes "ระวัง. และอยู่ทางตะวันออกของสถานีสำรวจ ";
								cutin "min02",0;
							}
							else if (checkquest(13087,HUNTING) == 2) {
								cutin "min02",0;
								mes "โอ้. ตกลง. คุณไม่จำเป็นต้องอธิบาย";
								emotion ET_OTL;
								callsub S_Quest,13087,13088,700000,180000;
							}
							else {
								cutin "ผพฦผฦไตฅภฏรๆฤซตๅ",3;  //centipede_larva_card
								mes "อึ๊บ! เป็นที่น่ารังเกียจมาก มองฉันสิ. ขนลุกเลยอยากสำรวจเพราะสนใจหม้อข้าวหม้อแกงลิง แต่ฉันเกลียดโรคจิตขามาก ... ";
								next;
								mes "[Mingmin]";
								mes "กลับมาหลังจากที่ 20 ^FF0000Centipede Larva ^000000 ถูกกำจัด คุณนึกภาพออกไหมว่าพวกเขาใหญ่โตขนาดไหน? แค่ความคิดก็ทำให้ผิวของฉันคลานได้";
								next;
								cutin "min02",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ขอโทษที่ไม่ได้ทำเอง ฉันไม่อายที่จะยอมรับว่ามันน่าขยะแขยงเกินไปสำหรับฉัน ";
									callsub S_Quest,13088,13087;
								}
							}
							break;
						case 5:
							if (checkquest(13090,PLAYTIME) == 0)
								mes "การวิจัยของ Cornus ยังคงดำเนินอยู่ โปรดค้นคว้าสำหรับนักผจญภัยคนอื่น ๆ";
							else if (checkquest(13089,HUNTING) == 1) {
								cutin "ฤฺธฃดฉฝบฤซตๅ",3;  //cornus_card
								mes "ฉันต้องการค้นคว้าสิ่งมีชีวิตบนม้าบินเหล่านั้นดังนั้นโปรดช่วยฉันด้วย ";
								next;
								mes "[Mingmin]";
								mes "แน่นอนมันจะช่วยนักผจญภัยของคุณด้วยในทางใดทางหนึ่งที่ฉันคิดว่า ";
								cutin "min01",0;
							}
							else if (checkquest(13089,HUNTING) == 2) {
								cutin "min03",0;
								mes "เป็นยังไงบ้าง? มอนสเตอร์ตัวนั้นง่ายสำหรับนักผจญภัยหรือ?";
								callsub S_Quest,13089,13090,700000,180000;
							}
							else {
								cutin "ฤฺธฃดฉฝบฤซตๅ",3;  //cornus_card
								mes "ฉันพบสัตว์ประหลาดที่ตอบสนองความต้องการในการวิจัยของฉันและช่วยให้นักผจญภัยได้รับประสบการณ์จาก Eden Group มันไม่ใช่สัตว์ประหลาดที่อ่อนแอหรือดุร้ายเหมือนกัน ";
								next;
								mes "[Mingmin]";
								mes "ฉันเชื่อว่านักผจญภัยจะจัดการคอร์นัสได้ในอนาคตดังนั้นจงออกไปจัดการกับ ^FF000030 Cornus ^000000 เพื่อที่ฉันจะได้ค้นคว้าความมีชีวิตนั้น ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "นี่อาจเป็นการปฏิวัติสำหรับนักผจญภัยกลุ่ม Eden ขอให้เป็นบวกกับคำขอนี้ ";
									callsub S_Quest,13090,13089;
								}
							}
							break;
						case 6:
							if (checkquest(13092,PLAYTIME) == 0)
								mes "ฉันยังคงหาข้อมูล Mystic Horns ที่คุณนำมา น่าสนใจทีเดียว. ฉันสามารถเอาชนะหมวกสีฟ้าได้ในไม่ช้า";
							else if (isbegin_quest(13091) == 1) {
								if (countitem(6023) > 19) {// Mystic_Horn
									cutin "min03",0;
									mes "ใช่คนนี้! มันคุ้มค่าที่จะทำ ขอขอบคุณที่รับคำขอนี้";
									emotion ET_THANKS;
									delitem 6023,20;
									callsub S_Quest,13091,13092,700000,180000;
								}
								else {
									cutin "ฤฺธฃดฉฝบฤซตๅ",3;  //cornus_card
									mes "ฉันยังคงค้นคว้า แต่ฉันหวังว่าจะดึงพลังเวทย์มนตร์จากสิ่งที่สัมผัสได้อย่างมหัศจรรย์.";
									next;
									mes "[Mingmin]";
									mes "มันจะเป็นประโยชน์สำหรับฉันและนักผจญภัยใช่ไหม ";
									cutin "min01",0;
								}
							}
							else {
								cutin "ฤฺธฃดฉฝบฤซตๅ",3;  //cornus_card
								mes "นี่คือเหตุผลที่ฉันยอมรับคำขอนี้จาก Eden Group หา 20 ^FF0000Mystic Horn ^000000 จากมอนสเตอร์ Cornus ";
								next;
								mes "[Mingmin]";
								mes "ฉันคิดว่าฉันสามารถใช้แตรนี้เพื่อสิ่งดีๆได้มันง่ายมากฉันจะแจ้งให้คุณทราบในภายหลัง";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "คุณได้รับประสบการณ์และฉันได้รับสื่อการวิจัย ข้อตกลงที่ดีใช่มั้ย? ยอดเยี่ยมมาก!";
									callsub S_Quest,13092,13091;
								}
							}
						}
						break;
					case 4:
						mes "คำขอนี้มีไว้สำหรับการวิจัยส่วนตัวของฉัน ฉันหาข้อมูลให้กับ Eden Group และพวกเขาสนับสนุนให้ฉันจ้างคนที่ช่วยงานวิจัยของฉัน";
						next;
						switch( select( "Ancient Mummy research", "Geffenia survey", "Juperos survey", "Fragment and Rusty Screw collecting." ) ) {
							mes "[Mingmin]";
						case 1:
							if (checkquest(13094,PLAYTIME) == 0)
								mes "งานวิจัยของ Ancient Mummy ยังคงดำเนินอยู่.";
							else if (checkquest(13093,HUNTING) == 1) {
								cutin "ฟกภฮผวฦฎนฬภฬถ๓ฤซตๅ",3;  //ancient_mummy_card
								mes "ถ้าฉันหาข้อมูลเสร็จสิ่งแรกที่ฉันจะทำคือกินพุดดิ้งสตรอเบอร์รี่อายุ 10 ปี ... ";
								next;
								mes "[Mingmin]";
								mes "เพื่อจบการวิจัยของฉันฉันต้องการให้คุณตามล่ามัมมี่โบราณสักตัว ";
								cutin "min01",0;
							}
							else if (checkquest(13093,HUNTING) == 2) {
								cutin "min03",0;
								mes "ทำได้ดีมาก พุดดิ้งสตรอเบอร์รี่ที่ยอดเยี่ยมไม่ใช่เรื่องง่ายที่จะทำ เชื่อฉัน. ฉันจะปล่อยให้คุณได้ลิ้มรสในไม่ช้า.";
								callsub S_Quest,13093,13094,700000,200000;
							}
							else {
								cutin "ฟกภฮผวฦฎนฬภฬถ๓ฤซตๅ",3;  //ancient_mummy_card
								mes "สิ่งที่ควรจะไปไม่ดี ส่วนใหญ่แล้วแต่คนต้องการเก็บและถนอมอาหารไว้กินนาน ๆ. ";
								next;
								mes "[Mingmin]";
								mes "คุณเคยเห็นผ้าพันแผลที่มัมมี่โบราณพันแผลไหม? พวกมันคงอยู่นานหลายศตวรรษ.";
								next;
								mes "[Mingmin]";
								mes "ฉันเดาว่ามันเน่าเสีย แต่เราสามารถทำเงินได้มากหากการวิจัยนี้ไปได้ดี สารกันบูดสามารถทำพุดดิ้งสตรอเบอร์รี่ที่ไม่เสียเวลา";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "การวิจัยนี้จำเป็นเพื่อให้ฉันสามารถทำพุดดิ้งสตรอเบอร์รี่ได้.";
									callsub S_Quest,13094,13093;
								}
							}
							break;
						case 2:
							if (checkquest(13096,PLAYTIME) == 0)
								mes "การวิจัยของ Geffenia เพิ่งเริ่มต้น มันกำลังเริ่มต้นดังนั้นไปอย่างช้าๆ ช่วยให้การวิจัย.";
							else if (checkquest(13095,HUNTING) == 1) {
								cutin "ผญลฅน๖ฝบฤซตๅ",3;  //succubus_card
								mes "ฉันไม่ได้คาดหวังมากเพราะมันอันตรายมากและไม่สะดวกในการเข้าถึง.";
								next;
								mes "[Mingmin]";
								mes "คำขอนี้พยายามหานักผจญภัยที่เคยสนุกกับมัน เรียบง่าย! สนุก! ประสบการณ์ที่คุ้มค่า! ";
								cutin "min01",0;
							}
							else if (checkquest(13095,HUNTING) == 2) {
								cutin "min03",0;
								mes "คุณมีความสามารถมากกว่าที่ฉันคิด หากคุณพบกลยุทธ์ฉันจะขอให้คุณในครั้งต่อไปเช่นกัน การวิจัยนี้ขึ้นอยู่กับคุณ ";
								callsub S_Quest,13095,13096,700000,200000;
							}
							else {
								cutin "ภฮลฅน๖ฝบฤซตๅ",3;  //incubus_card
								mes "นี่เป็นการสำรวจล่วงหน้าสำหรับการวิจัยอันล้ำค่าเนื่องจาก Geffenia เข้าถึงได้ง่าย ";
								next;
								mes "[Mingmin]";
								mes "คำขอเริ่มต้นจากวิธีหาทางเข้าโปรดดูว่ามีมอนสเตอร์ประเภทใดอยู่ในนั้น ก่อนอื่นเริ่มต้นด้วยการล่าสัตว์ ^FF000010 ^000000 Incubus, Succubus และ Violy จากนั้นให้ค้นคว้ารายละเอียดในภายหลัง";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ลองเริ่มจากน้ำพุ Geffen ถ้าคุณต้องการข้อมูลเพิ่มเติม ";
									callsub S_Quest,13096,13095;
								}
							}
							break;
						case 3:
							if (checkquest(13098,PLAYTIME) == 0)
								mes "พวกเขาดูเหมือนกันสำหรับฉัน ฉันควรค้นคว้าให้หนักขึ้น ";
							else if (checkquest(13097,HUNTING) == 1) {
								cutin "บฃณชล๕",3;  //venatu_card
								mes "มีอีกเหตุผลหนึ่งที่นักผจญภัยจำนวนมากถูกส่งไปยังจูเปรอสและนั่นคือคำขอที่แท้จริงของฉัน ";
								next;
								mes "[Mingmin]";
								mes "ถ้าคุณไปที่ Juperos? ฉันขอให้คุณรับคำขอเกี่ยวกับ ^FF0000Venatus ^000000";
								cutin "min01",0;
							}
							else if (checkquest(13097,HUNTING) == 2) {
								cutin "min03",0;
								mes "คุณไม่สับสน? ฉันยังไม่ทราบ อะไรคือความแตกต่าง? ทำได้ดีมาก";
								emotion ET_BLABLA;
								callsub S_Quest,13097,13098,700000,300000;
							}
							else {
								cutin "บฃณชล๕",3;  //venatu_card
								mes "Juperos โบราณที่ยังหลงเหลืออยู่นั้นลึกลับ มี ^FF0000secrets ^000000 มากมาย แต่นี่เป็นการสำรวจวิจัยสัตว์ประหลาดธรรมดา ๆ อีกครั้ง";
								next;
								mes "[Mingmin]";
								mes "สัตว์ประหลาด ^FF0000Venatu ^000000 ใน juperos มี 4 ประเภทของสี ซึ่งหมายความว่ามีมอนสเตอร์สี่ประเภท";
								next;
								mes "[Mingmin]";
								mes "ฉันคิดว่าพวกมันเหมือนกันเพราะหน้าตาเหมือนกัน เป็นเพียงฉันเท่านั้น? ฉันไม่สามารถแยกแยะได้ ตามล่าตัวเขียวส้มและน้ำเงินอย่างละ 30 ตัวเพื่อความแน่ใจ ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "มันจะไม่ใช่เรื่องง่ายที่จะอยู่คนเดียว แต่การมองหาเพื่อนร่วมผจญภัยคือสิ่งที่นักผจญภัยทำ";
									callsub S_Quest,13098,13097;
								}
							}
							break;
						case 4:
							if (checkquest(13100,PLAYTIME) == 0)
								mes "ฉันจะทำให้ดีกว่าที่คุณคิด";
							else if (isbegin_quest(13099) == 1) {
								if (countitem(7094) > 9 && countitem(7317) > 19) {
									cutin "min03",0;
									mes "Thank you. You will appreciate me if my research is completed.";
									emotion ET_BLABLA;
									delitem 7094,10;// Mystery_Piece
									delitem 7317,20;// Screw
									callsub S_Quest,13099,13100,700000,200000;
								}
								else {
									cutin "บฃณชล๕",3;  //venatu_card
									mes "คุณสามารถพบ ^FF0000Fragment และ Rusty Screw ^000000 ใน juperos ได้ทุกที่ มอนสเตอร์ส่วนใหญ่มีมัน ";
									next;
									mes "[Mingmin]";
									mes "ฉันรู้ว่ามันไม่ใช่เรื่องง่าย แต่ฉันอยากให้เธอทำตามคำขอนี้ กรุณา.";
									cutin "min01",0;
								}
							}
							else {
								cutin "บฃณชล๕",3;  //venatu_card
								mes "Juperos รุนแรงและลึกลับ เมื่อเขาปรากฏตัวดูเหมือนว่ามันจะไม่ใช่มิดการ์ด ";
								next;
								mes "[Mingmin]";
								mes "เป็นการวิจัยหลัก ฉันต้องรู้ว่าเขาเป็นใคร นั่นจะน่าทึ่งมาก คุณสามารถนำ 10 ^FF0000Fragment ^000000 และ 20 ^FF0000Rusty Screw ^000000 มาให้ฉันได้ไหม ";
								next;
								cutin "min01",0;
								if (select( "รับภารกิจ.", "ยกเลิก" ) == 1) {
									mes "[Mingmin]";
									mes "ฉันไม่ทราบว่าคุณสามารถพกพาได้กี่หยด แต่ฉันต้องการมากที่สุดเท่าที่จะทำได้ ขออนุญาตนำ 10 ^FF0000 Fragment ^000000 และ 20 ^FF0000 Rusty Screw ^000000 ";
									callsub S_Quest,13100,13099;
								}
							}
						}
					}
				}
			}
			else if (para_110lv01 < 5) {
				mes "คำขอสำหรับการวิจัยสัตว์ประหลาดดูเหมือนจะง่ายสำหรับคุณ ";
				next;
				mes "[Mingmin]";
				mes "ไม่มีอะไรที่ฉันทำได้เพราะนี่เป็นกฎของ Eden Group ";
				next;
				mes "[Mingmin]";
				mes "มีใครบางคนที่ขอให้คุณแก้คำขอของคุณ ";
			}
			else {
				cutin "min01",0;
				mes "สัญญาสิ้นสุดลง "+ strcharinfo (0) +" รางวัลสำหรับการวิจัยที่เสร็จสมบูรณ์ ^FF0000 ฉันจะลบการวิจัยที่ไม่สมบูรณ์ออกจากรายการภารกิจของคุณ ^000000";
				para_110lv01 = 6;
				next;
				callsub S_Hunting,
					13067,200000,"Raydric",
					13069,200000,"Khalitzburg",
					13071,200000,"Wanderer",
					13073,300000,"Ancient Mimic",
					13075,300000,"Deathword",
					13077,300000,"Owl Baron";
				callsub S_Gathering,13079,300000,"Bloody Page",7449,20;
				callsub S_Hunting,
					13081,180000,"Dark Pinguicula",
					13083,180000,"Nepenthes",
					13085,180000,"Naga",
					13087,180000,"Centipede Larva",
					13089,180000,"Cornus";
				callsub S_Gathering,13091,180000,"Mystic Horn",6023,20;
				callsub S_Hunting,
					13093,200000,"Ancient Mummy",
					13095,200000,"Geffenia expedition",
					13097,300000,"Juperos";
				callsub S_Gathering,13099,200000,"Fragments and Rusty Screws",7094,10,7317,20;
				mes "[Mingmin]";
				mes "มันไม่สะดวกเล็กน้อย ฉันจะอ่านให้คุณฟังอย่างสุภาพ ^FF0000อีกครั้ง ^000000 ในที่สาธารณะ ";
				next;
				mes "[Mingmin]";
				mes "อย่างไรก็ตาม "+ strcharinfo (0) +" ขอบคุณที่ช่วยฉันเริ่มค้นคว้า ";
				next;
				cutin "min03",0;
				mes "[Mingmin]";
				mes "ไม่ต้องกังวลฉันจะปฏิบัติต่อคุณเหมือนทาสหากเราพบกันนอก Eden Group หรือถ้าคุณบอกใครว่าฉันดีกับคุณ ";
				emotion ET_BLABLA;
				next;
				cutin "min01",0;
				mes "[Mingmin]";
				mes "เพียงเพราะตอนนี้ฉันเป็นคนดีไม่ได้หมายความว่าเราจะเป็นเพื่อนกันโดยอัตโนมัติ ";
			}
			break;
		case 2:
			if (para_110lv01 == 5)
				mes "คุณอยากรู้อะไรไหมฉันยุ่งอยู่กับการค้นคว้า";
			else
				mes "เป็นวันที่ดีสำหรับการค้นคว้า การพูดคุยกับคนที่มีสติปัญญาเพียงพอเป็นเรื่องสนุกเสมอ ";
			while(1) {
				next;
				switch( select( "^0000ffFriends^000000 story", "Something to research", "Orange beard gluey mushroom...", "จบการสนทนา" ) ) {
					mes "[Mingmin]";
				case 1:
					if (para_110lv01 < 5) {
						cutin "min02",0;
						mes "ฉันพบใบหน้าที่คุ้นเคยตั้งแต่ฉันมาถึง Eden Group ด้วยคำขอนี้ ^ff0000Rohtert ^000000 คือหนึ่ง ";
						next;
						mes "[Mingmin]";
						mes "ฉันเคยเข้าร่วมชมรมที่ค้นคว้าเกี่ยวกับสัตว์ประหลาดหายาก ฉันได้ยินมาว่ามันกลายเป็นกิลด์และอย่างไรก็ตามฉันก็ได้พบกับเขาที่นั่น ";
						next;
						mes "[Mingmin]";
						mes "ฉันดูเด็กลง แต่ฉันก็เหมือนน้องสาวของเขา มีหลายสิ่งหลายอย่างเกิดขึ้นในสโมสรแห่งนั้น อย่างไรก็ตามฉันไม่ควรทำต่อไป ฉันอาจจะบอกคุณในภายหลังเมื่อเรามีเวลา ";
					}
					else if (para_110lv01 == 5) {
						cutin "min03",0;
						mes "ฉันพบใบหน้าที่คุ้นเคยตั้งแต่ฉันมาถึง Eden Group ด้วยคำขอนี้ คุณเห็นผู้ชายคนนั้นไหม ^FF0000Rohtert ^000000 ที่มีผมสีขาวใกล้ทางเข้า? ";
						next;
						mes "[Mingmin]";
						mes "ตอนนี้เขาโตแล้ว ... เขาเคยตามฉันเรียกฉันว่าพี่สาวของเขา ผู้ชายหมวกสีฟ้าที่เคยเกาะติดเขาเหมือนกาวอยู่ที่ไหน พวกเขาทะเลาะกันหรือเปล่า";
						next;
						mes "[Mingmin]";
						mes "ผู้ชายหมวกน้ำเงินคนนั้น ... เขาเคยกวนประสาทฉันแม้กระทั่งที่คลับ ถ้าฉันเจอเขาอีกฉันอาจจะตบเขา ... ";
					}
					else {
						cutin "min02",0;
						mes "ฉันเคยคุยกับ Rohtert เป็นครั้งคราวขณะค้นคว้า มีเรื่องต้องคุยมากมายตั้งแต่ฉันออกจากกิลด์ ";
						next;
						mes "[Mingmin]";
						mes "ฉันคิดว่ากิลด์ยังคงอยู่รอบ ๆ เนื่องจากมีสมาชิกจำนวนมากแม้ว่าฉันจะอยู่ที่นั่นก็ตาม ";
						next;
						mes "[Mingmin]";
						mes "ฉันได้ยินมาว่าผู้ชายหมวกสีฟ้าทำให้ตัวเองเป็นหัวหน้ากิลด์ นึกว่าคนโง่เป็นเจ้านายโอ้โฮโฮโฮ ";
					}
					continue;
				case 2:
					if (para_110lv01 < 5) {
						cutin "min01",0;
						mes "ฉันวิจัยสัตว์ประหลาด โดยเฉพาะลักษณะนิสัยม้ามสถานที่และความสัมพันธ์กับสัตว์ประหลาดตัวอื่น ๆ เป็นต้น ";
					}
					else if (para_110lv01 == 5) {
						cutin "min03",0;
						mes "การค้นคว้าของวิเศษเหล่านี้มีอะไรมากกว่าการมีไว้ในครอบครอง ";
						next;
						mes "[Mingmin]";
						mes "แผนของฉันจนถึงตอนนี้เป็นเช่นนี้ เวทมนตร์ที่สกัดจาก Mystic Horn จะถูกบีบอัดโดย Fragments และ Screws จากนั้นฉันจะป้อนข้อมูลโบราณที่เขียนไว้ใน Bloody Pages ";
						next;
						mes "[Mingmin]";
						mes "ฉันหวังว่าจะสร้างอุปกรณ์จัดเก็บข้อมูลแบบพกพาขนาดเล็กบางประเภท ";
						next;
						mes "[Mingmin]";
						mes "ฉันจะรวยถ้าฉันทำวิจัยนี้เสร็จ! ฉันแน่ใจ";
					}
					else {
						cutin "min01",0;
						mes "คุณจำงานวิจัยส่วนตัวของฉันที่ฉันพูดถึงก่อนหน้านี้ได้หรือไม่? ไม่ใช่เรื่องง่ายแม้ฉันจะพยายามพัฒนา...";
						next;
						mes "[Mingmin]";
						mes "ฉันพยายามสร้างอุปกรณ์วิเศษที่คุณสามารถถือไว้ในมือได้ ";
						next;
						mes "[Mingmin]";
						mes "ไม่ฉันยอมแพ้กับเรื่องนี้ไม่ได้ ฉันจะทำให้มัน ";
					}
					continue;
				case 3:
					if (para_110lv01 < 5) {
						cutin "min02",0;
						mes "เห็ดส้ม? ฉันไม่รู้.";
					}
					else if (para_110lv01 == 5) {
						cutin "min01",0;
						mes "ไม่ต้องกังวล. โอเคอาจกังวลเพราะเห็ดอาจเติบโตในร่างกายของคุณ ฉันจะกำจัดมันถ้าคุณช่วยฉันค้นคว้า มันจะอันตรายถ้าใช้เวลานานกว่านี้ ";
					}
					else {
						cutin "min02",0;
						mes "... เห็ดหอมสีส้ม? ";
						next;
						mes "[Mingmin]";
						mes "ให้ฉันดู. อาจเป็นอันตราย ";
						next;
						mes "[Mingmin]";
						mes "คุณช่วยฉันค้นคว้าดังนั้นให้ฉันช่วยคุณ! ฉันจะกำจัดเชื้อสปอร์เห็ดของคุณ ";
						next;
						mes "[Mingmin]";
						mes "......";
						next;
						cutin "min03",0;
						mes "[Mingmin]";
						mes "...ฮิฮิ...";
						emotion ET_SMILE;
						next;
						mes "[Mingmin]";
						mes "อืมคุณช่างไร้เดียงสา คุณเชื่อหรือไม่? เหนียว.. เห็ดบางชนิดกำลังเติบโตในร่างกายคุณ? ฮ่า ๆ ๆ ";
						next;
						mes "[Mingmin]";
						mes "มันเป็นเพียงเรื่องตลก เรื่องตลกเล็กน้อยระหว่างเพื่อน";
						next;
						mes "[Mingmin]";
						mes "ฉันขอโทษถ้าคุณไม่สบายใจ คุณทำมันง่ายเกินไป..";
						next;
						emotion ET_SMILE;
						mes "[Mingmin]";
						mes "คุณสนุกกว่าที่ฉันคิด ไม่ต้องกังวล. เห็ดนั้นไม่มี ";
					}
					continue;
				case 4:
					if (para_110lv01 == 5) {
						cutin "min01",0;
						mes "โปรดช่วยฉันมีหลายสิ่งหลายอย่างให้ค้นคว้า ";
					}
					else {
						cutin "min02",0;
						mes "ฉันหวังว่าคุณจะมีช่วงเวลาที่ดี ";
					}
				}
				break;
			}
		}
	}
	close2;
	cutin "",255;
	end;

S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp getarg(2),getarg(3);
	return;
S_Hunting:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 3 ) {
		.@quest_id = getarg(.@i);
		switch( checkquest(.@quest_id,HUNTING) ) {
		case -1:
			if (checkquest(.@quest_id+1) > -1)
				erasequest (.@quest_id+1);
			continue;
		case 2:
			mes "[Mingmin]";
			mes "คุณหาข้อมูลเกี่ยวกับ "+ getarg (.@i + 2) +" เสร็จแล้ว ขอบคุณ.";
			getexp 700000,getarg(.@i+1);
			break;
		case 0:
		case 1:
			mes "[Mingmin]";
			mes "ฉันจะลบ "+ getarg (.@i + 2) +" การวิจัยจากรายการคำขอ ";
		}
		erasequest .@quest_id;
		next;
	}
	return;
S_Gathering:
	.@quest_id = getarg(0);
	if (isbegin_quest(.@quest_id) > 0) {
		if (getarg(5,0))
			.@count_item2 = countitem(getarg(5));
		mes "[Mingmin]";
		if (countitem(getarg(3)) >= getarg(4) && .@count_item2 >= getarg(6,0)) {
			if (.@count_item2)
				mes "คุณค้นคว้าเกี่ยวกับ "+ getitemname (getarg (3)) +" และ "+ getitemname (getarg (5)) +" เสร็จแล้ว ขอบคุณ.";
			else
				mes "คุณค้นคว้าเกี่ยวกับ "+ getitemname (getarg (3)) +" เสร็จแล้ว ขอบคุณ.";
			delitem getarg(3),getarg(4);
			if (getarg(5,0))
				delitem getarg(5),getarg(6);
			getexp 700000,getarg(1);
		}
		else {
			if (getarg(5,0))
				mes "ฉันจะลบการวิจัย "+ getitemname (getarg (3)) +" และ "+ getitemname (getarg (5)) +" จากรายการคำขอ ";
			else
				mes "ฉันจะลบ "+ getitemname (getarg (3)) +" การวิจัยออกจากรายการคำขอ ";
		}
		erasequest .@quest_id;
		next;
	}
	else if (isbegin_quest(.@quest_id+1) > 0)
		erasequest (.@quest_id+1);
	return;
}
