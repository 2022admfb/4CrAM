//===== rAthena Script =======================================
//= Eden Group Quests - Mission [56 - 70]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.7
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 56 - 70.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Added checkquest confirmation for -1. [Euphy]
//= 1.3 Fixed Puppet Master's Agony Quest (12217 -> 3259). [Joseph]
//= 1.4 Fixed invalid check (Magic Wand Quest). [Joseph]
//= 1.5 Optimized, based on code by Zopokx. [Euphy]
//= 1.6 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.7 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,42,38,3	script	Mission [56 - 70]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "เพื่อรับภารกิจเหล่านี้, ฉันต้องเข้าร่วม Eden Group ก่อน ฉันต้องหาเลขานุการ Lime Evenor และสมัครเป็นสมาชิก.";
		close;
	}
	mes "นี่คือรายชื่อการผจญภัยต่างๆสำหรับนักผจญภัยระดับ 56 ~ 70.";
	next;
	mes "ภารกิจมากมายเช่นการล่าสัตว์ประหลาดการจัดส่งสินค้าการค้นหาผู้คน.";
	next;
	switch(select("หมายเลขภารกิจ 1~5:หมายเลขภารกิจ 6~10:หมายเลขภารกิจ 11~15")) {
	case 1:
		switch(select("Bandage? คืออะไร:อันตราย Alligators.:นั่นไม่ใช่ mermaid...:ประเทศของฉันกำลังเรียกหาฉัน!")) {
			case 1: callsub L_HuntingQuest,3250,15000,5000,930,30;
			case 2: callsub L_HuntingQuest,3251,16000,6000;
			case 3: callsub L_HuntingQuest,3252,17000,8000;
			case 4: callsub L_Quest,3254,18000,8000,7043,10,1056,30;
		}
	case 2:
		switch(select("ตามล่า Wild Boar:การเตรียมฤดูร้อน:ความไม่พอใจของผู้หญิง:ส่วนผสมพิเศษ:Puppet Master's ทุกข์ทรมาน")) {
			case 1: callsub L_HuntingQuest,3255,16000,7000;
			case 2: callsub L_Quest,3256,15000,5000,918,30;
			case 3: callsub L_HuntingQuest,3257,17000,8000;
			case 4: callsub L_Quest,3258,16000,7000,7198,40;
			case 5: callsub L_HuntingQuest,3259,17000,7000,1060,30;
		}
	case 3:
		switch(select("Tiresome Flies:อันตราย Munak:ทำให้โลกเป็นสีเขียว:Magic Wand")) {
			case 1: callsub L_HuntingQuest,3260,16000,6000;
			case 2: callsub L_HuntingQuest,3261,16000,7000;
			case 3: callsub L_Quest,3262,17000,7000,1032,40;
			case 4: callsub L_Quest,3263,18000,8000,1021,30,7150,30;
		}
	}
	end;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "คุณต้องการที่จะยอมรับภารกิจนี้?";
		next;
		if(select("ยอมรับภารกิจ.:ยกเลิก.") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "ภารกิจเหล่านี้ไม่เหมาะกับระดับของฉัน ฉันควรมองหาภารกิจอื่น ๆ.";
				close;
			}
			setquest .@quest_id;
			mes "ฉันยอมรับภารกิจสำเร็จแล้ว ฉันต้องกลับมายืนยันการทำงานหลังจากเสร็จสิ้นภารกิจ.";
			next;
			mes "ภารกิจนี้ไม่มีการ จำกัด เวลา.";
		}
		close;
	}
	if (getargcount() > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id) < 2 && (countitem(getarg(3)) < getarg(4) || .@items == 1)) {
		mes "คุณมีภารกิจที่กำลังดำเนินอยู่ คุณต้องการตรวจสอบรายละเอียด?";
		next;
		if(select("ตรวจสอบรายละเอียด:ยกเลิก.") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else {
		mes "ฉันทำภารกิจได้ค่อนข้างดี ฉันควรรายงานตอนนี้?";
		next;
		if(select("รายงานภารกิจ:ยังไม่ต้องรายงาน.") == 1) {
			delitem getarg(3),getarg(4);
			if (.@items)
				delitem getarg(5),getarg(6);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "คุณทำภารกิจสำเร็จแล้ว รับรางวัล.";
		}
		close;
	}
	end;

L_HuntingQuest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id) == -1) {
		callsub L_Details, .@quest_id;
		next;
		mes "คุณต้องการที่จะยอมรับภารกิจนี้?";
		next;
		if(select("ยอมรับภารกิจ.:ยกเลิก.") == 1) {
			if (BaseLevel < 55 || BaseLevel > 70) {
				mes "ภารกิจเหล่านี้ไม่เหมาะกับระดับของฉัน ฉันควรมองหาภารกิจอื่น ๆ.";
				close;
			}
			setquest .@quest_id;
			mes "ฉันยอมรับภารกิจสำเร็จแล้ว ฉันต้องกลับมายืนยันการทำงานหลังจากเสร็จสิ้นภารกิจ.";
			next;
			mes "ภารกิจนี้ไม่มีการ จำกัด เวลา.";
		}
		close;
	}
	if (getargcount() > 3) {
		if (countitem(getarg(3)) < getarg(4))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (checkquest(.@quest_id,HUNTING) < 2 || .@items == 1) {
		mes "คุณมีภารกิจที่กำลังดำเนินอยู่ คุณต้องการตรวจสอบรายละเอียด?";
		next;
		if(select("ตรวจสอบรายละเอียด:ยกเลิก.") == 1)
			callsub L_Details, .@quest_id;
		close;
	} else if (checkquest(.@quest_id,HUNTING) == 2) {
		mes "ฉันทำภารกิจได้ค่อนข้างดี ฉันควรรายงานตอนนี้?";
		next;
		if(select("รายงานภารกิจ:ยังไม่ต้องรายงาน.") == 1) {
			if (.@items)
				delitem getarg(3),getarg(4);
			getexp getarg(1),getarg(2);
			erasequest .@quest_id;
			mes "คุณทำภารกิจสำเร็จแล้ว รับรางวัล.";
		}
		close;
	}
	end;

L_Details:
	switch(getarg(0)) {
	case 3250:
		mes "ภารกิจนี้ได้รับมอบหมายจาก บริษัท Rekenber จาก Lighthalzen ด้านล่างนี้คือรายละเอียด.";
		next;
		mes "Rekenber Corporation กำลังจะเปิดตัวโครงการใหม่ชื่อ 'Green Medical Center' สำหรับผลิตภัณฑ์แรกของโครงการเรากำลังเตรียมผ้าพันแผลเพื่อความเป็นอยู่.";
		next;
		mes "เกี่ยวกับโครงการนี้เราจำเป็นต้องค้นคว้าปฏิกิริยาของมอนสเตอร์เมื่อพวกมันถูกพันด้วย Rotten Bandages.";
		next;
		mes "หากคุณสนใจโปรดล่ามัมมี่ 30 ตัวและนำผ้าพันแผลเน่าเสีย 30 ชิ้นมาด้วย คุณสามารถพบได้ในพีระมิดทางตะวันตกของโมร็อกโก.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "- หัวหน้าผู้อำนวยการ Rekenber Corporation 'Julie E Delph' -";
		break;
	case 3251:
		mes "ภารกิจนี้ได้รับมอบหมายจากสมาคมสหกรณ์โคโมโด ด้านล่างนี้คือรายละเอียด.";
		next;
		mes "วันนี้จระเข้ออกไปเที่ยวอย่างดุเดือดพวกมันทำร้ายผู้หญิงและคนเมาที่ออกไปเที่ยวใกล้ชายหาด.";
		next;
		mes "นี่เป็นสถานการณ์ที่น่าอับอายมากสำหรับเราจุดพักผ่อนที่ยอดเยี่ยมที่สุดใน Rune-Midgarts.";
		next;
		mes "ดังนั้นมันจะมีประโยชน์มากถ้าคุณตามล่า Alligators 30 ตัวเพื่อทำความสะอาดสถานการณ์นี้.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		break;
	case 3252:
		mes "ภารกิจนี้ได้รับมอบหมายจากลูกค้าที่ไม่รู้จักจาก Izlude.";
		next;
		mes "ฉันชื่นชมชายหาดของ Izlude ตั้งแต่ฉันเกิดมา.";
		next;
		mes "ตอนที่ฉันยังเด็กฉันพบสิ่งมีชีวิตลึกลับในคุกใต้ดิน Izlude ฉันไม่เคยเห็นอะไรแบบนั้นมาก่อน...";
		next;
		mes "ฉันคิดทันทีว่ามันเป็นนางเงือก ฉันรู้สึกเสียใจมากฉันได้ศึกษาเกี่ยวกับนางเงือกมาทั้งชีวิต.";
		next;
		mes "แต่เมื่อศึกษาหนักขึ้นก็พบว่าสิ่งมีชีวิตนั้นไม่ใช่นางเงือก ... ใช่มันไม่ได้สวยงามเหมือนนางเงือกตัวอื่น ๆ...";
		next;
		mes "ในที่สุดฉันก็ติดตามข้อเท็จจริงว่ามันคือเมอร์แมนไม่ใช่เงือก!";
		next;
		mes "เมอร์แมน ... ฉันเสียเวลาทั้งชีวิตเพื่อศึกษาสัตว์ประหลาดตัวนี้! ดังนั้นโปรดตามล่า Mermans 30 ตัวเพื่อทำให้ฉันรู้สึกดีขึ้น.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- Izlude 'นางเงือกในฝันที่หายไป' --";
		break;
	case 3254:
		mes "ภารกิจนี้ได้รับมอบหมายจากเจ้าหน้าที่รัฐบาลของอาณาจักรรูน - มิดการ์ท.";
		next;
		mes "เพื่อป้องกันไม่ให้น้ำท่วมใน Prontera เราจึงตัดสินใจสร้างเขื่อนขนาดใหญ่เพื่อควบคุมปริมาณน้ำ.";
		next;
		mes "แต่เราขาดแคลนเสบียงและจำเป็นต้องรวบรวมทรายละเอียดและกรวด.";
		next;
		mes "ดังนั้นเราต้องการขอความช่วยเหลือ ผู้คนโปรดนำทรายละเอียด 10 เม็ดและกรวด 30 เม็ดมาให้เราเพื่อสร้างเขื่อน.";
		next;
		mes "คุณสามารถรับวัสดุเหล่านั้นได้จาก Sand Man ฉันขอให้คุณโชคดี!";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- เจ้าหน้าที่รัฐบาลแห่งราชอาณาจักรรูน - มิดการ์ท Dufre Kent --";
		break;
	case 3255:
		mes "ภารกิจนี้ได้รับมอบหมายจากศาลากลาง Payon.";
		next;
		mes "สัตว์ป่ามักสร้างความรำคาญให้กับเกษตรกรเสมอ.";
		next;
		mes "พวกมันโจมตีฟาร์มของเราและทำลายพืชผลทั้งหมด !! เราทนต่อไปไม่ไหวแล้ว!";
		next;
		mes "ดังนั้นเราจึงต้องการขอความช่วยเหลือจากนักผจญภัยผู้กล้าหาญ.";
		next;
		mes "โปรดล่า 30 Savages เพื่อช่วยพืชผลของเรา!";
		next;
		mes "คุณสามารถค้นหามอนสเตอร์เหล่านั้นรอบเมืองและสนามของเราได้อย่างง่ายดาย.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- สมาชิกศาลากลางจังหวัด Payon --";
		break;
	case 3256:
		mes "ภารกิจนี้ได้รับมอบหมายจาก Aragham ช่างตีเหล็กชาวโมร็อกโก.";
		next;
		mes "สวัสดี ~~~~ นักผจญภัย! นี่คือ Aragham ช่างตีเหล็กที่ร้อนแรงที่สุดในโมร็อกโก!";
		next;
		mes "ยังไงซะฉันก็มีลูกชายชื่ออารากัมจูเนียร์คนน่ารัก ฮ่า ๆ.";
		next;
		mes "เขาจะเข้าค่ายฤดูร้อนในช่วงซัมเมอร์นี้ แต่ไม่รู้ว่าจะว่ายน้ำอย่างไร.";
		next;
		mes "ฉันอยากจะสอนเขา แต่อย่างที่คุณรู้ว่าฉันเป็นช่างตีเหล็กการว่ายน้ำมาไกลมาก! ฮิฮิ.";
		next;
		mes "แต่ฉันคิดว่าถ้าฉันทำตีนกบมันจะดีมากสำหรับความมั่นใจของเขา~!";
		next;
		mes "เพื่อให้ทำได้ฉันต้องมีตีนผีเหนียว 30 ตัวจากกบโรดา คุณสามารถนำมาให้ฉัน?";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "ปล. อย่าเปรียบเทียบฉันกับ Hollgrehenn หรือ Antonio!!!!";
		next;
		mes "-- ช่างตีเหล็กที่ร้อนแรงที่สุด Aragham ช่างตีเหล็กชาวโมร็อกโก --";
		break;
	case 3257:
		mes "ภารกิจนี้ได้รับมอบหมายจาก Dorian นักประดิษฐ์จาก Izlude.";
		next;
		mes "คุณเคยได้ยินเรื่องราวที่ว่าหากผู้หญิงมีความขุ่นเคืองใจก็จะนำมาซึ่งภัยธรรมชาติ.";
		next;
		mes "ว้าวผู้ชายต้องระวังผู้หญิงอ่อนไหวเหล่านั้น! ตรวจสอบให้แน่ใจว่าพวกเขาไม่มีความขุ่นเคืองกับคุณ.";
		next;
		mes "ผู้หญิงสามารถนำปรากฏการณ์แปลก ๆ มาด้วยได้!";
		next;
		mes "ฉันรู้สึกทึ่งกับเรื่องราวนั้นมากจนฉันพยายามพิสูจน์ว่ามันเป็นเรื่องจริง.";
		next;
		mes "เลยต้องทำให้ผู้หญิงอารมณ์เสีย! ฮ่าฮ่าฉันรู้ว่ามันเป็นความคิดที่ดีใช่มั้ย?";
		next;
		mes "แต่นี่เป็นเพียงการศึกษาอย่างจริงจัง ... ดังนั้นโปรดตามล่าโซฮี 30 คนที่ดูเหมือนจะฟื้นจากวิญญาณที่ตกเป็นเหยื่อ.";
		next;
		mes "ดังนั้นฉันสามารถสังเกตคดีได้.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- Dorian นักประดิษฐ์ที่สง่างาม --";
		break;
	case 3258:
		mes "ภารกิจนี้ได้รับมอบหมายจาก Wallah จาก Payon.";
		next;
		mes "สวัสดีนักผจญภัย คุณเป็นอย่างไร? สุขภาพคุณเป็นอย่างไรบ้าง?";
		next;
		mes "ฉันเป็นเภสัชกรและฉันมีปัญหาในการหาส่วนผสมพิเศษเมื่อเร็ว ๆ นี้.";
		next;
		mes "เพราะฉันเป็นเด็กที่อ่อนไหวง่ายฉันจะทำอย่างไรให้ได้สิ่งเหล่านั้นด้วยตัวเอง.";
		next;
		mes "ดังนั้นโปรดช่วยฉันด้วย แค่นำใบไม้ขนาดใหญ่ 40 ใบมาให้ฉันนั่นก็เพียงพอที่จะทำยาพิเศษของฉันให้สำเร็จ!";
		next;
		mes "กรุณารีบนำมาให้เร็วที่สุดคนต้องการยาของฉัน.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- วัลลาห์ --";
		break;
	case 3259:
		mes "ภารกิจนี้ได้รับมอบหมายจาก Woonute ปรมาจารย์หุ่นเชิดจาก Geffen.";
		next;
		mes "คุณรู้ว่าการเป็นนายหุ่นเป็นงานที่ยากที่จะทำให้ผู้คนพอใจ.";
		next;
		mes "ผู้คนสามารถค้นหาและดูสิ่งต่างๆมากมายได้ด้วยตัวเองในทุกวันนี้.";
		next;
		mes "ความคาดหวังของพวกเขาสูงขึ้นเรื่อย ๆ ฉันไม่สามารถตอบสนองพวกเขาได้อีกต่อไป.";
		next;
		mes "แต่ถ้าฉันสามารถสร้างแนวคิดเกี่ยวกับของเล่นใหม่ ๆ เช่นของเล่นที่มีชีวิตได้มันจะเป็นการทำลายล้าง.";
		next;
		mes "ฉันได้ยินมาว่ามีตุ๊กตาที่เรียกว่า Marionettes คุณสามารถล่า Marionettes 30 ชิ้นให้ฉันได้ไหม และยังนำผมสีทอง 30 ชิ้นมาด้วยซึ่งจะเป็นวัตถุดิบที่ดีสำหรับของเล่นใหม่.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- หุ่นอาจารย์วูนูเต้ --";
		break;
	case 3260:
		mes "ภารกิจนี้ได้รับมอบหมายจากผู้ทำลายล้างจากพรอนเทรา.";
		next;
		mes "เมื่ออากาศอุ่นขึ้นแมลงก็ทวีจำนวนมากขึ้นเรื่อย ๆ มันอยู่เหนือการควบคุมแล้ว.";
		next;
		mes "ผู้คนในเมืองพรอนเทรานอนไม่หลับเพราะแมลงในบ้านทั้งหมด.";
		next;
		mes "ดังนั้นโปรดช่วยเราล่า 30 Hunter Flies จากนั้นแมลงวันจะลดลงอย่างช้าๆ.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- ผู้กำจัด Prontera --";
		break;
	case 3261:
		mes "ภารกิจนี้ได้รับมอบหมายจากชีสดองจาจาก Payon.";
		next;
		mes "คุณเคยได้ยินเกี่ยวกับ Munak?";
		next;
		mes "Munak สัตว์ประหลาดน่าเกลียดกำลังคุกคามผู้คนใน Payon.";
		next;
		mes "ดังนั้นเราจึงต้องการนักผจญภัยที่กล้าหาญเช่นคุณ!";
		next;
		mes "โปรดตามล่า 30 Munaks แล้ว Payon จะอยู่ในความสงบ.";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "- Payon ชีสดองจา -";
		break;
	case 3262:
		mes "ภารกิจนี้ได้รับมอบหมายจากพ่อค้าอัญมณี Abraham จากโมร็อกโก.";
		next;
		mes "ดอกไม้ที่สวยงามเล็ก ๆ ในทะเลทรายอันแห้งแล้ง ... ช่างเป็นฉากที่น่าเหลือเชื่อจริงๆ!";
		next;
		mes "การปลูกดอกไม้สามารถทำให้โลกใบนี้เป็นสีเขียวและจะสวยงามทุกที่.";
		next;
		mes "ฉันวางแผนที่จะปลูกดอกไม้ที่แข็งแรงในทะเลทรายโมร็อกโกเพื่อให้ดินดีขึ้น.";
		next;
		mes "ฉันต้องการ Maneater Blossom 40 จาก Flora ฉันรู้ว่า Maneater Blossom เป็นดอกไม้ที่น่าเกลียด แต่ก็ยังเป็นพืช.";
		next;
		mes "ฉันหวังว่าคุณจะสามารถตามล่า Flora และได้ Maneater Blossoms มาให้ฉัน 40 ตัว คุณกำลังทำให้โลกเป็นสีเขียว! ภูมิใจ!";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- โมรอคอิบราฮัม --";
		break;
	case 3263:
		mes "ภารกิจนี้ได้รับมอบหมายจากช่างตีเหล็ก Payon อันโตนิโอ.";
		next;
		mes "ไม้กายสิทธิ์! คุณเคยได้ยินเกี่ยวกับเรื่องนี้หรือไม่? มันเป็นไม้กายสิทธิ์ที่เหลือเชื่อ!";
		next;
		mes "หากคุณพูดคำวิเศษและแกว่งไม้กายสิทธิ์มันจะทำให้ความปรารถนาของคุณเป็นจริง.";
		next;
		mes "เลยตัดสินใจทำด้วยตัวเอง แล้วจะร่ำรวยมีพลัง ~! ฮ่า ๆ!!";
		next;
		mes "ในการทำไม้กายสิทธิ์ฉันต้องการวัสดุพิเศษ.";
		next;
		mes "กรุณานำ Dokebi Horns 30 อันและไม้ไผ่ 30 อันมาให้ฉัน ถ้าคุณทำเช่นนั้นฉันจะให้คุณยืมไม้กายสิทธิ์ของฉันสักครั้ง ฮ่า ๆ จัดการได้ดี?";
		next;
		mes "โปรดยืนยันกับเราหลังจากที่คุณทำภารกิจสำเร็จแล้วเราจะให้รางวัลที่เหมาะสมแก่คุณ.";
		next;
		mes "-- ช่างตีเหล็ก Payon อันโตนิโอ --";
		break;
	}
	return;
}
