//===== rAthena Script =======================================
//= Eden Group Quests - Mission [100-111]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 100 - 111.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,37,95,5	script	Gelkah#1	4_F_GELKA,{
	cutin "gelca01",2;
	if (checkweight(1201,1) == 0) {
		mes "^FF0000-ข้อความเตือน-";
		mes "- รอสักครู่ !! -";
		mes "- คุณมีรายการมากเกินไป -";
		mes "- ไม่สามารถรับไอเทมได้. -";
		mes "- ลดน้ำหนักของร่างกาย -";
		mes "- กรุณาลองอีกครั้ง. -^000000";
		close2;
		cutin "",255;
		end;
	}
	mes "[Gelkah]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "แสดงหลักฐานว่าคุณเป็นส่วนหนึ่งของ Eden Group หรือฉันไม่มีธุรกิจกับคุณ.";
		mes "ฉันกำลังส่งคำขอไปยังระดับพื้นฐาน ^FF0000100 - 110^000000 นักผจญภัย.";
	}
	else if (BaseLevel < 100) {
		mes "คุณไม่ใช่นักผจญภัยที่โตพอ คุณควรมีเลเวลพื้นฐานอย่างน้อย 100 ดังนั้น โดยพื้นฐาน แล้วอย่างน้อยก็เป็นคลาสที่สาม.";
		next;
		mes "[Gelkah]";
		mes "คำขอมาจาก ^FF0000ที่อื่น^000000 นอกเหนือจาก Midgard.";
	}
	else if (BaseLevel < 111) {
		if (para_100lv01 == 0) {
			mes "นักผจญภัย!";
			emotion ET_SMILE;
			next;
			cutin "gelca03",2;
			mes "[Gelkah]";
			mes "ฉันเพิ่งได้รับคำขอหลายรายการจากลูกค้าที่ต้องการความช่วยเหลือจากนักผจญภัยเช่นเดียวกับตัวคุณเอง.";
			next;
			if (select( "ฉันชอบคำขอ! ","ภารกิจที่น่าเบื่อ ... ไม่เป็นไร!" ) == 2) {
				mes "[Gelkah]";
				mes "คุณรู้ดีว่าคำขอที่ยากกว่าให้ผลตอบแทนที่ดีกว่า?";
				next;
			}
			mes "[Gelkah]";
			mes "อันที่จริงฉันไม่สามารถยอมรับเพียงแค่ schmo ใด ๆ นอกถนน อาจารย์ของฉันเกลียดคนที่ไม่เด็ดขาด.";
			next;
			if (select( "ใครเป็น master?","ฉันไม่แน่ใจ!" ) == 1) {
				cutin "gelca03",2;
				mes "[Gelkah]";
				mes "คนที่ยืนอยู่ข้างๆฉันคือนายของฉัน ... เขากลับมาหลังจากการฝึกฝนมายาวนาน.";
				next;
				mes "[Gelkah]";
				mes "ฉันเข้าใจว่าคุณอาจสงสัยว่าทำไมสุระถึงต้องการอาร์คบิชอปเป็นครู...";
			}
			else {
				cutin "gelca03",2;
				mes "[Gelkah]";
				if (BaseLevel < 110)
					mes "คุณคิดว่าคุณเป็นทหารผ่านศึกเพียงเพราะคุณกลายเป็นชนชั้นสาม...";
				else
					mes "ฉันรู้ว่าคุณไม่ได้เป็นมือใหม่ แต่อาจารย์ของฉันคิดว่าคุณไม่ต่างจากคนหนึ่ง ในความคิดของฉันคุณเป็นนักผจญภัยที่ยอดเยี่ยม!";
			}
			next;
			mes "[Gelkah]";
			mes "เราต้องการนักผจญภัยที่เต็มใจช่วยเหลือ คุณจะช่วยเรา?";
			next;
			cutin "gelca01",2;
			if (select( "ตกลงฉันจะช่วย "," ฉันไม่สนใจที่จะช่วยคุณ." ) == 2) {
				mes "[Gelkah]";
				mes "คุณจะรู้ได้อย่างไรโดยไม่ต้องทำ.";
			}
			else {
				mes "[Gelkah]";
				mes "นั่นคือสิ่งที่นักผจญภัยทำ!";
				next;
				mes "[Gelkah]";
				mes "ฉันต้องการทราบว่าคุณสามารถดำเนินการตามคำขอได้หรือไม่ ให้ฉันดู...";
				next;
				mes "[Gelkah]";
				mes "เก็บ 50 ^ff0000Brigan.^000000 เป็นการทดสอบที่ง่ายและซับซ้อน.";
				next;
				mes "[Gelkah]";
				mes "หาได้ง่ายจากสัตว์ประหลาดที่ Turtle Island หรือที่ Glastheim.";
				next;
				mes "[Gelkah]";
				mes "ให้ทำตามคำขอที่แท้จริงหลังจากล้างข้อง่ายๆ.";
				setquest 13002;// Brigan collecting
				para_100lv01 = 1;
				para_100lv02 += 1;
			}
		}
		else if (para_100lv01 == 1) {
			if (countitem(7054) > 49) {// Brigan
				mes "คุณนำ Brigans ทั้งหมดที่ฉันขอมาหรือไม่?";
				next;
				mes "[Gelkah]";
				mes "ฉันไม่จำเป็นต้องนับทีละคน คำขอนี้มีไว้สำหรับการคัดเลือกนักผจญภัยที่ดีที่สุด.";
				next;
				mes "[Gelkah]";
				mes "อย่าเสียใจเพราะในที่สุด Master จะต้องรับผิดชอบหากนักผจญภัยได้รับบาดเจ็บระหว่างการร้องขอ.";
				next;
				mes "[Gelkah]";
				mes "อาจารย์ตัดสินใจว่าความปลอดภัยของนักผจญภัยเป็นสิ่งสำคัญเนื่องจากคำขอใหม่จำนวนมากเป็นอันตราย.";
				next;
				cutin "rote01",2;
				mes "[Rohtert]";
				mes "อืม...";
				emotion ET_HNG, getnpcid(0, "Rohtert#12");
				next;
				cutin "gelca01",2;
				mes "[Gelkah]";
				mes "ฉันหวังว่าคุณจะไม่แอบมอง การประเมินความสามารถของคำขอมาตรฐานของคุณเกือบเสร็จสิ้นแล้ว.";
				next;
				mes "[Gelkah]";
				mes "ไม่มีเวลาทำ ^ff0000ตราของ Eden Group ขั้นสูง.^000000";
				next;
				mes "[Gelkah]";
				mes "^ff0000เวลาเป็นเงินผู้เริ่มเล่น!^000000";
				mes "ฉันคิดว่านั่นคือสิ่งที่ปรมาจารย์ที่แท้จริงน่าจะถูกต้อง?";
				next;
				mes "[Gelkah]";
				mes strcharinfo(0) +" จะทำรายการขอให้คุณไปคุยกับอาจารย์ของฉันเดี๋ยวนี้และแสดงความเคารพของคุณ.";
				delitem 7054,50;// Brigan
				completequest 13002;// Brigan collecting
				para_100lv01 = 2;
				getitem 6549,1;// Courtesy_Ticket
			}
			else {
				cutin "gelca02",2;
				mes "ยังไม่พบ 50 Brigan? อาจารย์บอกว่ามันเป็นงานง่ายๆ...";
				next;
				mes "[Gelkah]";
				mes "ได้โปรดรีบมา! เราต้องการนักผจญภัยตัวจริงเพื่อดำเนินการตามคำขอ.";
			}
		}
		else if (para_100lv01 == 2)
			mes "เดี๋ยวก่อน. ฉันกำลังทำรายการคำขอ ฉันไม่รู้ว่าฉันต้องให้อะไรกับคุณ เป็นผู้ชายที่ดี.";
		else if (para_100lv01 > 2) {
			mes "รายการคำขอพร้อมแล้ว คุณต้องการเริ่มต้น?";
			next;
			switch( select( "Hunting Quests", "Inquire regards", "Courtesy Ticket Exchange" ) ) {
				mes "[Gelkah]";
			case 1:
				mes "คุณไม่จำเป็นต้องรับคำขอทั้งหมด พร้อมกัน เลือกพื้นที่ขอที่เหมาะกับ ทักษะเฉพาะของคุณ สิ่งที่ช่วยให้คุณดำเนินการตามคำขอ และสนุกกับการเดินทางในเวลาเดียวกัน.";
				next;
				switch( select( "คำขอ Glastheim related", "คำขอ Ash Vacuum related", "คำขอ Ice Dungeon related", "คำขอ Magma Dungeon related", "คำขอ อื่นๆ" ) ) {
					mes "[Gelkah]";
				case 1:
					mes "นี่คือคำขอจากพื้นที่ Glastheim.";
					next;
					switch( select( "Carat Hunting", "Arclouse Hunting", "Anolian Hunting", "Sting Hunting", "Majoruros Hunting" ) ) {
						mes "[Gelkah]";
					case 1:
						if (checkquest(13004,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ สิ่งสำคัญคือคุณควรพักผ่อนสักพัก จะมีคำขอรอคุณอยู่เสมอ.";
						else if (checkquest(13003,HUNTING) == 1) {
							cutin "ฤณทตฤซตๅ",3;  //carat_card
							mes "คำขอนี้ขอเคลียร์ ^0000FF30^000000 ^FF0000Carat^000000 ด้านในของชั้นหนึ่งของปราสาท Glastheim.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณดำเนินการตามคำขอเรียบร้อยแล้ว.";
						}
						else if (checkquest(13003,HUNTING) == 2) {
							mes "การเยี่ยมชมปราสาทเก่าแก่ที่น่ากลัวนั้นต้องยาก ฉันแน่ใจว่ามันจะเป็นประสบการณ์ที่ดีสำหรับฉันสักวัน.";
							callsub S_Quest,13003,13004,106000,148000;
						}
						else {
							cutin "ฤณทตฤซตๅ",3;  //carat_card
							mes "คำขอนี้มาจากพ่อค้าจากโมร็อกโกซึ่งกำลังสูญเสียธุรกิจจากสัตว์ประหลาดเหล่านี้ใน Glastheim.";
							next;
							mes "[Gelkah]";
							mes "Eden Group ตระหนักดีว่านี่ไม่ใช่เหตุผลที่ดี แต่คำขอก็คือคำขอ ดังนั้นโปรดล้าง ^FF000030 Carat^000000 มอนสเตอร์ภายในปราสาท Glastheim.";
							callsub S_Quest,13004,13003;
						}
						break;
					case 2:
						if (checkquest(13006,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ โปรดพักผ่อน จะมีคำขอให้คุณเสมอ.";
						else if (checkquest(13005,HUNTING) == 1) {
							cutin "พฦลฉถ๓ฟ์ฝบฤซตๅ",3;  //arclouse_card
							mes "ช่วยกำจัด ^FF000022 Arclouse^000000.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13005,HUNTING) == 2) {
							mes "คุณทำได้ดีมาก.";
							emotion ET_BEST;
							callsub S_Quest,13005,13006,106000,148000;
						}
						else {
							cutin "พฦลฉถ๓ฟ์ฝบฤซตๅ",3;  //arclouse_card
							mes "คุณรู้หรือไม่ว่ามีถั่วที่ใช้เป็นอาหารที่นี่ในมิดการ์ด? เมื่อ Arclouse ม้วนมันจะคล้ายกับถั่ว.";
							next;
							mes "[Gelkah]";
							mes "สำนักงานใหญ่ของ Eden Group ต้องการตรวจสอบ Arclouse ในฐานะตัวนำโชคที่เป็นไปได้.";
							next;
							mes "[Gelkah]";
							mes "ช่วยกำจัด ^FF000022 Arclouse^000000.";
							if (callsub( S_Quest,13006,13005 ) == 0) {
								mes "[Gelkah]";
								mes "นั่นมันแย่เกินไป มันเป็นคำขอที่น่าสนใจ.";
							}
						}
						break;
					case 3:
						if (checkquest(13008,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน จะมีคำขอรออยู่เสมอ.";
						else if (checkquest(13007,HUNTING) == 1) {
							cutin "พฦณ๎ธฎพศฤซตๅ",3;  //anolian_card
							mes "พระภิกษุกำลังเดินทางไปแสวงบุญและขอความคุ้มครองจากสิ่งมีชีวิตที่มีลักษณะคล้ายจระเข้.";
							next;
							mes "[Gelkah]";
							mes "อย่างไรก็ตามคำขอนี้คือการล้างชาว Anolians ในทางน้ำใต้ดินเพื่อให้เดินทางไปแสวงบุญได้อย่างปลอดภัย.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13007,HUNTING) == 2) {
							mes "คุณทำได้ดีกับคำขอนี้.";
							callsub S_Quest,13007,13008,112000,186500;
						}
						else {
							cutin "พฦณ๎ธฎพศฤซตๅ",3;  //anolian_card
							mes "กลาสไฮม์ดูเหมือนจะเป็นสถานที่ศักดิ์สิทธิ์ที่ดีไม่เพียง แต่สำหรับนักผจญภัยเท่านั้น แต่ยังรวมถึงพระสงฆ์ด้วย ฉันไม่รู้ว่าอะไรดึงดูดพวกเขาที่นั่นจริงๆ.";
							next;
							mes "[Gelkah]";
							mes "การเดินทางจากอารามไปยังทางน้ำใต้ดินคือการเดินทาง คำขอคือการเคลียร์ ^FF000030 Anolian^000000 ในท่อระบายน้ำ Glastheim เพื่อการเดินทางที่ปลอดภัย.";
							callsub S_Quest,13008,13007;
						}
						break;
					case 4:
						if (checkquest(13010,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน จะมีคำขอรออยู่เสมอ.";
						else if (checkquest(13009,HUNTING) == 1) {
							cutin "ฝบฦรฤซตๅ",3;  //sting_card
							mes "มีสัตว์ประหลาด Sting จำนวนมากในทางน้ำใต้ดินและถ้ำใต้ดินดังนั้นจึงง่ายต่อการเคลียร์.";
							next;
							mes "[Gelkah]";
							mes "คำขอคือการเคลียร์ ^FF000030 Sting^000000 ในทางน้ำใต้ดินและถ้ำใต้ดินเพื่อการแสวงบุญที่ปลอดภัย.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13009,HUNTING) == 2) {
							mes "การเยี่ยมชมใต้ดินที่สกปรกและมีกลิ่นเหม็นต้องยาก มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13009,13010,112000,186500;
						}
						else {
							cutin "ฝบฦรฤซตๅ",3;  //sting_card
							mes "คุณเคยได้ยินเกี่ยวกับการแสวงบุญของพระภิกษุสงฆ์หรือไม่? มีเหล็กไนจำนวนมากในทางน้ำ และ ถ้ำใต้ดินจึงรบกวนพระที่สงบ.";
							next;
							mes "[Gelkah]";
							mes "ท่านอาจารย์รับคำขอนี้เพราะนับถือพระเหล่านั้น คำขอคือการเคลียร์ ^FF000030 Sting^000000 สัตว์ประหลาดในทางน้ำใต้ดินเพื่อการแสวงบุญที่ปลอดภัย.";
							callsub S_Quest,13010,13009;
						}
						break;
					case 5:
						if (checkquest(13012,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน การแสวงบุญจะดำเนินต่อไปดังนั้นจะมีการร้องขออีกครั้ง.";
						else if (checkquest(13011,HUNTING) == 1) {
							cutin "ธภฬภ๚ฟ์ทฮฝบฤซตๅ",3;  //majoruros_card
							mes "ฉันสงสัยว่าเหตุใดจึงอยู่เบื้องหลังการแสวงบุญของพระเหล่านี้และทำไมพวกเขาถึงทำมันต่อไป ... มันต้องมีเหตุผลที่ซ่อนอยู่.";
							next;
							mes "[Gelkah]";
							mes "ฉันเคยดูแลตามคำขอ แต่ฉันกังวล คำขอคือการเคลียร์ ^FF000030 Majoruros^000000 จากชั้น 2 ของถ้ำใต้ดิน.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13011,HUNTING) == 2) {
							mes "การเยี่ยมชมเพื่อแสวงบุญเป็นเวลานานจะต้องเหนื่อย.";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13011,13012,112000,252000;
						}
						else {
							cutin "ธภฬภ๚ฟ์ทฮฝบฤซตๅ",3;  //majoruros_card
							mes "สถานที่แสวงบุญของพระภิกษุอยู่ที่ชั้น 2 ของถ้ำใต้ดินของปราสาทกลาสต์ไฮม์ ไม่ใช่การเดินทางที่ง่าย แต่จะเป็นประสบการณ์ที่ดี.";
							next;
							mes "[Gelkah]";
							mes "Majoururos ไม่ใช่เรื่องง่ายที่จะล่าสัตว์ดังนั้นโปรดระวัง คำขอคือการเคลียร์ ^FF000030 Majoruros^000000 ในถ้ำใต้ดินชั้นสอง.";
							callsub S_Quest,13012,13011;
						}
					}
					break;
				case 2:
					mes "มีคำขอมากมายที่มาจาก Midgard Expedition.";
					next;
					if (select( "Hunting Quests","Trauma" ) == 1) {
						mes "[Gelkah]";
						if (checkquest(13014,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน โปรดพักผ่อน จะมีคำขอให้คุณเสมอ.";
						else if (checkquest(13013,HUNTING) == 1) {
							cutin "วษฑอลงถ๓ฤซตๅ",3;  //pinguicula_card
							mes "พื้นที่ขาออกลึกลับ .. สัตว์ประหลาดไม่เหมือนใคร.";
							next;
							mes "[Gelkah]";
							mes "นี่เป็นคำขอที่ผิดปกติ ช่วยกำจัด ^FF000030 Pinguicula^000000.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13013,HUNTING) == 2) {
							mes "การเยี่ยมชม Ash Vacuum ต้องมีความแข็งแกร่ง.";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13013,13014,106000,148000;
						}
						else {
							cutin "วษฑอลงถ๓ฤซตๅ",3;  //pinguicula_card
							mes "พิงกุอิคูลาเป็นหนึ่งในสัตว์ประหลาดตัวแรกที่นักผจญภัยพบเมื่อพวกเขาก้าวเข้าสู่สุญญากาศเถ้า พวกเขาคาดหวังว่าเราจะลดมอนสเตอร์รอบ ๆ Expedition Camp.";
							next;
							mes "[Gelkah]";
							mes "เป็นเรื่องธรรมดาที่จะเกิดอุบัติเหตุกับสัตว์ประหลาดตัวใหม่มากขึ้น มันเป็นคำขอทั่วไป ช่วยกำจัด ^FF000030 Pinguicula^000000.";
							callsub S_Quest,13014,13013;
						}
					}
					else {
						mes "[Gelkah]";
						if (checkquest(13016,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน โปรดพักผ่อน คำขอนี้ไม่คุ้มค่ากับการทำงานอย่างหนัก.";
						else if (checkquest(13015,HUNTING) == 1) {
							cutin "ท็ฝรฟรถ๓บฃฝบฦฤฤซตๅ",3;  //luciola_vespa_card
							mes "นี่เป็นคำขอที่แปลกและเป็นเหตุผลที่แปลก แต่เราควรยอมรับมันอยู่ดี.";
							next;
							mes "[Gelkah]";
							mes "ช่วยกำจัด ^FF000030 Luciola Vespa^000000.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13015,HUNTING) == 2) {
							mes "ต้องเป็นเรื่องยากมากที่จะทำคำขอแปลก ๆ เช่นนี้.";
							callsub S_Quest,13015,13016,112000,186500;
						}
						else {
							cutin "ท็ฝรฟรถ๓บฃฝบฦฤฤซตๅ",3;  //luciola_vespa_card
							mes "นี่เป็นคำขอส่วนบุคคลมาก มันผิดปกติ แต่คำขอนี้มาจากนักผจญภัยที่เป็นมิตรกับ Rohtert .. เป็นคำขอของ Pretty Boy ทำไมเขาถึงเขียนสิ่งนี้?";
							next;
							mes "[Gelkah]";
							mes "มันเป็นเรื่องส่วนตัวเกินไปที่เขาถูกผึ้งต่อยเมื่อเขายังเด็ก มันทำให้เขาบอบช้ำดังนั้นเขาจึงไม่สามารถเดินทางไปยัง Ash Vacuum ได้.";
							next;
							mes "Gelkah จ้องมองไปที่ Rohtert.";
							next;
							mes "[Gelkah]";
							mes "นี่ดูเหมือนจะเป็นคำขอสำหรับนักเดินทางที่เพิ่มขึ้นสู่โลกใหม่ ช่วยกำจัด ^FF000030 Luciola Vespa^000000.";
							callsub S_Quest,13016,13015;
						}
					}
					break;
				case 3:
					mes "มีคำขอมากมายที่อยู่ใกล้หรือใน Ice Dungeon ใน Rachel.";
					next;
					switch( select( "Desert Wolf Hunting", "Snowier Hunting", "Ice Titans..." ) ) {
						mes "[Gelkah]";
					case 1:
						if (checkquest(13018,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน.";
						else if (checkquest(13017,HUNTING) == 1) {
							cutin "ตฅภ๚ฦฎฟ๏วมฤซตๅ",3;  //desert_wolf_card
							mes "คุณอ่านหนังสือเก่าหรือไม่? เกี่ยวกับนักผจญภัยที่ต่อสู้กับหมาป่าตัวใหญ่และจบลงด้วยมิตรภาพ เรียกว่าแซมบ้ากับหมาป่า.";
							next;
							mes "[Gelkah]";
							mes "นวนิยายก็คือนวนิยาย ช่วยกำจัด ^FF000030 Desert Wolf^000000 ใกล้ทางเข้าคุกใต้ดินน้ำแข็งในราเชล.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13017,HUNTING) == 2) {
							mes "ไม่น่ารักขนาดนั้น?";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13017,13018,106000,148000;
						}
						else {
							cutin "ตฅภ๚ฦฎฟ๏วมฤซตๅ",3;  //desert_wolf_card
							mes "ถ้ำน้ำแข็งอยู่ทางเหนือของ Rachel โดยตรง ปัญหาคือหมาป่าทำให้นักท่องเที่ยวตกใจเมื่อพวกเขามุ่งหน้าไปดูถ้ำน้ำแข็ง.";
							next;
							mes "[Gelkah]";
							mes "คงจะสนุกถ้าเราสามารถพูดคุยและแซมบ้ากับหมาป่า! ช่วยกำจัด ^FF000030 Desert Wolf^000000 ใกล้ดันเจี้ยนน้ำแข็ง.";
							callsub S_Quest,13018,13017;
						}
						break;
					case 2:
						if (checkquest(13020,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน.";
						else if (checkquest(13019,HUNTING) == 1) {
							cutin "ฝบณ๋ภฬพ๎ฤซตๅ",3;  //snowier_card
							mes "ลมหายใจหยาบกร้านแววตาเย็นยะเยือก .. ไม่ใช่เรื่องง่ายขอให้ตัวเองอบอุ่น.";
							next;
							mes "[Gelkah]";
							mes "กรุณาเคลียร์ ^FF000030 Snowier^000000 บนชั้นสองของดันเจี้ยนน้ำแข็ง.";
							cutin "gelca01",2;
							next;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13019,HUNTING) == 2) {
							mes "การเยี่ยมชมดันเจี้ยนน้ำแข็งที่เยือกแข็งต้องมีความแข็งแกร่ง.";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13019,13020,106000,148000;
						}
						else {
							cutin "ฝบณ๋ภฬพ๎ฤซตๅ",3;  //snowier_card
							mes "คนที่เคยไปดันเจี้ยนน้ำแข็งบอกว่ามีสัตว์ประหลาดส่งเสียงดังรบกวนการมาเยือนของพวกเขา กรุณาล้างออกหากคุณสามารถทนได้.";
							next;
							mes "[Gelkah]";
							mes "กรุณาเคลียร์ ^FF000030 Snowier^000000 บนชั้นสองของดันเจี้ยนน้ำแข็ง.";
							callsub S_Quest,13020,13019;
						}
						break;
					case 3:
						if (checkquest(13022,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน.";
						else if (checkquest(13021,HUNTING) == 1) {
							cutin "พฦภฬฝบฦผลบฤซตๅ",3;  //ice_titan_card
							mes "กรุณาเคลียร์ ^FF000030 Ice Titan^000000 ที่ถ้ำน้ำแข็ง.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13021,HUNTING) == 2) {
							mes "การเยี่ยมชมถ้ำมืดอันหนาวเหน็บนั้นต้องยาก.";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13021,13022,112000,186500;
						}
						else {
							cutin "พฦภฬฝบฦผลบฤซตๅ",3;  //ice_titan_card
							mes "คุณเคยไปถ้ำน้ำแข็งทางเหนือของ Rachel หรือไม่? มีมอนสเตอร์ที่เรียกว่า Ice Titans อยู่ลึกเข้าไปในคุกใต้ดิน นักวิชาการต้องการตรวจสอบว่าพวกเขาเป็นโกเลมหรือไม่.";
							next;
							mes "[Gelkah]";
							mes "ขอให้ลดจำนวนลงเพื่อความปลอดภัยของนักวิชาการ กรุณาเคลียร์ ^FF000030 Ice Titan^000000 ที่ถ้ำน้ำแข็ง.";
							callsub S_Quest,13022,13021;
						}
					}
					break;
				case 4:
					mes "มีคำขอสำหรับการล่าสัตว์ประหลาดใน Magma Dungeon. ^DA26DAพเนจรสาวสวย^000000 กำลังขอสิ่งนี้จาก Comodo.";
					next;
					if (select( "Nightmare Terror Hunting","Deleter Hunting" ) == 1) {
						mes "[Gelkah]";
						if (checkquest(13024,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ ขอบคุณที่เก็บความทรงจำ .. แต่คุณควรพักผ่อน.";
						else if (checkquest(13023,HUNTING) == 1) {
							cutin "ณชภฬฦฎธพ๎ลืทฏฤซตๅ",3;  //nightmare_terror_card
							mes "ความทรงจำของผู้หญิงเป็นสิ่งสำคัญ! กรุณาล้าง s^FF000030 Nightmare Terror^000000.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13023,HUNTING) == 2) {
							mes "คำนึงถึงความทรงจำของผู้อื่น มีความผิดปกติในโลก มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13023,13024,106000,148000;
						}
						else {
							cutin "ณชภฬฦฎธพ๎ลืทฏฤซตๅ",3;  //nightmare_terror_card
							mes "คำขอนี้มาจากลูกค้าที่เสียใจเพราะดันเจี้ยนแมกม่าเต็มไปด้วยสัตว์ประหลาดเมื่อเธอกลับมาเพื่อกลับมาหลังจากเลิกกับคนรัก เห็นได้ชัดว่าเธอใช้เวลากับเขาที่นั่นนานมากและต้องการความสงบสุข.";
							next;
							mes "[Gelkah]";
							mes "ความทรงจำของผู้หญิงเป็นสิ่งสำคัญ! กรุณาเคลียร์ ^FF000030 Nightmare Terror^000000.";
							callsub S_Quest,13024,13023;
						}
					}
					else {
						mes "[Gelkah]";
						if (checkquest(13026,PLAYTIME) == 0)
							mes "คุณเพิ่งดำเนินการตามคำขอ คุณใช้เวลาพอที่จะทำให้เย็นลงหรือไม่? พักผ่อนให้เพียงพอ.";
						else if (checkquest(13025,HUNTING) == 1) {
							cutin "ฐ๘ม฿ต๔ธฎลอฤซตๅ",3;  //flying_deleter_card
							mes "ลูกค้าแสดงการเต้นรำที่ยอดเยี่ยมให้กับคนรัก อยู่มาวันหนึ่งพวกเขาถูกโจมตีจาก Deleter ขณะที่พวกเขากำลังเต้นรำอยู่ในดันเจี้ยนแมกมา.";
							next;
							mes "[Gelkah]";
							mes "การโจมตีทำลายการเต้นรำของพวกเขาและในที่สุดความสัมพันธ์ของพวกเขาก็เพราะผู้ชายคนนั้นตำหนิผู้หญิงที่ถูกทำร้าย.";
							next;
							mes "[Gelkah]";
							mes "ตอนนี้เธอโทษผู้ลบดังนั้นเธอจึงทำตามคำขอนี้ กรุณาเคลียร์ ^FF000030 Flying Deleter^000000.";
							next;
							cutin "gelca03",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13025,HUNTING) == 2) {
							cutin "gelca03",2;
							mes "Eden Group ปกป้องความรักความยุติธรรมและ Midgard! เย่!";
							mes "มันคงเป็นความทรงจำที่ดี.";
							callsub S_Quest,13025,13026,106000,148000;
						}
						else {
							cutin "ฐ๘ม฿ต๔ธฎลอฤซตๅ",3;  //flying_deleter_card
							mes "ลูกค้าแสดงการเต้นรำที่ยอดเยี่ยมให้กับคนรัก อยู่มาวันหนึ่งพวกเขาถูกโจมตีจาก Deleter ขณะที่พวกเขากำลังเต้นรำอยู่ในดันเจี้ยนแมกมา.";
							next;
							mes "[Gelkah]";
							mes "การโจมตีทำลายการเต้นรำของพวกเขาและในที่สุดความสัมพันธ์ของพวกเขาก็เพราะผู้ชายคนนั้นตำหนิผู้หญิงที่ถูกทำร้าย.";
							next;
							mes "[Gelkah]";
							mes "ตอนนี้เธอโทษผู้ลบดังนั้น เธอจึงทำตามคำขอนี้ กรุณาเคลียร์ ^FF000030 Flying Deleter^000000.";
							callsub S_Quest,13026,13025;
						}
					}
					break;
				case 5:
					mes "นี่คือรายการคำขออื่น ๆ จากทั่ว Midgard.";
					next;
					switch( select( "Loli Ruri Hunting - Niflheim", "Medusa Hunting - Comodo", "Anubis Hunting - Sphinx" ) ) {
						mes "[Gelkah]";
					case 1:
						if (checkquest(13041,PLAYTIME) == 0)
							mes "I think you should rest a bit. Even adventurers need their rest right?";
						else if (checkquest(13040,HUNTING) == 1) {
							cutin "ทฮธฎท็ธฎฤซตๅ",3;  //loli_ruri_card
							mes "Eden Group กำลังวางแผนที่จะแจ้งให้ทราบว่านักผจญภัยที่ยังไม่บรรลุนิติภาวะจะไม่ถูกส่งไปตามคำขอในเวลากลางคืนเพราะมันอันตรายเกินไป.";
							next;
							mes "[Gelkah]";
							mes "ฉันสงสัยว่าทำไมพวกเขาถึงวางแผนแบบนี้ ^FF0000nonsense^000000... ทำให้ฉันสงสัยว่าพวกเขารู้อะไรเกี่ยวกับการผจญภัยหรือไม่.";
							next;
							mes "[Gelkah]";
							mes "เราควรยอมรับคำของ่ายๆนี้ก่อนที่เราจะเริ่มรับคำขอที่ไร้สาระไปมากกว่านี้ กรุณาเคลียร์ ^FF000030 Loli Ruri^000000 ใน Niflheim.";
							next;
							cutin "gelca02",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13040,HUNTING) == 2) {
							mes "คุณมีบาดแผลทางจิตใจหรือไม่? ฉันดีใจที่คุณกลับมาอย่างปลอดภัย.";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13040,13041,112000,186500;
						}
						else {
							cutin "ทฮธฎท็ธฎฤซตๅ",3;  //loli_ruri_card
							mes "สมาคมลับขอความคุ้มครองกลุ่ม Eden จาก Loli Ruris เพราะพวกเขาเชื่อว่าพวกเขามีพลังทางจิต.";
							next;
							mes "[Gelkah]";
							mes "พวกเขากล่าวว่าพวกเขาจะรักษานักผจญภัยของ Eden Group ที่เหนื่อยล้าเพื่อแลกกับคำขอ แต่เราไม่แน่ใจว่าพวกเขาจะทำตามได้.";
							next;
							mes "[Gelkah]";
							mes "เราควรยอมรับคำของ่ายๆต่อไป กรุณาเคลียร์ ^FF000030 Loli Ruri^000000 ใน Niflheim.";
							callsub S_Quest,13041,13040;
						}
						break;
					case 2:
						if (checkquest(13043,PLAYTIME) == 0)
							mes "การผจญภัยเป็นสิ่งสำคัญ แต่การพักผ่อนก็สำคัญเช่นกัน.";
						else if (checkquest(13042,HUNTING) == 1) {
							cutin "ธตฮป็ฤซตๅ",3;  //medusa_card
							mes "ฉันไม่รู้ว่าทำไมมาสเตอร์ถึงชอบคำขอแบบนี้ เขายังเคยบอกว่าคำขอใด ๆ เป็นคำขอที่ดีสำหรับนักผจญภัย...";
							next;
							mes "[Gelkah]";
							mes "กรุณาเคลียร์ ^FF000030 Medusa^000000 ในถ้ำตะวันตกของโกโมโด.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13042,HUNTING) == 2) {
							mes "หาดโคโมโดเป็นยังไงบ้าง? ลองดูรอบ ๆ แทนที่จะให้ความสนใจกับคำขอมากเกินไป.";
							mes "จะเป็นประสบการณ์ที่ดีอย่างแน่นอน.";
							callsub S_Quest,13042,13043,106000,148000;
						}
						else {
							cutin "ธตฮป็ฤซตๅ",3;  //medusa_card
							mes "นี่คือคำขอจากนักออกแบบทรงผมในอัลเบอร์ตา เขาอยากรู้ว่างูบนหัวของเมดูซ่าเป็นงูหรือผมแฟนซีกันแน่...";
							next;
							mes "[Gelkah]";
							mes "เห็นได้ชัดว่าพวกมันเป็นงู แต่พวกเราเป็นใครกันที่จะทำลายความฝันของไอ้บ้านี่?";
							next;
							mes "[Gelkah]";
							mes "เจ้านายของฉันยอมรับคำขอนี้เพียงเพราะมันบ้า กรุณาเคลียร์ ^FF000030 Medusa^000000 สัตว์ประหลาดในถ้ำทางตะวันตกของ Comodo.";
							callsub S_Quest,13043,13042;
						}
						break;
					case 3:
						if (checkquest(13045,PLAYTIME) == 0)
							mes "คำขอจากสฟิงซ์เสร็จสิ้น โปรดพักผ่อน.";
						else if (checkquest(13044,HUNTING) == 1) {
							cutin "พฦดฉบ๑ฝบฤซตๅ",3;  //anubis_card
							mes "ฉันควรตรวจสอบเกี่ยวกับลูกค้าด้วยตัวเอง แต่ตอนนี้ขอเคลียร์ ^FF000020 Anubis^000000 ที่ชั้น 4 ของดันเจี้ยนสฟิงซ์.";
							next;
							cutin "gelca01",2;
							mes "[Gelkah]";
							mes "โปรดแจ้งให้ฉันทราบเมื่อคุณได้ยกเลิกคำขอนี้.";
						}
						else if (checkquest(13044,HUNTING) == 2) {
							cutin "gelca02",2;
							mes "ดันเจี้ยนสฟิงซ์เป็นเขาวงกต.";
							mes "มันจะเป็นประสบการณ์ที่ดีสำหรับคุณ.";
							callsub S_Quest,13044,13045,112000,186500;
						}
						else {
							cutin "พฦดฉบ๑ฝบฤซตๅ",3;  //anubis_card
							mes "ลูกค้านิรนามส่งคำขอให้ช่วยเคลียร์มอนสเตอร์ในดันเจี้ยนสฟิงซ์เนื่องจากนักผจญภัยแทบจะไม่ได้ไปที่โมร็อกโกอีกต่อไป มอนสเตอร์ดูเหมือนจะออกอาละวาดที่นั่น.";
							next;
							mes "[Gelkah]";
							mes "กรุณาเคลียร์ ^FF000020 Anubis^000000 on the 4th floor of Sphinx dungeon.";
							callsub S_Quest,13045,13044;
						}
					}
				}
				break;
			case 2:
				if (para_100lv02 < 20) {
					.@ran = rand(1,3);
					if (.@ran == 1)
						mes "เรายุ่งมากกับการร้องขอที่ต้องได้รับการดูแล นี่เป็นครั้งแรกของฉันที่ต้องรับมือกับความกดดันมากมาย.";
					else if (.@ran == 2)
						mes "ฉันคิดว่า Eden Group กำลังยุ่งอยู่ แต่ฉันนึกไม่ออกว่าจะเยอะขนาดนี้ ฉันควรทำมากกว่านี้ในฐานะนักผจญภัย!";
					else {
						cutin "gelca02",2;
						mes "ฉันหวังว่าฉันจะได้ดื่มน้ำผลไม้สดสักแก้ว...";
					}
				}
				else if (para_100lv02 < 40) {
					.@ran = rand(1,3);
					if (.@ran == 1) {
						cutin "gelca03",2;
						mes "บางครั้งนักผจญภัยจะนำสิ่งของพิเศษหรือถ้วยรางวัลจากที่ที่พวกเขาไปเยี่ยมเพื่อขอ มันดีมาก!";
					}
					else if (.@ran == 2)
						mes "ถ้ามีอะไรดีๆ "+ strcharinfo (0) +" ฉันจะพยายามให้คุณสองสิ่ง ฉันไม่แน่ใจว่าจะมีเพียงพอที่จะให้เพราะนักผจญภัยจำนวนมาก.";
					else
						mes "การจัดการเอกสารต้องยุ่งยาก ง่ายกว่ามากที่จะออกไปอยู่ในสนาม.";
				}
				else if (para_100lv02 < 60) {
					if (para_100lv01 > 3) {
						.@ran = rand(1,3);
						if (.@ran == 1)
							mes "ฉันจะแจ้งให้คุณทราบหากมีสิ่งดีๆ.";
						else if (.@ran == 2)
							mes "คุณคุ้นเคยกับคำขอหรือไม่? เป็นเรื่องแปลกที่ฉันแนะนำนักผจญภัยแทนการร้องขอด้วยตัวเอง.";
						else
							mes "จะเป็นการดีถ้าคุณเคลียร์ทีละรายการหลังจากได้รับคำขอทั้งหมดจาก Glastheim.";
					}
					else {
						cutin "gelca03",2;
						mes "ฉันไม่ว่าง แต่กำลังเคลียร์คำขอจำนวนมาก "+ strcharinfo (0) +" ฉันซาบซึ้ง มาดูกัน. ฉันได้รับอาหารดีๆจากนักผจญภัยที่เขาได้รับระหว่างการร้องขอ.";
						next;
						mes "[Gelkah]";
						mes "อ๊ะฉันกินหมดแล้วเหรอ? โอเคเหลือเพียงอันเดียว กรุณาแวะมาทีหลัง!";
						para_100lv01 = 4;
						getitem 608,1;// Seed_Of_Yggdrasil
					}
				}
				else if (para_100lv02 < 80) {
					cutin "gelca03",2;
					if (para_100lv01 > 4)
						mes "เชียร์! "+ strcharinfo (0) +" ฉันไม่แน่ใจว่าเขาคิดว่าคุณเป็นลูกศิษย์ของเขาหรือเปล่า เขาแค่ควบคุมไม่ได้.";
					else {
						mes "มีของขวัญจำนวนมากเข้ามาเมื่อใดก็ตามที่คำขอเสร็จสมบูรณ์ นี่เอานี่เป็นรางวัล.";
						para_100lv01 = 5;
						getitem 607,1;// Yggdrasilberry
					}
				}
				else if (para_100lv02 > 100) {
					mes "มันเป็นงานที่ยาก แต่ก็สนุกกับผู้เชี่ยวชาญด้วย "+ strcharinfo (0) +"?";
					next;
					switch( select( "ฉันเกลียดคนที่อารมณ์ร้อน "," ฉันต้องการให้คุณเป็นมาสเตอร์ "," ฉันจะติดตามคุณไปที่การต่อสู้." ) ) {
						mes "[Gelkah]";
					case 1:
						mes "ฉันเข้าใจคุณ. ฉันทะเลาะกันตอนที่เจออาจารย์ครั้งแรกในหมู่บ้านโมรา.";
						next;
						mes "[Gelkah]";
						mes "เขาเกือบจะร้องไห้เมื่อฉันพบเขาครั้งแรก มันเป็นเรื่องยากที่จะแสร้งทำเป็นแพ้เขา กรุณาเก็บเป็นความลับ.";
						emotion ET_SMILE;
						break;
					case 2:
						if (Sex == SEX_MALE)
							mes "ฉันไม่แน่ใจว่าฉันถูกตัดออกสำหรับงานนั้นหรือไม่ นอกจากนี้...";
						else
							mes "ฉันขอโทษที่ต้องพูดแบบนี้ แต่ฉันเป็นเพียงศิษย์ของอาจารย์ในตอนนี้และตลอดไป ... เขาพูด ^FF0000ฉัน ไม่จำเป็นต้องมีนักเรียนหญิงที่น่าเกลียด^000000 แต่ฉันยังคงยึดติดกับเขา.";
						break;
					case 3:
						cutin "gelca04",2;
						mes "โอ้ไม่ ... โปรดอย่าทำอย่างนั้น ฉันขอโทษโปรดเก็บไว้เป็นความลับสำหรับอาจารย์ ฉันประมาท.";
						emotion ET_SHY;
						break;
					}
					next;
					cutin "gelca03",2;
					mes "[Gelkah]";
					if (para_100lv01 > 5)
						mes "ขอให้โชคดีกับคำขอที่เหลือ.";
					else {
						mes "ขอให้โชคดีกับคำขอที่เหลือ. ^FF0000อาจารย์บอกว่าเขามีบางอย่างจะพูดกับคุณ.^000000";
						para_100lv01 = 6;
					}
				}
				else
					mes "ฉันขอโทษ แต่ฉันยุ่งนิดหน่อย เราคุยทีหลังได้มั๊ย?";
				break;
			case 3:
				cutin "gelca03",2;
				mes "ต้องการอะไร?";
				next;
				if (countitem(6549) > 0) {// Courtesy_Ticket
					if (select( "^FF0000Courtesy Ticket^000000? ","แลกตั๋วเพื่อรับรางวัล." ) == 1) {
						mes "[Gelkah]";
						mes "ฉันรู้ว่ามันฟังดูเด็ก ขวา? รูปร่างก็เหมือนกับการ์ดที่เราเคยเล่นตอนเด็ก ๆ.";
						next;
						if (select( "มันคืออะไร?", "เอ่อพวกนั้นสนุกดี." ) == 1) {
							cutin "gelca02",2;
							mes "[Gelkah]";
							mes "ฉันขอโทษ ... มันต้องเป็นที่นิยมในหมู่บ้านของเราเท่านั้น โยนความร้อนและปล่อยให้มันบินได้.";
							emotion ET_SORRY;
							next;
						}
						cutin "gelca01",2;
						mes "[Gelkah]";
						mes "อย่างที่คุณเห็นอาจารย์สร้าง มันขึ้นมาเองและใส่รูปของเขาลงไป เขาใช้ในการเดินทาง เพื่อหาวัสดุกระดาษเพื่อทำบัตร.";
						next;
						cutin "gelca02",2;
						mes "[Gelkah]";
						mes "หากไม่ได้ยื่นอย่าง ถูกต้องคำขอ ในการค้นหาเอกสารจะรวมอยู่ในรายการคำขอ.";
						next;
						mes "[Gelkah]";
						mes "เขาใจดีมากแม้ว่า เขาจะดูไม่เหมือนก็ตาม อาจารย์ใช้มันเพื่อมอบบางสิ่ง บางอย่างให้กับนักผจญภัยเพื่อการอุทิศตน.";
						next;
						cutin "gelca01",2;
						mes "[Gelkah]";
						mes "ให้เรื่องราว เกี่ยวกับการผจญภัย หรือถ้วยรางวัลโดยใช้การ์ด 1,10,20 และ 30 ใบ.";
						next;
						mes "[Gelkah]";
						mes "มันจะเป็นประสบการณ์ที่ดี ถ้าคุณได้ฟังเรื่องราวของนักผจญภัย มีการผจญภัยมากมายที่เรา ได้ยินจากปรมาจารย์.";
						next;
						mes "[Gelkah]";
						mes "เราเรียกมันว่า Courtesy Ticket .";
					}
					else {
						mes "[Gelkah]";
						mes "คุณต้องการใช้บัตรนี้อย่างไร?";
						next;
						if (select( "แลกของรางวัล","แลก EXP" ) == 1) {
							cutin "gelca03",2;
							mes "[Gelkah]";
							mes "คุณสามารถเลือกถ้วยรางวัลในกระเป๋าใบนี้ โปรดอย่าผิดหวังแม้ว่าคุณจะเลือกสิ่งที่คุณไม่ต้องการ คุณต้องการแลกเปลี่ยนกี่คน?";
							next;
							.@amount = select( "Exchange Courtesy Ticket ^FF00001^000000", "Exchange Courtesy Ticket ^FF000010^000000" ) == 1 ? 1 : 10;
							mes "[Gelkah]";
							mes "กรุณาให้บัตรและใส่มือของคุณในกระเป๋าเพื่อเลือก.";
							next;
							if (countitem(6549) < .@amount) {// Courtesy_Ticket
								mes "[Gelkah]";
								mes "^FF0000Courtesy Ticket^000000 ไม่เพียงพอ.";
							}
							else {
								mes "การเรียกดู";
								next;
								.@ran = rand(1,1000);
								if (.@amount == 1) {
									if (.@ran <= 100)
										setarray .@item[0],505,2;// Blue_Potion
									else if (.@ran <= 200)
										setarray .@item[0],547,10;// White_Slim_Potion
									else if (.@ran == 333)
										setarray .@item[0],603,1;// Old_Blue_Box
									else if (.@ran == 777)
										setarray .@item[0],617,1;// Old_Violet_Box
									else
										setarray .@item[0],547,5;// White_Slim_Potion
								}
								else {
									if (.@ran <= 100)
										setarray .@item[0],608,1;	// Seed_Of_Yggdrasil
									else if (.@ran <= 200)
										setarray .@item[0],547,100;	// White_Slim_Potion
									else if (.@ran >= 300 && .@ran <= 399)
										setarray .@item[0],603,1;	// Old_Blue_Box
									else if (.@ran >= 600 && .@ran <= 650)
										setarray .@item[0],616,1;	// Old_Card_Album
									else if (.@ran >= 700 && .@ran <= 800)
										setarray .@item[0],617,1;	// Old_Violet_Box
									else
										setarray .@item[0],547,60;	// White_Slim_Potion
								}
								delitem 6549,.@amount;// Courtesy_Ticket
								getitem .@item[0], .@item[1];
								mes "คุณล้วงมือเข้าไปในกระเป๋าและคว้า ^0000FF"+ .@item[1] +" "+ getitemname(.@item[0]) +"^000000.";
								next;
								if (.@amount == 10)
									cutin "gelca03",2;
								mes "[Gelkah]";
								mes "ใช้ประโยชน์จากมัน.";
							}
						}
						else {
							mes "[Gelkah]";
							mes "โอเคฉันมีเรื่องให้คุณแลกกับบัตรใจดี.";
							next;
							switch( select( "มอบ ^FF000010^000000 Courtesy Ticket", "มอบ ^FF000020^000000 Courtesy Ticket", "มอบ ^FF000030^000000 Courtesy Ticket pass" ) ) {
								mes "[Gelkah]";
							case 1:
								if (countitem(6549) < 10)// Courtesy_Ticket
									mes "คุณมีน้ำใจไม่เพียงพอ.";
								else {
									.@story = rand(1,3);
									if (.@story == 1) {
										cutin "gelca02",2;
										mes "คุณเคยเห็นเรือจมใกล้อัลเบอร์ตาหรือไม่?";
										next;
										mes "Master Rohter และฉันเดินทางลึกเข้าไปในเรือและต่อสู้กับสัตว์ประหลาดที่เรียกว่า ^FF0000Drake ^000000 วันนั้นฉันตั้งใจแน่วแน่ว่าจะเป็นนักผจญภัยที่ดีที่สุด.";
										.@exp = 200000;
										.@jexp = 148000;
									}
									else if (.@story == 2) {
										cutin "gelca03",2;
										mes "ครั้งหนึ่งเจ้านายและฉันไปที่ถ้ำทางตะวันตกของ Comodo และเมื่อเราเห็นสัตว์ประหลาด Medusa ... ";
										next;
										mes "โอ้เด็กชายใบหน้าของเขาดูล้ำค่า!";
										emotion ET_BIGTHROB;
										emotion ET_THINK, playerattached();
										.@exp = 180000;
										.@jexp = 168000;
									}
									else {
										mes "ครั้งหนึ่งฉันหลงทางในเขาวงกตทางตอนเหนือของพรอนเทรา ฉันคิดว่าฉันจะอยู่ในแวดวงตลอดไปจนกระทั่งในที่สุดฉันก็ทำมันออกมา.";
										next;
										mes "มีข่าวลือว่าถ้าคุณหลงทางในเขาวงกตนั้นคุณจะกลายเป็นสัตว์ประหลาดตัวใดตัวหนึ่งที่นั่น.";
										.@exp = 220000;
										.@jexp = 128000;
									}
									if (JobLevel < 6)
										.@jexp = 106000;
									getexp .@exp,.@jexp;
									delitem 6549,10;
									para_100lv02 += 1;
								}
								break;
							case 2:
								if (countitem(6549) < 20)// Courtesy_Ticket
									mes "คุณมีน้ำใจไม่เพียงพอ.";
								else {
									.@story = rand(1,3);
									if (.@story == 1) {
										cutin "gelca04",2;
										mes "เมื่อฉันพบเส้นทางลับเพื่อไปยัง Jawaii ... สำหรับคู่รักเท่านั้น แต่พวกเราบางคนก็รู้วิธีไปที่นั่น";
										next;
										mes "ไม่มีประโยชน์ที่จะอยู่ที่นั่นโดยไม่มีคู่ แต่ก็ดี.";
										emotion ET_THROB;
										.@exp = 550000;
										.@jexp = 340000;
									}
									else if (.@story == 2) {
										cutin "gelca03",2;
										mes "ครั้งหนึ่งอาจารย์เป็นส่วนหนึ่งของกิลด์ที่เรียกว่า wing something ... Chicken wing...?";
										next;
										mes "มันเป็นกิลด์ที่สนุกเพราะไม่มีใครจริงจังกับเรื่องนี้ แต่มาสเตอร์เบื่อกับความขี้เกียจของพวกเขาเขาจึงออกไป.";
										.@exp = 400000;
										.@jexp = 240000;
									}
									else {
										mes "คุณเคยไปถึง ^FF0000endless tower ^000000 แล้วหรือยัง? อาจารย์กับฉันไปที่นั่น แต่ไปได้แค่ชั้น 20 เพราะเข่าของเขายื่นออกมาขณะปีนเขา...";
										next;
										mes "^FF0000 หอคอยไร้ที่สิ้นสุด ^000000 อยู่ที่ไหนสักแห่งในทะเล คุณต้องหาผู้ชายชื่อ Jansor หรือ Jans-something ใน Alberta แล้วเขาจะพาคุณไปที่นั่น.";
										.@exp = 550000;
										.@jexp = 290000;
									}
									if (JobLevel < 6)
										.@jexp = 206000;
									getexp .@exp, .@jexp;
									delitem 6549,20;
									para_100lv02 += 1;
								}
								break;
							case 3:
								if (countitem(6549) < 30)// Courtesy_Ticket
									mes "คุณมีน้ำใจไม่เพียงพอ.";
								else {
									.@story = rand(1,2);
									if (.@story == 1) {
										cutin "gelca04",2;
										mes "กิลด์ของอาจารย์ถูกไล่ที่ Ash Vacuum เนื่องจากหัวหน้ากิลด์ของพวกเขาหายตัวไปหลังจากการต่อสู้กับมอนสเตอร์ Cornus บางตัว.";
										next;
										mes "[Gelkah]";
										mes "ตั้งแต่นั้นมาเขาก็เริ่มมีความหมายกับนักผจญภัยที่อ่อนแอกว่า...";
										next;
										mes "[Gelkah]";
										mes "ฉันไม่คิดว่าเขาจะได้เจอกิลด์มาสเตอร์คนนั้นอีกเลยหลังจากวันนั้น.";
										getexp 650000,488000;
									}
									else {
										cutin "gelca03",2;
										mes "[Gelkah]";
										mes "อาจารย์เริ่มจากการเป็นพระสนับสนุนยอดนิยมที่อาราม Prontera.";
										next;
										mes "[Gelkah]";
										mes "แต่เขายังถูกมองว่าเป็นตัวก่อปัญหาเนื่องจากลักษณะการแข่งขันของเขาเขาจึงจากไป.";
										getexp 700000,430000;
									}
									delitem 6549,30;
									para_100lv02 += 1;
								}
								break;
							}
						}
					}
				}
				else {
					mes "[Gelkah]";
					mes "โปรดกลับมาหาฉันหากคุณได้รับความกรุณามากกว่านี้!";
				}
			}
		}
	}
	else {
		cutin "gelca03",2;
		if (para_100lv02 > 100)
			mes strcharinfo (0) + "! คุณทำได้ดีมาก! มีคำขอมากกว่านี้ แต่ขอสงวนไว้สำหรับนักผจญภัยคนอื่น ๆ.";
		if (para_100lv02 < 101)
			mes "ฉันไม่คิดว่าคุณเหมาะสมกับคำขอที่ฉันให้ได้ โปรดค้นหาการผจญภัยอื่น ๆ.";
		callsub S_Hunting,13003,106000,148000,"Carat";// <quest id>, <job exp if joblevel < 6>, <job exp>, <quest name>
		callsub S_Hunting,13005,106000,148000,"Arclouse";
		callsub S_Hunting,13007,112000,186500,"Anolian";
		callsub S_Hunting,13009,112000,186500,"Sting";
		callsub S_Hunting,13011,112000,252000,"Majoruros";
		callsub S_Hunting,13013,106000,148000,"Pinguicula";
		callsub S_Hunting,13015,112000,186500,"Luciola Vespa";
		callsub S_Hunting,13017,106000,148000,"Desert Wolf";
		callsub S_Hunting,13019,106000,148000,"Snowier";
		callsub S_Hunting,13021,112000,186500,"Ice Titan";
		callsub S_Hunting,13023,106000,148000,"Nightmare Terror";
		callsub S_Hunting,13025,106000,148000,"Flying Deleter";
		callsub S_Hunting,13040,112000,186500,"Loli Ruri";
		callsub S_Hunting,13042,106000,148000,"Medusa";
		callsub S_Hunting,13044,112000,186500,"Anubis";
		setarray .@quest_id[0],13004,13006,13008,130010,13012,13014,13016,13018,13020,13022,13024,13026,13041,13043,13045;
		for ( .@i = 0; .@i < 15; .@i++ ) {
			if (checkquest(.@quest_id[.@i],PLAYTIME) == 2) {
				erasequest .@quest_id[.@i];
				break;
			}
		}
	}
	close2;
	cutin "",255;
	end;
S_Quest:
	if (getarg(2,0)) {
		getitem 6549,1;// Courtesy_Ticket
		para_100lv02 += 1;
		if (JobLevel < 6)
			getexp 550000,getarg(1);
		else
			getexp 550000,getarg(2);
	}
	else {
		next;
		cutin "gelca01",2;
		if (select( "ยอมรับคำขอ.","ยกเลิก." ) == 2)
			return 0;
		mes "[Gelkah]";
		mes "โปรดแจ้งให้ฉันทราบ เมื่อคุณเคลียร์คำขอแล้ว.";
		if (getarg(0) == 13012) {
			next;
			mes "[Gelkah]";
			mes "การเดินทางไปยังชั้นล่างสุดของ Glastheim เป็นการเดินทางระยะไกล แต่อาจสนุกถ้าคุณไปกับปาร์ตี้.";
		}
	}
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	return 1;

S_Hunting:
	switch( checkquest(getarg(0),HUNTING) ) {
	case -1:
		return;
	case 2:
		next;
		mes "[Gelkah]";
		mes "คุณดำเนินการตามคำขอแล้ว";
		mes "^FF0000 "+ getarg (3) +" ล่าสัตว์ ^000000.";
		mes "ฉันจะทำเครื่องหมายว่าคุณได้ดำเนินการตามคำขอนี้แล้ว.";
		para_100lv02 += 1;
		if (JobLevel < 6)
			getexp 550000,getarg(1);
		else
			getexp 550000,getarg(2);
		break;
	case 0:
	case 1:// untranslated these cases
	}
	erasequest getarg(0);
	close2;
	cutin "",255;
	end;
}

moc_para01,41,95,5	script	Rohtert#12	4_M_ROTERT,{
	mes "[Rohtert]";
	if (!callfunc("F_HasEdenGroupMark")) {
		cutin "rote01",2;
		mes "คุณไม่ได้เป็นสมาชิกของ Eden Group ด้วยซ้ำ คุณมาทำอะไรที่นี่?";
		mes "ฉันกำลังจัดการกับนักผจญภัยระดับพื้นฐานเท่านั้น ^FF0000100 - 110 ^000000.";
	}
	else {
		cutin "rote03",2;
		mes "......";
		next;
		mes "[Rohtert]";
		if (BaseLevel < 100)
			mes "มือใหม่ ... ไปฝึกฝนตัวเองให้มากขึ้น อะไร? ไม่ชอบวิธีที่ฉันพูด? แล้วแข็งแรงขึ้น.";
		else if (BaseLevel > 110) {
			mes "คุณเป็นผู้ใหญ่เกินไปของนักผจญภัย คุณไม่มีธุระกับฉัน เพียงแค่ปล่อยให้ ธุรกิจของฉันอยู่กับนักผจญภัยระดับสามคนใหม่จนถึงระดับ 110.";
			if (checkquest(13047,PLAYTIME) == 2)
				erasequest 13047;
			if (checkquest(13049,PLAYTIME) == 2)
				erasequest 13049;
			if (checkquest(13048,HUNTING) > -1)
				erasequest 13048;
			if (checkquest(13046,HUNTING) > -1)
				erasequest 13046;
		}
		else if (para_100lv01 < 2) {
			mes "ใช่ waddya ต้องการจากฉันเด็ก? คุณคิดว่าฉันจะบอกว่าตกลงฉันจะเป็นนายของคุณและสอนคุณทุกอย่างที่ฉันรู้?";
			next;
			mes "[Rohtert]";
			mes "ฉันไม่ใช่องค์กรการกุศลเพื่อให้หลงทาง.";
		}
		else if (para_100lv01 == 2) {
			mes "คุณเป็นนักผจญภัยคนใหม่?";
			next;
			mes "[Rohtert]";
			mes "มีหลายสิ่งที่ต้องทำและคุณกำลังเสียเวลากับฉัน คุยกับ Gelkah เธอช่างพูดมากกว่าที่ฉันเคยเป็นกับคุณ.";
			next;
			mes "[Rohtert]";
			mes "คุณดูกล้าหาญพอที่จะเป็นนักผจญภัยที่ดี แต่คุณไม่มีอะไรนอกจากสามเณรในสายตาของฉัน.";
			para_100lv01 = 3;
		}
		else if (para_100lv01 == 3) {
			mes "ฉันเคยเป็นนักผจญภัยของ Eden Group เหมือนกับคุณ แต่แน่นอนว่าฉันเก่งที่สุด.";
			next;
			mes "[Rohtert]";
			mes "ฉันเดาว่าตอนนี้ฉันเกษียณแล้วเพราะทักษะการผจญภัยของฉันดีเกินกว่าที่จะแข่งขันกับเด็ก ๆ ทุกคน.";
		}
		else if (para_100lv01 == 4) {
			mes "อย่างไรก็ตามฉันกำลังทำสิ่งนี้ให้กับ Eden Group.";
			next;
			mes "[Rohtert]";
			mes "ฉันกำลังมองหาคนที่นับถือมิตรภาพหรือความภักดีเช่น Spoon Stub.";
			next;
			if (select( "Spoon คืออะไร?", "คุณเป็นคนไม่ดี." ) == 1) {
				cutin "rote03",2;
				mes "[Rohtert]";
				mes "คุณรู้? ช้อน Stub Zombie Prisoners ทิ้งพวกเขาและดูเหมือนว่าพวกเขาจะมีประโยชน์ แต่จริงๆแล้วพวกมันก็ไร้ค่า.";
				next;
				mes "[Rohtert]";
				mes "อย่างไรก็ตามฉันเดาว่าคุณดูเหมือนเป็นคนที่ภักดี ......";
			}
			else {
				mes "[Rohtert]";
				mes "นั่นทำให้ฉันรู้สึกแปลก ๆ นั่นไม่เลว แต่ฉันคิดว่าฉันแย่และฉันไม่สนใจ";
			}
			next;
			cutin "rote04",2;
			mes "[Rohtert]";
			mes "อย่าลืมรักษาความกล้าหาญของนักผจญภัยโง่ ๆ.";
		}
		else if (para_100lv01 == 5) {
			cutin "rote03",2;
			mes "ตกลง ! คุณกำลังทำดีที่สุดเท่าที่จะทำได้.";
			next;
			mes "[Rohtert]";
			mes "ฉันกลัวว่าคุณจะกลายเป็นเหมือน Gelkah เธอเหมือนลูกหมาหลงทาง อย่าเป็นเหมือนเธอเลย ตกลง?";
		}
		else if (para_100lv01 == 6) {
			cutin "rote03",2;
			mes "คุณไม่ว่างมองไปรอบ ๆ.";
			next;
			mes "[Rohtert]";
			if (checkquest(13046) == 2) {
				if (checkquest(13047,PLAYTIME) == 0) {
					mes "ฉันทำธุรกิจของฉันเสร็จแล้ว";
					mes "ฉันมีหน้าที่เคลียร์เอกสาร ฉันเกลียดการทำสิ่งนี้.";
					next;
					mes "[Rohtert]";
					mes "ฉันเดาว่าฉันควรคุยกับเขาอีกครั้ง ... พรุ่งนี้กลับมา!";
				}
				else if (checkquest(13047,PLAYTIME) == 2) {
					mes "โอ้นานแค่ไหนแล้ว?";
					erasequest 13047;// Tendrilion Request Timer
				}
				else if (checkquest(13049,PLAYTIME) == 0)
					mes "สีขาวเป็นกระดาษและสีดำเป็นตัวอักษร ฉันเกลียดเอกสารสาธารณะ แต่ฉันต้องทำเช่นนี้ฉันจึงไม่ว่างสักพัก.";
				else if (checkquest(13048,HUNTING) == 1)
					mes "ฉันรู้ว่าการตามล่า Tens Drillion ไม่ได้ยากเกินไปสำหรับคุณ";
				else if (checkquest(13048,HUNTING) == 2) {
					cutin "rote02",2;
					mes "ดี! คุณกำลังกลายเป็นนักผจญภัยที่ดี! ทำให้ดีที่สุด!";
					erasequest 13048;// Tendrilion Request
					setquest 13049;// Tendrilion Request Timer
					getexp 352000,253000;
				}
				else {
					cutin "rote02",2;
					mes "Tens Drillion ฉันคิดว่าเราควรสอนบทเรียนเพิ่มเติมเพื่อแสดงให้เห็นว่าใครเป็นเจ้านาย!";
					next;
					if (select( "ให้บทเรียนที่ดีแน่นอน "," ฉันคิดว่าครั้งเดียวก็เพียงพอแล้ว" ) == 2) {
						mes "[Rohtert]";
						mes "ยังมีนักผจญภัยอีกมากมายที่สามารถทำเช่นนี้ได้.";
					}
					else {
						cutin "rote04",2;
						mes "[Rohtert]";
						mes "ไม่ใช่เพราะฉันขี้ขลาด.";
						next;
						mes "[Rohtert]";
						mes "โชคดี. ตรวจสอบให้แน่ใจว่าคุณเสร็จสิ้นในครั้งนี้ ฮะฮะ!";
						if (checkquest(13049,PLAYTIME) == 2)
							erasequest 13049;
						setquest 13048;// Tendrilion Request
					}
				}
			}
			else if (checkquest(13046,HUNTING) == 1)
				mes "คุณยังมีสามเณร? หากคุณไม่สามารถจัดการสิ่งนี้ได้ด้วยตัวเองให้ถามเพื่อนของคุณเพราะ นี่ไม่ใช่สิ่งที่ง่ายที่สุดที่จะทำ.";
			else if (checkquest(13046,HUNTING) == 2) {
				mes "คุณกลับมาอย่างปลอดภัย!";
				mes "ฉันเดาว่าคุณไม่ได้เป็นคนขัดผิวอย่างที่ฉันคิด.";
				completequest 13046;// Tendrilion Request
				setquest 13047;// Tendrilion Request Timer
				getexp 352000,253000;
			}
			else {
				cutin "rote04",2;
				mes "คุณคิดจะทำธุระง่ายๆ?";
				next;
				if (select( "ไม่ฉันไม่รังเกียจ! "," ธุระง่ายๆมีไว้สำหรับเรื่องง่ายๆ." ) == 2) {
					mes "[Rohtert]";
					mes "คุณเป็นนักผจญภัยที่แปลกประหลาด...";
				}
				else {
					mes "[Rohtert]";
					mes "วันหนึ่งที่ลมแรงฉันกำลังเก็บดอกไม้ทางตะวันตกเฉียงใต้ของโพสต์กองกำลังพันธมิตรมิดการ์ดเพื่อตอบสนองคำขอของกลุ่มอีเดน.";
					next;
					mes "[Rohtert]";
					mes "จากนั้นก็ปรากฏตัวขึ้น.";
					next;
					select("อะไร?");
					cutin "rote02",2;
					mes "[Rohtert]";
					mes "มันคือ ^FF0000Tens Drillion. ^000000 ";
					next;
					cutin "gelca03",2;
					mes "[^FF0000Gelkah^000000]";
					mes "อ๊ะ! มันจริง ^FF0000Tendrilion ^000000 !! อาจารย์!";
					next;
					cutin "rote04",2;
					mes "[Rohtert]";
					mes "ใช่ครับมันฝรั่งโปทาห์!";
					emotion ET_HNG;
					next;
					cutin "gelca02",2;
					mes "[^FF0000Gelkah^000000]";
					mes "......";
					next;
					cutin "rote04",2;
					mes "[Rohtert]";
					mes "เราตระหนักเมื่อเราเผชิญหน้ากับดวงตาของสัตว์ร้ายว่ามันดูเหมือนสิงโต แต่มันไม่ใช่สัตว์ธรรมดา.";
					emotion ET_HNG;
					next;
					mes "[Rohtert]";
					mes "^8008F8 ฉันรู้ว่ามันถูกฆ่าหรือถูกฆ่า ^000000";
					next;
					mes "[Rohtert]";
					mes "แน่นอนว่าฉันเอาชนะสัตว์ร้ายตัวนั้นได้อย่างง่ายดาย! มันง่ายกว่าการเอาขนมจาก Poring.";
					next;
					mes "[Rohtert]";
					mes "แต่ที่แปลกใจคือสัตว์ร้ายกลับขึ้นมา ฉันถูกบังคับให้เอาชนะมันอีก 12 ครั้งก่อนที่มันจะหยุดนิ่ง นี่ไม่ใช่ Tens Drillion ธรรมดา.";
					next;
					mes "[Rohtert]";
					mes "ฉันได้ยินมาว่ามันยังคงเป็นสถานที่ที่น่าไปพักผ่อน แต่ตอนนี้ฉันค่อนข้างยุ่ง.";
					next;
					mes "[Rohtert]";
					mes "ฉันต้องการให้คุณดูแลมัน สำหรับฉันมันเป็นเรื่องปกติที่จะอยู่ที่นั่นคนเดียว แต่สำหรับคุณ .. ฉันแนะนำให้คุณนำนักผจญภัยมามากกว่านี้.";
					next;
					select("คุณแน่ใจว่าคุณทำสิ่งนี้เอง?");
					mes "[Rohtert]";
					mes "การที่ฉันเข้มแข็งสำคัญกว่าจำนวน เพียงแค่ไปที่นั่นและนำสัตว์ร้ายออกมา.";
					next;
					mes "[Rohtert]";
					mes "คุณจะพบทางตะวันตกของ Midgard Allied Forces Post ที่ Ash Vacuum.";
					next;
					select("ฉันเดาว่าฉันไม่มีทางเลือก.");
					cutin "rote03",2;
					mes "[Rohtert]";
					mes "คุณยังมีสามเณร? หากคุณไม่สามารถจัดการได้ด้วยตัวเองให้ถามเพื่อนของคุณเพราะมันไม่ใช่เรื่องง่าย.";
					next;
					cutin "gelca02",2;
					mes "[^FF0000Gelkah^000000]";
					mes "....... "+ strcharinfo (0) +" รอ...";
					next;
					cutin "gelca01",2;
					mes "[^FF0000Gelkah^000000]";
					mes "ถ้าคุณพบว่ามันไม่ต้องกลัว อาจารย์ไม่ต้องต่อสู้กับ Tendrilion 12 ครั้ง";
					next;
					cutin "gelca03",2;
					mes "[^FF0000Gelkah^000000]";
					mes "ในความเป็นจริงอาจารย์เมามากจนแทบจำเรื่องนี้ไม่ได้.";
					next;
					cutin "gelca04",2;
					mes "[^FF0000Gelkah^000000]";
					mes "ฉันแค่ทิ้งมันไว้คนเดียวเพราะมันน่ารักนิดหน่อยที่มาสเตอร์ต่อสู้กับ Tendrilion หนึ่งตัว ตอนนี้กลายเป็นปัญหา.";
					next;
					cutin "gelca01",2;
					mes "[^FF0000Gelkah^000000]";
					mes "โปรดกลับมาหลังจากเคลียร์ Tendrilion หนึ่งครั้ง โปรดเก็บเรื่องนี้ไว้เป็นความลับจากอาจารย์.";
					setquest 13046;// Tendrilion Request
				}
			}
		}
	}
	close2;
	cutin "",255;
	end;
}
