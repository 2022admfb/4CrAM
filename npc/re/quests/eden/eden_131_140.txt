//===== rAthena Script =======================================
//= Eden Group Quests - Mission [131-140]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 131 - 140.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,41,76,3	script	Ragi#1	4_M_RAGI,{
	cutin "ragi01",0;
	mes "[Ragi]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "ยินดีต้อนรับเฉพาะนักผจญภัยจาก Eden Group ที่นี่.";
		mes "ฉันกำลังร้องขอไปยังระดับพื้นฐาน ^FF0000130-140 ^000000 นักผจญภัยเท่านั้น";
	}
	else if (para_130lv01 == 0) {
		mes "สวัสดี. ฉันเป็นสมาชิกใหม่ของวง Dandelion Ragi ฉันกำลังทำเพลงในนามของไอจี คุณสนใจเรื่องราว?";
		next;
		mes "[Ragi]";
		mes "ฮ่า ๆ . ฉันขอโทษที่ร้องขอต่อนักผจญภัยแม้ว่าฉันจะเป็นมือใหม่ก็ตาม.";
		next;
		mes "[Ragi]";
		mes "เพียงแค่ดูที่รายการ คุณสามารถช่วยฉันได้ไหมถ้ามันโอเคกับคุณ?";
		para_130lv01 = 1;
	}
	else {
		mes "ฉัน กำลังรวบรวมเรื่องราว... เพื่อทำเพลงในนามของ ไอจี ";
		next;
		if (select( "ขอรวบรวมเรื่องราว ","สนทนา" ) == 1) {
			mes "[Ragi]";
			if (BaseLevel < 131) {
				cutin "ragi02",0;
				mes "นี่คือคำขอของไอจี แต่มันอันตราย คุณควรโตขึ้นมากกว่านี้";
			}
			else if (BaseLevel < 141) {
				mes "เพลงและเรื่องราวนี้เกี่ยวกับสัตว์ประหลาด ^FF0000 บอกฉันว่าคุณรู้สึกอย่างไร ในขณะที่คุณต่อสู้กับพวกเขา^000000";
				next;
				switch( select( "เรื่องเล่าการสูญเสีย", "เรื่องเล่าเกี่ยวกับปีศาจ", "เรื่องเล่าจากคนไกล" ) ) {
					mes "[Ragi]";
				case 1:
					mes "Lighthalzen มีเรื่องราวมากมายที่ฉันแน่ใจ ว่าเศร้าและสูญเสีย ไม่ควรลืมเรื่องราว ของพวกเขาดังนั้น โปรดบอกฉันว่าคุณสามารถทำอะไรได้บ้าง.";
					next;
					switch( select( "Egnigem Cenia (Swordman Monster)",
									"Armeyer Dinze (Merchant Monster)",
									"Wickebine Tres (Thief Monster)",
									"Kavach Icarus (Archer Monster)",
									"Errende Ebecee (Acolyte Monster)",
									"Laurell Weinder (Mage Monster)" ) ) {
						mes "[Ragi]";
					case 1:
						if (checkquest(13144,PLAYTIME) == 0)
							mes "นักผจญภัยบางคนเล่าเรื่องราวให้ฉันฟังเล็กน้อย แต่พวกเขาไม่เพียงพอที่จะทำเพลงออกมา";
						else if (checkquest(13143,HUNTING) == 1) {
							cutin "ภฬฑืดฯมชผผดฯพฦฤซตๅ",3;  //ygnizem_card
							mes "เธอเป็นนักดาบที่น่าเศร้าในสถานที่ห่างไกลเช่นนี้ คุณคิดว่าอะไรมาที่นี่?";
							next;
							mes "[Ragi]";
							mes "สิ่งเหล่านี้เป็นเรื่องราวที่สำคัญมากสำหรับใครบางคน ฉันอยากทำเพลง ที่มีเรื่องราวเหล่านี้ ช่วยผมหน่อยครับ";
							cutin "ragi01",0;
						}
						else if (checkquest(13143,HUNTING) == 2) {
							mes "คุณเคยได้ยินเรื่องราวใหม่ ๆ หรือไม่? โปรดเล่าเรื่องราวให้ฉันฟังหากคุณมีเรื่องราวใด ๆ จากการเดินทางของคุณ.";
							callsub S_Quest,13143,13144,1100000,300000;
						}
						else {
							cutin "ภฬฑืดฯมชผผดฯพฦฤซตๅ",3;  //ygnizem_card
							mes "ผู้หญิงดาบที่แข็งแกร่ งกว่านักดาบใด ๆ Egnigem Cenia นั่นคือชื่อของเธอ ฉันไม่แน่ใจว่าเป็นเธอหรือคนอื่น";
							next;
							mes "[Ragi]";
							mes "ฉันไม่สามารถสูญเสียเรื่องราวของเธอ ฉันขอให้คุณหยุดด้วยจริงๆ ";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "มันจะอันตรายกว่าที่คุณคิด แต่เรื่องราวจะต้องถูกถ่ายทอดสำหรับทุกคน";
								callsub S_Quest,13144,13143;
							}
						}
						break;
					case 2:
						if (checkquest(13146,PLAYTIME) == 0)
							mes "นักผจญภัยบางคนเล่าเรื่องราวให้ฉันฟังเล็กน้อย แต่ก็ไม่เพียงพอที่จะทำเพลงออกมาได้";
						else if (checkquest(13145,HUNTING) == 1) {
							cutin "พฦธฃธถภฬพ๎ต๒มฆฤซตๅ",3;  //armaia_card
							mes "เธอจะเดินทางต่อไปได้ไกลแค่ไหน? ฉันเดาว่าเธอไม่สามารถทำได้คนเดียว.";
							next;
							mes "[Ragi]";
							mes "คุณไม่สงสัยว่าทำไมการเดินทางนั้นควรหยุดลง?";
							next;
							mes "[Ragi]";
							mes "ฉันอยากทำเพลงที่มีเรื่องราวเหล่านี้ โปรดช่วยฉันด้วย.";
							cutin "ragi01",0;
						}
						else if (checkquest(13145,HUNTING) == 2) {
							mes "คุณเคยได้ยินเรื่องราวใหม่ ๆ หรือไม่? โปรดเล่าเรื่องราวให้ฉันฟังหากคุณมีเรื่องราวจากการเดินทางของคุณ";
							callsub S_Quest,13145,13146,1100000,300000;
						}
						else {
							cutin "พฦธฃธถภฬพ๎ต๒มฆฤซตๅ",3;  //armaia_card
							mes "ลูกสาวของพ่อค้า Armeyer Dinze เธอดูเหมือนผู้หญิงที่ร่ำรวย แต่เธอดูไม่ปลอดภัย.";
							next;
							mes "[Ragi]";
							mes "อะไรทำให้เธอหายไป? หรือเพื่ออะไร โปรดฟังเรื่องราวของเธอ";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "มันจะอันตรายกว่าที่คุณคิด แต่เรื่องราวต้องถูกถ่ายทอด";
								callsub S_Quest,13146,13145;
							}
						}
						break;
					case 3:
						if (checkquest(13148,PLAYTIME) == 0)
							mes "นักผจญภัยบางคนเล่าเรื่องราวให้ฉันฟังเล็กน้อย แต่พวกเขาไม่เพียงพอที่จะทำเพลงออกมา.";
						else if (checkquest(13147,HUNTING) == 1) {
							cutin "ศึฤษนูภฮฦฎธฎฝบฤซตๅ",3;  //whikebain_card
							mes "อะไรที่อาจทำให้เธอนึกถึง? เธอสนใจ แต่เรื่องเงิน.";
							next;
							mes "[Ragi]";
							mes "ฉันสงสัยว่าหัวใจของเธอไปถึงไหนแล้ว? โปรดฟังเรื่องราวของเธอ โปรดบอกฉัน.";
							next;
							mes "[Ragi]";
							mes "โปรดทำเพลงให้พวกเรา.";
							cutin "ragi01",0;
						}
						else if (checkquest(13147,HUNTING) == 2) {
							mes "คุณเคยได้ยินเรื่องราวใหม่ ๆ หรือไม่โปรดเล่าเรื่องราวให้ฉันฟังหากคุณมีเรื่องราวจากการเดินทาง.";
							callsub S_Quest,13147,13148,1100000,300000;
						}
						else {
							cutin "ศึฤษนูภฮฦฎธฎฝบฤซตๅ",3;  //whikebain_card
							mes "ไม่มีใครรู้ว่ามันเริ่มต้นอย่างไร Wickebine Tres เป็นชื่อที่สามารถพบเพื่อนแท้ได้";
							next;
							mes "[Ragi]";
							mes "เธอเป็นคนที่เห็นแก่เงินเท่านั้น การเดินทางของเธอดำเนินต่อไปไกลแค่ไหน? โปรดฟังเรื่องราวของเธอใน Lighthalzen.";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "มันจะอันตรายกว่าที่คุณคิด แต่ต้องมีการถ่ายทอดเรื่องราว.";
								emotion ET_BEST;
								callsub S_Quest,13148,13147;
							}
						}
						break;
					case 4:
						if (checkquest(13150,PLAYTIME) == 0)
							mes "นักผจญภัยบางคนเล่าเรื่องราวให้ฉันฟังเล็กน้อย แต่พวกเขาไม่เพียงพอที่จะทำเพลงออกมา.";
						else if (checkquest(13149,HUNTING) == 1) {
							cutin "ฤซนูลฉภฬฤซท็ฝบฤซตๅ",3;  //kavac_card
							mes "ใช่. ดูเหมือนว่าฉันจะเคยได้ยินชื่อเดียวกันนี้จาก Payon ฉันไม่แน่ใจว่ามันหมายความว่าเดียวกัน";
							next;
							mes "[Ragi]";
							mes "โปรดลองหาเขาหรือธนูที่ Lighthalzen.";
							next;
							mes "[Ragi]";
							mes "โปรดอย่าลืมเล่าเรื่องที่คุณได้ยินให้ฉันฟัง ";
							cutin "ragi01",0;
						}
						else if (checkquest(13149,HUNTING) == 2) {
							mes "คุณเคยได้ยินเรื่องราวใหม่ ๆ หรือไม่? โปรดเล่าเรื่องราวให้ฉันฟังหากคุณมีเรื่องราวใด ๆ จากการเดินทางของคุณ.";
							callsub S_Quest,13149,13150,1100000,300000;
						}
						else {
							cutin "ฤซนูลฉภฬฤซท็ฝบฤซตๅ",3;  //kavac_card
							mes "Kavach Icarus เป็นชื่อที่มีมือที่เร็วกว่าตาและยิงธนูได้อย่างรวดเร็ว ";
							next;
							mes "[Ragi]";
							mes "ฉันได้ยินว่าเงาของเขาและลูกศรของเขากำลังหลงอยู่ที่ไหนสักแห่ง โปรดไปที่ Lighthalzen และเล่าเรื่องราวของเขาให้ฉันฟัง ";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "โปรดระวังลูกศรของเขา คุณอาจวางลงบนพื้นเย็นก่อนที่เรื่องนี้จะจบ";
								callsub S_Quest,13150,13149;
							}
						}
						break;
					case 5:
						if (checkquest(13152,PLAYTIME) == 0)
							mes "นักผจญภัยบางคนเล่าเรื่องราวให้ฉันฟังเล็กน้อย แต่พวกเขาไม่เพียงพอที่จะทำเพลงออกมา.";
						else if (checkquest(13151,HUNTING) == 1) {
							cutin "ภฬทปตๅฟกบฃฝรฤซตๅ",3;  //erend_card
							mes "ไม่ว่าจะเป็นเพื่อมิตรภาพหรือภารกิจก็ตาม หากการเดินทางนั้นเป็นของใครบางคนการหยุดการเดินทางนั้นคงไม่ใช่เรื่องง่าย";
							next;
							mes "[Ragi]";
							mes "อะไรทำให้การเดินทางของพวกเขาหยุดลง? มันอยู่ในเรื่องราวแบบไหน?";
							next;
							mes "[Ragi]";
							mes "โปรดตรวจสอบเรื่องราวนั้นใน Lighthalzen";
							cutin "ragi01",0;
						}
						else if (checkquest(13151,HUNTING) == 2) {
							mes "คุณเคยได้ยินเรื่องราวใหม่ ๆ หรือไม่? โปรดเล่าเรื่องราวให้ฉันฟังหากคุณมีเรื่องราวใด ๆ จากการเดินทางของคุณ";
							callsub S_Quest,13151,13152,1100000,300000;
						}
						else {
							cutin "ภฬทปตๅฟกบฃฝรฤซตๅ",3;  //erend_card
							mes "Errende Ebecee คือคนที่เริ่มต้นการเดินทางเพื่อตามหาใครสักคน ";
							next;
							mes "[Ragi]";
							mes "แต่ฉันเดาว่า ... การเดินทางของเขาอาจหยุดอยู่ที่ไหนสักแห่งใน Lighthalzen โปรดฟังเรื่องราวของเขา";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "โปรดระวังอย่าหลงทางเช่นนี้ ^FF0000 poor souls ^000000.";
								callsub S_Quest,13152,13151;
							}
						}
						break;
					case 6:
						if (checkquest(13154,PLAYTIME) == 0)
							mes "นักผจญภัยบางคนเล่าเรื่องราวให้ฉันฟังเล็กน้อย แต่พวกเขาไม่เพียงพอที่จะทำเพลงออกมา.";
						else if (checkquest(13153,HUNTING) == 1) {
							cutin "ถ๓ฟ์ทผบแด๕ฤซตๅ",3;  //rawrel_card
							mes "ยังไงซะ. ฉันได้อ่านหนังสือชื่อ ^FF0000 วิธีการเป็นพ่อมด ^000000..";
							next;
							mes "[Ragi]";
							mes "ตามหนังสือเล่มนั้นเวทมนตร์บางอย่างในวัย 25 ปีอาจแข็งแกร่งกว่าพ่อมดที่มีอายุมาก.";
							next;
							mes "[Ragi]";
							mes "คุณกำลังฟังอะไรอย่างจริงจัง? ขอจบเรื่องลอเรลล์เวนเดอร์ส! ";
							cutin "ragi01",0;
						}
						else if (checkquest(13153,HUNTING) == 2) {
							mes "คุณเคยได้ยินเรื่องราวใหม่ ๆ หรือไม่? โปรดเล่าเรื่องราวให้ฉันฟังหากคุณมีเรื่องราวใด ๆ จากการเดินทางของคุณ";
							callsub S_Quest,13153,13154,1100000,300000;
						}
						else {
							cutin "ถ๓ฟ์ทผบแด๕ฤซตๅ",3;  //rawrel_card
							mes "ลอเรลล์เวนเดอร์ผู้ตัดสินใจเดินทางอย่างยากลำบากเพื่อตอบแทนบุญคุณสำหรับการกระทำของพ่อมดบางคน.";
							next;
							mes "[Ragi]";
							mes "แต่ ... พ่อมดที่มีพรสวรรค์เหล่านี้กลับไม่ได้? ใครทำให้พวกเขา? ค้นหาเบาะแสใน Lighthalzen";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "ฉันชอบเรื่องนี้ แต่ระวังถ้าคุณไม่อยากจบเรื่องด้วยตัวเอง ";
								callsub S_Quest,13154,13153;
							}
						}
					}
					break;
				case 2:
					mes "คุณรู้เรื่องราวเกี่ยวกับซาตาน โมร็อกโกหรือไม่? ฉันอยากจะร้องเพลงเกี่ยวกับเรื่องราวของพวกเขาแม้เวลาจะผ่านไป";
					next;
					switch( select( "Angel Incarnation", "Black Nightmare Incarnation", "Ghost Incarnation" ) ) {
						mes "[Ragi]";
					case 1:
						if (checkquest(13156,PLAYTIME) == 0) {
							mes "สัตว์ประหลาดจากโมร็อคมีปีกเหมือนนางฟ้า บางทีซาตานโมร็อกโกอยากเป็นนางฟ้า?";
							next;
							mes "[Ragi]";
							mes "ฮ่าฮ่าอย่าถือสามันเป็นเพียงเรื่องตลกของผู้เล่าเรื่องโปรดให้ฉันเล่าเรื่องใหม่ในคราวหน้า";
						}
						else if (checkquest(13155,HUNTING) == 1) {
							mes " รูปลักษณ์ภายนอกดูดี แต่ภายใน .. ฉันแน่ใจว่าพวกเขามีเลือดเนื้อและเลือด!";
							next;
							mes "[Ragi]";
							mes " แน่นอนฉันไม่ได้ตรวจสอบด้วยตัวเอง แต่ฉันเป็นแค่คนเล่าเรื่องอย่างที่คุณเห็น";
							next;
							mes "[Ragi]";
							mes "แต่มันไม่ปลอดภัย .. ดังนั้นระวัง! ช่วยเล่าเรื่องเพิ่มเติมให้ฉันฟังหน่อย! บางทีฉันอาจจะเจอซาตานโมร็อคที่ไหนสักแห่ง?";
						}
						else if (checkquest(13155,HUNTING) == 2) {
							mes "ว้าว. มีเรื่องราวมากมายมาหาฉัน! ต้องขอบคุณคุณ!";
							callsub S_Quest,13155,13156,1100000,200000;
						}
						else {
							mes "อืม ... เพลงนี้ไม่เกี่ยวกับเจ้าชายแห่งความมืด แต่เอามาจากคนที่อ้างว่าเคยเห็นเขา";
							next;
							mes "[Ragi]";
							mes "โปรดช่วยฉันเล่าเรื่องโดยการค้นหาการบินของโมร็อกโก ";
							next;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "อย่าลืมมีทูตสวรรค์ที่กำลังมองหาโมร็อกโกอย่างน้อย 30 ตัว";
								emotion ET_KEK;
								callsub S_Quest,13156,13155;
							}
						}
						break;
					case 2:
						if (checkquest(13158,PLAYTIME) == 0) {
							mes "ยังไม่ได้สร้างเรื่องราวดังนั้นฉันจะถามในครั้งต่อไป!";
							cutin "ragi02",0;
						}
						else if (checkquest(13157,HUNTING) == 1) {
							mes "รูปร่างไม่สมบูรณ์ แต่มีฟันที่แหลมคมมาก";
							next;
							mes "[Ragi]";
							mes "คุณไม่คิดว่าจะเป็นเรื่องตลกมากเหรอ?";
						}
						else if (checkquest(13157,HUNTING) == 2) {
							mes "ว้าว. มีเรื่องราวมากมายมาหาฉัน! ต้องขอบคุณคุณ!";
							callsub S_Quest,13157,13158,1100000,200000;
						}
						else {
							mes " ฉันไม่รู้ว่าทุกอย่างมาจากซาตานโมรอคเองหรือเปล่า แต่สิ่งต่างๆเช่นนี้ดูน่ากลัว";
							next;
							mes "[Ragi]";
							mes "แต่ฉันคิดว่ามันจะมีเรื่องราวอยู่ในนั้นด้วย นั่นคือเหตุผลที่สิ่งมีชีวิตเหล่านั้นถูกสร้างขึ้น โปรดมองหาสิ่งที่เป็นสีดำและเหมือนน้ำมันดิน";
							next;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "ค้นหาและกำจัด 30 Dark Incarnation of Morroc";
								callsub S_Quest,13158,13157;
							}
						}
						break;
					case 3:
						if (checkquest(13160,PLAYTIME) == 0)
							mes "สิ่งมีชีวิตที่ไม่มีเสื้อผ้ามีรูปร่างประหลาดมาก ฮ่า ๆ อย่าไปสนใจมันเลย ฉันเป็นแค่คนเล่าเรื่อง";
						else if (checkquest(13159,HUNTING) == 1) {
							mes "เรื่องนี้ควรเผยแพร่ออกไปสำหรับนักผจญภัยที่อ่อนแอเช่นฉัน! เพราะไม่ใช่ทุกคนที่จะเป็นนักผจญภัยได้";
							next;
							mes "[Ragi]";
							mes "บางทีนักผจญภัยอาจจะมีบุญมากที่ได้รู้เรื่องราวแบบนี้ ดังนั้นโปรดมองหาสิ่งมีชีวิตที่อยู่ใกล้โมร็อกโก?";
						}
						else if (checkquest(13159,HUNTING) == 2) {
							mes "ว้าว. มีเรื่องราวมากมายมาหาฉัน! ต้องขอบคุณคุณ!";
							callsub S_Quest,13159,13160,1100000,200000;
						}
						else {
							mes "อันนี้แปลก .. แต่เขาก็เป็นร่างอวตารของมอร็อคด้วย";
							next;
							mes "[Ragi]";
							mes "แต่ฉันอยากรู้อยากเห็นเพราะมันดูเหมือนผีและการโจมตีไม่ได้ผล ";
							next;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "โปรดสร้างเรื่องราวหลังจากการล่า Ghost Incarnation of Morroc อย่างน้อย 30 ตัว! ";
								callsub S_Quest,13160,13159;
							}
						}
					}
					break;
				case 3:
					mes "ฉันไม่เคยไปที่ Ash Vacuum แต่ฉันได้ยินมาว่ามีสิ่งมีชีวิตที่น่าสนใจมากมายที่นั่นใน Scaraba Hall ";
					next;
					switch( select( "Uni-Horn Scaraba Story", "Horn Scaraba Story", "Antler Scaraba Story", "Rake Horn Scaraba Story" ) ) {
						mes "[Ragi]";
					case 1:
						if (checkquest(13162,PLAYTIME) == 0)
							mes "ฉันกำลังสร้างเรื่องราวเกี่ยวกับ Uni-Horn Scaraba หนึ่งหมายถึงน้อยกว่าสอง! ~ ฉันเริ่มต้น แต่มันยากที่จะจบ คุณสามารถช่วยฉันได้ไหม?";
						else if (checkquest(13161,HUNTING) == 1) {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "พวกเขายังมีเรื่องราวบางอย่างใช่มั้ย? อย่างแรกฉันสงสัยเกี่ยวกับรูปร่างของคนที่มีเขาข้างเดียว ";
							next;
							cutin "ragi01",0;
						}
						else if (checkquest(13161,HUNTING) == 2) {
							mes "ฉันสงสัยว่า Uni-Horn Scarabas คิดอย่างไร? ฉันอยากจะทำเพลง ขอบคุณสำหรับความช่วยเหลือของคุณ!";
							callsub S_Quest,13161,13162,1100000,200000;
						}
						else {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "โลกใหม่เต็มไปด้วยสัตว์ประหลาดที่น่าทึ่ง พวกเขาจะกลายเป็นความฝันสำหรับเด็ก ๆ ที่อยากเป็นนักผจญภัย ";
							next;
							mes "[Ragi]";
							mes "ฉันอยากทำเพลงเกี่ยวกับ ^FF0000Uni-Horn Scaraba ^000000 !? เด็ก ๆ จะชอบมาก ";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "คุณสามารถพบพวกมันได้ใกล้ที่ไหนสักแห่งทางตะวันออกที่ Scaraba Hall ";
								callsub S_Quest,13162,13161;
							}
						}
						break;
					case 2:
						if (checkquest(13164,PLAYTIME) == 0)
							mes "ฉันกำลังทำเรื่อง ... สองหมายถึงน้อยกว่าสาม ~ ฉันเริ่มก่อเรื่อง แต่ยังไม่จบ คุณจะช่วยฉันไหม ";
						else if (checkquest(13163,HUNTING) == 1) {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "สิ่งที่เป็นแตรรบกวนฉันเล็กน้อย ฮอร์นหนึ่งกับสองแตรต่างกันอย่างไร? แค่รูปลักษณ์? ";
							next;
							mes "[Ragi]";
							mes "บางทีมันอาจจะไม่จำเป็น แต่ความอยากรู้อยากเห็นเป็นสิ่งสำคัญสำหรับนักเล่าเรื่อง ฮ่า ๆ ๆ ๆ ";
							cutin "ragi01",0;
						}
						else if (checkquest(13163,HUNTING) == 2) {
							mes "คุณกลับมาอย่างปลอดภัย! เป็นยังไงบ้าง? Horn Scaraba ดูเหมือนจะเป็นเรื่องราวที่สนุกสนาน คุณทำได้ดีมากจริงๆ";
							callsub S_Quest,13163,13164,1100000,200000;
						}
						else {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "เราพูดถึง Horn Scaraba? ยังมีอีกหนึ่งที่เราควรกล่าวถึง .. นั่นคือ Horn Scaraba!";
							next;
							mes "[Ragi]";
							mes "ฉันไม่รู้ว่าทำไม ฉันเพิ่งได้ยินจากผู้คน ฉันต้องการทำเพลงดังนั้นโปรดมองดูโลกของพวกเขา! ";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "คุณสามารถพบพวกมันได้ใกล้ ๆ ที่ไหนสักแห่งทางตะวันออกที่ Scaraba Hall ";
								callsub S_Quest,13164,13163;
							}
						}
						break;
					case 3:
						if (checkquest(13166,PLAYTIME) == 0)
							mes "ดูเหมือนจะมีเรื่องราวเพิ่มเติมเกี่ยวกับ Antler Scaraba โปรดช่วยฉันด้วยถ้าคุณมีเวลา";
						else if (checkquest(13165,HUNTING) == 1) {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "ดูเหมือนว่าจะมี Scarabas พิเศษมากขึ้นตามข่าวลือ ตัวที่แข็งแกร่งและน่ากลัวกว่า ";
							next;
							mes "[Ragi]";
							mes "คุณคิดว่า Antler Scaraba อาจเป็นหนึ่งในนั้นหรือไม่ ";
							cutin "ragi01",0;
						}
						else if (checkquest(13165,HUNTING) == 2) {
							mes "ต้องเหนื่อยกับการเดินทางไกล! โปรดบอกฉันว่ามีเรื่องราวที่น่าสนใจมากกว่านี้! ";
							callsub S_Quest,13165,13166,1100000,200000;
						}
						else {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "อันนี้ไม่เหมือนใคร! เขามีลักษณะเหมือนกวางชนิดหนึ่งจึงเรียกว่าเขากวางสการาบา ";
							next;
							mes "[Ragi]";
							mes "คุณไม่สงสัยเหรอว่าทำไมมันถึงมีเขาแบบนั้น? ฉันคิดว่ามันมีเรื่องราวที่น่าสนใจที่ผู้คนจะชอบ ฉันขอถามนักผจญภัยได้ไหม?";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "คุณสามารถค้นหาเรื่องราวเกี่ยวกับ Antler Scaraba ได้หรือไม่? ประสบการณ์ตัวเองเป็นสิ่งที่ดีที่สุดฉันเดา!";
								callsub S_Quest,13166,13165;
							}
						}
						break;
					case 4:
						if (checkquest(13168,PLAYTIME) == 0)
							mes "ฉันรู้สึกว่า Rake Horn Scaraba มีเรื่องราวมากขึ้น กรุณาช่วยฉันถ้าคุณมีเวลา ";
						else if (checkquest(13167,HUNTING) == 1) {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "นี่แผนของฉัน ฉันกำลังทำ 4 ข้อความสำหรับ Scaraba แต่ละอัน ";
							next;
							mes "[Ragi]";
							mes "ใครฟังหรือร้องก็งงจะร้องเกรียน ฟังดูน่าสนุกใช่มั้ย ";
							cutin "ragi01",0;
						}
						else if (checkquest(13167,HUNTING) == 2) {
							mes "ขอบคุณ ! ฉันมั่นใจว่าจะสามารถทำเพลงที่ยอดเยี่ยมได้ ";
							callsub S_Quest,13167,13168,1100000,200000;
						}
						else {
							cutin "ฝบฤซถ๓นูฤซตๅ",3;  //scaraba_card
							mes "ฉันเริ่มสับสน ฉันจะทำเพลงเรื่อง Rake Horn Scarabas";
							next;
							mes "[Ragi]";
							mes "เพลงเริ่มต้นด้วย Uni-horn Scaraba จากนั้นร้องร่วมกับ Rake Horn Scaraba คุณจะช่วยฉันไหม ";
							next;
							cutin "ragi01",0;
							if (select( "ยอมรับภารกิจ", "ยกเลิก." ) == 1) {
								mes "[Ragi]";
								mes "โปรดค้นหาเรื่องราวเกี่ยวกับ Rake Horn Scaraba จาก Scaraba Hall เรื่องราวการต่อสู้หรือประสบการณ์ก็ไม่เป็นไร";
								callsub S_Quest,13168,13167;
							}
						}
						break;
					}
				}
			}
			else {
				mes "ฉันจะทำเรื่องหรือเพลงต่อไป แต่นักผจญภัยควรก้าวเข้าสู่การผจญภัยครั้งใหม่ ";
				next;
				callsub S_Erasequest,
					13143,1,"Egnigem Cenia",
					13145,1,"Armeyer Dinze",
					13147,1,"Wickebine Tres",
					13149,1,"Kavach Icarus",
					13151,1,"Errende Ebecee",
					13153,1,"Laurell Weinder",
					13155,2,"the Angel-looking Incarnation of Morocc",
					13157,2,"Dark Incarnation of Morocc",
					13159,2,"Ghost Incarnation of Morocc",
					13161,3,"Uni-Horn Scaraba",
					13163,3,"Horn Scaraba",
					13165,3,"Antler Scaraba",
					13167,3,"Rake Horn Scaraba";
				mes "[Ragi]";
				mes "ฉันหวังว่าจะได้พบคุณอีกครั้งเมื่อเราอำลาด้วยรอยยิ้ม ";
			}
		}
		else {
			mes "[Ragi]";
			mes "ฉันยุ่งอยู่กับการทำธุระของเซอร์ไอกู คุณมีคำถามหรือไม่ ";
			next;
			switch( select( "คุณไปทำผมที่ไหน ","ช่วยร้องเพลงให้ฉันหน่อย ","คุณมีเพื่อนผู้หญิงไหม ","เกี่ยวกับไอจี" ) ) {
				mes "[Ragi]";
			case 1:
				mes "มีช่างทำผมคนหนึ่งใน อัลเบอร์ตา เขาตัดผมด้วยกรรไกรทำอาหาร ดูผิดกฎหมาย ";
				next;
				mes "[Ragi]";
				mes "บางครั้งทรงผมหรือสีก็แปลก ... แต่โชคดีที่ไม่ตาย";
				break;
			case 2:
				mes "ฉันยังไม่ได้ทำเพลงในฐานะสมาชิกของวง สักวันฉันจะทำเพลงที่ยอดเยี่ยมให้คุณ!";
				break;
			case 3:
				cutin "ragi03",0;
				mes "......";
				next;
				cutin "ragi02",0;
				mes "[Ragi]";
				mes "ไอกูอยู่ในใจเท่านั้น ฉันไม่สามารถคิดอย่างอื่นได้ ฉันสบายดีแม้ว่ามันจะเป็นแค่รักข้างเดียวก็ตาม..";
				break;
			case 4:
				mes "ฉันจะไม่มีวันลืมวันแรกที่ได้พบเธอเธอจ้องมองฉันจากทุกคนในท่าเรือ Izlude ตอนนี้ฉันช่วยเธอด้วยการทำธุระเล็ก ๆ น้อย ๆ";
				next;
				mes "[Ragi]";
				mes "แต่อนิจจาฉันคิดว่าไอกูชอบคนอื่น ^FF0000 เราไม่เคยเจอ ... แต่ฉันเกลียดเขา ^000000 ";
				next;
				mes "[Ragi]";
				mes "แต่ฉันเกลียดตัวเองมากกว่าเพราะฉันอยากอยู่ใกล้ ๆ เธอแม้ว่าเธอจะไม่เคยเห็นฉันก็ตาม ฮ่าฮ่าฉันจริงจังเกินไปหรือเปล่า";
				next;
				mes "[Ragi]";
				mes " ฉันเป็นแค่นักเล่าเรื่อง .. เรื่องไหน ๆ ก็แต่งได้ส่วนใหญ่มันมี แต่ความจริง";
			}
		}
	}
	close2;
	cutin "",255;
	end;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp getarg(2),getarg(3);
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 3 ) {
		.@quest_id = getarg(.@i);
		switch( checkquest(.@quest_id,HUNTING) ) {
		case -1:
			if (checkquest(.@quest_id+1) > -1)
				erasequest (.@quest_id+1);
			continue;
		case 0:
		case 1:
			mes "[Ragi]";
			switch( getarg(.@i+1) ) {
			case 1:
				mes "เรื่องราวที่เหลือของ "+ getarg (.@i + 2) +" คือโอเคฉันจะถามคนอื่น ";
				break;
			case 2:
			case 3:
				mes "ฉันจะถามนักผจญภัยคนอื่น ๆ เกี่ยวกับเรื่องราวของ "+ getarg (.@i + 2) +"";
				break;
			}
			break;
		case 2:
			mes "[Ragi]";
			switch( getarg(.@i+1) ) {
			case 1:
				mes "คุณเคยได้ยินเรื่องราวของ "+ getarg (.@i + 2) +" หรือไม่? ขอบคุณ. ฉันสัญญาว่าจะทำเพลงที่ดี ";
				getexp 1100000,300000;
				break;
			case 2:
				mes "เรื่องราวของโมร็อกโกดีพอสำหรับเพลงหรือไม่? คุณทำได้ดีมาก ";
				getexp 1100000,200000;
				break;
			case 3:
				mes "การผจญภัยของคุณเป็นอย่างไร? มันสนุกไหม? ขอให้ฉันได้ยินเรื่องราวการผจญภัยของคุณสักวัน ";
				getexp 1100000,200000;
				break;
			}
		}
		erasequest .@quest_id;
		next;
	}
	return;
}
