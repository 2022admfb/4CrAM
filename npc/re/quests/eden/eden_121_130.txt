//===== rAthena Script =======================================
//= Eden Group Quests - Mission [121-130]
//===== By: ==================================================
//= Capuche
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Repeatable quests for players between BaseLevel 121 - 130.
//===== Additional Comments: =================================
//= 1.0 First version.
//= 1.1 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.2 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,81,3	script	Melody-Jack#1	4_M_MELODY,8,8,{
	mes "[Melody-Jack]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "คุณเป็นใคร? ออกไปจากที่นี่!";
		mes "ฉันคุยกับนักผจญภัยระดับพื้นฐานเท่านั้น ^FF0000121-130 ^000000.";
	}
	else if (para_120lv01 == 0)
		mes "อะไร? อย่ารบกวนฉัน. ตอนนี้ฉันไม่ว่าง.";
	else if (para_120lv01 == 1) {
		mes "คุณได้รับคำขอจากสาวน้อยคนนั้น? ตัวเล็กช่างพูดและเปลือยเปล่าอยู่ตรงนั้น?";
		next;
		select("ตอบว่าใช่ ","หลีกเลี่ยงการตอบ.");
		mes "[Melody-Jack]";
		mes "ไม่ฉันไม่สนใจมันจะดีถ้าคุณก้าวเข้ามาในห้องนี้ไม่ใช่เพราะคุณหลงทาง ฉันต้องการใครนอกจากคนโง่แบบนั้น";
		next;
		mes "[Melody-Jack]";
		mes "โปรดฟังฉันฉันจะเสนอข้อเสนอแนะที่น่าสนใจ.";
		next;
		mes "[Melody-Jack]";
		mes "ฉันคือ ^ff0000Melody - Jack! ^000000 ชายที่เก่งที่สุดที่มีทักษะกริชที่ดีที่สุด.";
		next;
		mes "Melody-Jack กวาดกริชไปในทิศทางของคุณ";
		next;
		mes "[Melody-Jack]";
		mes "ฉันมีคุณสมบัติที่จะทำภารกิจลับ และตอนนี้คุณโชคดี";
		next;
		mes "[Melody-Jack]";
		mes "ดูเหมือนว่าฉันจะต้องสูญเสียภารกิจข้อตกลงและสัญญาไปหนึ่งอย่างฉันไม่สามารถทำมันได้ในตอนนี้";
		next;
		mes "[Melody-Jack]";
		mes "รับภารกิจของฉันและแก้คำขอของไอกู ฉันจะชดเชยให้คุณ";
		next;
		mes "[Melody-Jack]";
		mes "ข้อตกลงของเราจะถูกเก็บไว้เป็นความลับในไอจี หากคุณไม่มี 9 ชีวิต เริ่มกันเลย.";
		para_120lv01 = 2;
	}
	else if (para_120lv01 > 1) {
		mes "......";
		next;
		if (select( "แสดงภารกิจ ! ","พูดคุยเล็ก ๆ น้อย ๆ." ) == 2) {
			mes "[Melody-Jack]";
			mes "อะไร? คุณสนใจฉัน?";
			next;
			switch( select( "ฉันสนใจ ","เกี่ยวกับคำขอ ","เกี่ยวกับข้อตกลง ","สัญญาคืออะไร?" ) ) {
				mes "[Melody-Jack]";
			case 1:
				mes "หายไปเลย ฉันไม่มีความสนใจในตัวคุณ.";
				close;
			case 2:
				mes "ฉันไม่สามารถหยุดมันได้เนื่องจาก เครดิตที่ไม่ดีของฉัน มันทำให้ฉันบ้า.";
				next;
				mes "[Melody-Jack]";
				mes "ฉันโชคดีที่ได้ทำสัญญาสองครั้ง กับนักผจญภัยเช่นคุณ พวกเขาไม่รวมอยู่ในสัญญาของเรา ";
				close;
			case 3:
				mes "พูดตามตรงฉันไม่คิดว่า จะมีใครที่น่าไว้วางใจ ความไว้วางใจหรือคำสัญญาเป็นเรื่องงี่เง่า ";
				next;
				mes "[Melody-Jack]";
				mes "ฉันเชื่อใจเพียงฉันและข้อตกลง ของฉัน ความไว้วางใจหรือ คำสัญญาจะว่างเปล่า หากพวกเขาพัง แต่หากข้อตกลงผิดพลาดจะมีการชดเชยหรือแก้แค้น ";
				close;
			case 4:
				mes "เด็กทั้งหมดนี้เป็นใคร?";
				next;
				mes "[Melody-Jack]";
				mes "ฉันออกมาเพื่ออะไร แต่ฉันควรจะทำตามหน้าที่ของฉัน ฉันไม่ได้เป็นหนี้ใคร ";
				close;
			}
		}
		mes "[Melody-Jack]";
		if (BaseLevel < 121)
			mes "โอ้ฉันทำพลาด คุณไม่ใช่นักผจญภัยระดับ ที่ฉันกำลังมองหา ฉันกำลังมองหานักผจญภัยระดับ 121 ถึง 130 พื้นฐาน.";
		else if (BaseLevel < 131) {
			mes "ฉันไม่ชอบ แต่ข้อตกลงคือข้อตกลง โปรดกลับมาหลังจากเคลียร์มอนสเตอร์ในสถานที่ที่ไอกูขอให้ค้นหา.";
			next;
			switch( select( "Thanatos Tower related ", "Rachel Shrine related ", "Nameless Island related ", "Abyss Lake related " ) ) {
				mes "[Melody-Jack]";
			case 1:
				mes "หอคอย Thanatos เป็นสถานที่ที่น่ารำคาญที่สุด ไม่ต้องกังวลเกี่ยวกับค่าตอบแทนของคุณ ฉันรักษาคำพูดของฉัน";
				next;
				switch( select( "Baroness of Retribution", "Lady Solace", "Mistress of Shelter", "Dame of Sentinel" ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13120,PLAYTIME) == 0)
						mes "ฉันคิดว่าฉันเสร็จสิ้นการแสดงของวันนี้แล้ว ฉันไม่สามารถผ่อนคลายได้หากสัตว์ประหลาดปรากฏตัวขึ้นอย่างกะทันหัน";
					else if (checkquest(13119,HUNTING) == 1) {
						cutin "ฝลน๚ภวด๋วเภฺฤซตๅ",3;  //retribution_card
						mes "ฉันไม่ชอบสิ่งมีชีวิตนั้นไม่ใช่เพราะไอกูไม่ชอบพวกมัน ... กล้าดียังไง? ใครกำลังลงโทษใคร ";
						next;
						mes "[Melody-Jack]";
						mes "นี่คือสิ่งมีชีวิตที่อันตรายที่สุดในโลก สิ่งมีชีวิตเหล่านี้ไม่สมควรได้รับความยุติธรรม ";
						next;
						cutin "",255;
						mes "[Melody-Jack]";
						mes "เชื่อใจได้แน่นอนค่ะ เพราะฉันเชื่อในข้อตกลงที่แท้จริง ให้กวาดสิ่งมีชีวิตออกไป";
					}
					else if (checkquest(13119,HUNTING) == 2) {
						mes "คุณล้างมันหรือยัง? เพื่อไม่ให้เด็กน้อยคนนั้นรำคาญสักวัน ทำได้ดีมาก";
						callsub S_Quest,13119,13120,1;
					}
					else {
						cutin "ฝลน๚ภวด๋วเภฺฤซตๅ",3;  //retribution_card
						mes "อย่างที่ทราบกันดีว่า Thanatos Tower เป็นที่ที่ไอจีใส่ใจมากที่สุด มันเป็นสถานที่ที่อันตรายที่สุดในบางแง่ ";
						next;
						mes "[Melody-Jack]";
						mes "กรุณาเคลียร์มอนสเตอร์เพื่อช่วยไอกู มือสมัครเล่นจะทำให้เรื่องยุ่ง";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ต้องมีจำนวนมาก อย่าลืมเก็บเรื่องนี้เป็นความลับ ";
							callsub S_Quest,13120,13119;
						}
					}
					close;
				case 2:
					if (checkquest(13122,PLAYTIME) == 0)
						mes "ฉันคิดว่าฉันเสร็จสิ้นการแสดงของวันนี้แล้ว ฉันไม่สามารถผ่อนคลายได้ ... จะเป็นอย่างไรถ้าสัตว์ประหลาดปรากฏตัวขึ้นมาทันที ";
					else if (checkquest(13121,HUNTING) == 1) {
						cutin "ภงทฮวฯดยภฺ",3;  //solace_card
						mes "อาคารดูเหมือนจะไม่ได้รับการออกแบบโดยมนุษย์และมีการเล่นทำนองแปลก ๆ อยู่เสมอ ";
						next;
						mes "[Melody-Jack]";
						mes "อย่าตกอยู่ในอารมณ์ของทำนองเพลงเพราะนั่นอาจเป็นเพลงสุดท้ายที่คุณได้ยิน ";
						next;
						cutin "",255;
						mes "[Melody-Jack]";
						mes "จำไว้ว่ามีดนตรีแปลก ๆ ที่คมเหมือนมีดที่จะฆ่าคนในโลก ";
					}
					else if (checkquest(13121,HUNTING) == 2) {
						mes "คุณเคยได้ยินเพลงที่พวกเขากำลังเล่นหรือไม่? คุณจะมีโอกาสถ้าเด็กน้อยคนนั้นเริ่มเล่นการแสดงอีกครั้ง ";
						callsub S_Quest,13121,13122,1;
					}
					else {
						cutin "ภงทฮวฯดยภฺ",3;  //solace_card
						mes "เมโลดี้ที่สวยงาม อาจฟังดูสวยงามเพราะสิ่งมีชีวิตกำลังเล่น";
						next;
						mes "[Melody-Jack]";
						mes "อย่างไรก็ตามนั่นเป็นอีกหนึ่งอุปสรรค ข้อตกลงเกิดขึ้นก่อนผลประโยชน์ส่วนตัวได้ทุกเมื่อ เลดี้ปลอบใจชัด ๆ ";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "เราเพียงแค่รักษาจุดสิ้นสุดของข้อตกลง ล้างสิ่งมีชีวิตเหล่านั้นเดี๋ยวนี้ ";
							callsub S_Quest,13122,13121;
						}
					}
					close;
				case 3:
					if (checkquest(13124,PLAYTIME) == 0)
						mes "ฉันคิดว่าฉันเสร็จสิ้นการแสดงของวันนี้แล้ว ฉันไม่สามารถผ่อนคลายได้ ... จะเป็นอย่างไรถ้าสัตว์ประหลาดปรากฏตัวขึ้นอย่างกะทันหัน?";
					else if (checkquest(13123,HUNTING) == 1) {
						cutin "พศฝฤภปมึดยภฺฤซตๅ",3;  //shelter_card
						mes "คุณรู้จักนางฟ้าที่กลายเป็นสัตว์ประหลาดหรือไม่? ไม่รู้ว่าเป็นนางฟ้าหรือเปล่า แต่รูปลักษณ์ของมันเป็นทูตสวรรค์";
						next;
						mes "[Melody-Jack]";
						mes "อย่าตื่นตระหนกเนื่องจากรูปลักษณ์ที่สวยงามไม่ว่าจะเป็นนางฟ้าหรือสิ่งมีชีวิตก็ตาม คุณควรทำงานของคุณ.";
						next;
						mes "[Melody-Jack]";
						mes "ทุกอย่างมีเวลา บางทีคุณอาจร้องขอสิ่งนี้เพราะเวลานั้นมาแล้ว.";
						cutin "",255;
					}
					else if (checkquest(13123,HUNTING) == 2) {
						mes "ดี. บอกให้ฉันรู้ว่ามีใครกำลังมองหาฉันอยู่ ถ้าเป็นเช่นนั้นฉันอาจต้องเปลี่ยนรายละเอียดของข้อตกลง ";
						callsub S_Quest,13123,13124,1;
					}
					else {
						cutin "พศฝฤภปมึดยภฺฤซตๅ",3;  //shelter_card
						mes "คุณรู้จักนางฟ้าที่กลายเป็นสัตว์ประหลาดหรือไม่? ไม่รู้ว่าเป็นนางฟ้าหรือเปล่า แต่รูปลักษณ์ของมันเป็นนางฟ้า";
						next;
						mes "[Melody-Jack]";
						mes "ฉันไม่สนใจหรอกว่ามันจะเป็นนางฟ้าจริงๆหรือแค่สิ่งมีชีวิต เคลียร์ Mistress of Shelter";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ดอกไม้ที่กำลังจะร่วงหล่นลงมาตามสายฝนและลม นั่นคือสิ่งที่คุณต้องทำ ";
							emotion ET_BEST;
							callsub S_Quest,13124,13123;
						}
					}
					close;
				case 4:
					if (checkquest(13126,PLAYTIME) == 0)
						mes "ฉันจบการแสดงสำหรับวันนี้แล้ว ฉันไม่สามารถผ่อนคลายได้ถ้าสัตว์ประหลาดปรากฏตัวขึ้นกะทันหัน ";
					else if (checkquest(13125,HUNTING) == 1) {
						cutin "ฐจฝรวฯดยภฺฤซตๅ",3;  //observation_card
						mes "คุณอาจรู้สึกว่าพวกเขากำลังเฝ้าดูเมื่อคุณก้าวเข้าไปในพื้นที่ชั้นบนของหอคอย Thanatos ";
						next;
						mes "[Melody-Jack]";
						mes "Dame of Sentinel ไม่ใช่เรื่องง่ายที่จะรับมือดังนั้นจงเตรียมพร้อมที่จะต่อสู้ ";
						next;
						mes "[Melody-Jack]";
						mes "เป็นวันตายดี จะเป็นคุณหรือพวกเขาก็ทำให้ดีที่สุด ";
						cutin "",255;
					}
					else if (checkquest(13125,HUNTING) == 2) {
						mes "คุณดูดี. บางทีคุณอาจจะดีกว่าที่ฉันคิด? ฉันไม่ได้ตรวจสอบด้วยตาของฉัน ";
						callsub S_Quest,13125,13126,1;
					}
					else {
						cutin "ฐจฝรวฯดยภฺฤซตๅ",3;  //observation_card
						mes "คุณจะพบพวกเขาหากไอกูร้องขอ Dame of Sentinels ";
						next;
						mes "[Melody-Jack]";
						mes "ฉันหวังว่าพวกเขาจะเคลียร์เช่นกันเพื่อให้เธอร้องขอการผจญภัยครั้งนี้ได้อย่างง่ายดาย ";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "เรื่องขอ ... อะไรก็ได้ ";
							callsub S_Quest,13126,13125;
						}
					}
					close;
				}
			case 2:
				mes "การผจญภัยของไอกูนำไปสู่ศาลเจ้าราเชล โอเค ... มันไม่แปลกถ้า ^FF0000 มนุษย์คนนั้น ^000000 ไอกูที่กำลังมองหาอยู่จะพบได้ทุกที่";
				next;
				switch( select( "Vanberk and Isilla", "Hodremlin", "Agav and Echio" ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13128,PLAYTIME) == 0)
						mes "คุณอาจใช้เวลาพักผ่อนสักวัน คุณเคลียร์พอแล้ว แต่ความคลั่งไคล้เพิ่มขึ้น ";
					else if (checkquest(13127,HUNTING) == 1) {
						cutin "ภฬฝวถ๓ฤซตๅ",3;  //isilla_card
						mes "คนที่เชื่อว่าตนถูกต้องอันตรายที่สุด โลกนี้เต็มไปด้วยคนโง่";
						next;
						mes "[Melody-Jack]";
						mes "สุดท้ายบังคับให้คนอื่นเชื่อในสิ่งที่พวกเขาเชื่อ ช่วยให้ Vanberk และ Isilla ชัดเจน ";
						next;
						mes "[Melody-Jack]";
						mes "ไม่ใช่ธุรกิจของฉัน แต่ครั้งนี้แตกต่างออกไป เด็กน้อยอาจตกอยู่ในอันตรายดังนั้นรีบไปกำจัดสัตว์ประหลาดเหล่านี้";
						cutin "",255;
					}
					else if (checkquest(13127,HUNTING) == 2) {
						mes "เพียงแค่วันเดียว จะเป็นไรไป";
						callsub S_Quest,13127,13128,1;
					}
					else {
						cutin "ฦวบฃธฃลฉฤซตๅ",3;  //vanberk_card
						mes "สถานที่ศักดิ์สิทธิ์ในราเชลห้ามคนปกติเข้า คุณต้องรู้ว่า";
						next;
						mes "[Melody-Jack]";
						mes "พวกเขาคิดว่าพวกเขาสามารถหาเบาะแสได้ที่นั่น แต่ไม่ใช่ที่ที่ปลอดภัย คุณควรเคลียร์ Vanberk และ Isilla ล่วงหน้า";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ล้างทั้งหมดโดยเร็วที่สุด.";
							emotion ET_SPARK;
							callsub S_Quest,13128,13127;
						}
					}
					close;
				case 2:
					if (checkquest(13130,PLAYTIME) == 0)
						mes "คนส่วนใหญ่ที่ไม่รู้จักเป็นอันตรายดังนั้นควรล้างพวกเขาก่อน ฉันจะแจ้งให้คุณทราบหากมีอะไรเกิดขึ้น";
					else if (checkquest(13129,HUNTING) == 1) {
						cutin "ศฃตๅทฝธฐฤซตๅ",3;  //hodremlin_card
						mes "ฟันเย็นอาจใกล้เข้ามา ฟันมันเร็วกว่ามือคุณ";
						next;
						mes "[Melody-Jack]";
						mes "สถานที่ศักดิ์สิทธิ์ของราเชลจะใกล้เข้ามาจากทุกที่ คุณควรระวังอันนี้";
						next;
						mes "[Melody-Jack]";
						mes "แค่คิดว่างานในมือ แค่นั้นก็เพียงพอแล้ว";
						cutin "",255;
					}
					else if (checkquest(13129,HUNTING) == 2) {
						mes "ฉันไม่กังวลขนาดนั้น แต่คุณปลอดภัยไหม ไม่โดนกัด?";
						callsub S_Quest,13129,13130,1;
					}
					else {
						cutin "ศฃตๅทฝธฐฤซตๅ",3;  //hodremlin_card
						mes "ความคลั่งไคล้เป็นปัญหา ปัญหาที่ร้ายแรงกว่าคือการปรากฏตัวขึ้นอย่างกะทันหัน";
						next;
						mes "[Melody-Jack]";
						mes "กำจัด ^FF0000Hodremlin ^000000 โดยไม่ลังเลใด ๆ เพราะมันอันตรายมาก.";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "พวกเขาต้องการมือที่เร็วกว่าศีรษะ ฉันไม่กังวล แต่คุณควรระวัง ";
							callsub S_Quest,13130,13129;
						}
					}
					close;
				case 3:
					if (checkquest(13132,PLAYTIME) == 0)
						mes "ถ้าคุณสามารถพูดคุย. ไม่มีคำขอในขณะนี้ พักผ่อนสักวัน.";
					else if (checkquest(13131,HUNTING) == 1) {
						cutin "ฟกลฐฟภฤซตๅ",3;  //echio_card
						mes "เต็มไปด้วยความบ้าคลั่งที่นี่ ฉันแค่รู้สึกขยะแขยง ";
						next;
						mes "[Melody-Jack]";
						mes "มันเป็นความรู้สึกของฉัน แต่คุณอาจจะชอบ ฉันแน่ใจว่า ^FF0000Agav ^000000 และ ^FF0000Echio ^000000 จะไม่ชอบคุณ ";
						next;
						mes "[Melody-Jack]";
						mes "พวกเขาจะวิ่งเข้ามาหาคุณ อย่าลังเลและจดจ่ออยู่กับงาน";
						cutin "",255;
					}
					else if (checkquest(13131,HUNTING) == 2) {
						mes "พบใคร? ฉันจะไม่กังวล สัญญาคือสัญญาและข้อตกลงคือข้อตกลง.";
						callsub S_Quest,13131,13132,1;
					}
					else {
						cutin "พฦฐกบ๊ฤซตๅ",3;  //agav_card
						mes "เป็นความจริงที่ว่ามันจะอันตรายมากขึ้นภายในสถานที่ศักดิ์สิทธิ์ของราเชล แต่ไอกูจะไม่สนใจสิ่งที่เราไม่แน่ใจ";
						next;
						mes "[Melody-Jack]";
						mes "นั่นก็ไม่ได้แย่ขนาดนั้น แต่ต้องมีคนมาช่วย? มาเคลียร์ ^FF0000 ^000000 และ ^FF0000 Echo ^000000 ภายใน Rachel Shrine.";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ฉันไม่สามารถจากลาได้";
							callsub S_Quest,13132,13131;
						}
					}
					close;
				}
			case 3:
				mes "นักผจญภัยทั่วไปไม่รู้จักเกาะนิรนาม ไอกูก็สนใจในนั้น";
				next;
				switch( select( "Ragged Zombie Hunting", "Zombie Slaughter Hunting", "Banshee Hunting" ) ) {
					mes "[Melody-Jack]";
				case 1:
					if (checkquest(13134,PLAYTIME) == 0)
						mes "Ragged Zombie จะลดลง มันเป็นเวลาที่ดีที่จะดำเนินการตามคำขอของไอกูต่อไปอีกหนึ่งนาที ";
					else if (checkquest(13133,HUNTING) == 1) {
						cutin "ดฉด๕ฑโมปบ๑ฤซตๅ",3;  //ragged_zombie_card
						mes "เป็นเรื่องง่ายที่จะพบกับทุกสิ่งบนเกาะนิรนาม น่าเบื่อมาก.";
						next;
						mes "[Melody-Jack]";
						mes "กำจัดมันและช่วยไอกูและนักผจญภัยคนอื่น ๆ ";
						cutin "",255;
					}
					else if (checkquest(13133,HUNTING) == 2) {
						mes "คุณอาบน้ำหรือยัง? มันมีกลิ่นที่นี่ แต่ฉันแน่ใจว่าคุณมีช่วงเวลาที่ยากลำบากกับกลิ่น";
						callsub S_Quest,13133,13134,1;
					}
					else {
						cutin "ดฉด๕ฑโมปบ๑ฤซตๅ",3;  //ragged_zombie_card
						mes "ผู้ตายกลับมาในเวลากลางคืน กลับมาฟังดูแปลก ๆ มาตัดอึ";
						next;
						mes "[Melody-Jack]";
						mes "ขอให้เคลียร์ ^FF0000Ragged Zombie ^000000 เป็นเรื่องง่ายเพราะพวกเขาปรากฏตัวทุกที่ ";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ใช้ ^FF0000 บา ธ ^000000 แล้วคุณจะไม่ได้กลิ่นอีกต่อไป ";
							callsub S_Quest,13134,13133;
						}
					}
					close;
				case 2:
					if (checkquest(13136,PLAYTIME) == 0)
						mes "ฉันรู้ว่าคำขอไปยังเกาะนิรนามไม่ใช่เรื่องง่าย แต่ฉันจะยินดีถ้าคุณเข้าร่วมอีกครั้ง ";
					else if (checkquest(13135,HUNTING) == 1) {
						cutin "มปบ๑ฝฝทฏลอฤซตๅ",3;  //zombie_slaughter_card
						mes "ไม่ใช่เรื่องง่ายสำหรับฉันที่จะก้าวเข้าสู่เกาะนิรนาม ฉันไม่อดทนขนาดนั้น";
						next;
						mes "[Melody-Jack]";
						mes "แต่นักผจญภัยควรท้าทายสักครั้ง ^FF0000Zombie Slaughter ^000000 คุ้มค่าที่จะเคลียร์";
						cutin "",255;
					}
					else if (checkquest(13135,HUNTING) == 2) {
						mes "คุณค่อนข้างดี! คุณเคลียร์พวกเขา กว่าจะกลับก็ต้องใช้เวลา";
						callsub S_Quest,13135,13136,1;
					}
					else {
						cutin "มปบ๑ฝฝทฏลอฤซตๅ",3;  //zombie_slaughter_card
						mes "พวกมันอันตรายไม่ต่างจาก Ragged Zombie เพราะมันแกว่งอะไรที่แหลม";
						next;
						mes "[Melody-Jack]";
						mes "กรุณาล้าง ^FF0000Zombie Slaughter ^000000 ระวังหลีกเลี่ยงใบมีด";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "คุณมีหลายสิ่งที่ต้องดูแลดังนั้นฉันจะยึดมั่นในคำพูดของฉัน ล้างพวกเขา";
							callsub S_Quest,13136,13135;
						}
					}
					close;
				case 3:
					if (checkquest(13138,PLAYTIME) == 0)
						mes "แบนชีเคลียร์แล้ว คำขอได้รับการแก้ไขแล้ว แต่ยังไม่สิ้นสุด จะต้องอีกครั้งไม่ช้าก็เร็ว ";
					else if (checkquest(13137,HUNTING) == 1) {
						cutin "น๊ฝรฤซตๅ",3;  //banshee_card
						mes "คุณจะเห็นว่ามีสิ่งที่ไม่คาดคิดขณะออกผจญภัยบางครั้งโดยไม่คำนึงถึงสัตว์ประหลาดและเพื่อนร่วมทาง ";
						next;
						mes "[Melody-Jack]";
						mes "คำขอนี้เป็นเช่นนั้นสำหรับฉัน ฉันไม่ชอบสิ่งมีชีวิตหรือเหตุผลของการร้องขอนี้ ";
						cutin "",255;
					}
					else if (checkquest(13137,HUNTING) == 2) {
						mes "คุณคงจะเหนื่อย คุณไปที่ที่ยากเกินความสามารถของคุณ อย่าโกรธ. ฉันจะตอบแทนคุณสำหรับสิ่งนั้น ";
						callsub S_Quest,13137,13138,1;
					}
					else {
						cutin "น๊ฝรฤซตๅ",3;  //banshee_card
						mes "กรุณาล้าง ^FF0000Banshee ^000000 ภายในเกาะนิรนาม อย่าบอกว่าไม่มี คุณก็รู้ว่ามันมีไว้สำหรับไอจีเช่นกัน ";
						next;
						mes "[Melody-Jack]";
						mes "ทั้งแบนชีและไอจีน่ารำคาญ ฉันจะไม่ทำตามคำขอนี้ แต่คุณจะแตกต่างออกไป ขวา?";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ดู? มีคนต้องการคำขอนี้ เคลียร์ ^FF0000Banshees ^000000 ที่เกาะนิรนาม.";
							callsub S_Quest,13138,13137;
						}
					}
					close;
				}
			case 4:
				mes "Abyss Lake มีคำขอจากเด็กหญิงตัวเล็ก ๆ ขยันมาก.";
				next;
				if (select( "Ferus และ Bewler.", "Gold และ Blue Acidus." ) == 1) {
					mes "[Melody-Jack]";
					if (checkquest(13140,PLAYTIME) == 0)
						mes "มังกรเขียวและมังกรแดงทำให้ฉันนึกถึงแตงโม";
					else if (checkquest(13139,HUNTING) == 1) {
						cutin "ทนตๅฦไทฏฝบฤซตๅ",3;  //ferus_card
						mes "Abyss Lake ... คุณอาจจะรู้วิธีการเดินทาง? คุณไม่ใช่นักผจญภัยด้วยข้อมูลนั้น?";
						next;
						mes "[Melody-Jack]";
						mes "ในกรณีที่ฉันจะแจ้งเบาะแสว่าจะพบกุญแจและทางเข้าแถว ๆ นั้น";
						cutin "",255;
					}
					else if (checkquest(13139,HUNTING) == 2) {
						mes "คุณล้างมังกรทั้งหมดหรือไม่? พักผ่อนสักวัน.";
						callsub S_Quest,13139,13140,1;
					}
					else {
						cutin "ฑืธฐฦไทฏฝบฤซตๅ",3;  //ferus__card
						mes "Abyss Lake เต็มไปด้วยมังกรเข้าและออก มังกรไม่ได้พิเศษขนาดนั้น แต่มีมากมาย.";
						next;
						mes "[Melody-Jack]";
						mes "ระหว่าง Ferus กับ Bewler อันไหนอันตรายกว่ากัน? ฉันไม่รู้. เคลียร์ทั้งคู่.";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ไม่มีสีเขียวหรือสีแดงมารบกวน แต่ยังไงก็ตามกลับมาหลังจากเคลียร์ทั้งหมด";
							callsub S_Quest,13140,13139;
						}
					}
				}
				else {
					mes "[Melody-Jack]";
					if (checkquest(13142,PLAYTIME) == 0)
						mes "พักผ่อน. เราควรไปปราบมังกรเอซิตัสอีกครั้ง";
					else if (checkquest(13141,HUNTING) == 1) {
						cutin "บํท็พ๎ฝรด๕ฝบฤซตๅ",3;  //acidus__card
						mes "Abyss Lake อยู่ไม่ใกล้ แต่ก็คุ้มค่าแก่การเยี่ยมชม";
						next;
						mes "[Melody-Jack]";
						mes "นี่เป็นเทคนิคการฝึก แต่แกล้งทำเป็นว่าคุณกำลังช่วยใครบางคน จะมีรางวัลให้";
						cutin "",255;
					}
					else if (checkquest(13141,HUNTING) == 2) {
						mes "คุณเป็นนักผจญภัยที่มีพรสวรรค์มากกว่าที่ฉันคิด หากคุณพบวิธีการดูแลในครั้งต่อไป การศึกษานี้ขึ้นอยู่กับคุณ";
						callsub S_Quest,13141,13142,1;
					}
					else {
						cutin "ฐ๑ตๅพ๎ฝรด๕ฝบ",3;  //acidus_card
						mes "มีถ้ำที่มังกรอาศัยอยู่ภายในทะเลสาบอบิส มีมากมาย แต่สิ่งที่บินได้นั้นน่ารำคาญ";
						next;
						mes "[Melody-Jack]";
						mes "ปราบพวกมันให้ดีที่สุดเท่าที่จะทำได้ อย่าสนใจสีของพวกเขาเพราะพวกเขาทั้งหมดต้องเคลียร์";
						next;
						cutin "",255;
						if (select( "ยอมรับภารกิจ.", "ยกเลิก." ) == 1) {
							mes "[Melody-Jack]";
							mes "ปราบมอนสเตอร์ Gold และ Blue Acidus ที่ระดับสุดท้ายของ Abyss Lake";
							callsub S_Quest,13142,13141;
						}
					}
				}
				close;
			}
		}
		else {
			mes "คุณช่วยไอกูเสร็จแล้วหรือเลิก? อย่างไรก็ตามฉันไม่มีธุระอะไรให้คุณอีกแล้ว ";
			next;
			callsub S_Erasequest,
				13119,"Baroness of Retribution",
				13121,"Lady Solace",
				13123,"Mistress of Shelter",
				13125,"Dame of Sentinel",
				13127,"Vanberk and Isilla",
				13129,"Hodremlin",
				13131,"Agav and Echio",
				13133,"Ragged Zombie",
				13135,"Zombie Slaughter",
				13137,"Banshee",
				13139,"Ferus ",
				13141,"Acidus";
			mes "[Melody-Jack]";
			mes "ฉันไม่แน่ใจว่าคุณพบสิ่งที่คุณกำลังมองหาหรือไม่ ... ฉันไม่สามารถเกี่ยวข้องได้อีกต่อไป ขอบคุณ.";
		}
	}
	close;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp 900000,200000;
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		.@quest_id = getarg(.@i);
		switch( checkquest(.@quest_id,HUNTING) ) {
		case -1:
			if (checkquest(.@quest_id+1) > -1)
				erasequest (.@quest_id+1);
			continue;
		case 0:
		case 1:
			mes "[Melody-Jack]";
			mes getarg(.@i+1) +" ถูกเคลีย ไม่จำเป็นต้องมีการร้องขอเพิ่มเติม ";
			break;
		case 2:
			mes "[Melody-Jack]";
			mes getarg(.@i+1) +" สำเร็จ คุณต้องได้รับรางวัล ตอบแทน ";
			getexp 900000,200000;
			break;
		}
		erasequest .@quest_id;
		next;
	}
	return;
OnTouch_:
	if (para_120lv01 == 1) {
		emotion ET_SURPRISE;
		mes "[Melody-Jack]";
		mes "สวัสดี! ... รอ.? ";
		close;
	}
	end;
}

moc_para01,17,77,5	script	Aigu#1	4_F_IU,{
	mes "[Aigu]";
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "โอ้ทำไมคุณไม่เป็นส่วนหนึ่งของ Eden Group ";
		mes "ฉันคุยกับนักผจญภัยระดับพื้นฐานเท่านั้น ^FF0000121-130 ^000000 ";
	}
	else if (para_120lv01 == 0) {
		cutin "igu01",2;
		mes "ไฮ? วันที่ดีเช่นวันนี้ ขอความกรุณาได้ไหม ฉันได้ยินมาว่านักผจญภัยใน Eden Group เป็นคนดี.";
		next;
		if (select( "ฉันไม่ใช่คนใจดี ","แน่นอนฉันจะช่วย" ) == 1) {
			cutin "igu05",2;
			mes "[Aigu]";
			mes "เออ.. กูเกลียด";
		}
		else {
			cutin "igu03",2;
			mes "[Aigu]";
			mes "จริงๆ? ฉันจะแนะนำตัวเอง! ฉันเป็นหัวหน้าวง ^ff0000Dandelion ในไอจี ^000000 ";
			next;
			mes "[Aigu]";
			mes "ฉันเพิ่งกลับมาจากการแสดงใน ท้องถิ่น มีใครบางคนที่ฉันอยากจะพบ แต่ตอนนี้หายไปฉันจึงมาที่ Eden Group เพื่อขอความช่วยเหลือ ";
			next;
			mes "[Aigu]";
			mes "ฉันเพิ่งตัดสินใจขอด้วยตัวเองเพราะไม่มีใครดูแลและทุกคนก็ยุ่ง ขอบคุณสำหรับความช่วยเหลือ ";
			para_120lv01 = 1;
		}
	}
	else if (para_120lv01 > 0) {
		cutin "igu01",2;
		mes "ยินดีที่ได้กลับมาที่ Midgard! น่าตื่นเต้นมากเหมือนวันแรก";
		next;
		if (select( "คุณขออะไร ","พูดเรื่องอื่น" ) == 2) {
			cutin "igu01",2;
			mes "[Aigu]";
			mes "วงดนตรีแดนดิไลออนเดินทางไปทั่วโลกและถ่ายทอดดนตรีบำบัดสู่ผู้คน ";
			next;
			cutin "igu02",2;
			mes "[Aigu]";
			mes "จริงๆแล้วเพลงของเราไม่มีเวทมนตร์ แต่ฉันมั่นใจว่ามันช่วยคนได้เพราะฉันร้องเพลง";
		}
		else if (BaseLevel < 121) {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "ฉันคิดว่ามันยากสำหรับคุณที่จะได้รับคำขอของฉัน? สำหรับตอนนี้ฉันคิดว่ามันจะทำให้คนหลงทางมากขึ้น แล้วการเดินทางให้กว้างขึ้นล่ะ?";
		}
		else if (BaseLevel < 131) {
			mes "[Aigu]";
			mes "ฉันสร้างที่พักพิงสำหรับคนที่ฉันกำลังมองหาและนักผจญภัยที่หลงทาง ฉันอยากรู้ว่ามีนักผจญภัยคนไหนบ้าง";
			next;
			switch( select( "Thanatos Tower search", "Rachel Sanctuary search", "Nameless Island search", "Abyss Lake search" ) ) {
				mes "[Aigu]";
			case 1:
				if (checkquest(13115,PLAYTIME) == 0)
					mes "ฉันจะขอค้นหาอีกครั้งหลังจากนี้สักพักเพราะมีความหวังตั้งแต่มีคนพบร่องรอย";
				else if (isbegin_quest(13111) == 1) {
					cutin "igu04",2;
					mes "คุณรู้ว่ามีคนไปที่นั่น แต่คุณไม่รู้ว่าคือใคร? ขอบคุณ. แต่ผมขอให้คุณค้นหาอีกครั้งในวันพรุ่งนี้เนื่องจากเราไม่ทราบว่าเป็นใคร.";
					callsub S_Quest,13111,13115,1250000,300000;
				}
				else if (isbegin_quest(13107) == 1)
					mes "คุณอาจพบที่พักพิงที่ด้านบนของหอคอย แม้แต่ที่พักพิงนั้นก็มีเพียงน้ำและกองไฟเท่านั้น";
				else {
					mes "นักผจญภัยจะหลงทางได้ง่ายเพราะมอนสเตอร์อันตรายและความสูงของหอคอยธานาทอส แต่มันเป็นไปไม่ได้สำหรับนักผจญภัยบางคนดังนั้นโปรดตรวจสอบให้ฉัน.";
					next;
					if (select( "เริ่มค้นหา ","ยกเลิก." ) == 1) {
						mes "[Aigu]";
						mes "หอคอย Thanatos อยู่ทางเหนือของสาธารณรัฐ Schwarzwald! มันอยู่ไม่ไกลจาก Juno หรือ Hugel";
						callsub S_Quest,13115,13107;// Thanatos Tower Search
					}
				}
				break;
			case 2:
				if (checkquest(13116,PLAYTIME) == 0)
					mes "ค้นหาราเชลก็เพียงพอแล้ว ฉันจะถามคุณอีกครั้งหลังจากนั้นสักครู่";
				else if (isbegin_quest(13112) == 1) {
					mes "ไปเที่ยวเป็นอย่างไรบ้าง? ไม่มีข่าวพิเศษ แต่มันยังไม่เสร็จฉันจะขอให้คุณค้นหาอีกครั้ง.";
					callsub S_Quest,13112,13116,1000000,300000;
				}
				else if (isbegin_quest(13108) == 1)
					mes "ไม่สามารถเข้าไปในสถานที่ศักดิ์สิทธิ์ด้วยวิธีปกติ ไม่ใช่เรื่องง่าย แต่เราสามารถหาเบาะแสได้ที่ Rachel";
				else {
					cutin "igu03",2;
					mes "ภายในศาลเจ้าราเชลมีวิหารศักดิ์สิทธิ์ เป็นไปได้ที่จะติดอยู่ที่นั่นเมื่อคุณเข้าไปข้างใน";
					next;
					mes "[Aigu]";
					mes "โปรดตรวจสอบว่ามีร่องรอยของนักผจญภัยที่หลงทางอยู่ทางตะวันออกเฉียงใต้ของชั้น 2 ในโซนตรงกลางหรือไม่.";
					next;
					if (select( "เริ่มค้นหา.", "ยกเลิก." ) == 1) {
						cutin "igu05",2;
						mes "[Aigu]";
						mes "ขอให้คุณปลอดภัยในการค้นหา ";
						callsub S_Quest,13116,13108;// Rachel holy ground Search
					}
				}
				break;
			case 3:
				if (checkquest(13117,PLAYTIME) == 0)
					mes "การค้นหาเกาะนิรนามจะดำเนินต่อไปในเวลาอื่น ยังไงก็พักสักหน่อย";
				else if (isbegin_quest(13113) == 1) {
					mes "ขอบคุณ! มีนักผจญภัยพเนจรช่วยด้วย! เราควรค้นหาต่อไปอยู่ดี โปรดช่วยฉันในครั้งต่อไปอีกครั้ง ";
					callsub S_Quest,13113,13117,900000,300000;
				}
				else if (isbegin_quest(13109) == 1) {
					cutin "igu04",2;
					mes "บางที ... ออกไปเที่ยวดื่มกับคนตาย แต่ช่วยคุยด้วย ";
				}
				else {
					cutin "igu03",2;
					mes "ใช่บางทีพวกเขาไม่สามารถมาได้เพราะพวกเขาถูกจับโดยคนตาย! เกาะนิรนามไม่เป็นไร! โปรดค้นหาเกาะนิรนาม ";
					next;
					if (select( "เริ่มค้นหา.", "ยกเลิก." ) == 1) {
						cutin "igu01",2;
						mes "[Aigu]";
						mes "ฉันจะขอให้คุณค้นหาใต้ดินที่ 1 ของอารามบนเกาะนิรนาม ฉันไม่สามารถให้คุณรับความเสี่ยงนั้นได้เว้นแต่คุณจะเป็นอาสาสมัคร ";
						callsub S_Quest,13117,13109;// Unknown island Search
					}
				}
				break;
			case 4:
				if (checkquest(13118,PLAYTIME) == 0)
					mes "ฉันจะพยายามค้นหา Abyss Lake อีกครั้งในอีกสักครู่ ";
				else if (isbegin_quest(13114) == 1) {
					cutin "igu05",2;
					mes "คุณกลับมาอย่างปลอดภัย แต่ฉันเดาว่าไม่มีผลดีต่อใบหน้าของคุณ ฉันจะค้นหามากกว่านี้ไม่ทำให้ผิดหวัง! ขอบคุณ.";
					callsub S_Quest,13114,13118,900000,200000;
				}
				else if (isbegin_quest(13110) == 1) {
					cutin "igu05",2;
					mes "บางทีอาจจะผิดหวังและล้มลงเพราะไม่พบสัตว์ประหลาดที่พื้นทะเลสาบ";
				}
				else {
					mes "ฉันขอแนะนำให้ค้นหา Abyss Lake ตรงถ้ำกลางทะเลสาบ ";
					next;
					mes "[Aigu]";
					mes "ใช่ จุดจบของการผจญภัยที่ดีต้องมังกร! ฉันได้ยินมาว่ามีสัตว์ประหลาดอยู่ที่พื้นทะเลสาบ ";
					next;
					if (select( "เริ่มค้นหา.", "ยกเลิก." ) == 1) {
						mes "[Aigu]";
						mes "Abyss Lake เป็นวิธีที่เร็วที่สุดในการไปยัง Hugel กรุณาเยี่ยมชมถ้ำใต้ดินชั้น 3! ";
						callsub S_Quest,13118,13110;// Abyss Lake Search
					}
				}
			}
		}
		else {
			cutin "igu04",2;
			mes "[Aigu]";
			mes "ฉันไม่สามารถให้งานค้นหาได้อีกในตอนนี้! คุณต้องการความท้าทายที่ยากขึ้นเหมาะกับระดับของคุณ ";
			callsub S_Erasequest,
				13107,"Thanatos Tower",
				13108,"Rachel Shrine",
				13109,"Nameless Island",
				13110,"Abyss Lake";
			callsub S_Erasequest2,
				13111,"Thanatos Tower",1250000,300000,
				13112,"Rachel Shrine",1000000,300000,
				13113,"Nameless Island",900000,300000,
				13114,"Abyss Lake",900000,200000;
			for ( .@i = 13115; .@i <= 13118; .@i++ )
				if (isbegin_quest(.@i)) erasequest .@i;
		}
	}
	close2;
	cutin "",255;
	end;
S_Quest:
	if (isbegin_quest(getarg(0)))
		erasequest getarg(0);
	setquest getarg(1);
	if (getarg(2,0))
		getexp getarg(2),getarg(3);
	return;
S_Erasequest:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 2 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "โอ้คุณทำงานใน "+ getarg (.@i +1) +" งานผจญภัย ให้ฉันล้างบันทึกของคุณเพื่อให้คุณทำงานได้ดีขึ้น.";
			erasequest getarg(.@i);
		}
	}
	return;
S_Erasequest2:
	.@total = getargcount();
	for ( .@i = 0; .@i < .@total; .@i += 4 ) {
		if (isbegin_quest(getarg(.@i))) {
			next;
			mes "[Aigu]";
			mes "โอ้เสร็จแล้ว "+ getarg (.@i + 1) +" ผจญภัย ขอบคุณที่ช่วยงานนี้!.";
			erasequest getarg(.@i);
			getexp getarg(.@i+2),getarg(.@i+3);
		}
	}
	return;
}

-	script	Trace of an adventurer	-1,{
	.@quest = 13106 + atoi(strnpcinfo(2));
	.@delay = 13110 + atoi(strnpcinfo(2));
	if (isbegin_quest(.@quest) == 1) {
		mes "ฉันพบร่องรอยของใครบางคนที่อยู่ที่นี่ แต่ไม่พบเบาะแส กลับไปรายงานไอจีกันดีกว่า";
		erasequest .@quest;
		setquest .@delay;
	}
	else
		mes "มีร่องรอยของใครบางคนกำลังอยู่ที่นี่.";
	close;
}
thana_step,167,292,5	duplicate(Trace of an adventurer)	Trace of an adventurer#1	4_TRACE
ra_san05,265,69,5	duplicate(Trace of an adventurer)	Trace of an adventurer#2	4_TRACE
abbey02,155,290,5	duplicate(Trace of an adventurer)	Trace of an adventurer#3	4_TRACE
abyss_03,102,140,5	duplicate(Trace of an adventurer)	Trace of an adventurer#4	4_TRACE

tha_t06,126,128,3	script	Suspicious Monster#1	4_DEVIRUCHI,{
	mes "[นักผจญภัยปลอมตัว]";
	mes "ฉันแค่ทำงานในสถานที่อันตรายแห่งนี้ในราคา 5,000 zeny ... คุณเป็นนักผจญภัยที่ได้รับคำขอจาก Eden Group หรือไม่?";
	next;
	switch( select( "ใช่","ไม่" ) ) {
		mes "[นักผจญภัยปลอมตัว]";
	case 1:
		if (BaseLevel < 121 || BaseLevel > 130) {
			mes "ฉันเห็นว่านั่นไม่ใช่คุณในแวบแรก!";
			close;
		}
		mes "นักผจญภัยที่ดำเนินการขอไอกูจะถูกวาร์ปไปที่ระดับบนในนามของไอกู.";
		next;
		if (select( "ไปทันที.", "ยกเลิก." ) == 1) {
			mes "[นักผจญภัยปลอมตัว]";
			if (isbegin_quest(13107) != 1)
				mes "ดูเหมือนว่าคุณจะไม่ได้ทำตามคำขอผจญภัย Thanatos Tower ฉันขอโทษ แต่ฉันทำสิ่งนี้เพื่อไอจีไม่ใช่คุณ ";
			else {
				mes "ระวัง! คุณไม่สามารถลงมาได้เมื่อคุณขึ้นไป สำหรับไอจี!";
				close2;
				warp "thana_step",69,369;
				end;
			}
		}
		close;
	case 2:
		mes "หรือโปรดแสร้งทำเป็นไม่รู้จักฉัน! ฉันจะตกอยู่ในอันตรายถ้าคุณเป่าผม!";
		close;
	}
}
