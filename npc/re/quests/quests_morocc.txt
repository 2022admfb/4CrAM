//===== rAthena Script =======================================
//= Morocc Quest NPCs
//===== By:==================================================
//= Euphy
//===== Current Version:=====================================
//= 1.1
//===== Compatible With:=====================================
//= rAthena Project
//===== Description:=========================================
//= [Official Conversion]
//= Quest NPCs related to Morocc.
//===== Additional Comments:=================================
//= 1.0 First Version.
//= 1.0a Gathered remaining dialogues. [Euphy]
//= 1.1 Added new warpers for Satan Morocc maps. [Euphy]
//============================================================

// Stop Post Quest
// ============================================================
morocc,152,167,4	duplicate(William)	William#moc	89

// Binoculars Quest
// ============================================================
morocc,169,168,4	duplicate(Alchemist)	Alchemist#moc	64

// Resurrection of Satan Morocc
// ============================================================
-	script	Continental Guard#man	-1,{
	mes "[หน่วยพิทักษ์ทวีป]";
	mes "เราได้รับคำสั่งจากสำนักงานใหญ่ให้บล็อกการเข้าถึงพื้นที่นี้เนื่องจากตรวจพบปรากฏการณ์อวกาศ - เวลาที่ผิดปกติ";
	next;
	mes "[หน่วยพิทักษ์ทวีป]";
	mes "หากคุณต้องการไปยังพื้นที่ถัดไปคุณจะต้องรับผิดชอบต่อความปลอดภัยของคุณเอง";
	next;
	switch(select("ไม่เป็นไร:เข้าสู่สนาม")) {
	case 1:
		mes "[หน่วยพิทักษ์ทวีป]";
		mes "ฉลาดเลือก.";
		mes "เดินทางปลอดภัย.";
		close;
	case 2:
		mes "[หน่วยพิทักษ์ทวีป]";
		mes "ฉันเห็น.";
		mes "ฉันจะส่งคุณไปที่สนามในไม่ช้า";
		close2;
		warp "moc_fild20",208,207;
		end;
	}
}
moc_fild07,380,202,1	duplicate(Continental Guard#man)	Continental Guard#man1	852
moc_fild11,189,360,6	duplicate(Continental Guard#man)	Continental Guard#man2	852
prt_fild10,263,23,1	duplicate(Continental Guard#man)	Continental Guard#man3	852
prt_fild09,95,19,1	duplicate(Continental Guard#man)	Continental Guard#man4	852
prt_fild09,246,17,1	duplicate(Continental Guard#man)	Continental Guard#man5	852
moc_fild13,32,171,6	duplicate(Continental Guard#man)	Continental Guard#man6	852
moc_fild16,124,381,4	duplicate(Continental Guard#man)	Continental Guard#man7	852
moc_fild16,333,380,4	duplicate(Continental Guard#man)	Continental Guard#man8	852
moc_fild01,84,19,1	duplicate(Continental Guard#man)	Continental Guard#man9	852

morocc,176,101,3	duplicate(Continental Official)	Continental Official#mor	754

// Pyramids Nightmare Mode::nightmare_pry
//============================================================
moc_prydb1,103,54,3	script	Suspicious Cat#night	547,{
	mes "[แมวน่าสงสัย]";
	mes "ฉันไม่อยากเชื่อเลยว่ามัมมี่จะทำแบบนี้กับฉันได้ โอ้มนุษย์มันเจ็บ ไม่มีใครควรรู้เกี่ยวกับเรื่องนี้ ....";
	next;
	if(select("มัมมี่? เกิดอะไรขึ้น:แสร้งทำเป็นไม่รู้อะไรเลย") == 2)
		close;
	mes "[แมวน่าสงสัย]";
	mes "คุณจับฉัน! ยี้ ... ฉันไม่ได้คาดหวังว่าชื่อเสียงของฉันในฐานะ tomb raider จะได้รับความเสียหายเช่นนี้ ...";
	next;
	mes "[แมวน่าสงสัย]";
	mes "แต่มาเถอะพวกมันแปลกมาก! พวกมันคือมัมมี่! แค่มัมมี่รู้มั้ย? พวกเขาไม่น่าจะแข็งแกร่งขนาดนั้น!";
	next;
	if(select("มัมมี่ที่แข็งแกร่ง? พวกเขาอยู่ที่ไหน:จบการสนทนา") == 2) {
		mes "[แมวน่าสงสัย]";
		mes "Whaaat ~ คุณไม่อยากได้ยินเรื่องราวของฉันเหรอ? ละเอียด! คุณไม่ควรเอ่ยชื่อฉันที่อื่นดีกว่า!";
		close;
	}
	mes "[แมวน่าสงสัย]";
	mes "ฮ่า ๆ มันเป็นความลับ";
	mes "ฉันพบในขณะที่ฉันกำลังขโมย ... ไม่ฉันหมายถึงแค่เดินผ่านพีระมิด";
	next;
	mes "[แมวน่าสงสัย]";
	mes "เมื่อฉันเห็นมัมมี่มาหาฉันฉันก็เหมือน 'อะไรก็ได้' และไม่สนใจ แต่แล้วเขาก็ล้มฉันลงพร้อมตบหน้า! เขาทำแบบนั้นกับแมวน่ารักอย่างฉันได้ยังไง! คุณประหลาด ... รอดู!";
	next;
	if(select("ฉันก็อยากไปเหมือนกัน:จบการสนทนา") == 2)
		close;
	mes "[แมวน่าสงสัย]";
	mes "ไม่มีทาง! ฉันใช้เวลามากมายในการค้นหาเส้นทางนั้น !!";
	next;
	mes "[แมวน่าสงสัย]";
	mes "อืม ... แต่ถ้าคุณยืนยันก็แค่ให้ฉัน 5,000 Zeny ฉันจะนำคุณไป";
	mes "อะไร ~? อย่าให้ฉันดู! คุณมีความคิดบ้างไหมว่าการที่แมวอยู่รอดในมิดการ์ดนั้นยากแค่ไหน!";
	next;
	mes "[แมวน่าสงสัย]";
	mes "^FF0000 แต่ลองคิดดูคนอ่อนแออย่างคุณจะไม่รอดที่นั่น อาจจะเป็นเวลาสักครู่ถ้าคุณโชคดี^000000";
	mes "มันไม่ใช่ธุรกิจของฉัน แต่ฉันเตือนคุณแล้ว!";
	next;
	switch(select("จ่าย 5,000 zeny แล้วออกเดินทาง:ลืมมันไป")) {
	case 1:
		if (Zeny < 5000) {
			mes "[แมวน่าสงสัย]";
			mes "คิดว่าจะไปไหน! คุณยากจนมาก!";
			close;
		}
		set Zeny,Zeny-5000;
		mes "[แมวน่าสงสัย]";
		mes "นี่มันเงินเก่าอะไรกันเนี่ย ... คุณไม่มีอะไรแวววาวหรือแวววาวเลยเหรอ!";
		mes "อ๊ะตามฉันมา!";
		close2;
		warp "moc_prydn1",93,96;
		end;
	case 2:
		mes "[แมวน่าสงสัย]";
		mes "เอ้ยคุณไม่มีเงิน 5,000 Zeny เลยเหรอ?";
		close;
	}
}

moc_prydn1,94,98,3	script	Suspicious Cat#night2	547,{
	mes "[แมวน่าสงสัย]";
	mes "แม่โง่คนนั้นตบฉันอีกครั้งในขณะที่คุณกำลังมองไป ดม ... กลับกันเถอะเมี๊ยว!";
	next;
	switch(select("กลับไปที่ Thief Guild:Eliminate Verit:Eliminate Ancient Mummy:มันไม่มีอะไรเลย")) {
	case 1:
		mes "[แมวน่าสงสัย]";
		mes "ถูกต้อง ... กลับกันเถอะ";
		close2;
		warp "moc_prydb1",100,57;
		end;
	case 2:
		set .@playtime,checkquest(2290,PLAYTIME);
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[แมวน่าสงสัย]";
			mes "ดูเวริทนั่นสิเขากลัวเรา! ค๊าาาาาา! เราปลอดภัยอย่างน้อยก็อีกสักพัก!";
			close;
		} else if (.@playtime == 2)
			erasequest 2290;
		set .@hunting,checkquest(2289,HUNTING);
		if (.@hunting == -1) {
			mes "[แมวน่าสงสัย]";
			mes "คุณรู้ไหมว่าแมวและสุนัขไม่เข้ากัน?";
			next;
			mes "[แมวน่าสงสัย]";
			mes "ดู Verit ที่น่าเกลียดและน่ากลัว เขาทำหน้าบึ้งกระดิกหางและพยายามเข้ามาใกล้มากขึ้น";
			mes "โอ้คุณไม่มีหางใช่มั้ย? เขาคิดว่าฉันมีหนามอยู่ข้างๆเขาและเขากำลังพยายามที่จะเริ่มการต่อสู้ในตอนนี้!";
			next;
			mes "[แมวน่าสงสัย]";
			mes "ดูเขาตะกละและน้ำลายไหล ... ฉันไม่ชอบวิธีหายใจของเขาเหมือนกัน ฉันทนไม่ได้ที่เขาทำเสียงฮุบพวกนั้น ...";
			mes "เห็นได้ชัดว่าเขารอที่จะโจมตีฉันจากด้านหลัง";
			next;
			mes "[แมวน่าสงสัย]";
			mes "เอ้ยฉันอาจจะข่วนหน้าเขาแรง ๆ ถ้าไม่ใช่เพราะปวดท้อง!";
			mes "เฮ้ไม่ใช่เพราะฉันกลัว Majoruros!";
			next;
			mes "[แมวน่าสงสัย]";
			mes "ถ้าคุณต้องการกลับบ้านคุณควรเอาชนะสุนัขขี้เหร่ตัวนั้นดีกว่า! มิฉะนั้นเขาจะกัดคุณ!";
			next;
			switch(select("ช่วยเขา:อยู่ห่าง ๆ")) {
			case 1:
				mes "[แมวน่าสงสัย]";
				mes "ดีเป้าเลข 20! โชคดี!";
				setquest 2289;
				close;
			case 2:
				mes "[แมวน่าสงสัย]";
				mes "ป๊ะ! คุณไม่สนใจฉันเหรอ? ดีคุณคนทรยศ!";
				close;
			}
		} else if (.@hunting == 0 || .@hunting == 1) {
			mes "[แมวน่าสงสัย]";
			mes "การฝึกของ Verit เป็นไปด้วยดีหรือไม่?";
			next;
			switch(select("แน่นอน:ฉันต้องการหยุด")) {
			case 1:
				mes "[แมวน่าสงสัย]";
				mes "ดี. ดีแล้วทำต่อไป!";
				close;
			case 2:
				mes "[แมวน่าสงสัย]";
				mes "อะไร? คุณขาดความรับผิดชอบมาก!";
				erasequest 2289;
				close;
			}
		} else if (.@hunting == 2) {
			mes "[แมวน่าสงสัย]";
			mes "เฮ้คุณมีประโยชน์จริง! เก่งมาก!";
			mes "เวอริทคงไม่กล้ามาแถวนี้อีกแล้วใช่มั้ย? มูฮาฮา!";
			changequest 2289,2290;
			getexp 300000,100000;
			close;
		}
	case 3:
		set .@playtime,checkquest(2291,PLAYTIME);
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[แมวน่าสงสัย]";
			mes "ดีฉันจะใช้เวลาที่คุณได้รับให้เป็นประโยชน์! แต่ฉันต้องทำอะไรสักอย่างเกี่ยวกับอาการปวดท้องนี้ก่อน .....";
			close;
		} else if (.@playtime == 2)
			erasequest 2291;
		set .@hunting,checkquest(2292,HUNTING);
		if (.@hunting == -1) {
			mes "[แมวน่าสงสัย]";
			mes "สมบัติล้ำค่าควรจะซ่อนอยู่ในที่ลับลึกที่นี่!";
			mes "ฉันแน่ใจว่าสมบัติล้ำค่าของมัมมี่ซ่อนอยู่ในชั้นใต้ดินที่สอง";
			next;
			mes "[แมวน่าสงสัย]";
			mes "แต่มัมมี่โบราณจับฉันไม่ทันและตีหัวก่อนที่ฉันจะรู้ตัว!";
			next;
			mes "[แมวน่าสงสัย]";
			mes "ฉันแน่ใจว่าพวกเขากำลังพยายามหยุดฉันไม่ให้ค้นหาสมบัติโดยการลดเซลล์สมองที่ดีกว่าของฉัน !!";
			next;
			mes "[แมวน่าสงสัย]";
			mes "พวกเขาหยุดฉันไม่ได้! ไปกำจัดมัมมี่โบราณกันเถอะ !!";
			next;
			mes "[แมวน่าสงสัย]";
			mes "อย่าถามว่าทำไม! ฉันแน่ใจว่าคุณคงไม่ต้องการให้สมบัติที่สวยงามเหล่านั้นถูกซ่อนไว้ในความมืดเช่นกัน!";
			next;
			switch(select("ช่วยเขา:อยู่ห่าง ๆ")) {
			case 1:
				mes "[แมวน่าสงสัย]";
				mes "ได้เลยเป้าหมายคือ 20! โชคดี!";
				setquest 2292;
				close;
			case 2:
				mes "[แมวน่าสงสัย]";
				mes "คุณช่างเย็นชาเหลือเกิน";
				close;
			}
		} else if (.@hunting == 0 || .@hunting == 1) {
			mes "[แมวน่าสงสัย]";
			mes "การต่อสู้กับมัมมี่โบราณเป็นไปด้วยดีหรือไม่?";
			next;
			switch(select("แน่นอน:ฉันต้องการหยุด")) {
			case 1:
				mes "[แมวน่าสงสัย]";
				mes "ดี. ดีแล้วทำต่อไป!";
				close;
			case 2:
				mes "[แมวน่าสงสัย]";
				mes "อะไร? คุณขาดความรับผิดชอบมาก!";
				erasequest 2292;
				close;
			}
		} else if (.@hunting == 2) {
			mes "[แมวน่าสงสัย]";
			mes "เฮ้คุณมีประโยชน์จริง! เก่งมาก!";
			mes "Ancient Mummy คงไม่กล้ามาที่นี่สักพักใช่มั้ย? มูฮาฮา!";
			changequest 2292,2291;
			getexp 600000,200000;
			close;
		}
	case 4:
		close;
	}
}

