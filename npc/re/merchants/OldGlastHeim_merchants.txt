//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Temporal Boots enchants NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version merged from idathena.
//=     Credit exneval. [Capuche]
//============================================================

// Merchant
//============================================================
glast_01,210,273,5	script	Hugin's Butler#pa0829	1_F_04,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "กระเป๋ามีน้ำหนักเกิน. ลดน้ำหนักก่อนแล้วค่อยมาใหม่.";
		close;
	}
	mes "[Hugin's Butler]";
	mes "นำ Temporal Crystal มาแลกรองเท้า Temporal boots ได้. เลือกอันที่เจ้าต้องการสิ.";
	next;
	switch( select( "ยกเลิก", "ซื้อ Temporal Boots (ต้องการ 1 Temporal Crystal)", "อัพเกรด Temporal Boots (ต้องการ 5 Temporal Crystals)", "อัพเกรด Modified Boots (ต้องการ 5 Temporal Crystals)" ) ) {
	case 1:
		mes "[Hugin's Butler]";
		mes "กลับมาหาได้ทุกเมื่อที่เจ้าต้องการ.";
		close;
	case 2:
		if (countitem(6607) > 0) {
			mes "[Hugin's Butler]";
			mes "ส่งมอบ Temporal Boots ให้. พวกเราสามารถอัพเกรด Temporal Boots ของเจ้าได้โดยใช้ 5 Temporal Crystals.";
			delitem 6607,1;// Temporal_Crystal
			getitem 2499,1;// Temporal_Boots
			close;
		}
		mes "[Hugin's Butler]";
		mes "เจ้าต้องมี Temporal Crystals. กลับมาใหม่เมื่อเจ้ามี Temporal Crystals มาด้วยโดยเจ้าหามันได้ใน Old Glast Heim.";
		close;
	case 3:
		setarray .@equip_type[0],
			22000,	// Temporal_STR_Boots
			22001,	// Temporal_INT_Boots
			22002,	// Temporal_AGI_Boots
			22003,	// Temporal_VIT_Boots
			22004,	// Temporal_DEX_Boots
			22005;	// Temporal_LUK_Boots
		break;
	case 4:
		setarray .@equip_type[0],
			22107,	// Modified_STR_Boots
			22108,	// Modified_INT_Boots
			22109,	// Modified_AGI_Boots
			22110,	// Modified_VIT_Boots
			22111,	// Modified_DEX_Boots
			22112;	// Modified_LUK_Boots
		break;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin's Butler]";
		mes "รองเท้าของเจ้าไม่สามารถอัพเกรดได้.";
		mes "สวมใส่ ^0000FFTemporal Boots^000000 ที่เจ้าต้องการอัพเกรดมา, และค่อยคุยกับข้าใหม่อีกครั้ง.";
		close;
	}
	if (getequipid(EQI_SHOES) != 2499) {
		mes "[Hugin's Butler]";
		mes "เจ้าไม่ได้สวมใส่ Temporal Boots มา. ค่อยคุยกับข้าอีกครั้งหลังจากสวมใส่มันมาแล้ว.";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Hugin's Butler]";
		mes "มี Temporal Crystals ไม่พอ. เจ้าต้องมี 5 Temporal Crystals สำหรับการอัพเกรด.";
		close;
	}
	mes "[Hugin's Butler]";
	mes "เจ้าสามารถแลก ^0000FF1 Temporal Boots และ 5 Temporal Crystals^000000 กับ 1 อย่างต่อไปนี้ได้. ^FF0000ค่าตีบวกก่อนหน้านี้จะหายไปเมื่อเจ้าได้แลกเปลี่ยนไปแล้ว^000000.";
	next;
	.@s = select( "ยกเลิก", "รองเท้า Str", "รองเท้า Int", "รองเท้า Agi", "รองเท้า Vit", "รองเท้า Dex", "รองเท้า Luk" ) - 2;
	if (.@s == -1) {
		mes "[Hugin's Butler]";
		mes "กลับมาหาได้ทุกเมื่อที่เจ้าต้องการ.";
		close;
	}
	setarray .@type$[0], "Strength", "Intelligence", "Agility", "Vitality", "Dexterity", "Lucky";
	mes "[Hugin's Butler]";
	mes "เจ้าแน่ใจที่จะซื้อ Temporal ^FF0000" + .@type$[.@s] + "^000000 Boots? ไม่มีการคืนเงินน่ะ.";
	next;
	if (select( "ขอคิดก่อน", "ใช่, ข้าแน่ใจ." ) == 1) {
		mes "[Hugin's Butler]";
		mes "Ok, กลับมาหาได้ทุกเมื่อที่เจ้าต้องการ.";
		close;
	}
	mes "[Hugin's Butler]";
	mes "รองเท้าของเจ้าได้ถูกอัพเกรดแล้ว.";
	if (getequipid(EQI_SHOES) == 2499) {
		delequip EQI_SHOES;
		delitem 6607,5;// Temporal_Crystal
		getitem .@equip_type[.@s],1;
	}
	close;
}

// Enchant and Socket NPC
//============================================================
glast_01,212,273,4	script	Hugin's Magician#pa0829	1_F_01,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "กระเป๋ามีน้ำหนักเกิน. ลดน้ำหนักก่อนแล้วค่อยมาใหม่.";
		close;
	}
	mes "[Hugin's Magician]";
	mes "เจ้าสามารถออฟรองเท้า Temporal Boots ได้. โปรดระวังถ้ารองเท้ามีช่องใส่การ์ดจะไม่สามารถออฟได้, และจะสามารถอัพเกรดรองเท้าเฉพาะที่สวมใส่อยู่เท่านั้น.";
	next;
	if (select("สอบถามวิธีการออฟ ?","เพิ่มความสามารถให้รองเท้า Temporal Boots") == 1) {
		mes "[Hugin's Magician]";
		mes "เจ้าสามารถเก็บ ^0000FFCoagulated Spell^000000 ได้เวลาเจ้าลงดันเจี้ยน.";
		next;
		mes "[Hugin's Magician]";
		mes "ข้าสามารถออฟรองเท้า Temporal Boots ได้แค่ 6 แบบเท่านั้น. โดยที่รองเท้านั้นต้องไม่ใช่^0000FFรองเท้าแบบธรรมดา^000000.";
		next;
		mes "[Hugin's Magician]";
		mes "กรุณาจำไว้ถ้าเจ้าเพิ่มช่องใส่การ์ดให้รองเท้าแล้ว, มันจะออฟไม่ได้อีกเลย.";
		next;
		mes "[Hugin's Magician]";
		mes "ข้าจะไม่สุ่มความสามารถให้. แต่เจ้าสามารถเลือกความสามารถเองได้เลย.";
		next;
		mes "[Hugin's Magician]";
		mes "เฉพาะในช่องที่ 4 เท่านั้น, ที่ความสามารถจะเริ่มจากอ่อนไปหาความสามารถที่แข็งแกร่งยิ่งขึ้น.";
		next;
		mes "[Hugin's Magician]";
		mes "ในการอัพเกรดครั้งสุดท้าย, มันต้องใช้ไอเท็ม Coagulated Spell จำนวนมาก. อย่าลืมในจุดนี้ล่ะ.";
		next;
		mes "[Hugin's Magician]";
		mes "เมื่อเจ้าอัพเกรดช่อง 4 เต็มขั้นแล้ว, ถ้าเจ้าจ่ายเพิ่มเจ้าจะได้สุ่มโบนัสความสามารถในช่องที่ 3.";
		next;
		mes "[Hugin's Magician]";
		mes "อย่างไรก็ตามถ้าออฟล้มเหลวไอเท็มจะไม่สูญหายไป, พวกข้าขอการันตีว่าการ์ดและค่าตีบวกจะคงเดิม. แต่พวกข้าไม่สามารถลบออฟได้.";
		next;
		mes "[Hugin's Magician]";
		mes "ฟิ้ววว...";
		mes "ข้าได้บอกพื้นฐานในการออฟแก่เจ้าแล้ว. หวังว่าจะได้เจอกันอีก.";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin's Magician]";
		mes "ได้ใส่รองเท้ามาหรือเปล่า ?";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6
	setarray .@enchant_cost[0],1,4,15,30,10;

	switch(.@equip_id) {
	case 22000:	// Temporal_STR_Boots
	case 22001:	// Temporal_INT_Boots
	case 22002:	// Temporal_AGI_Boots
	case 22003:	// Temporal_VIT_Boots
	case 22004:	// Temporal_DEX_Boots
	case 22005:	// Temporal_LUK_Boots

	case 22107:	// Modified_STR_Boots
	case 22108:	// Modified_INT_Boots
	case 22109:	// Modified_AGI_Boots
	case 22110:	// Modified_VIT_Boots
	case 22111:	// Modified_DEX_Boots
	case 22112:	// Modified_LUK_Boots
		.@equip_name$ = getequipname(EQI_SHOES);
		setarray .@card[0],
			getequipcardid(EQI_SHOES,0),
			getequipcardid(EQI_SHOES,1),
			getequipcardid(EQI_SHOES,2),
			getequipcardid(EQI_SHOES,3);
		copyarray .@equip_card[0], .@card[0], 4;	// for final check
		.@equip_refine = getequiprefinerycnt(EQI_SHOES);

		if (.@card[2] > 0) {
			mes "[Hugin's Magician]";
			mes "รองเท้าของเจ้าอัพเกรดเต็มขั้นแล้ว. พวกเราคงทำอะไรไปมากกว่านี้ไม่ได้แล้ว.";
			close;
		}
		if (.@card[3] == 0) {// 4th slot 1st try enchanting
			.@cost = .@enchant_cost[0];
			mes "[Hugin's Magician]";
			mes "ต้องการที่จะออฟ ^0000FF" + .@equip_name$ + "^000000?";
			mes "สำหรับการออฟครั้งแรก, เจ้าต้องมี ^0000FF" + .@cost + "^000000 Coagulated Spell มาด้วย.";
			next;
			.@s = select( "ออก", "Fighting Spirit", "Archery", "Spell", "Vitality", "Attack Speed", "Lucky" ) - 2;
			if (.@s == -1) {
				mes "[Hugin's Magician]";
				mes "Ok, กลับมาใหม่เมื่อเจ้าพร้อม.";
				close;
			}
			.@card[3] = .@enchant_1[.@s];
			.@string$ = "enchant number ^6300001^000000.";
		}
		else {
			for ( .@enchant_num = 1; .@enchant_num < 5; .@enchant_num++ ) {
				for ( .@enchant_type = 0; .@enchant_type < 6 && .@card[3] != getd( ".@enchant_" + .@enchant_num + "[" + .@enchant_type + "]" ); .@enchant_type++ )
					continue;
				if (.@enchant_type < 6)
					break;
			}
			if (.@enchant_num == 5) {
				mes "[Hugin's Magician]";
				mes "มีบางอย่างผิดพลาด.";
				close;
			}
			.@cost = .@enchant_cost[.@enchant_num];

			mes "[Hugin's Magician]";
			if (.@enchant_num == 4) {
				.@card[2] = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
				.@string$ = "^990000Bonus effect ^000000 upgrade.";
				mes "ในช่องที่ 4 ได้อัพเกรดออฟจนสุดแล้ว. เจ้าอยากจะสุ่มโบนัสออฟพิเศษในช่องที่ 3 ไหม ? เจ้าต้องมี ^0000ff" + .@cost + "^000000 Coagulated Spell มาด้วย.";
			}
			else {
				.@number = .@enchant_num + 1;
				.@card[3] = getd( ".@enchant_" + (.@enchant_num+1) + "[" + .@enchant_type + "]" );
				.@string$ = "enchant ครั้งที่ ^630000" + .@number + "^000000.";
				mes "การออฟ ^0000FF" + .@equip_name$ + "^000000's ในช่องที่ 4 ครั้งที่ ^0000FF" + .@number + "^000000 นั้น. ต้องการ ^0000FF" + .@cost + "^000000 Coagulated Spell.";
			}
			next;
			if (select("ออก","อัพเกรดความสามารถ!") == 1) {
				mes "[Hugin's Magician]";
				mes "Ok, กลับมาใหม่เมื่อเจ้าพร้อม.";
				close;
			}
		}
		if (countitem(6608) < .@cost) {
			mes "[Hugin's Magician]";
			mes "หืมม, เจ้าขาดอีก " + (.@cost - countitem(6608)) + " Coagulated Spell. ไปหามาเพิ่ม, แล้วค่อยกลับมาคุยกับข้า.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		delitem 6608,.@cost;// Coagulated_Spell

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_SHOES, .@equip_id) || callfunc("F_IsEquipCardHack", EQI_SHOES, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", EQI_SHOES, .@equip_refine))
			close;

		delequip EQI_SHOES;
		mes "[Hugin's Magician]";
		mes "พยายามที่จะ " + .@string$;
		getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card[2],.@card[3];
		close;
	default:
		mes "[Hugin's Magician]";
		mes "ไอเท็มไม่ถูกต้อง.";
		mes "จำไว้, ข้าออฟ Temporal Boots ได้แค่ 6 แบบเท่านั้น.";
		close;
	}
}

glast_01,210,270,0	script	Hugin's Craftsman#pa0829	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "กระเป๋ามีน้ำหนักเกิน. ลดน้ำหนักก่อนแล้วค่อยมาใหม่.";
		close;
	}
	mes "[Hugin's Craftsman]";
	mes "Magic Master ชอบอวดว่าการออฟรองเท้า Temporal Boots นั้นดีที่สุด. แต่, ข้ามีความคิดอีกอย่าง.";
	next;
	mes "[Hugin's Craftsman]";
	mes "ข้าคิดว่าการเพิ่มช่องใส่การ์ดนั่นแหล่มกว่า.";
	next;
	mes "[Hugin's Craftsman]";
	mes "แค่นำ ^0000FF5 Temporal Crystals^000000 มาให้ข้า, และข้าจะเพิ่มช่องใส่การ์ดในรองเท้า Temporal Boots ให้เจ้า. แต่, มันอาจจะ ^FF0000ล้มเหลวและสูญหายได้^000000. ยังอยากทำอยู่ไหม?";
	next;
	if (select( "ยกเลิก", "ลองเสี่ยงเพิ่มช่องใส่การ์ด" )  == 1) {
		mes "[Hugin's Craftsman]";
		mes "Ok, กลับมาหาได้ทุกเมื่อที่เจ้าต้องการ.";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin's Craftsman]";
		mes "เจ้าแน่ใจนะ ว่าใส่รองเท้ามาด้วย ?";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);
	switch(.@equip_id) {
	case 22000:	callsub( S_Slot,22006 );	// Temporal_STR_Boots
	case 22001:	callsub( S_Slot,22009 );	// Temporal_INT_Boots
	case 22002:	callsub( S_Slot,22010 );	// Temporal_AGI_Boots
	case 22003:	callsub( S_Slot,22007 );	// Temporal_VIT_Boots
	case 22004:	callsub( S_Slot,22008 );	// Temporal_DEX_Boots
	case 22005:	callsub( S_Slot,22011 );	// Temporal_LUK_Boots

	case 22107:	callsub( S_Slot,22113 );	// Modified_STR_Boots
	case 22108:	callsub( S_Slot,22114 );	// Modified_INT_Boots
	case 22109:	callsub( S_Slot,22115 );	// Modified_AGI_Boots
	case 22110:	callsub( S_Slot,22116 );	// Modified_VIT_Boots
	case 22111:	callsub( S_Slot,22117 );	// Modified_DEX_Boots
	case 22112:	callsub( S_Slot,22118 );	// Modified_LUK_Boots
	default:
		mes "[Hugin's Craftsman]";
		mes "นี่มันไม่ถูกต้อง! พวกเราต้องการ ^0000FFAdvanced Temporal Boots^000000 เท่านั้น! ^0000FFต้องเป็นรองเท้าที่มีค่าสเตตัสและไม่มีรู^000000!";
		close;
	}

S_Slot:
	.@equip_id = getequipid(EQI_SHOES);
	mes "[Hugin's Craftsman]";
	mes "^FF0000ออฟและค่าตีบวกทั้งหมดจะหายไปเมื่อมันล้มเหลว.^000000";
	mes "ยังอยากเสี่ยงอยู่ไหม ?";
	next;
	if (select( "ยกเลิก", "ข้าโอเคกับมันน่ะ!" ) == 1) {
		mes "[Hugin's Craftsman]";
		mes "Ok, กลับมาหาได้ทุกเมื่อที่เจ้าต้องการ.";
		close;
	}
	mes "[Hugin's Craftsman]";
	mes "ขอบอกอีกครั้ง, ^FF0000ถ้ารองเท้ามีรูจะไม่สามารถออฟได้.^000000";
	mes "ยังอยากทำต่อไหม ?";
	next;
	if (select( "ยกเลิก", "ข้ารู้แล้ว. ลองเจาะรูเลย." ) == 1) {
		mes "[Hugin's Craftsman]";
		mes "Ok, กลับมาหาได้ทุกเมื่อที่เจ้าต้องการ.";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Hugin's Craftsman]";
		mes "เจ้าต้องนำ ^0000FF5 Temporal Crystal^000000มาด้วย. เจ้าก็รู้ว่าไม่มีของฟรีในโลก...";
		close;
	}
	delitem 6607,5;// Temporal_Crystal
	if (getequipid(EQI_SHOES) == .@equip_id) {
		delequip EQI_SHOES;
		if (rand(1,100) < 50) {
			mes "[Hugin's Craftsman]";
			mes "อ้ากกกก, มันล้มเหลว. ครั้งหน้าจะทำให้ดีกว่านี้.";
			specialeffect2 EF_PHARMACY_FAIL;
			close;
		}
		mes "[Hugin's Craftsman]";
		mes "สำเร็จ! มันเป็นของเจ้า.";
		specialeffect2 EF_MAXPOWER;
		getitem getarg(0),1;
	}
	close;
}

// Temporal shoes slotted - npc enchant
glast_01,184,283,4	script	Dark magic master#pa082	1_F_01,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "กระเป๋ามีน้ำหนักเกิน. ลดน้ำหนักก่อนแล้วค่อยมาใหม่.";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "เจ้าแบกของมาเยอะเกิน. ไปลดของก่อนแล้วค่อยกลับมาใหม่.";
		close;
	}
	// requirements
	.@coagulated_id = 6608;// Coagulated_Spell
	.@polluted_id = 6755;// Polluted_Spell
	.@cost_zeny = 100000;

	mes "[Dark magic master]";
	mes "ข้าจัดการกับ Temporal Boots ที่มีรูเท่านั้น.";
	next;
	if (select( "กรรมวิธีการออฟ ?", "เพิ่มความสามารถให้กับ Temporal Boots" ) == 1) {
		mes "[Dark magic master]";
		mes "เจ้าควรไปถาม Hugin's magic master ในเรื่องทั่วไป. ส่วนข้าจะบอกเกี่ยวกับการออฟที่ลึกซึ้งกว่า.";
		next;
		mes "[Dark magic master]";
		mes "ข้าต้องการ 4 อย่าง. ^0000ff" + callfunc("F_InsertComma",.@cost_zeny) + "z, " + getitemname(.@polluted_id) + ", " + getitemname(.@coagulated_id) + ", Temporal Boots(รู) Series^000000. ถ้าเจ้ามีไม่ครบ, ข้าก็ช่วยเจ้าไม่ได้.";
		next;
		mes "[Dark magic master]";
		mes "มันเป็นงานอดิเรกยามว่าง ของข้า.. ในการออฟ Temporal Boots ที่มีรูจะใช้ polluted และ coagulated spell.";
		next;
		mes "[Dark magic master]";
		mes "แน่นอนว่า, เจ้าต้องยอมรับความเสี่ยงที่ของจะถูกทำลายในเมื่อเจ้าออฟซ้ำแล้วซ้ำเล่า.";
		next;
		mes "[Dark magic master]";
		mes "ลำดับการออฟจะเหมือนกับ Hugin's magic master. โดยอ้างอิงจากคำอธิบายของเขา.";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);

	switch(.@equip_id) {
	case 22006:	// Temporal_Str_Boots_
	case 22007:	// Temporal_Vit_Boots_
	case 22008:	// Temporal_Dex_Boots_
	case 22009:	// Temporal_Int_Boots_
	case 22010:	// Temporal_Agi_Boots_
	case 22011:	// Temporal_Luk_Boots_
	case 22113:	// Modified_Str_Boots_
	case 22114:	// Modified_Int_Boots_
	case 22115:	// Modified_Agi_Boots_
	case 22116:	// Modified_Vit_Boots_
	case 22117:	// Modified_Dex_Boots_
	case 22118:	// Modified_Luk_Boots_
		break;
	case -1:
		mes "[Dark magic master]";
		mes "เจ้าไม่ได้สวมใส่รองเท้ามา?";
		close;
	default:
		mes "[Dark magic master]";
		mes "นี่มันไม่ใช่รองเท้าที่ถูกต้อง. จำเอาไว้, ข้าออฟแค่ Temporal Boots ที่มีรู 6 ประเภทเท่านั้น..";
		close;
	}
	// enchants ID
	setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
	setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
	setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
	setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6

	// requirements amount
	setarray .@en_amount_coag[0],3,10,20,40,50;
	setarray .@en_amount_polluted[0],1,2,4,7,10;

	// (custom) chances of success (official value unknown)
	setarray .@chances[0],100,70,70,70,70;

	// data of item equipped
	.@equip_name$ = getequipname(EQI_SHOES);
	setarray .@card[0],
		getequipcardid(EQI_SHOES,0),
		getequipcardid(EQI_SHOES,1),
		getequipcardid(EQI_SHOES,2),
		getequipcardid(EQI_SHOES,3);
	copyarray .@equip_card[0], .@card[0], 4;
	.@equip_refine = getequiprefinerycnt(EQI_SHOES);

	if (.@card[2] > 0) {
		mes "[Dark magic master]";
		mes "รองเท้าได้ผ่านการอัพเกรดเต็มที่แล้ว. พวกเราคงทำอะไรไปมากกว่านี้ไม่ได้.";
		close;
	}
	if (.@card[3] == 0) {// 4th slot 1st try enchanting
		.@amount_coag = .@en_amount_coag[0];
		.@amount_poll = .@en_amount_polluted[0];

		mes "[Dark magic master]";
		mes "ต้องการที่จะออฟ ^0000ff" + .@equip_name$ + " (รู)^000000? สำหรับการออฟครั้งแรก, เจ้าต้องมี ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + ", " + .@amount_coag + " " + getitemname(.@coagulated_id) + ", และ " + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000.";
		mes "มันมีความเสี่ยงที่จะล้มเหลว..";
		next;
		.@s = select( "ออก", "Fighting Spirit", "Archery", "Spell", "Vitality", "Attack Speed", "Lucky" ) - 2;
		if (.@s == -1) {
			mes "[Dark magic master]";
			mes "Ok, กลับมาใหม่เมื่อเจ้าพร้อม.";
			close;
		}
		.@card[3] = .@enchant_1[.@s];
		.@string$ = "enchant ครั้งที่ ^6300001^000000.";
		.@num = 0;
	}
	else {
		for ( .@num = 1; .@num < 5; .@num++ ) {
			for ( .@type = 0; .@type < 6 && .@card[3] != getd( ".@enchant_" + .@num + "[" + .@type + "]" ); .@type++ )
				continue;
			if (.@type < 6)
				break;
		}
		if (.@num == 5) {
			mes "[Dark magic master]";
			mes "มีบางอย่างผิดพลาด.";
			close;
		}
		.@amount_coag = .@en_amount_coag[.@num];
		.@amount_poll = .@en_amount_polluted[.@num];

		mes "[Dark magic master]";
		if (.@num == 4) {
			.@card[2] = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
			.@string$ = "^990000 Bonus effect ^000000 upgrade.";
			mes "เจ้าอยากจะสุ่มโบนัสออฟพิเศษในช่องที่ 3 ไหม ? เจ้าต้องมี ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + ", " + .@amount_coag + " " + getitemname(.@coagulated_id) + ", และ " + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000. แต่มีความเสี่ยงสูงที่จะล้มเหลว..";
		}
		else {
			.@level = .@num + 1;
			.@card[3] = getd( ".@enchant_" + (.@num+1) + "[" + .@type + "]" );
			.@string$ = "enchant ครั้งที่ ^990000" + .@level + "^000000.";
			mes "การออฟ ^0000ff" + .@equip_name$ + " (รู)^000000's ช่องที่ 4 ในครั้งที่ ^0000ff" + .@level + "^000000. ต้องการ ^0000ff" + .@amount_poll + " " + getitemname(.@polluted_id) + ", " + .@amount_coag + " " + getitemname(.@coagulated_id) + ", และ " + callfunc("F_InsertComma",.@cost_zeny) + " z^000000. มันมีความเสี่ยงที่จะล้มเหลว..";
		}
		next;
		if (select( "ออก", "อัพเกรดความสามารถ!" ) == 1) {
			mes "[Dark magic master]";
			mes "Ok, กลับมาใหม่เมื่อเจ้าพร้อม.";
			close;
		}
	}
	if (countitem(.@coagulated_id) < .@amount_coag) {
		mes "[Dark magic master]";
		mes "หืมม, เจ้าไม่มี " + (.@amount_coag - countitem(.@coagulated_id)) + " " + getitemname(.@coagulated_id) + " มากพอ, ที่จะออฟมากกว่านี้.";
		close;
	}
	if (countitem(.@polluted_id) < .@amount_poll) {
		mes "[Dark magic master]";
		mes "หืมม, เจ้าไม่มี " + (.@amount_poll - countitem(.@polluted_id)) + " " + getitemname(.@polluted_id) + " มากพอ, ที่จะออฟมากกว่านี้.";
		close;
	}
	if (Zeny < .@cost_zeny) {
		mes "[Dark magic master]";
		mes "เจ้าต้องมี ^0000ff" + callfunc("F_InsertComma",.@cost_zeny) + " zeny^000000. ไม่มีของฟรีเจ้าก็รู้...";
		close;
	}
	delitem .@coagulated_id, .@amount_coag;
	delitem .@polluted_id, .@amount_poll;
	Zeny -= .@cost_zeny;

	// anti-hack
	if (callfunc("F_IsEquipIDHack", EQI_SHOES, .@equip_id) || callfunc("F_IsEquipCardHack", EQI_SHOES, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]) || callfunc("F_IsEquipRefineHack", EQI_SHOES, .@equip_refine))
		close;

	delequip EQI_SHOES;
	if (.@chances[.@num] < rand(1,100)) {
		specialeffect2 EF_LORD;
		mes "[Dark magic master]";
		mes "อ้ากกกก, มันล้มเหลว. ครั้งหน้าน่าจะดีกว่านี้.";
		close;
	}
	specialeffect2 EF_REPAIRWEAPON;
	mes "[Hugin's Magician]";
	mes "พยายามที่จะ " + .@string$;
	getitem2 .@equip_id,1,1,.@equip_refine,0,0,0,.@card[2],.@card[3];
	close;
}

// Items exchange for card
glast_01,216,292,5	script	Portrait collector#0002	4_M_OILMAN,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "กระเป๋ามีน้ำหนักเกิน. ลดน้ำหนักก่อนแล้วค่อยมาใหม่.";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "เจ้าแบกของมาเยอะเกิน. ไปลดของก่อนแล้วค่อยกลับมาใหม่.";
		close;
	}
	.@coagulated_id = 6608;		// Coagulated_Spell
	.@polluted_id = 6755;		// Polluted_Spell
	.@white_card = 4608;		// White_Knightage_Card
	.@khalitzburg_card = 4609;	// Khaliz_Knightage_Card

	mes "[Portrait collector]";
	mes "ข้าได้สะสมศิลปะมาเป็นเวลาหนึ่งปีแล้ว. ถึงเวลาที่ข้าจะเอาออกมาโชว์ให้ผู้คนได้ดูแล้ว.";
	next;
	mes "[Portrait collector]";
	mes "เฮ้, เจ้าพอจะมี " + getitemname(.@polluted_id) + " หรือ " + getitemname(.@coagulated_id) + " เยอะไหม ? ถ้าพอมี, เจ้าไม่สนที่จะทำการแลกเปลี่ยนศิลปะกับข้าดูหรอ ?";
	next;
	switch( select( "ยกเลิก", "แลกเปลี่ยนกับ " + getitemname(.@white_card), "แลกเปลี่ยนกับ " + getitemname(.@khalitzburg_card) ) ) {
	case 1:
		mes "[Portrait collector]";
		mes "เยี่ยม, OK. กลับมาใหม่เมื่อเจ้าพร้อม.";
		close;
	case 2:
		.@reward_id = .@white_card;
		.@amount_coag = 3000;
		.@amount_polluted = 70;
		break;
	case 3:
		.@reward_id = .@khalitzburg_card;
		.@amount_coag = 5000;
		.@amount_polluted = 100;
		break;
	}
	mes "[Portrait collector]";
	mes "เจ้าสนใจที่จะแลกเปลี่ยนกับ " + getitemname(.@reward_id) + "?";
	next;
	if (countitem(.@coagulated_id) < .@amount_coag)
		.@color$[0] = "^666666";
	if (countitem(.@polluted_id) < .@amount_polluted)
		.@color$[1] = "^666666";
	switch( select( "ยกเลิก", sprintf("%s%s %s^000000", .@color$[0], callfunc("F_InsertComma",.@amount_coag), getitemname(.@coagulated_id)), sprintf("%s%s %s^000000", .@color$[1], callfunc("F_InsertComma",.@amount_polluted), getitemname(.@polluted_id)) ) ) {
	case 1:
		mes "[Portrait collector]";
		mes "เยี่ยม, OK. กลับมาใหม่เมื่อเจ้าพร้อม.";
		close;
	case 2:
		.@id = .@coagulated_id;
		.@amount = .@amount_coag;
		break;
	case 3:
		.@id = .@polluted_id;
		.@amount = .@amount_polluted;
		break;
	}
	if (countitem(.@id) < .@amount) {
		mes "[Portrait collector]";
		mes "หืมม, เจ้ามีของไม่พอ. ไปเอามาเพิ่มซะ.";
		close;
	}
	mes "[Portrait collector]";
	mes "เจ้าแน่ใจแล้วใช่ไหม ?";
	next;
	if (select( "ยกเลิก", "แน่นอน" ) == 1) {
		mes "[Portrait collector]";
		mes "เยี่ยม, OK. กลับมาใหม่เมื่อเจ้าพร้อม.";
		close;
	}
	mes "[Portrait collector]";
	mes "เป็นการแลกเปลี่ยนที่ดี.";
	delitem .@id, .@amount;
	getitem .@reward_id,1;
	close;
}
