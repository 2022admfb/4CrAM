//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By:================================================== 
//= rAthena Dev Team
//===== Current Version:===================================== 
//= 1.0
//===== Compatible With:===================================== 
//= rAthena Project
//===== Description:========================================= 
//= [Official Conversion]
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments:================================= 
//= 1.0 Split Geneticist shop from main file. [Euphy]
//=     Updated dialogue to match the official script.
//============================================================ 

// Main NPC::craft_book_alche_skill_npc
//============================================================
alde_alche,31,186,3	script	Craft Book Merchant#alde	883,{
	mes "[Craft Book Merchant]";
	mes "ยินดีต้อนรับ.";
	mes "ฉันมาที่นี่เพื่อขาย";
	mes "^0000FFหนังสืองานฝีมือพิเศษ ^000000 สำหรับนักพันธุศาสตร์";
	next;
	mes "[Craft Book Merchant]";
	mes "นักพันธุศาสตร์มีทักษะบางอย่าง";
	mes "ที่ต้องใช้หนังสืองานฝีมือเพื่อเปิดใช้งาน";
	next;
	mes "[Craft Book Merchant]";
	mes "ยิ่งคุณมีหนังสืองานฝีมือมากเท่าไหร่";
	mes "คุณสามารถใช้ทักษะได้มากขึ้น";
	next;
	mes "[Craft Book Merchant]";
	mes "โปรดเข้าใจว่า";
	mes "ฉันเชี่ยวชาญในการขายหนังสือพิเศษเหล่านี้และ";
	mes "ฉัน ^FF0000 ไม่ขายสินค้าอื่น ๆ ^000000";
	next;
	mes "[Craft Book Merchant]";
	mes "คุณต้องการดูหนังสืองานฝีมือของฉันหรือไม่?";
	next;
	if(select("แน่นอน:ไม่.") == 2) {
		mes "[Craft Book Merchant]";
		mes "ขอบคุณสำหรับการเยี่ยมชมร้านค้าของฉัน";
		mes "ขอให้ปลอดภัยในการเดินทาง";
		close;
	}
	if (checkweight(1201,1) == 0) {
		mes "[Craft Book Merchant]";
		mes "ขออภัยพื้นที่ กระเป๋า ของคุณใกล้เต็มแล้ว";
		mes "กรุณาล้างพื้นที่ กระเป๋า ของคุณก่อน";
		close;
	}
	if (MaxWeight - Weight < 2500) {
		mes "[Craft Book Merchant]";
		mes "กรุณาล้างพื้นที่่ กระเป๋า ของคุณก่อน";
		close;
	}
	mes "[Craft Book Merchant]";
	mes "เอาล่ะนี่คือรายชื่อหนังสืองานฝีมือที่มีอยู่";
	next;
	switch(select("[Apple Bomb Craft Book] 100,000 zeny:[Pineapple Bomb Craft Book] 100,000 zeny:[Coconut Bomb Craft Book] 100,000 zeny:[Melon Bomb Craft Book] 100,000 zeny:[Banana Bomb Craft Book] 100,000 zeny:[Plant Gene Cultivation Method] 100,000 zeny:[Superior Potion Craft Manual] 100,000 zeny:[Mix Cooking Book] 100,000 zeny:[Health Improvement Research Book] 100,000 zeny:[Vigor Drink Recipe] 100,000 zeny:Close")){
	case 1:set .@item,6279; break;
	case 2:set .@item,6280; break;
	case 3:set .@item,6281; break;
	case 4:set .@item,6282; break;
	case 5:set .@item,6283; break;
	case 6:set .@item,6284; break;
	case 7:set .@item,6285; break;
	case 8:set .@item,11022; break;
	case 9:set .@item,11023; break;
	case 10:set .@item,11024; break;
	case 11:
		mes "[Craft Book Merchant]";
		mes "ขอบคุณที่อุดหนุน.";
		mes "โปรดมาอีกครั้ง.";
		close;
	}
	mes "[Craft Book Merchant]";
	mes "^0000FF ["+getitemname(.@item)+"] ค่าใช้จ่าย";
	mes "100,000 zeny";
	mes "คุณต้องการซื้อจำนวนเท่าใด";
	next;
	input .@amount;
	if (.@amount == 0) {
		mes "[Craft Book Merchant]";
		mes "คุณต้องการดูหนังสือที่แตกต่างกันหรือไม่?";
		close;
	}
	if (.@amount < 1 || .@amount > 99 ){
		mes "[Craft Book Merchant]";
		mes "คุณไม่สามารถซื้อมากกว่า 100 ครั้งได้";
		close;
	}
	mes "[Craft Book Merchant]";
	mes "คุณเข้ามาแล้ว"+.@amount+"x ^0000FF ["+getitemname(.@item)+"] ^000000 ที่จะซื้อ";
	mes "คุณต้องการดำเนินการต่อหรือไม่";
	next;
	if (select("ใช่:ไม่ใช่") == 1) {
		set .@total, .@amount * 100000;
		if (Zeny < .@total) {
			mes "[Craft Book Merchant]";
			mes "ฉันขอโทษ แต่คุณมีเงินไม่พอ";
			close;
		}
		if (checkweight(.@item,.@amount) == 0){
			mes "[Craft Book Merchant]";
			mes "ดูเหมือนคุณจะแบกทุกอย่างไม่ได้";
			mes "โปรดตรวจสอบพื้นที่ในคลังของคุณ";
			close;
		}
		mes "[Craft Book Merchant]";
		mes "ขอบคุณที่อุดหนุน.";
		set Zeny,Zeny - .@total;
		getitem .@item,.@amount;
		close;
	}
	mes "[Craft Book Merchant]";
	mes "กรุณาสละเวลาของคุณ";
	mes "ก่อนตัดสินใจ";
	close;
}

