auction_03,32,72,4	script	::ShadowBlacksmith	-1,{
	.@zeny_cost = 200000; // Zeny cost is 200,000 according to official script [Secret]
	disable_items;
	mes "[Shadow Blacksmith]";
	mes "คุณต้องการ ปรับแต่ง ไอเท็มเงาหรือไม่ ? เลือกยาพิษ!"; 
	next;
	setarray .@indices[1], EQI_SHADOW_ARMOR, EQI_SHADOW_WEAPON, EQI_SHADOW_SHIELD, EQI_SHADOW_SHOES, EQI_SHADOW_ACC_R, EQI_SHADOW_ACC_L;
	.@indlen = getarraysize(.@indices) - 1;
	for(.@i = 1; .@i <= .@indlen; .@i++)
		.@menu$ = .@menu$ + (getequipisequiped(.@indices[.@i]) ? getequipname(.@indices[.@i]) : F_getpositionname(.@indices[.@i]) +"-[Not equipped]") +":";
	.@menu$ = .@menu$ + "Refine info";
	.@choice = select(.@menu$);
	if (.@choice == .@indlen + 1) { // Refine info
		mes "[Shadow Blacksmith]";
		mes "เมื่อไอเทมเงาถูกขัดเกลามันจะได้รับโบนัสพิเศษมากเหมือนไอเท็มปกติ";
		next;
		mes "[Shadow Blacksmith]";
		mes "อาวุธ: ATK, MATK + 1 เพิ่มขึ้นสำหรับความสำเร็จในการปรับแต่ง +1 แต่ละครั้ง"; 
		mes "Etc: เพิ่ม HP + 10 สำหรับความสำเร็จในการปรับแต่ง +1 แต่ละครั้ง"; 
		next;
		mes "[Shadow Blacksmith]";
		mes "Oridecon และ Elunium ทุกประเภทสามารถใช้ในการปรับแต่งไอเท็มเงาได้แต่ละครั้งจะมีค่าใช้จ่าย 20,000 zeny ด้วย"; 
		next;
		mes "[Shadow Blacksmith]";
		mes "แร่ HD สามารถใช้กับอุปกรณ์ที่มีการ ปรับแต่งระดับ +7 เป็นอย่างน้อยและจะป้องกันการแตกหักตราบเท่าที่คุณยังคุยกับฉันอยู่"; 
		close;
	}

	.@part = .@indices[.@choice];

	if (!getequipisequiped(.@part)) {
		mes "[Shadow Blacksmith]";
		mes "คุณยังไม่ได้ติดตั้งอุปกรณ์ !!. ";
		close;
	}
	.@equip_id = getequipid(.@part);
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);

	while(1) {
		.@equip_refine = getequiprefinerycnt(.@part);
		mes "[Shadow Blacksmith]";
		mes "ฉันต้องการ" + callfunc("F_InsertComma", .@zeny_cost) +  "zeny เป็นค่าธรรมเนียมสำหรับการพยายามปรับแต่งแต่ละครั้ง"; 
		mes "เลือกแร่ของคุณและเริ่มการกลั่น"; 
		next;

		.@material[0] = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
		.@material[1] = getequiprefinecost(.@part, REFINE_COST_ENRICHED, REFINE_MATERIAL_ID);
		.@material[2] = getequiprefinecost(.@part, REFINE_COST_HD, REFINE_MATERIAL_ID);

		if (countitem(.@material[0]))
			.@mate$[0] = getitemname(.@material[0]);
		else
			.@mate$[0] = "^8C8C8C"+ getitemname(.@material[0]) +"^000000";
		if (countitem(.@material[1]))
			.@mate$[1] = getitemname(.@material[1]);
		else
			.@mate$[1] = "^8C8C8C"+ getitemname(.@material[1]) +"^000000";
		if (.@equip_refine > 6 && countitem(.@material[2]))
			.@mate$[2] = getitemname(.@material[2]);
		else
			.@mate$[2] = "^8C8C8C"+ getitemname(.@material[2]) +"^000000";

		switch( select( .@mate$[0], .@mate$[1], .@mate$[2], "Cancel" ) ) {
		case 1:// NORMAL
			.@choose = .@material[0];
			.@refine_type = REFINE_COST_NORMAL;
			break;
		case 2:// ENRICHED
			.@choose = .@material[1];
			.@refine_type = REFINE_COST_ENRICHED;
			break;
		case 3:// HD
			if (.@equip_refine < 7) {
				mes "[Shadow Blacksmith]";
				mes "คุณได้ยกเลิกการปรับแต่งแล้ว"; 
				close;
			}
			.@hoihoi = 1;
			.@choose = .@material[2];
			.@refine_type = REFINE_COST_HD;
			break;
		case 4:
			mes "[Shadow Blacksmith]";
			mes "คุณได้ยกเลิกการปรับแต่งแล้ว"; 
			close;
		}
		if (!countitem(.@choose)) {
			mes "[Shadow Blacksmith]";
			mes "คุณมีไม่พอ"+ getitemname(.@choose) +".";
			close;
		}
		if (Zeny < .@zeny_cost) {
			mes "[Shadow Blacksmith]";
			mes "คุณมี zeny ไม่พอ"; 
			close;
		}
		if (.@equip_refine > 9) {
			mes "[Shadow Blacksmith]";
			mes "อุปกรณ์เงาสามารถปรับแต่งได้สูงสุด 10 ... "; 
			close;
		}
		if (!getequipisenableref(.@part)) {
			mes "[Shadow Blacksmith]";
			mes "สิ่งนี้ไม่สามารถกลั่นได้"; 
			close;
		}
		if (getequippercentrefinery(.@part, .@refine_type) < 100) {
			mes "[Shadow Blacksmith]";
			mes "ระดับการปรับแต่งความปลอดภัยของอุปกรณ์เงาคือ +4"; 
			if (!.@hoihoi)
				mes "อุปกรณ์เงาอาจถูกทำลายในการปรับแต่งครั้งต่อ ๆ ไปอยากเริ่มไหม"; 
			else
				mes "การปรับแต่งครั้งต่อไปถ้าล้มเหลวจะทำให้การปรับแต่งลดลงคุณต้องการปรับแต่งหรือไม่"; 
			next;
			if (select("Proceed","Cancel") == 2) {
				mes "[Shadow Blacksmith]";
				mes "ฉันรู้แล้ว!"; 
				close;
			}
		}
		mes "[Shadow Blacksmith]";
		mes "ถ้าอย่างนั้น .. ก็ไม่มีอะไรเกิดขึ้น!"; 
		next;
		delitem .@choose,1;
		Zeny -= .@zeny_cost;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) || callfunc("F_IsEquipRefineHack", .@part, .@equip_refine))
			close;

		if (getequippercentrefinery(.@part, .@refine_type) > rand(100)) {
			successrefitem .@part;
			mes "[Shadow Blacksmith]";
			mes "ได้ผล! ได้ผล!  ยินดีด้วย .."; 
			next;
		} else {
			if (.@hoihoi)
				downrefitem .@part;
			else
				failedrefitem .@part;
			mes "[Shadow Blacksmith]";
			mes "โอ้ !!! ม้ายยยย ไว้คราวหน้า ข้าจะทำให้ดีกว่านี้";
			close;
		}
	}
}

//moc_paraup,45,185,5	duplicate(ShadowBlacksmith)	Shadow Blacksmith#eden1	4_F_JOB_BLACKSMITH // Commented out until it's added to the map index
prt_in,61,54,3	duplicate(ShadowBlacksmith)	Shadow Blacksmith#itemmall	4_F_JOB_BLACKSMITH
auction_03,32,72,4	duplicate(ShadowBlacksmith)	Shadow Refine#itemmall	4_F_JOB_BLACKSMITH