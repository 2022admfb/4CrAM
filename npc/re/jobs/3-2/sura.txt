//===== rAthena Script ======================================= 
//= Sura Job Quest
//===== By: ================================================== 
//= Muad_Dib, Gepard
//===== Current Version: ===================================== 
//= 1.5
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Job change Quest from Monk / Champion -> Sura.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Masao]
//= 1.1 Removed disablenpc which causes players stucked in waitingroom. [Joseph]
//= 1.2 Updated script, optimized. [Euphy]
//= 1.3 Added GM management function. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//= 1.5 Fixed players stuck in waiting room, event triggers properly [Luciar]
//============================================================

ve_in,237,125,0	script	King Crab#job_shu	107,{
	if (job_shu == 0) {
		if (BaseJob == Job_Monk && BaseLevel >= 99 && JobLevel >= 50) {
			set .@job_check,1;
			if (VIP_SCRIPT) {
				callfunc "VIP_Third_Class";
				set job_shu,1;
				setquest 11155;
				next;
			}
		}
		mes "[King Crab]";
		mes "ค๊ะฮ่าฮ่าฮ่า";
		mes "หนูน้อยคนนั้นโดนระเบิดเพียงครั้งเดียวของฉัน!";
		next;
		mes "[Sludge Worm]";
		mes "พวกที่หยิ่งผยองสมควรได้รับบทเรียนอันขมขื่น";
		mes "มือใหม่ตัวน้อยกล้าโจมตีคุณได้ยังไงเนี่ยหัวหน้า!";
		next;
		if (!.@job_check) {
			mes "[King Crab]";
			mes "ค๊ะฮ่าฮ่าฮ่า !!!!";
			mes "เบียร์อีกหนึ่งแก้วที่นี่ !!!";
			mes "วันนี้จะดื่ม !!!";
			next;
			mes "[Sludge Worm]";
			mes "ราชาปู !!! จะติดตามตลอดไป !!";
			close;
		}
		mes "[King Crab]";
		mes "ฮ่าฮ่าฮ่าฮ่าฮ่า !!!!";
		next;
		mes "[Sludge Worm]";
		mes "เฮ้! ที่นี่ !!! เบียร์อีกแก้ว !!!";
		mes "ทำอะไรน่ะถ้วยของเจ้านายฉันหมดแล้ว! มาเร็ว!";
		next;
		mes "[Waitress]";
		mes "ใช่ฉันจะเอามาให้ทันที";
		next;
		mes "[Sludge Worm]";
		mes "ช้าเกินไป !!! คิดว่าเจ้านายของฉันเป็นใครหือ!?";
		next;
		mes "[Waitress]";
		mes "ฉัน ... ฉันขอโทษจริงๆครับ ... ";
		next;
		mes "[King Crab]";
		mes "K k k k k. พอแล้ว";
		mes "ผู้หญิงน่ารักคนนั้นดูเหมือนจะกลัวเธอ!";
		mes "เฮ้ผู้หญิง ~ เราไม่ใช่คนน่ากลัว ~~";
		next;
		mes "[Waitress]";
		mes "อ๊าาาาาาาาาาอย่าทำแบบนี้ !!";
		next;
		mes "- Bang -";
		donpcevent "???#bcmd::OnEnable";
		next;
		mes "[???]";
		mes "นี่ไง";
		mes "ฉันแอบดูคุณทำหน้าบูดเหมือนคุณเป็นเจ้าของที่นี่";
		next;
		mes "[???]";
		mes "เสียงดังเกินไปแล้วถ้าจะพูดเรื่องไร้สาระอีกก็กลับบ้านเถอะ";
		next;
		mes "[Sludge Worm]";
		mes "อะไรนะ!";
		mes "ฉันไม่รู้ว่าคุณเป็นใคร แต่คุณต้องเสียสติไปแล้ว!";
		mes "รู้มั้ยว่าเจ้านายของฉันคือใคร !!";
		next;
		mes "[Sludge Worm]";
		mes "นี่มันปูยักษ์ !!!";
		mes "ใครสามารถทำลายกองทัพทั้งหมดด้วยหมัดของเขา !!!";
		next;
		mes "[???]";
		mes "คุณทำสิ่งสกปรกทุกอย่างเช่นหน้าสกปรกของคุณ";
		mes "คุณเป็นแค่หัวขโมยที่ทำให้คุณพอใจ";
		next;
		mes "[King Crab]";
		mes "อะไร";
		mes "คุณกล้าพูดแบบนั้นได้ยังไง";
		mes "เพียงเพราะคุณเป็นผู้หญิงไม่ได้หมายความว่าฉันจะปล่อยมันไป";
		next;
		mes "[Sludge Worm]";
		mes "หึ! เจ้านายบ้าจริง!";
		mes "ฉันสามารถเอาชนะเธอได้ด้วยการเป่าครั้งเดียว ... ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "เฮ้! คุณกล้าใช้ความรุนแรงกับผู้หญิงได้ยังไง!?";
		next;
		mes "[???]";
		mes "ถอยออกไปเถอะเจ้าน้อย ... ";
		next;
		mes "[???]";
		mes "โอ้ฉันดีใจที่ได้ยินเช่นนั้น";
		mes "ฉันจะไม่โจมตีก่อนเพราะฉันอยู่ในระหว่างการฝึก";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "อะไร";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "อื้อ?!";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "อ๊าาาาาาาาาา !!!!!!!!";
		specialeffect EF_FIRESPLASHHIT,AREA,"King Crab#job_shu";
		specialeffect EF_FIRESPLASHHIT,AREA,"Sludge Worm#job_shu";
		next;
		mes "- พลังมหัศจรรย์อะไรเบอร์นั้น -";
		mes "- ฉันไม่เคยเห็นแบบนี้ -";
		mes "- สกิลก่อน ... -";
		next;
		mes "[???]";
		mes "อารมณ์บูด ... ";
		next;
		mes "[Waitress]";
		mes "ท่า ... ขอบคุณจริงๆ ... ";
		mes "ฉันจะตอบแทนคุณได้อย่างไร";
		next;
		mes "[???]";
		mes "ไม่ใช่เรื่องใหญ่ไม่เป็นไร";
		next;
		mes "[Waitress]";
		mes "คุณจะกลับที่พักหรือเปล่า";
		mes "ถ้าตกลงกับคุณได้ฉันขอส่งอาหารให้คุณได้ไหม";
		next;
		mes "[???]";
		mes "โอ้ไม่จำเป็นจริงๆ";
		mes "แค่ขนมปังกับซุปอุ่น ๆ ก็เพียงพอแล้วสำหรับฉัน";
		mes "งั้นฉันต้องลาก่อนบาย";
		donpcevent "???#bcmd::OnDisable";
		next;
		mes "[Waitress]";
		mes "บาย ... ";
		mes "...";
		mes ".....";
		mes "........(แก้มเปล่งปลั่ง)";
		set job_shu,1;
		setquest 11155;
		close;
	} else {
		if (strnpcinfo(0) == "King Crab#job_shu") {
			mes "[King Crab]";
			mes "มือใหม่ตัวเล็ก ๆ มาเขี่ยฉันได้ยังไง Me! The great King Crab ... ";
		} else {
			mes "[Sludge Worm]";
			mes "บอส ... ตอนนี้เราพ้นตำแหน่งนายกไปแล้วเหรอ";
			mes "สะอื้น ... สะอื้น ... ";
		}
		close;
	}
}
ve_in,241,128,4	duplicate(King Crab#job_shu)	Sludge Worm#job_shu	110

ve_in,244,126,3	script	Waitress#job_shu	69,{
	mes "[Waitress]";
	if (job_shu == 0) {
		mes "ไม่นะ ~";
		mes "ฉันเกลียดลูกค้าที่มีเสียงดังและไม่สุภาพ!";
		mes "ฉันหวังว่าจะมีคนด่าว่าฉันนะ";
		close;
	} else if (job_shu == 1) {
		if (BaseJob == Job_Monk) {
			mes "โอ้ที่รัก ~";
			mes "ฉันขอบคุณจริงๆ" + jobname (Class) + "ที่ช่วยฉันมาสักพักแล้ว";
			next;
		}
		mes "อ่าคนที่เราเคยเห็นมาก่อนเหรอ";
		mes "เธออยู่ในหมู่บ้านนี้มาหลายวันแล้ว";
		mes "เธอไม่เก่งขนาดนี้เลยเหรอ";
		next;
		mes "[Waitress]";
		mes "ถึงเธอจะเป็นผู้หญิงเหมือนฉัน แต่เธอก็เท่มาก!";
		mes "อ้าาา ~ หุ่นสวยและน่าฟัดขนาดนั้น ... ";
		next;
		mes "[Waitress]";
		mes "โอ้ย !!";
		mes "ฉันลืมทำอะไรบางอย่าง!";
		mes "ฉันควรจะทำอาหารทันทีแล้วก็เอาอาหารไปที่โรงแรมเลยอ่าาา ~";
		next;
		mes "[Waitress]";
		mes "ถ้าคุณยังไม่รู้ว่าจะพักที่ไหนโปรดใช้ ^ f57d7dthe Inn ที่ด้านบนของอาคารนี้ ^ 000000";
		close;
	}
	mes "ตอนนี้ลูกค้าเงียบไปหน่อย";
	mes "อ่ารู้สึกดีขึ้นแล้ว !!";
	close;
}

ve_in,240,131,0	script	???#bcmd	484,{
	end;
OnInit:
	disablenpc "???#bcmd";
	end;
OnEnable:
	enablenpc "???#bcmd";
	initnpctimer;
	end;
OnDisable:
	disablenpc "???#bcmd";
	stopnpctimer;
	end;
OnTimer600000:
	donpcevent "???#bcmd::OnDisable";
	stopnpctimer;
	end;
}

ve_in,97,149,0	script	#Suraryokan	139,3,3,{
OnTouch:
	if (job_shu == 1) {
		set .@n$, "["+strcharinfo(0)+"]";
		mes "[???]";
		mes "นี่ใคร?!";
		next;
		mes .@n$;
		mes "ฉันเป็นคนที่ผับเมื่อครู่นี้เอง";
		mes "มีอะไรจะถาม";
		next;
		mes "[???]";
		mes "อย่ามารบกวนฉันแค่ไป!";
		next;
		mes .@n$;
		mes "ได้โปรดฉันแค่อยากคุย";
		next;
		mes "[???]";
		mes "...";
		next;
		mes .@n$;
		mes "อย่าสนใจฉันแค่คุยกับฉัน";
		next;
		mes .@n$;
		mes "แป๊บเดียวไม่เป็นไรเหรอ";
		next;
		mes .@n$;
		mes "ฉันไม่ได้มาที่นี่เพื่อทำร้ายคุณ";
		next;
		mes .@n$;
		mes "คุณจะปฏิบัติกับคนที่มาที่ประตูของคุณได้อย่างไร?";
		next;
		mes .@n$;
		mes "คุณจะไปไกลเกินไป";
		next;
		mes .@n$;
		mes "หลังจากที่ฉันเห็นคุณต่อสู้ ... ";
		next;
		mes .@n$;
		mes "ฉันคิดว่าคุณต้องฝึกศิลปะการต่อสู้และฉันมาที่นี่เพื่อพบคุณ";
		next;
		mes .@n$;
		mes "แต่ที่เห็นนายยืนฉันอยู่ข้างนอกแบบนี้ ... ";
		next;
		mes .@n$;
		mes "คุณต้องก้าวหน้าในศิลปะการต่อสู้ ... ";
		next;
		mes "[???]";
		mes "ดื้อแค่ไหน ... ";
		next;
		mes "[???]";
		mes "คุณต้องการอะไร?";
		set job_shu,2;
		close;
	}
	end;
}

ve_in,98,159,0	script	Fighter#job_shu	484,{
	if (job_shu < 2) {
		mes "[Fighter]";
		mes "อ่าาา ~ เหนื่อยจัง";
		mes "การฝึกมันยากมากและฉันต้องไปอีกยาวไกล!";
		close;
	} else if (job_shu == 2) {
		mes "["+strcharinfo(0)+"]";
		mes "ฉันอยากรู้เกี่ยวกับสกิลที่คุณใช้เมื่อสักครู่";
		next;
		mes "[???]";
		mes "...";
		mes "......";
		mes ".........";
		mes "............ มือใหม่อย่างเธอไม่จำเป็นต้องรู้";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "มือใหม่?!";
		mes "ฉันฝึกศิลปะการต่อสู้มาเรื่อย ๆ ";
		mes "ฉันมั่นใจว่าจะไม่มีวันแพ้";
		next;
		mes "[???]";
		mes "งั้นคุณก็เอาชนะฉันได้เหมือนกัน";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ไม่ ... ";
		mes "ฉันคิดว่ามันมากเกินไป ... ";
		mes "พูดตรงๆฉันรู้สึกทึ่งมากสำหรับคุณ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ฉันเคยคิดว่าตัวเองแข็งแกร่งพอ แต่หลังจากที่ได้เห็นคุณต่อสู้ฉันก็รู้ว่าฉันเต็มไปด้วยความคิด";
		next;
		mes "[???]";
		mes "Huuuh ~ ไม่ว่าคุณจะแข็งแกร่งแค่ไหนก็ตาม";
		mes "คุณไม่สามารถหลีกเลี่ยงการเข้าถึงขีด จำกัด ที่แท้จริงของคุณได้";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "คุณเป็นแค่คนที่ฉันคาดหวัง";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ฉันคิดว่าคุณคงจะรู้";
		mes "จะเอาชนะข้อ จำกัด นี้ได้อย่างไร";
		next;
		mes "[???]";
		mes "หยุดเถอะ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อะไรทำไม";
		next;
		mes "[???]";
		mes "คุณเป็นมนุษย์หรือเปล่า";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อะไร";
		mes "แน่นอนฉันเป็นมนุษย์";
		next;
		mes "[???]";
		mes "การจะแข็งแกร่งขึ้นนั้นยากเกินไปสำหรับมนุษย์คนหนึ่ง";
		mes "ถ้าคุณต้องการเพียงแค่ซื้ออุปกรณ์ที่ดีกว่า";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "แล้วเธอเป็นอะไร";
		mes "คุณหมายความว่าคุณไม่ใช่มนุษย์?";
		next;
		mes "[???]";
		mes "เกินขีด จำกัด ... ";
		mes "เป็นไปไม่ได้สำหรับมนุษย์";
		mes "มันเป็นทางของผี ... ";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ผี?";
		next;
		mes "[???]";
		mes "ฉันเลิกเป็นมนุษย์แล้วหลังจากเลือกทางนี้";
		mes "หนทางสู่การเป็นสุระละทิ้งการปกป้องจากพระเจ้า";
		next;
		mes "[???]";
		mes "คุณจะไม่มีเวลาพักผ่อนถ้าเลือกทางนี้";
		mes "คุณจะกลายเป็นหุ่นเชิดสำหรับสงคราม ... ";
		next;
		switch(select("แต่ฉันยังอยากรู้มากกว่านี้: งั้นฉันเลิกแล้ว")) {
		case 1:
			mes "[???]";
			mes "คุณไม่ใช่คนงี่เง่าเหรอ";
			mes "ทำไมคุณถึงมีความสุขกับการเป็นผี";
			next;
			mes "[???]";
			mes "คุณจะเสียใจดังนั้นจงพอใจกับชีวิตปัจจุบันของคุณ";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ตอนนี้ฉันได้พบคุณแล้วฉันจะไม่ใช้ชีวิตแบบนี้อีกต่อไป!";
			next;
			mes "[???]";
			mes "ฮะ ... เธอทำให้ฉันเหนื่อยจริงๆ";
			next;
			mes "[???]";
			mes "เอาล่ะ ^ f57d7d ไปฆ่า Desert Wolves 100 ตัว ^ 000000";
			mes "หลังจากนั้นฉันจะคิดเรื่องนี้อีกครั้ง";
			set job_shu,3;
			changequest 11155,11156;
			close;
		case 2:
			mes "[???]";
			mes "ทางเลือกที่ดี";
			mes "คุณไม่จำเป็นต้องมองหาปัญหา";
			close;
		}
	} else if (job_shu == 3) {
		if (checkquest(11156,HUNTING) == 2) {
			mes "[???]";
			mes "ว้าว ~";
			mes "คุณฆ่าหมาป่าพวกนั้นจริงๆเหรอ?";
			mes "ฉันคิดว่าคุณยอมแพ้แล้ว";
			next;
			mes "[???]";
			mes "คำพูดของผู้หญิงคือความผูกพัน !!";
			mes "คำสัญญาคือคำสัญญา";
			next;
			mes "[???]";
			mes "ไปที่ ^ f57d7dEl Mes Gorge ทางตะวันออกเฉียงใต้ของ Juno ^ 000000";
			mes "เพื่อนร่วมงานของฉัน ^ f57d7dBuddy ^ 000000 จะฝึกตัวเองใกล้ใจกลางพื้นที่นั้น";
			next;
			mes "[Bruno]";
			mes "บอกเขาว่า 'บรูโน' ส่งคุณไปที่นั่นและหลังจากนั้นก็เหมาะกับตัวเอง";
			set job_shu,4;
			changequest 11156,11157;
			close;
		}
		mes "[???]";
		mes "ฮะ ... เธอทำให้ฉันเหนื่อยจริงๆ";
		next;
		mes "[???]";
		mes "เอาล่ะ ^ f57d7d ไปฆ่า Desert Wolves 100 ตัว ^ 000000";
		mes "แล้วฉันจะคิดเรื่องนี้อีกครั้ง";
		close;
	} else if (job_shu == 4) {
		mes "[Bruno]";
		mes "ไปที่ ^ f57d7dEl Mes Gorge ทางตะวันออกเฉียงใต้ของ Juno ^ 000000";
		mes "เพื่อนร่วมงานของฉัน ^ f57d7dBuddy ^ 000000 จะฝึกตัวเองใกล้ใจกลางพื้นที่นั้น";
		next;
		mes "[Bruno]";
		mes"บอกเขาว่า ''Bruno' ส่งคุณไปที่นั่นและหลังจากนั้นก็เหมาะกับตัวเอง";
		close;
	} else if (job_shu == 100) {
		mes "[Bruno]";
		mes "เฮ้ ~!";
		mes "เป็นไงบ้าง";
		next;
		mes "[Bruno]";
		mes "คุณไม่มีของขวัญให้ครูหรือ";
		close;
	}
	mes "[Bruno]";
	mes "ฉันขอแนะนำให้คุณพิจารณาใหม่ว่าเป็น Sura";
	mes "สุระแข็งแกร่งและเท่ห์ แต่นั่นไม่ใช่ทั้งหมด";
	next;
	mes "[Bruno]";
	mes "ฉัน?";
	mes "ถ้าฉันต้องเลือกงานอีกฉันจะเลือก Sura แน่นอน";
	close;
}

yuno_fild07,254,176,7	script	Buddy#job_shu	753,{
	if (job_shu < 4) {
		mes "[Buddy]";
		mes "Haaap!!! Haaaap!!! Whoo!! Haahhp!!!";
		next;
		mes "[Buddy]";
		mes "ฉันไม่รู้ว่าทำไมคุณถึงมาที่นี่ แต่โปรดอย่ายืนอยู่ข้างหลังฉันจนกว่าคุณจะอยากตาย";
		close;
	} else if (job_shu == 4) {
		mes "[Buddy]";
		mes "Haaap!!! Haaaap!!! Whoo!! Haahhp!!!";
		next;
		mes "[Buddy]";
		mes "ฉันไม่รู้ว่าทำไมคุณถึงมาที่นี่ แต่โปรดอย่ายืนอยู่ข้างหลังฉันจนกว่าคุณจะอยากตาย";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "'Bruno'  ส่งฉันมาที่นี่";
		next;
		mes "[Buddy]";
		mes "อ๊ะ! เพื่อนร่วมงานแนะนำฉันให้รู้จัก!";
		mes "อย่างไรก็ตามทำไมคุณมาที่นี่เพื่อดูฉัน";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ฉันถามบรูโนเกี่ยวกับความแข็งแกร่งของเธอและเธอก็แนะนำฉันให้กับคุณ";
		next;
		mes "[Buddy]";
		mes "เด็กผู้หญิงคนนั้น ... ไม่หรอกล้อเล่นนะ";
		mes "คุณต้องเป็นคนพิเศษเพื่อให้เธอส่งคุณมาหาฉัน";
		next;
		mes "[Buddy]";
		mes "อืมฉันไม่สามารถตอบคำถามของคุณได้เพราะฉันอยู่ในขั้นตอนการฝึกฝนตนเองดังนั้นฉันจะพาคุณไปหานายของเรา";
		next;
		mes "[Buddy]";
		mes "เจ้านายของฉันไม่ชอบเสียงรบกวนดังนั้นโปรด ^ f57d7dwait ในห้องนั่งเล่นเงียบ ๆ ^ 000000";
		next;
		mes "[Buddy]";
		mes "ทางนี้ ... ";
		set job_shu,5;
		changequest 11157,11158;
		close2;
		warp "sword_1-1",215,244;
		end;
	} else if (job_shu == 100) {
		mes "[Buddy]";
		mes "อ๊ะไม่เห็นนานเลย" + strcharinfo (0) + ".";
		mes "คุณมาที่นี่เพื่อพบเจ้านายของฉันหรือไม่";
		next;
		switch(select("Yes:No")) {
		case 1:
			mes "[Buddy]";
			mes "ฉันจะพาไปทันที";
			close2;
			warp "sword_1-1",216,168;
			end;
		case 2:
			mes "[Buddy]";
			mes "แล้วทำไมคุณถึงมาที่นี่";
			mes "คุณไม่ได้มาที่นี่เพื่อพบฉันไม่ใช่เหรอ";
			close;
		}
	}
	mes "[Buddy]";
	mes strcharinfo (0) + "คุณยังไม่ได้พบกับนายหรือ";
	next;
	mes "[Buddy]";
	mes "เจ้านายของฉันไม่ชอบเสียงรบกวนดังนั้นโปรด ^ f57d7dwait ในห้องนั่งเล่นเงียบ ๆ ^ 000000";
	next;
	mes "[Buddy]";
	mes "ทางนี้ ... ";
	close2;
	warp "sword_1-1",215,244;
	end;
}

sword_1-1,223,243,4	script	Drawing Room	753,{
	end;
OnInit:
	waitingroom "Drawing Room",20,"Drawing Room::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "sword_2-1",223,205;
	donpcevent "Buddy#Sura_Salon::OnEnable";
	disablewaitingroomevent;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

sword_1-1,223,243,4	script	Buddy#job_shu_Salong	753,{
	mes "[Buddy]";
	mes "เจ้านายของฉันไม่ชอบเสียงรบกวนดังนั้นโปรด ^ f57d7dwait ในห้องนั่งเล่นเงียบ ๆ ^ 000000";
	next;
	switch(select("ไปที่ห้องนั่งเล่น: ออกไปข้างนอก")) {
	case 1:
		mes "[Buddy]";
		mes "กรุณารอที่ห้องนั่งเล่นด้านบน";
		close;
	case 2:
		mes "[Buddy]";
		mes "แล้วเจอกันใหม่";
		close2;
		warp "yuno_fild07",255,178;
		end;
	}
}

sword_2-1,1,1,0	script	Buddy#Sura_Salon	66,{
	end;
OnInit:
	disablenpc "Buddy#Sura_Salon";
	end;
OnEnable:
	enablenpc "Buddy#Sura_Salon";
	initnpctimer;
	end;
OnReset:
	killmonster "sword_2-1","Buddy#Sura_Salon::OnMyMobDead";
	end;
OnDisable:
	disablenpc "Buddy#Sura_Salon";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("sword_2-1","Buddy#Sura_Salon::OnMyMobDead") < 1) {
		monster "sword_2-1",219,210,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	}
	end;
OnTimer3000:
	mapannounce "sword_2-1","The master is now in the middle of training, and you need to wait in the living room around 5 minutes.",bc_map;
	end;
OnTimer4000:
	mapannounce "sword_2-1","Ah! You may have hallucinations in the living room, so please be careful.",bc_map;
	end;
OnTimer5000:
	mapannounce "sword_2-1","This is the policy of the master, so please don't feel unpleasant.",bc_map;
	end;
OnTimer33000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"It can't be true",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Illusion",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer93000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Just Imagination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer153000:
	mapannounce "sword_2-1","If you're bored, may I bring you some magazines?",bc_map;
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer213000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Never mind",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Can you see me?",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer273000:
	monster "sword_2-1",219,210,"",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer300000:
	mapannounce "sword_2-1","The master has arrived. Soon I'll show you into the master's room.",bc_map;
	end;
OnTimer303000:
	donpcevent "Buddy#job_shuaneh::OnEnable";
	end;
OnTimer305000:
	donpcevent "Buddy#Sura_Salon::OnReset";
	end;
OnTimer315000:
	mapannounce "sword_2-1","Come on here.",bc_map;
	donpcevent "Buddy#job_shuaneh::OnDisable";
	donpcevent "#Sura_garajjom::OnEnable";
	end;
OnTimer320000:
	mapannounce "sword_2-1","I guess you're not ready to meet the master yet...",bc_map;
	mapwarp "sword_2-1","yuno_fild07",255,178;
	donpcevent "#Sura_garajjom::OnDisable";
	donpcevent "Buddy#Sura_Salon::OnDisable";
	donpcevent "Drawing Room::OnEnable";
	end;
}

sword_2-1,223,205,7	script	Buddy#job_shuaneh	753,{
	if (job_shu > 4) {
		mes "I'll show you the way. This way.";
		close2;
		warp "sword_1-1",216,168;
		donpcevent "Buddy#job_shuaneh::OnDisable";
		end;
	}
	mes "คุณมาที่นี่ทำไม?";
	close2;
	warp "yuno_fild07",248,179;
	end;
OnInit:
OnDisable:
	disablenpc "Buddy#job_shuaneh";
	end;
OnEnable:
	enablenpc "Buddy#job_shuaneh";
	end;
}

sword_2-1,223,205,0	script	#Sura_garajjom	139,10,10,{
OnTouch_:
	warp "sword_1-1",216,168;
	end;
OnInit:
OnDisable:
	disablenpc "#Sura_garajjom";
	end;
OnEnable:
	enablenpc "#Sura_garajjom";
	end;
}

sword_1-1,222,169,5	script	Bruno#job_shu	484,{
	if (job_shu > 4) {
		mes "[Bruno]";
		mes "เฮ้ ~" +strcharinfo(0) + "ว่าไง";
		next;
		switch(select("ไปคุยกันเถอะ:ออกไปข้างนอก")) {
		case 1:
			if (job_shu == 100) {
				mes "[Bruno]";
				mes "นายของฉัน?";
				mes "เขาบอกว่าเขาทำให้โลกนี้หมดความคิดและเขาเป็นผีที่ยอมแพ้การเป็นมนุษย์ แต่ดูเหมือนว่าเขาจะมีความปรารถนาอย่างแรงกล้าต่อโลก";
				next;
				mes "[Bruno]";
				mes "ฉันก็เป็นชาวสุระเหมือนกัน แต่ฉันก็ยังสนุกกับการเดินทางและพบปะผู้คนมากมาย ~";
				next;
				mes "[Bruno]";
				mes "ในสมัยก่อนเจ้านายของฉันมีเพื่อนสนิทอยู่คนหนึ่ง แต่หลังจากกลายมาเป็นสุระ";
				next;
				mes "[Bruno]";
				mes "ฉันได้ยินมาว่าเขาเขียนจดหมายถึงเพื่อนว่าเขาเสียชีวิตแล้ว";
				mes "บางครั้งเขาก็ไปเยี่ยม สถานที่แห่งความทรงจำ อย่างลับๆ";
				next;
				mes "[Bruno]";
				mes "มันจะดีกว่าถ้าไม่บอกว่าเขาตายไปแล้วก็ไปพบเพื่อนของเขาต่อไป";
				mes "เขาเป็นคนขี้งกในโคลน";
				next;
				mes "[Bruno]";
				mes "แต่เขาเจ๋งและยอดเยี่ยมมาก";
				mes "โฮโฮ";
				next;
				mes "[Bruno]";
				mes "เหตุผลที่ฉันเดินทางไปเรื่อย ๆ คือการค้นหา 'เพื่อนที่มีค่า' ของเจ้านายของฉันและไม่ฝึกฝนตัวเองให้มากกว่านี้";
				next;
				mes "[Bruno]";
				mes "ฉันรู้สึกหนักใจเมื่อเห็นเจ้านายของฉันคิดถึงเพื่อนของเขา ... ";
				mes "ไม่ใช่ ... ไม่ใช่เพื่อ ... นาย!";
				close;
			}
			mes "[Bruno]";
			mes "ว้าว ~ ฉันคิดว่าคุณจะโดนสลบก่อนที่จะเจอเจ้านายของฉัน แต่ในที่สุดคุณก็มาถึงที่นี่แล้ว?";
			next;
			mes "[Bruno]";
			mes "อย่างไรก็ตามตอนนี้คุณอยู่ที่นี่ดังนั้นฉันจึงเป็นหัวหน้าของคุณทันที";
			next;
			mes "[Bruno]";
			mes "เราไม่สนใจอายุของคุณในโลกของเรา!";
			close;
		case 2:
			mes "[Bruno]";
			mes "กลับมาอีกครั้งเมื่อคุณต้องการ ~";
			mes "ฉันจะแสดงหลักสูตรการฝึกฝนของคนตายให้คุณดู ~";
			close2;
			warp "yuno_fild07",255,178;
			end;
		}
	}
	mes "[Bruno]";
	mes "คุณเป็นอะไร";
	mes "คุณเจอที่นี่ได้ยังไง!";
	mes "ไปให้พ้น !!!";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_1-1,223,167,2	script	Master#job_shu	483,{
	mes "[Master]";
	if (job_shu == 5) {
		mes "คุณต้องตาสว่างถึงจะเห็นว่าในที่สุดคุณก็มาถึงห้องนี้";
		next;
		mes "[Master]";
		if (SkillPoint != 0) {
			mes "คุณไม่สามารถเป็น Sura ได้เว้นแต่คุณจะใช้คะแนนทักษะทั้งหมดของคุณ";
			close;
		}
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 3500) {
			mes "ดูเหมือนคุณจะมีภาระหนักโปรดลบบางส่วนออก";
			close;
		}
		if (BaseLevel < 99 || JobLevel < 50) {
			mes "คุณดูเหมือนจะไม่ถึงขีด จำกัด ของมนุษย์คุณยังต้องไปอีกไกลเพื่อที่จะได้เป็นสุระ";
			mes "คุณดูเหมือนจะไม่ถึงขีด จำกัด ของมนุษย์คุณยังต้องไปอีกไกลเพื่อที่จะได้เป็นสุระ";
			close;
		}
		if (BaseJob != Job_Monk) {
			mes "หืม?";
			close;
		}
		mes "แน่นอน ~";
		mes "แสบตา";
		next;
		mes "[Bruno]";
		mes "อาจารย์! นี่คือเสื้อโค้ทขนหมาป่า";
		mes "ใส่ตอนอากาศหนาว";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "อ๊าก! นั่นคือหมาป่าที่ฉัน ... จับ ... อื้อ";
		next;
		mes "- Whack -";
		next;
		mes "[Bruno]";
		mes "โฮโฮโฮ ~";
		mes "คุณมีอะไรจะพูด," + strcharinfo (0) + "?";
		mes "คุณกลายเป็นสมาชิกคนหนึ่งของครอบครัวเราไปด้วยกันเถอะ ~";
		next;
		mes "[Master]";
		mes "ใช่แล้วตอนนี้ฉันรับคุณเป็นนักเรียนของฉันด้วย";
		next;
		mes "[Master]";
		mes "ฉันคิดว่าคุณคงรู้แล้ว แต่การใช้ชีวิตในฐานะสุระคงไม่ง่ายอย่างนั้น";
		next;
		mes "[Master]";
		mes "ฉันก็ปิดตัวเองจากโลกถ้ามีวันหนึ่งที่เรากลับไปที่โลกอีกครั้งมันต้องเป็นวันที่โลกต้องการผีสำหรับสงคราม";
		next;
		mes "[Master]";
		mes "ฉันหวังว่าวันนั้นจะไม่มาถึง แต่ ... ฉันจะยอมเป็นผีและฆ่าศัตรูทั้งหมด";
		next;
		if (ismounting()) {
			mes "[Master]";
			mes "งั้น ... ได้โปรดลงจากสัตว์ขี่ของคุณ!";
			mes "มันเสียมารยาทกับสถานการณ์นี้";
			close;
		}
		mes "[Master]";
		mes "คุณควรฝึกฝนตัวเองเรื่อย ๆ จนถึงวันนั้น";
		set job_shu,100;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11158;
		getitem 5754,1; //Blazing_Soul
		getitem 2795,1; //Green_Apple_Ring
		close;
	} else if (job_shu > 5) {
		mes "ครั้งหนึ่งฉันยังเป็นชายหนุ่มธรรมดาที่เดินทางบ่อยและได้พบเพื่อนมากมาย";
		next;
		mes "[Master]";
		mes "อย่างที่คุณรู้สึกในที่สุดฉันก็ต้องเผชิญกับข้อ จำกัด ของฉัน";
		next;
		mes "[Master]";
		mes "บางครั้งฉันก็สงสัยว่า 'ฉันควรจะต้องยอมแพ้ทุกอย่างเพื่อที่จะมีพลังมากกว่านี้ ...";
		next;
		mes "[Master]";
		mes "แต่เสร็จแล้ว ... ";
		close;
	}
	mes "นี่ไม่ใช่ที่ที่คุณจะอยู่ได้";
	mes "กรุณากลับไป";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_2-1,1,2,0	script	Button Girl#sura	66,{
	callfunc "F_GM_NPC";
	switch(select("Turn on arena.:Turn off arena.:Turn on the living room.:Turn on Buddy.:Turn on Gara.:Turn off all.:Close")) {
	case 1:
		mes "Turn on arena.";
		donpcevent "Drawing Room::OnEnable";
		close;
	case 2:
		mes "Turn off arena.";
		donpcevent "Drawing Room::OnDisable";
		close;
	case 3:
		mes "Turn on the living room.";
		donpcevent "Buddy#Sura_Salon::OnEnable";
		close;
	case 4:
		mes "Turn on Buddy.";
		donpcevent "Buddy#job_shuaneh::OnEnable";
		close;
	case 5:
		mes "Turn on Gara.";
		donpcevent "#Sura_garajjom::OnEnable";
		close;
	case 6:
		mes "Turn off all.";
		donpcevent "Drawing Room::OnDisable";
		donpcevent "Buddy#Sura_Salon::OnReset";
		donpcevent "Buddy#Sura_Salon::OnDisable";
		donpcevent "Buddy#job_shuaneh::OnDisable";
		donpcevent "#Sura_garajjom::OnDisable";
		close;
	case 7:
		close;
	}
}
