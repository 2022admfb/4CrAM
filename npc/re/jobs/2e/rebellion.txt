//===== rAthena Script =======================================
//= Rebellion Job Quest
//===== Description: =========================================
//= [Official Conversion]
//= Job change Quest from Gunslinger -> Rebellion.
//===== Changelogs: ==========================================
//= 1.0 First version. [exneval]
//= 1.1 Syntax change. [Capuche]
//============================================================

moc_fild12,261,318,3	script	Suspicious Man	4_M_MOCASS2,2,2,{
	end;
OnTouch:
	if (Class == Job_Gunslinger && BaseLevel >= 99 && JobLevel >= 70) {
		if (isbegin_quest(12340) == 0) {
			mes "[ ชายต้องสงสัย ]";
			mes "ในที่สุดฉันก็พบคุณ Vagabond Spike!";
			next;
			select("พูดอะไร?");
			mes "[ ชายต้องสงสัย ]";
			mes "* อย่าแสร้งทำเป็นว่าคุณไม่รู้ดูโปสเตอร์นี้สิ!";
			next;
			select("ฉันเห็นความคล้ายคลึง แต่นั่นไม่ใช่ฉัน.");
			mes "["+strcharinfo(0)+"]";
			mes "ลืมความคล้ายคลึงไปเลยคุณเอาโปสเตอร์มาจากไหน ฉันไม่เคยก่ออาชญากรรม";
			next;
			mes "[ ชายต้องสงสัย ]";
			mes "ฉันรู้ว่าคุณพยายามโกหกเพื่อหาทางออก";
			next;
			mes "[ ชายต้องสงสัย ]";
			mes "^0000ffEinbroch Private Pub ^000000 ได้เปิดตัวโปสเตอร์ที่ต้องการแล้ว มีนักล่าเงินรางวัลมากมาย";
			next;
			mes "[ ชายต้องสงสัย ]";
			mes "ฉันไม่สนหรอก ว่า คุณจะเป็นคนที่ใช่ตราบใด ที่คุณมองมัน และ ฉันจะทำให้คุณตายหรือมีชีวิตอยู่";
		}
		else {
			mes "[ ชายต้องสงสัย ]";
			mes "นี่คุณอีกแล้ว! คุณมีความกล้าที่จะกลับมาที่นี่!";
		}
		next;
		if (select("ยกเลิก:เข้าร่วม.") == 1)
			mes "^0000ffI ควรทำงานก่อนแล้วค่อยคิดทีหลัง ^000000";
		else
			mes "^0000ff เขาไม่ใช่ นักล่าเงินรางวัลคนเดียว หลังจาก ถ้าเขาพูดความจริงฉันต้องการ เวลา ในการวางกลยุทธ์ ^000000";
		close2;
		if (isbegin_quest(12340) == 0)
			setquest 12340;// Rebellion Job Change Quest
		warp "einbroch",49,98;
	}
	end;
}

einbroch,54,97,3	script	Bouncer	4_M_REBELLION,{
	if (BaseClass != Job_Gunslinger) {
		mes "[ Bouncer ]";
		mes "ไม่ต้องวุ่นวายไปรอบ ๆ สถานที่";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[ Bouncer ]";
		mes "อยู่ระหว่างการปรับปรุงไม่สามารถเข้าได้";
		close;
	case 1:
		mes "[ Bouncer ]";
		mes "* อ้าปากค้าง * คุณ! ฉันคิดว่าคุณถูกจับไปแล้ว โอ้ !";
		next;
		select("ฉันไม่ใช่อาชญากรที่คุณคิด!");
		mes "[ Bouncer ]";
		mes "คุณไม่ใช่เหรอ";
		next;
		mes "^0000ffคนโกหกตรวจสอบใบหน้าของคุณ ^000000";
		next;
		mes "[ Bouncer ]";
		mes "หืมคุณมีความคล้ายคลึงกันมาก แม้ว่าคุณจะดูไม่น่ากลัวเท่าอาชญากร";
		next;
		select("ฉันต้องการคุยกับผู้จัดพิมพ์โปสเตอร์.");
		mes "[ Bouncer ]";
		mes "ปัญหาของคุณไม่ได้อยู่ในลีกของฉันเข้ามาเถอะ ถ้าฉันรู้ว่าคุณเป็นตัวจริง จะเห็นว่าคุณจะไม่ได้เห็นข้างนอกอีกเลย";
		close2;
		warp "job_gun",81,29;
		end;
	case 2:
		mes "[ Bouncer ]";
		mes "กลับมาทำไม?";
		next;
		if (select("ฉันต้องการเข้าไป:ยกเลิก") == 2) {
			mes "[ Bouncer ]";
			mes "อืมม";
			close;
		}
		mes "[ Bouncer ]";
		mes "เอาเลย.";
		close2;
		warp "job_gun",70,28;
		end;
	}
}

job_gun,68,33,3	script	Ruthless Rebellion	4_F_REBELLION,{
	mes "[ Ruthless Rebellion ]";
	mes "คุณรู้วิธีใช้ปืนหรือไม่?";
	close;
}

job_gun,78,33,3	script	Suspicious Gunslinger	4_M_JOB_BLACKSMITH,{
	mes "[ Suspicious Gunslinger ]";
	mes "... ";
	next;
	mes "[ Suspicious Gunslinger ]";
	mes "อย่าบอกว่าฉันดูเหมือนช่างตีเหล็กที่คุณรู้จัก";
	close;
}

job_gun,74,24,6	script	Drunken Man	4_M_LIEMAN,{
	mes "[ Drunken Man ]";
	mes "* สะอึก * โอ้ฉันเป็นเจ้าของที่นี่";
	mes "เดี๋ยวก่อนฉันอยู่ในผับหรือที่อื่นดี Zzz ... ";
	close;
}

job_gun,64,33,6	script	Anxious Man	4_M_ALCHE_A,{
	mes "[ Anxious Man ]";
	mes "อาจจะมาผิดที่นี่ดูไม่เหมือนผับ";
	close;
}

job_gun,86,29,3	script	Kulbertinov	4_M_REBELLION,{
	if (Class == Job_Rebellion) {
		mes "[ Kulbertinov ]";
		mes "อยากเจอหัวหน้ามั้ย?";
		next;
		if (select("ตกลง.:ไม่ล่ะ.") == 1) {
			close2;
			warp "job_gun",220,130;
			end;
		}
		end;
	}
	if (Class != Job_Gunslinger) {
		mes "[ Kulbertinov ]";
		mes "คุณกำลังมองหาฉันอยู่หรือเปล่า";
		close;
	}
	switch( isbegin_quest(12340) ) {
	case 0:
		mes "[ Kulbertinov ]";
		mes "ให้ฉันช่วยไหม";
	case 1:
		mes "[ Kulbertinov ]";
		mes "คุณต้องการอะไร";
		next;
		select ("ฉันเข้าใจผิดคิดว่าเป็นอาชญากร!.");
		mes "[ Kulbertinov ]";
		mes "เดี๋ยวก่อน ... มว้ากกก!";
		next;
		mes "^0000ff เขาสแกนใบหน้าของคุณแล้ว ^000000";
		next;
		mes "[ Kulbertinov ]";
		mes "ฉันขอโทษสักครู่ฉันคิดว่า Spike กลับมามีชีวิตอีกครั้งเขาพบศพเมื่อสามวันก่อน";
		next;
		select("เขาถูกจับแล้ว?");
		mes "[ Kulbertinov ]";
		mes "ใช่แล้วนักล่าค่าหัวก็จ่ายเงินไปแล้วข่าวยังไม่แพร่ออกไปและคุณเป็นคนถ่มน้ำลายของเขาไปคิด";
		next;
		select("คุณต้องทำอะไรสักอย่าง!");
		mes "[ Kulbertinov ]";
		mes "ฉันขอโทษ แต่ฉันไม่สามารถคุยกับนักล่าเงินรางวัลทุกคนแทนคุณได้แน่นอนฉันยินดีที่จะชดเชยความไม่สะดวกของคุณ";
		next;
		mes "[ Kulbertinov ]";
		mes "เฮ้คุณเข้าร่วมการจัดอันดับของเราได้อย่างไรในที่สุดคุณจะไม่ถูกยิง";
		next;
		select("คุณอยากให้ฉันกลายเป็นนักล่าเงินรางวัล?");
		mes "[ Kulbertinov ]";
		mes "ไม่การล่าเงินรางวัลเป็นเพียงงานอดิเรกเราเป็นผู้เชี่ยวชาญด้านอาวุธปืนทั้งหมดในโลก";
		next;
		mes "[ Kulbertinov ]";
		mes "วิญญาณไร้กังวล!";
		mes "เวทมนตร์พ่นไฟ!";
		mes "เราเรียกตัวเองว่า ^0000ffRebellion ^000000";
		next;
		mes "[ Kulbertinov ]";
		mes "คุณดูเหมือนคุณรู้วิธีใช้ปืนคุณต้องการเข้าร่วมกลุ่มของเราอย่างไรคุณจะไม่ต้องกังวลกับนักล่าเงินรางวัลคนอื่น ๆ อีกต่อไปด้วย";
		next;
		if (select("ขอฉันคิดให้มากกว่านี้:ฉันคิดว่าฉันไม่มีทางเลือก.") == 1) {
			mes "[ Kulbertinov ]";
			mes "คุณสามารถคิดซ้ำแล้วซ้ำเล่า แต่คำตอบจะไม่เปลี่ยนแปลงเพียงแค่รับข้อเสนอ";
		}
		else {
			mes "[ Kulbertinov ]";
			mes "คิดดีทำตอนนี้ดีกว่า";
		}
		next;
		mes "[ Kulbertinov ]";
		mes "คุยกับ Enwin Conick เธอนำทางวิญญาณที่ล่องลอยเช่นคุณไปสู่หนทางแห่งการกบฏฉันจะส่งคำพูดไปหาเธออาอย่าจ่อปืนไปที่เธอ";
		completequest 12340;
		setquest 12341;// Rebellion Job Change Quest
		close2;
		warp "job_gun",19,15;
		end;
	case 2:
		mes "[ Kulbertinov ]";
		mes "คุณมีธุระกับเอลวินคอนนิคหรือเปล่า";
		next;
		if (select("ยกเลิก.:พาฉันไปหา Elwin Conick.") == 1) {
			mes "[ Kulbertinov ]";
			mes "เอาล่ะ.. ดูแล วอดก้าสักช็อต?";
			close;
		}
		mes "[ Kulbertinov ]";
		mes "โอเคฉันจะบอกให้เธอรู้";
		close2;
		warp "job_gun",19,15;
		end;
	}
}

job_gun,19,24,3	script	Elwin Conick#1	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[ Elwin Conick ]";
		mes "ถ้าไม่อยากเข้าร่วมกบฏก็ออกไปเถอะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12341) == 1) {
		mes "[ Elwin Conick ]";
		mes "มาร่วมกบฏกับเราไหม";
		next;
		mes "^0000ffผู้หญิงตัวเตี้ย ๆ ตาแหลมจ้องตาคุณ ^000000";
		next;
		mes "[ Elwin Conick ]";
		mes "ลงชื่อที่นี่";
		next;
		mes "^0000ffฝ่ายที่ลงนามข้างล่าง '_____' จะไม่พูด ถึงการทดสอบแม้ว่าเขาหรือเธอจะได้รับบาดเจ็บการถูกทำลายหรือเสียชีวิตจากอุบัติเหตุที่ไม่คาดฝันซึ่งอาจเกิดขึ้นระหว่างการทดสอบก็ตาม ^000000";
		next;
		select("อะไร??!");
		mes "[ Elwin Conick ]";
		mes "คุณต้องการเข้าร่วมกับเราหรือไม่?";
		next;
		if (select("ฉันยอมแพ้:ฉันจะเข้าร่วม.") == 2) {
			//mes "[ Elwin Conick ]";
			//mes "ทางเลือกของคุณ";
			//close2;
			//warp "job_gun",81,29;
			//end;
			if (Class == Job_Gunslinger) {
			if (BaseLevel < 99 || JobLevel < 70) {
				mes "[ Kulbertinov ]";
				mes "หือ? คุณดูอ่อนแอจังฉันไม่มีอะไรจะพูดอีกแล้ว";
				close;
			}
			if (SkillPoint) {
				mes "[ Elwin Conick ]";
				mes "โอ้ไม่นะ ~ คุณยังมีแต้มทักษะอยู่";
				close;
			}
			mes "[ Elwin Conick ]";
			mes "Kulbertinov เอลวินและฉันได้ประเมินผลการทดสอบการกบฏของคุณแล้ว";
			next;
			mes "[ Elwin Conick ]";
			mes "และ ... ";
			next;
			mes "[ Elwin Conick ]";
			mes "คุณมีคุณสมบัติตรงตามข้อกำหนดทั้งหมดของเรารวมถึงทัศนคติและผลการทดสอบ";
			next;
			mes "[ Elwin Conick ]";
			mes "ถูกต้องตอนนี้คุณเป็นสมาชิกของ Rebellion แล้วเตรียมตัวให้พร้อมที่จะว้าว";
			next;
			mes "[ Elwin Conick ]";
			mes "และเอลวินต้องการให้คุณมีสิ่งนี้ดูแลมันให้ดีเขาทำเพื่อคุณ";
			jobchange Job_Rebellion;
			setquest 12345;
			completequest 12341;
			completequest 12345;
			getitem 13119,1;// Freedom_Flame
			next;
			mes "[ Elwin Conick ]";
			mes "แล้วพบกันใหม่.";
			close2;
			warp "job_gun",81,29;
			end;
		}
		if (Class == Job_Rebellion) {
			mes "[ Elwin Conick ]";
			mes "ชีวิตนี้น่าจะสนุกไม่น่าเบื่อ";
			next;
			mes "[ Elwin Conick ]";
			mes "และด้วยการเป็นกบฏชีวิตของคุณควรได้รับการอัปเกรดดังนั้นไปสร้างการเดินทางที่ยิ่งใหญ่ทำให้เราภาคภูมิใจ!";
			close2;
			warp "job_gun",81,29;
			end;
		}
			mes "[ Ivan Sidorenko ]";
			mes "คุณมีอะไรจะพูดกับฉันไม่ใช่เหรอ";
			close;
		//close;
		}
		mes "[ Elwin Conick ]";
		mes "อืม";
		close2;
		//warp "job_gun",81,41;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		mes "[ Elwin Conick ]";
		mes "คุณอยากลองอีกครั้งไหม";
		next;
		if (select("ฉันยอมแพ้:ทำมัน.") == 1) {
			mes "[ Elwin Conick ]";
			mes "ทางเลือกของคุณ";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[ Elwin Conick ]";
		mes "ตามฉันมา";
		close2;
		warp "job_gun",117,41;
		end;
	}
	if (isbegin_quest(12343) > 0) {
		mes "[ Elwin Conick ]";
		if (isbegin_quest(12343) == 1)
			mes "คุณยังมีการทดสอบการยิงของ Ivan คุณต้องการลองอีกครั้งหรือไม่";
		else
			mes "การทดสอบการยิงของอีวานยังไม่เสร็จสิ้นคุณต้องการลองอีกครั้งหรือไม่";
		next;
		if (select("ฉันยอมแพ้:ทำมัน.") == 1) {
			mes "[ Elwin Conick ]";
			mes "มันไม่คุ้มที่จะยอมแพ้ตอนนี้ออกไปพักผ่อนเถอะ";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[ Elwin Conick ]";
		mes "อย่าลืมทำให้ดีที่สุด";
		close2;
		warp "job_gun",120,130;
		end;
	}
	if (isbegin_quest(12345) == 1) {
		mes "[ Elwin Conick ]";
		mes "อีวานกำลังรอคุณไปหาเขา";
		close2;
		warp "job_gun",81,29;
		end;
	}
	else{
		
	}
	mes "[ Elwin Conick ]";
	mes "คุณไม่มีการทดสอบอะไรอีกแล้วทำไมคุณไม่ออกไปดื่ม?";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,117,46,3	script	Elwin Conick#2	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[ Elwin Conick ]";
		mes "ถ้าไม่อยากเข้าร่วมกบฏก็ออกไปเถอะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
	.@quest_12341 = isbegin_quest(12341);
	.@quest_12342 = isbegin_quest(12342);
	if (.@emt_chk == 3169) {
		mes "^0000ff คุณอยู่ในแบบทดสอบแล้วหากคุณทำแบบทดสอบทั้งหมดเสร็จแล้วให้รายงานต่อ Elwin Conick ในห้อง Assembly Room ^000000";
		close;
	}
	if (.@emt_chk) {
		mes "[ Elwin Conick ]";
		mes "มองดูตัวเองอยากทดสอบจริงหรือ?";
		next;
		mes "^0000ffElwin เห็นคุณกลายร่างเป็นแบบอื่นและเธอก็โกรธมากรอให้ร่างปัจจุบันของคุณจบลงแล้วจะพบเธออีกครั้ง ^000000";
		next;
		if (select("Wait.:Go away.") == 2) {
			mes "[ Elwin Conick ]";
			mes "กลับมาเมื่อคุณไม่ได้อยู่ในรูปแบบใด ๆ ";
			close2;
			warp "job_gun",81,29;
			end;
		}
		mes "[ Elwin Conick ]";
		mes "มันไม่คุ้มที่จะยอมแพ้ตอนนี้ออกไปพักผ่อนเถอะ";
		close;
	}
	if (.@quest_12341 == 1 || .@quest_12342 == 1) {
		if (countitem(6746)) {
			mes "[ Elwin Conick ]";
			mes "สิ่งประดิษฐ์เหล็กที่เหลือจะต้องส่งคืนหลังการทดสอบ";
			delitem 6746,countitem(6746);// Steel_Article
			close;
		}
		if (countitem(6747)) {
			mes "[ Elwin Conick ]";
			mes "สิ่งประดิษฐ์เหล็กที่เหลือจะต้องส่งคืนหลังการทดสอบ";
			delitem 6747,countitem(6747);// Steel_Article_
			close;
		}
		if ((countitem(13118) - isequipped(13118)) > 0) {
			mes "[ Elwin Conick ]";
			mes "หมดเวลาแล้วคุณสอบไม่ผ่านขอฉันเอาวัสดุทดสอบที่เหลือทั้งหมดของคุณ";
			delitem 13118,1;// Tiny_Flame
			close;
		}
		if (.@quest_12342 == 1)
			.@menu$ = "Got it.";
		else {
			.@menu$ = "Okay.";
			setquest 12342;// Rebellion Job Change Quest
			completequest 12341;
		}
		mes "[ Elwin Conick ]";
		mes "ฟังนะฉันจะไม่พยายามอธิบายการทดสอบให้คุณฟังอีกต่อไป";
		next;
		select( .@menu$ );
		mes "[Elwin Conick]";
		if (.@quest_12341 == 1) {
			mes "นี่คือที่ที่มีการหล่อแม่พิมพ์อาวุธปืนทุกที่ที่คุณสามารถเห็น Steel Artifacts กำลังถูกผลิตขึ้น";
			next;
			mes "[ Elwin Conick ]";
			mes "We Rebellions คือผู้เชี่ยวชาญด้านอาวุธที่มีหัวใจอยู่ที่นี่คุณจะได้เรียนรู้การสร้างและประกอบอาวุธปืน";
			next;
			mes "[ Elwin Conick ]";
			mes "ระบบจะขอให้คุณควบคุมตัวควบคุมเตาที่อยู่ใกล้ทางเข้าและเลือกสิ่งประดิษฐ์เหล็กมาตรฐาน 5 ชิ้น";
			next;
			mes "[ Elwin Conick ]";
			mes "จากนั้นคุณจะพาพวกเขาไปที่ห้อง Assembly Room ถัดไปและรวบรวมข้อมูลอาวุธปืนคุณจะมีเวลา 3 นาทีในการทำสิ่งนั้นคุณควรเร็วและมีสมาธิไม่งั้นคุณจะสอบไม่ผ่าน";
			next;
			mes "[ Elwin Conick ]";
			mes "ฉันจะอยู่ในห้องถัดไปรอให้คุณเอาอาวุธปืนมาให้ฉันเพื่อตรวจสอบเวลาที่ผ่านไปคุณจะเปลี่ยนเป็นงานอื่น ^0000ff ถ้าคุณผ่านไป 3 นาทีแล้วกลับสู่สภาวะปกติให้ไป กลับมาหาฉันแล้วเริ่มการทดสอบอีก ครั้ง ^000000 ";
		}
		else {
			mes "ระบบจะขอให้คุณควบคุมสวิตช์บนเครื่องและเลือกสิ่งประดิษฐ์เหล็กมาตรฐาน 5 ชิ้น";
			next;
			mes "[ Elwin Conick ]";
			mes "จากนั้นคุณจะนำพวกเขาไปที่ห้อง Assembly Room ถัดไปและประกอบเป็นอาวุธปืนที่สมบูรณ์คุณจะมีเวลา 3 นาทีในการทำทุกอย่างให้เสร็จ ^0000ff หากต้องการตรวจสอบเวลาที่ผ่านไปคุณจะเปลี่ยนเป็นงานอื่น ^000000. ";
			next;
			mes "[ Elwin Conick ]";
			mes "ฉันจะอยู่ในห้องถัดไปรอให้คุณนำอาวุธปืนมาให้ฉันจำไว้ว่า ^0000ff คุณไม่ผ่านการทดสอบเมื่อการเปลี่ยนแปลงของคุณหมดอายุภายใน 3 นาที ^000000";
		}
		transform 3169,180000;
		close;
	}
	mes "[ Elwin Conick ]";
	mes "อ้าวหายไปไหนต้องลงทะเบียนก่อนแล้วค่อยกลับมาที่นี่";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,198,39,3	script	Elwin Conick#3	4_F_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[ Elwin Conick ]";
		mes "ถ้าไม่อยากเข้าร่วมกบฏก็ออกไปเถอะ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) == 1) {
		.@emt_chk = getstatus(SC_MONSTER_TRANSFORM,1);
		if (.@emt_chk == 3169) {
			if ((countitem(13118) - isequipped(13118)) < 1) {// don't count if equipped
				mes "[ Elwin Conick ]";
				mes "เสร็จแล้วหรือยังไม่เห็นอาวุธปืนครบมือในคลังของคุณ";
				close;
			}
			mes "[ Elwin Conick ]";
			mes "มันห่วย แต่ก็ไม่แย่ถ้าให้เวลาคุณผ่านไป";
			delitem 13118,1;// Tiny_Flame
			next;
			mes "[ Elwin Conick ]";
			mes "ขอฉันเอาวัสดุทดสอบที่เหลือทั้งหมดของคุณมาใช้";
			delitem 6746,countitem(6746);// Iron_Artifact
			delitem 6747,countitem(6747);// Steel_Artifact
			next;
			mes "[ Elwin Conick ]";
			mes "ตอนนี้คุณสามารถไปยังหลักสูตรถัดไปได้ Ivan Sidorenko จะดูแลการทดสอบของคุณ";
			completequest 12342;
			close2;
			warp "job_gun",120,133;
			end;
		}
		if (.@emt_chk) {
			mes "[ Elwin Conick ]";
			mes "มองดูตัวเองอยากทดสอบจริงหรือ?";
			next;
			mes "^0000ffElwin เห็นคุณกลายร่างเป็นแบบอื่นและเธอก็โกรธมากรอให้ร่างปัจจุบันของคุณจบลงแล้วจะพบเธออีกครั้ง ^000000";
			next;
			if (select("Wait.:Go away.") == 2) {
				mes "[ Elwin Conick ]";
				mes "กลับมาเมื่อคุณไม่ได้อยู่ในรูปแบบใด ๆ ";
				close2;
				warp "job_gun",81,29;
				end;
			}
			mes "[ Elwin Conick ]";
			mes "มันไม่คุ้มที่จะยอมแพ้ตอนนี้ออกไปพักผ่อนเถอะ";
			close;
		}
		mes "[ Elwin Conick ]";
		mes "หมดเวลาแล้วคุณสอบไม่ผ่านขอฉันเอาวัสดุทดสอบที่เหลือทั้งหมดของคุณ";
		delitem 6746,countitem(6746);// Iron_Artifact
		delitem 6747,countitem(6747);// Steel_Artifact
		delitem 13118,(countitem(13118) - isequipped(13118));// Tiny_Flame
		next;
		mes "[ Elwin Conick ]";
		mes "มาเริ่มกันใหม่";
		close2;
		warp "job_gun",117,41;
		end;
	}
	mes "[ Elwin Conick ]";
	mes "อ้าวหายไปไหนต้องลงทะเบียนก่อนแล้วค่อยกลับมาที่นี่";
	close2;
	warp "job_gun",81,29;
	end;
}

job_gun,165,18,0	script	Furnace Controller#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "เฉพาะผู้ใช้ที่ได้รับอนุญาตเท่านั้นที่สามารถดำเนินการได้";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {// J_REB_SHECIL1
		if (isbegin_quest(12342) == 1) {
			mes "คุณต้องการขุดแม่พิมพ์ขึ้นมาจากเตาหรือไม่";
			next;
			if (select("Quit.:Begin.") == 1) {
				mes "เปลี่ยนเป็นโหมดสแตนด์บาย";
				close;
			}
			mes "Initializing Furnace Controller ZX-3100...";
			disablenpc "Furnace Controller#1";
			donpcevent "Furnace Controller#2::OnStart";
			close;
		}
	}
	mes "ไม่ใช่ผู้ใช้ที่ได้รับอนุญาตยกเลิกการดำเนินการ";
	close;
}

job_gun,214,36,0	script	Auto Anvil#1	CLEAR_NPC,{
	if (Class != Job_Gunslinger) {
		mes "เฉพาะผู้ใช้ที่ได้รับอนุญาตเท่านั้นที่สามารถดำเนินการได้";
		close;
	}
	if ((countitem(13118) - isequipped(13118)) > 0) {// Tiny_Flame
		mes "ตรวจพบการสร้างอาวุธปืนโดยไม่ได้รับอนุญาตรายงานต่อผู้จัดการเอลวิน";
		close;
	}
	if (getstatus(SC_MONSTER_TRANSFORM,1) == 3169) {
		if (countitem(6747) < 5) {// Steel_Article_
			mes "คุณไม่มีสิ่งประดิษฐ์เหล็กมาตรฐาน";
			close;
		}
		if (isbegin_quest(12342) == 1) {
			mes "คุณต้องการใช้ทั่งอัตโนมัติและสร้างสิ่งประดิษฐ์เหล็กมาตรฐานให้เป็นอาวุธปืนหรือไม่";
			next;
			if (select("Quit.:Begin.") == 1) {
				mes "เพื่อความปลอดภัยของคุณโปรดถอยห่างจากทั่งอัตโนมัติ";
				close;
			}
			if (rand(1,10) > 6) {
				mes "คุณสร้างอาวุธปืนโดยใช้ทั่งอัตโนมัติสำเร็จแล้ว ^ff0000 โปรดรายงานก่อนที่การเปลี่ยนแปลงของคุณจะหมดอายุ ^000000";
				specialeffect EF_REPAIRWEAPON;
				getitem 13118,1;// Tiny_Flame
			}
			else {
				mes "สร้างอาวุธปืนไม่สำเร็จ";
				specialeffect EF_SUI_EXPLOSION;
			}
			delitem 6747,5;// Steel_Article_
			close;
		}
	}
	mes "ไม่ใช่ผู้ใช้ที่ได้รับอนุญาตยกเลิกการดำเนินการ";
	close;
}

job_gun,165,18,0	script	Furnace Controller#2	CLEAR_NPC,{
	mes "ระบบกำลังยุ่งอยู่กับการขุดลอกแม่พิมพ์ที่เสร็จสมบูรณ์จากเตา";
	close;

OnStart:
	enablenpc "Furnace Controller#2";
	sleep 3000;
	mapannounce "job_gun","Furnace Controller: Dredging a mold up from the furnace... Workers, stand by at the production line.",bc_map,"0x00ff44";
	sleep 500;
	for ( .@i = 0; .@i < 30; ++.@i ) {
		.@eff_rnd = rand(1,100);
		if (.@eff_rnd < 33)
			donpcevent "Heating Furnace#3::OnStart";
		else if (.@eff_rnd > 66)
			donpcevent "Heating Furnace#4::OnStart";
		else
			donpcevent "Heating Furnace#5::OnStart";
		sleep 200;
		.@d_itemx = rand(127,143);
		.@d_itemy = rand(26,29);
		makeitem 6746,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article
		if (rand(1,10) > 7)
			makeitem 6747,1,"job_gun",.@d_itemx,.@d_itemy;// Steel_Article_
		sleep 500;
	}
	sleep 2000;
	mapannounce "job_gun","Furnace Controller: Finishing the operation... Entering Standby mode in 15 seconds.",bc_map,"0x00ff44";
	sleep 15000;
	disablenpc "Furnace Controller#2";
	enablenpc "Furnace Controller#1";
	end;
OnInit:
	disablenpc "Furnace Controller#2";
	end;
}

job_gun,130,31,0	script	Heating Furnace#3	CLEAR_NPC,{
	end;
OnStart:
	specialeffect EF_FIREPILLAR;
	sleep 500;
	specialeffect EF_FIREPILLARBOMB;
	end;
}
job_gun,136,31,0	duplicate(Heating Furnace#3)	Heating Furnace#4	CLEAR_NPC
job_gun,141,31,0	duplicate(Heating Furnace#3)	Heating Furnace#5	CLEAR_NPC

job_gun,120,138,3	script	Ivan Sidorenko#1	4_M_REBELLION,{
	if (Class != Job_Gunslinger) {
		mes "[ Ivan Sidorenko ]";
		mes "นี่คือพื้นที่ทดสอบการถ่ายทำคุณไม่สามารถมาที่นี่ได้ตามใจชอบ";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12342) < 2) {
		mes "[ Ivan Sidorenko ]";
		mes "ดูเหมือนจะมีข้อผิดพลาดคุณไม่สามารถอยู่ที่นี่ได้";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (isbegin_quest(12343) == 0) {
		mes "[ Ivan Sidorenko ]";
		mes "เดี๋ยวก่อนมีสมาธิ - เรากำลังใช้กระสุนสดที่นี่และคุณสามารถยิงได้ทุกเมื่อ";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "ขอฉันทำให้เร็วที่สุดโดยทั่วไปคุณจะวิ่งในสนามยิงปืนนี้ซึ่งประกอบด้วย 5 ส่วน";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "เป้าหมายคือค้นหาศัตรูท่ามกลางฝูงชนอย่างรวดเร็วและไม่พลาด";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "ในบรรดาเป้าหมายทั้งหมดคุณต้องยิงเฉพาะ ^0000ffStandard-issue Targets ^000000 ถ้าคุณทำผิดพลาดคุณจะเสียคะแนน";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "ลองดูสิ ^0000ffStandard-issue Targets ^000000 มีไม่มากเท่าไหร่ให้ลอกตา";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "^0000ff คุณมีเวลา 3 นาทีในการจบการทดสอบนี้หากคุณทำไม่ได้ก่อนที่การเปลี่ยนแปลงของคุณจะหมดอายุ ^000000 คุณจะล้มเหลวหากเป็นเช่นนั้นให้เดินตามทางกลับมาหาฉัน";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "^0000ff คุณจะล้มเหลวถ้าคุณพลาดเป้าหมายปัญหามาตรฐานหรือยิง 3 เป้าหมายไม่ถูกต้อง ^000000 มาเริ่มกันเลย";
		setquest 12343;// Rebellion Job Change Quest
		setquest 12344;// Rebellion Job Change Quest
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	if (isbegin_quest(12343) == 1) {
		if (checkquest(12343,HUNTING) == 2)
			.@j_r_s = 10;
		if (checkquest(12344,HUNTING) == 2)
			.@j_r_f = 2;
		if (getstatus(SC_MONSTER_TRANSFORM,1) != 3169)
			.@j_r_t = 5;
		.@j_r_c = .@j_r_s - .@j_r_f - .@j_r_t;
		if (.@j_r_c > 9) {
			mes "[ Ivan Sidorenko ]";
			mes "เก่งมากไม่คิดว่าจะผ่านไปเร็วขนาดนี้";
			completequest 12343;
			completequest 12344;
			setquest 12345;// Rebellion Job Change Quest
			next;
			mes "[ Ivan Sidorenko ]";
			mes "คุณสามารถออกไปรอข้างนอกได้ฉันจะนำรายงานการประเมินของอาจารย์คนอื่นมาให้คุณดู";
			close2;
			warp "job_gun",210,132;
			end;
		}
		if (.@j_r_c == 8) {
			mes "[ Ivan Sidorenko ]";
			mes "คุณเร็วพอ แต่ยิงผิดเป้าหมายมากเกินไปคุณล้มเหลว";
		}
		else if (.@j_r_c == 5) {
			mes "[ Ivan Sidorenko ]";
			mes "คุณแม่นยำพอ แต่คุณใช้เวลามากเกินไปการกบฏที่ช้าไม่สามารถอยู่รอดได้นานพอ";
		}
		else if (.@j_r_c < 5) {
			mes "[ Ivan Sidorenko ]";
			mes "คุณล้มเหลวทั้งความเร็วและความแม่นยำคุณรอดมาได้อย่างไร?";
		}
		else {
			mes "[ Ivan Sidorenko ]";
			mes "มีปัญหาดูเหมือนสถิติจะผิดอืม ... อาจเกิดขึ้นได้ในบางครั้ง";
		}
		erasequest 12343;
		erasequest 12344;
		next;
		mes "[ Ivan Sidorenko ]";
		mes "ลองใหม่คราวนี้ระวังให้มากขึ้น";
		setquest 12343;
		setquest 12344;
		transform 3169,180000;
		close2;
		warp "job_gun",102,146;
		end;
	}
	mes "[ Ivan Sidorenko ]";
	mes "ยังไงก็ไม่ออก";
	next;
	mes "[ Ivan Sidorenko ]";
	mes "คุณสามารถออกไปรอข้างนอกได้ฉันจะนำรายงานการประเมินของอาจารย์คนอื่นมาให้คุณดู";
	close2;
	warp "job_gun",210,132;
	end;
}

job_gun,1,4,0	script	#target	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#target::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#target";
	while( mobcount("job_gun","#target::OnMyMobDead") < 16 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Standard-issue Target",3169,1,"#target::OnMyMobDead";
	}
	end;
}

job_gun,1,5,0	script	#nontarget	CLEAR_NPC,{
	end;
OnStart:
	killmonster "job_gun","#nontarget::OnMyMobDead";
OnInit:
OnMyMobDead:
	hideonnpc "#nontarget";
	while( mobcount("job_gun","#nontarget::OnMyMobDead") < 56 ) {
		.@m_rg = rand(1,5);
		if (.@m_rg == 1)
			monster "job_gun",rand(88,99),rand(143,147),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 2)
			monster "job_gun",rand(92,96),rand(95,122),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 3)
			monster "job_gun",rand(143,159),rand(99,100),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else if (.@m_rg == 4)
			monster "job_gun",rand(157,158),rand(137,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
		else
			monster "job_gun",rand(119,134),rand(156,160),"Defective Target",3170,1,"#nontarget::OnMyMobDead";
	}
	end;
}

job_gun,220,132,3	script	Ivan Sidorenko#2	10062,{
	if (!checkweight(1201,1)) {
		mes "คุณมีรายการมากเกินไปโปรดลดจำนวนเพื่อดำเนินการต่อ";
		close;
	}
	if ((MaxWeight - Weight) < 1000) {
		mes "สินค้าในคลังของคุณกำลังชั่งน้ำหนักคุณลดน้ำหนักของคุณก่อน";
		close;
	}
	if (isbegin_quest(12345) == 0) {
		mes "[ Ivan Sidorenko ]";
		mes "ต้องมีเรื่องผิดพลาดแน่ ๆ คุณไม่ควรอยู่ที่นี่ต่อไป";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Gunslinger) {
		if (BaseLevel < 99 || JobLevel < 70) {
			mes "[ Ivan Sidorenko ]";
			mes "หือ? คุณดูอ่อนแอจังฉันไม่มีอะไรจะพูดอีกแล้ว";
			close;
		}
		if (SkillPoint) {
			mes "[ Ivan Sidorenko ]";
			mes "โอ้ไม่นะ ~ คุณยังมีแต้มทักษะอยู่";
			close;
		}
		mes "[ Ivan Sidorenko ]";
		mes "Kulbertinov เอลวินและฉันได้ประเมินผลการทดสอบการกบฏของคุณแล้ว";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "และ ... ";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "คุณมีคุณสมบัติตรงตามข้อกำหนดทั้งหมดของเรารวมถึงทัศนคติและผลการทดสอบ";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "ถูกต้องตอนนี้คุณเป็นสมาชิกของ Rebellion แล้วเตรียมตัวให้พร้อมที่จะว้าว";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "และเอลวินต้องการให้คุณมีสิ่งนี้ดูแลมันให้ดีเขาทำเพื่อคุณ";
		jobchange Job_Rebellion;
		completequest 12345;
		getitem 13119,1;// Freedom_Flame
		next;
		mes "[Ivan Sidorenko]";
		mes "See you later.";
		close2;
		warp "job_gun",81,29;
		end;
	}
	if (Class == Job_Rebellion) {
		mes "[ Ivan Sidorenko ]";
		mes "ชีวิตนี้น่าจะสนุกไม่น่าเบื่อ";
		next;
		mes "[ Ivan Sidorenko ]";
		mes "และด้วยการเป็นกบฏชีวิตของคุณควรได้รับการอัปเกรดดังนั้นไปสร้างการเดินทางที่ยิ่งใหญ่ทำให้เราภาคภูมิใจ!";
		close2;
		warp "job_gun",81,29;
		end;
	}
	mes "[ Ivan Sidorenko ]";
	mes "คุณมีอะไรจะพูดกับฉันไม่ใช่เหรอ";
	close;
}

job_gun,220,138,4	shop	Prop Vending Machine	2_VENDING_MACHINE1,13210:-1,13211:-1,13212:-1,13213:-1,13214:-1,7663:-1,7664:-1,7665:-1

job_gun,216,138,4	script	Butler Karlex	1_M_01,{
	mes "[Butler Karlex]";
	mes "ใช้พื้นที่เก็บข้อมูลชั่วคราวนี้เพื่อเก็บสิ่งของของคุณให้ปลอดภัยในระหว่างการทดสอบขั้นสูง";
	next;
	.@s = select("ยกเลิก.:เปิดคลัง.");
	mes "[Butler Karlex]";
	mes "ขอบคุณที่ใช้บริการ.";
	close2;
	if (.@s == 2)
		openstorage;
	end;
}

job_gun,56,26,0	warp	rebelroom#1	2,2,einbroch,49,97
job_gun,18,10,0	warp	rebelroom#2	2,2,job_gun,81,29
job_gun,165,22,0	warp	rebelroom#3	2,2,job_gun,196,35
job_gun,197,49,0	warp	rebelroom#4	2,2,job_gun,157,21
job_gun,88,144,0	warp	shootingrange#1	3,3,job_gun,93,123
job_gun,105,97,0	warp	shootingrange#2	3,3,job_gun,125,98
job_gun,156,115,0	warp	shootingrange#3	3,3,job_gun,157,137
job_gun,152,154,0	warp	shootingrange#4	3,3,job_gun,135,154
job_gun,130,146,0	warp	shootingrange#5	3,3,job_gun,120,130


job_gun,1,1,0	script	Admin1	CLEAR_NPC,{
	callfunc "F_GM_NPC";
	if (callfunc("F_GM_NPC",1854,0) != 1)
		end;
	mes "[ผู้จัดการเวลา]";
	mes "อยากกลับกี่โมง";
	next;
	switch( select("ยกเลิก:ทดสอบการวางไข่ของมอนสเตอร์: เริ่มจากจุดเริ่มต้น:Furnace Zone:Shooting Zone:ทำการทดสอบให้เสร็จสมบูรณ์") ) {
	case 1:
		end;
	case 2:
		donpcevent "#target::OnStart";
		donpcevent "#nontarget::OnStart";
		end;
	case 3:
		callsub S_Quest,0,0;
	case 4:
		callsub S_Quest,12341,12341;
	case 5:
		callsub S_Quest,12342,12342;
	case 6:
		callsub S_Quest,12345,12344;
	}
	end;

S_Quest:
	setarray .@arguments[0], getarg(0), getarg(1);
	for ( .@quest = 12340; .@quest <= 12345; ++.@quest ) {
		if (isbegin_quest(.@quest) > 0)
			erasequest .@quest;
		if (.@quest <= .@arguments[0])
			setquest .@quest;
		if (.@quest <= .@arguments[1])
			completequest .@quest;
	}
	end;
}
