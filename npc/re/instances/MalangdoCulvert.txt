//===== rAthena Script ======================================= 
//= Malangdo Culvert
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Clean the culverts and defeat the Coelacanth.
//= Daily and weekly quests are available.
//= Contains a normal mode and hard mode.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//=     Letters "n" and "h" in NPC names indicate difficulty.
//= 1.0a There is no minimum party size on official servers.
//= 1.0b Fixed incorrect use of 'close'. [Joseph]
//= 1.1 Instance system rewrite. [Euphy]
//============================================================ 

// Quest NPCs :: mal_yong
//============================================================
mal_in01,172,28,2	script	Albo#mal	561,{
	if (checkweight(1201,1) == 0) {
		mes "คุณมีวัตถุหลายชนิดเกินไปลองดำเนินการต่อหลังจากลดขนาดวัตถุเหล่านั้นแล้ว";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "คุณมีของหนักมากเกินไปพยายามทำต่อไปหลังจากลดน้ำหนักแล้ว";
		close;
	}
	if (BaseLevel < 90) {
		mes "[Albo]";
		mes "คุณเป็นใคร";
		mes "เราไม่จำเป็นต้องอ่อนแอ !!";
		close;
	}
	setarray .@quests[0],12271,12272,12273,12274;
	setarray .@names$[0],
		"General Culvert Daily Service","^990000Hard Culvert Daily Service^000000",
		"General Culvert Weekly Service","^990000Hard Culvert Weekly Service^000000";
	set .@menu$,"Why are doing this job?:";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		if (checkquest(.@quests[.@i],PLAYTIME) > -1) {
			set .@status[.@i],2;
			set .@menu$, .@menu$+"^aaaaaa- There is no new task -^000000:";
		} else if (.@i%2 && BaseLevel < 140) {
			set .@status[.@i],0;
			set .@menu$, .@menu$+"^aaaaaaCan't take this job because level is too low^000000:";
		} else {
			set .@status[.@i],1;
			set .@menu$, .@menu$+.@names$[.@i]+":";
		}
	}
	mes "[Albo]";
	mes "เราข้ามทะเล!";
	mes "พวกเราคือวีรบุรุษผู้ยิ่งใหญ่ของ Culvert! เราไม่ให้อภัยอาหารทะเลที่มุ่งเป้าไปที่ Culvert!";
	next;
	set .@i, select(.@menu$)-1;
	if (.@i == 0) {
		mes "[Albo]";
		mes "เราเคยเป็นลูกน้องของ Mr. Missing! เราเกือบจะกลายเป็นผู้พิทักษ์โบราณสถานแห่งนี้แล้ว";
		next;
		mes "[Albo]";
		mes "อย่างไรก็ตามเมื่อน้ำทะเลท่วมทรัพยากรอันมีค่าทั้งหมดความฝันของฉัน ... ความฝันทั้งหมดของฉันก็สลายหายไป ... ";
		next;
		mes "[Albo]";
		mes "เราไม่ให้อภัยอาหารทะเลที่มารุกรานดินแดนของเราฉันจะกินมันด้วยน้ำพริกเผา !!!";
		next;
		mes "^ 0000ffAlbo เสียงสั่นด้วยความโกรธเขาตบริมฝีปากตัวเองเมื่อเอ่ยถึงรสเผ็ด ^ 000000";
		close;
	}
	switch(.@status[.@i-1]) {
	case 0:
		mes "[Albo]";
		mes "งานนั้นยากสำหรับคุณจริงๆฉันบอกคุณไม่ได้ทำไมคุณไม่หางานอื่น";
		close;
	case 1:
		break;
	case 2:
		mes "[Albo]";
		mes "I'm sorry. There is no new task yet! If I find any work, I'll tell you so!";
		close;
	}
	switch(.@i) {
	case 1: // General Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12255,12271,"Deep Sea Crab";
			case 2: callsub L_GiveQuest,.@i,12256,12271,"Deep Sea Marse";
			case 3: callsub L_GiveQuest,.@i,12257,12271,"Deep Sea Vadon";
			case 4: callsub L_GiveQuest,.@i,12258,12271,"Deep Sea Shellfish";
			case 5: callsub L_GiveQuest,.@i,12259,12271,"Deep Sea Kukre";
			case 6: callsub L_GiveQuest,.@i,12260,12271,"Deep Sea Cornutus";
		}
	case 2: // Hard Culvert Daily Service
		switch(rand(1,6)) {
			case 1: callsub L_GiveQuest,.@i,12261,12272,"Deep Sea Marc";
			case 2: callsub L_GiveQuest,.@i,12262,12272,"Deep Sea Sword Fish";
			case 3: callsub L_GiveQuest,.@i,12263,12272,"Deep Sea Strouf";
			case 4: callsub L_GiveQuest,.@i,12264,12272,"Deep Sea Anolian";
			case 5: callsub L_GiveQuest,.@i,12265,12272,"Deep Sea Obeanue";
			case 6: callsub L_GiveQuest,.@i,12266,12272,"Deep Sea Kapha";
		}
	case 3: // General Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12267,12273,"Weird Coelacanth";
			case 2: callsub L_GiveQuest,.@i,12268,12273,"Gloomy Coelacanth";
		}
	case 4: // Hard Culvert Weekly Service
		switch(rand(1,2)) {
			case 1: callsub L_GiveQuest,.@i,12269,12274,"Violent Coelacanth";
			case 2: callsub L_GiveQuest,.@i,12270,12274,"Mutant Coelacanth";
		}
	default:
		mes "[Albo]";
		mes "Uh!? Where is the task? Let me find that quickly. You must speak with Madeca!";
		close;
	}
	end;

// callsub L_GiveQuest,.@i,<quest 1>,<quest 2>,<monster>;
L_GiveQuest:
	setquest getarg(1);
	setquest getarg(2);
	mes "[Albo]";
	mes "วันนี้วีรบุรุษแห่งคัลเวิร์ต!";
	mes "เราส่งเสียงของเราและกำหนดวันที่จะกำจัด ^ 0000ff" + getarg (3) + "^ 000000!";
	next;
	mes "[Albo]";
	switch(getarg(0)) {
	case 1:
	case 2:
		mes "มาเลยฮีโร่อย่ากลัวพวกเขาไปเลยเพราะมันเป็นภารกิจวันเดียว!";
		break;
	case 3:
		mes "ฉันจะให้เวลาคุณหนึ่งสัปดาห์เพื่อทำงานนี้ให้เสร็จ! ยุติทั้งหมดในคุกใต้ดินคุกใต้ดินโหมดปกติ!";
		break;
	case 4:
		mes "ฉันจะให้เวลาคุณหนึ่งสัปดาห์เพื่อทำงานนี้ให้เสร็จ! ยุติงานทั้งหมดใน Culvert Dungeon, Hard Mode!";
		break;
	}
	next;
	mes "คุณได้รับคำขอให้กำจัด ^ 005500" + getarg (3) + "^ 000000 ถ้าคุณต้องการรายละเอียดเพิ่มเติมโปรดตรวจสอบหน้าต่างเควส";
	close;
}

mal_in01,172,26,2	script	Madeca#mal	544,{
	if (checkweight(1201,1) == 0) {
		mes "คุณมีวัตถุหลายชนิดเกินไปลองดำเนินการต่อหลังจากลดขนาดวัตถุเหล่านั้นแล้ว";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "คุณมีของหนักมากเกินไปพยายามทำต่อไปหลังจากลดน้ำหนักแล้ว";
		close;
	}
	if (BaseLevel < 90) {
		mes "[Madeca]";
		mes "กะรรรรรรรร !!";
		mes "มากินอาหารทะเลเลว ๆ ต้มหรือย่างกันเถอะ!";
		next;
		mes "[Madeca]";
		mes "คุณเป็นใครคุณเป็นคนขี้งอนขนาดนี้แม้แต่สาหร่ายก็ยังฆ่าคุณได้!";
		close;
	}
	mes "[Madeca]";
	mes "โอ้?";
	mes "คุณคือคนที่ดูแล Albo พี่ใหญ่ของฉันมันเป็นงานหนัก!";
	next;
	set .@i, select("คุณกำลังทำอะไรที่นี่: ค่าตอบแทนการบริการรายวันของท่อระบายน้ำทั่วไป: ค่าตอบแทนการบริการรายวันของท่อระบายน้ำแบบแข็ง: ค่าตอบแทนการบริการรายสัปดาห์ของท่อระบายน้ำทั่วไป: ค่าตอบแทนการบริการรายสัปดาห์")-1;
	if (.@i == 0) {
		mes "[Madeca]";
		mes "ฉันมาที่นี่เพื่อช่วยพี่ใหญ่ของฉัน Albo!";
		next;
		mes "[Madeca]";
		mes "เราจัดหาวัตถุดิบล้ำค่าให้กับมนุษย์ที่ทำกากตะกอนจากอาหารทะเลที่ไม่ดีใน Culverts!";
		next;
		mes "[Madeca]";
		mes "เราให้เหรียญเกรด A ~ B เป็นค่าตอบแทนสำหรับการบริการประจำวันและ Sea God Wrath อันล้ำค่าเป็นค่าตอบแทนสำหรับการบริการรายสัปดาห์";
		next;
		mes "[Madeca]";
		mes "ถ้าคุณดูแลตามคำขอของพี่ใหญ่ Albo ฉันจะเจอคุณบ่อยมากดังนั้นมาอยู่ใกล้ ๆ กันเถอะ!";
		close;
	}
	mes "[Madeca]";
	mes "ฉันไม่สามารถให้ค่าชดเชยสำหรับคำขอที่หมดอายุแล้วให้ฉันตรวจสอบว่าของคุณหมดอายุหรือไม่";
	next;
	specialeffect2 EF_SPHERE;
	progressbar "0xFFFF00",3;
	specialeffect2 EF_STEAL;
	switch(.@i) {
	case 1: // General Culvert Daily Service
		if (checkquest(12271,PLAYTIME) == 2)
			callsub L_EraseQuest,12255,12256,12257,12258,12259,12260,12271;
		else {
			// Reward: 2x B Grade Coin
			callsub L_CheckQuest,12255,"Deep Sea Crab",6419,2;
			callsub L_CheckQuest,12256,"Deep Sea Marse",6419,2;
			callsub L_CheckQuest,12257,"Deep Sea Vadon",6419,2;
			callsub L_CheckQuest,12258,"Deep Sea Shellfish",6419,2;
			callsub L_CheckQuest,12259,"Deep Sea Kukre",6419,2;
			callsub L_CheckQuest,12260,"Deep Sea Cornutus",6419,2;
		}
		break;
	case 2: // Hard Culvert Daily Service
		if (checkquest(12272,PLAYTIME) == 2)
			callsub L_EraseQuest,12261,12262,12263,12264,12265,12266,12272;
		else {
			// Reward: 1x A Grade Coin
			callsub L_CheckQuest,12261,"Deep Sea Marc",6418,1;
			callsub L_CheckQuest,12262,"Deep Sea Sword Fish",6418,1;
			callsub L_CheckQuest,12263,"Deep Sea Strouf",6418,1;
			callsub L_CheckQuest,12264,"Deep Sea Anolian",6418,1;
			callsub L_CheckQuest,12265,"Deep Sea Obeaune",6418,1;
			callsub L_CheckQuest,12266,"Deep Sea Kapha",6418,1;
		}
		break;
	case 3: // General Culvert Weekly Service
		if (checkquest(12273,PLAYTIME) == 2)
			callsub L_EraseQuest,12267,12268,12273;
		else {
			// Reward: 1x Sea God's Wrath
			callsub L_CheckQuest,12267,"Weird Coelacanth",6423,1;
			callsub L_CheckQuest,12268,"Gloomy Coelacanth",6423,1;
		}
		break;
	case 4: // Hard Culvert Weekly Service
		if (checkquest(12274,PLAYTIME) == 2)
			callsub L_EraseQuest,12269,12270,12274;
		else {
			// Reward: 5x Sea God's Wrath
			callsub L_CheckQuest,12269,"Violent Coelacanth",6423,5;
			callsub L_CheckQuest,12270,"Mutant Coelacanth",6423,5;
		}
		break;
	default:
		mes "[Madeca]";
		mes "มีบางอย่างผิดปกติโปรดตรวจสอบอีกครั้ง!";
		close;
	}
	mes "[Madeca]";
	mes "ไม่เห็นภารกิจที่เสร็จสมบูรณ์เลย!";
	close;

L_EraseQuest:
	for(set .@j,0; .@j<getargcount(); set .@j,.@j+1) {
		if (checkquest(getarg(.@j)) > -1)
			erasequest getarg(.@j);
	}
	mes "[Madeca]";
	mes "อืมฉันขอโทษคุณเกินเวลาที่กำหนดฉันจึงไม่สามารถจ่ายเงินให้คุณได้";
	close;

// callsub L_CheckQuest,<quest ID>,<monster>,<reward item ID>,<reward item amount>;
L_CheckQuest:
	if (checkquest(getarg(0),HUNTING) == 2) {
		mes "[Madeca]";
		mes "คุณโดนลงโทษ ^ 0000ff" + getarg (1) + "^ 000000!";
		mes "นี่คือเงินของคุณ!";
		erasequest getarg(0);
		specialeffect2 EF_STEAL;
		getitem getarg(2),getarg(3);
		close;
	}
	return;
}

// Instance Creation :: pump
//============================================================
mal_in01,160,34,4	script	Missing, the Cleaner	545,{
	if (BaseLevel < 90) {
		mes "[Missing, the Cleaner]";
		mes "Are you talking to me?!";
		next;
		mes "[Missing, the Cleaner]";
		mes "ฉันไม่แน่ใจว่าอะไรทำให้คุณมาที่นี่ แต่คุณเข้าไม่ได้!";
		mes "คำสั่งของโทมัส ... ";
		next;
		mes "^ 770099 คุณต้องเป็นระดับพื้นฐาน 90 จึงจะเข้าได้ ^ 000000";
		close;
	}
	if (in_canal_n == 0) {
		mes "[Missing, the Cleaner]";
		mes "(trembling)";
		next;
		mes "^ 660066 แมวตัวนี้รูปร่างไม่ดีมันเบา ^ 000000";
		next;
		if(select("Pet the cat:Are you ok?") == 1) {
			mes "[Missing, the Cleaner]";
			mes "คุณกำลังพูดกับฉันเหรอ?!";
			next;
			mes "^ 660066A แมวตัวสั่นกำลังมองมาที่คุณ ^ 000000";
			close;
		}
		mes "[Missing, the Cleaner]";
		mes "โรงเรียน!";
		next;
		mes "[Missing, the Cleaner]";
		mes "เรามีปัญหามากมายในช่วง";
		mes "การทำความสะอาดท่อระบายน้ำ แต่ a";
		mes "แมวต้องทำ ... ";
		next;
		select("What culvert are you cleaning?");
		mes "[Missing, the Cleaner]";
		mes "มันอาจจะดูไม่คุ้นเคยสำหรับ";
		mes "คนนอก แต่จริงๆแล้วที่นี่";
		mes "ทำขึ้นเพื่อเก็บของที่ระลึกและ";
		mes "ของหรูหรา.";
		next;
		mes "[Missing, the Cleaner]";
		mes "หลังจากที่มีการสั่นสะเทือนครั้งใหญ่";
		mes "เกาะนี้ของเริ่มรั่ว";
		mes "แถว ๆ นี้นั่นคือสาเหตุที่ฉันกลายเป็น";
		mes "ทำความสะอาดที่นี่แม้ว่าฉันจะใช้";
		mes "เป็นผู้จัดการ";
		next;
		mes "^ 660066 แมวเหมือนกำลังร้องไห้ ... ^ 000000";
		next;
		mes "[Missing, the Cleaner]";
		mes "มีท่อระบายน้ำตามทางนี้";
		mes "ไปชั้นใต้ดินมีหลายอย่าง";
		mes "พวกที่ทำร้ายฉันฉันเบื่อ";
		mes "ต่อสู้กับพวกมันหลายครั้ง a";
		mes "วัน!";
		next;
		mes "[Missing, the Cleaner]";
		mes "โอ้! คุณสัมผัสขนของฉัน!";
		mes "ขนของฉันร่วงหมดแล้ว ... หมดแล้ว";
		mes "ความผิดของพวกนั้น ~";
		next;
		mes "^ 660066 ตอนนี้คุณสังเกตเห็นว่าขนของแมวหายไปในบางที่ ^ 000000";
		next;
		select("Why don't you just quit!");
		mes "[Missing, the Cleaner]";
		mes "ฉันยังต้องทำมาหากินใช่ไหม";
		next;
		mes "[Missing, the Cleaner]";
		mes "บางครั้งฉันก็ได้กล่องนำโชค";
		mes "หรือดีกว่านั้นฉันจะได้กระป๋อง ... ";
		next;
		if(select("โอเคไชโย!: ฉันจะช่วยถ้าเป็นไปได้ ...") == 1) {
			mes "[Missing, the Cleaner]";
			mes "ขอบคุณครับผมจะกลับไปเทน้ำ!";
			close;
		}
		mes "[Missing, the Cleaner]";
		mes "(turns down his voice)";
		mes "ถ้าอยากช่วยจริงก็เข้ามาใกล้ ๆ !";
		next;
		select("(get in closer)");
		mes "[Missing, the Cleaner]";
		mes "ไม่ใช่ทุกคนที่สามารถเป็นผู้ช่วยได้";
		mes "ที่นี่ แต่ฉันไม่สามารถช่วยเลือก";
		mes "ทุกคนในขณะนี้ ... ";
		next;
		mes "[Missing, the Cleaner]";
		mes "ถ้าคุณต้องการลงไปที่นี่ขอรหัสนี้หน่อย";
		next;
		mes "^ 660066 แมวเขียนบันทึกเพื่อขอรหัสถึงคุณ ^ 000000";
		next;
		select("Open the memo.");
		mes "^ 660000Aragam ดูถูกฉัน ^ 000000";
		next;
		mes "[Missing, the Cleaner]";
		mes "คุณต้องอยู่ที่นี่กับเพื่อน ๆ ";
		mes "เพราะคุณไม่สามารถเข้าไปคนเดียวได้! and";
		mes "รหัสผ่านจะมาจาก";
		mes "หัวหน้าพรรค!";
		next;
		mes "[Missing, the Cleaner]";
		mes "สิ่งที่สำคัญก็คือคุณต้อง";
		mes "มี Seagod Protection ติดตัวไปด้วย";
		next;
		mes "[Missing, the Cleaner]";
		mes "อย่าลืม! ฉันจะไม่เปิด";
		mes "ประตูถ้าคุณไม่มี Seagod Protection!";
		set in_canal_n,1;
		close;
	}
	if (rentalcountitem(6436) == 0) {
		mes "[Missing, the Cleaner]";
		mes "คุณดูไม่เหมือนคุณมี Seagod Protection ตอนนี้ฉันเปิดประตูไม่ได้!";
		close;
	}
	set .@party_id,getcharid(1);
	set .@md_name$,"Culvert";
	if (!.@party_id) {
		mes "^ 0000ff คุณต้องจัดงานเลี้ยง";
		mes "มากกว่า 1 สมาชิกหรือเป็นสมาชิก";
		mes "ของปาร์ตี้แล้วกลับมา ^ 000000";
		close;
	}
	set .@playtime, checkquest(12254,PLAYTIME);
	if (.@playtime == -1) {
		if (is_party_leader() == true) {
			mes "[Missing, the Cleaner]";
			mes "อืมมมีอะไรหรอบอกรหัสผ่านถ้าคุณเป็นหัวหน้า!";
			next;
			switch(select("ไม่ ... ไม่มีอะไร: อารากัมดูถูกฉัน: เข้าไปในท่อระบายน้ำ")) {
			case 1:
				mes "[Missing, the Cleaner]";
				mes "ช่างเป็นมือใหม่";
				close;
			case 2:
				if (instance_create(.@md_name$) < 0) {
					mes "Party name: "+getpartyname(.@party_id);
					mes "Party leader: "+strcharinfo(0);
					mes "^0000ff"+.@md_name$+"^000000 - Reservation Failed!";
					close;
				}
				mes "^3333FF"+.@md_name$+"^000000 - Reserving";
				mes "หลังจากทำการจองแล้วคุณ";
				mes "ต้องเลือก Enter the Culvert";
				close;
			case 3:
				callsub L_Enter,0;
			}
		}
		if(select("Enter Culvert:Cancel") == 2)
			end;
		callsub L_Enter,1;
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "คุณสามารถเข้าไปใน Culvert ได้ถ้าประตูเปิดอยู่";
		next;
		if(select("เข้าสู่ Culvert: ยกเลิก") == 2)
			close;
		callsub L_Enter,0;
	} else if (.@playtime == 2) {
		mes "^ 0000ff ประตูสู่ท่อระบายน้ำเปิดอีกครั้ง ^ 000000";
		erasequest 12254;
		close;
	}
	end;
L_Enter:
	switch(instance_enter("Culvert")) {
	case IE_OTHER:
		mes "เกิดข้อผิดพลาดที่ไม่รู้จัก";
		close;
	case IE_NOINSTANCE:
		mes "ประตูสู่ท่อระบายน้ำยังคงปิดอยู่";
		mes "คุณต้องรอจนกว่าคุณจะสามารถเข้าหรือค้นหาหัวหน้าปาร์ตี้ที่สามารถสร้างอินสแตนซ์ได้";
		close;
	case IE_NOMEMBER:
		mes "เฉพาะสมาชิกปาร์ตี้เท่านั้นที่สามารถเข้าร่วมได้";
		close;
	case IE_OK:
		mapannounce "mal_in01", strcharinfo(0)+" of the party "+getpartyname(getcharid(1))+" is entering the Culvert.",bc_map,"0x00ff99";
		if (checkquest(12254) == -1) setquest 12254;
		//warp "1@pump",63,98;
		if (getarg(0) == 0) close;
		else end;
	}
}

// Instance: Common Scripts
//============================================================
1@pump,63,100,4	script	Missing, the Cleaner#0	545,{
	if (is_party_leader() == false) {
		mes "[Missing, the Cleaner]";
		mes "ฉันกำลังพยายามคุยกับหัวหน้าของคุณตอนนี้โปรดอย่ารบกวนและรอ!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "นี่คือท่อระบายน้ำใต้ดินที่คุณจะทำความสะอาดกับเพื่อนของคุณ!";
	next;
	mes "[Missing, the Cleaner]";
	mes "ฉันจะเปิดประตูก่อนนะเธอรู้ไหมว่าจะสู้ยังไง";
	next;
	switch(select("ฉันส่งขนมปังเก่งมาก: ฉันรู้วิธีต่อสู้ :^ffffff ขอเพชรมาให้ฉัน ^ 000000 ")) {
	case 1:
		mes "[Missing, the Cleaner]";
		mes "เอาล่ะฉันจะให้คุณทำความสะอาดที่นี่ตามปกติ! ฉันจะเตรียมที่มุมขวามือโปรดตามฉันมา!";
		next;
		if(select("รอ! ฉันเปลี่ยนใจ!: ก็ตามใจ ~ ") == 1) {
			mes "[Missing, the Cleaner]";
			mes "หือยังไม่พร้อมใช่ไหมคุยกับฉันอีกครั้งเมื่อคุณพร้อม";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"Missing: Move toward the 3 o'clock direction and wait for my next order!",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("Missing, the Cleaner#0");
		enablenpc instance_npcname("Missing, the Cleaner#n");
		close;
	case 2:
		if (BaseLevel < 140) {
			mes "[Missing, the Cleaner]";
			mes "อืมม ... คิดถึงคิดว่าตัวเองอ่อนแอ! บอกความจริง !!";
			close;
		}
		mes "[Missing, the Cleaner]";
		mes "หือ? คุณมีทักษะในการต่อสู้งั้นเหรอมีอยู่ที่หนึ่งที่ฉันไม่สามารถทำความสะอาดได้ ... ทำไมคุณไม่ไปที่นั่น!";
		next;
		mes "[Missing, the Cleaner]";
		mes "ฉันต้องบอกคุณว่ามันนานมากแล้ว ~~ นานมากแล้วที่เราทำความสะอาดสถานที่แห่งนั้นดังนั้นใครก็ตามที่เลเวล 140 จะเข้าไปไม่ได้! คุณแน่ใจหรือว่ายังอยากไป?";
		next;
		if(select("รอ! ฉันเปลี่ยนใจแล้ว!: แน่นอนฉันเตรียมตัวแล้ว ~") == 1) {
			mes "[Missing, the Cleaner]";
			mes "หืม? คุยกับฉันอีกครั้งเมื่อคุณพร้อม";
			close;
		}
		set 'party_id,getcharid(1);
		mapannounce instance_mapname("1@pump"),"Missing: I'll go in first, so follow me! I'll open up a gate at the 3 o'clock direction!",bc_map,"0xff88ff",FW_NORMAL,15;
		disablenpc instance_npcname("Missing, the Cleaner#0");
		enablenpc instance_npcname("Culvert Entrance#i");
		close;
	case 3:
		mes "[Missing, the Cleaner]";
		mes "ฉันบอกแล้วว่าอย่าเล่นกับฉัน ~!";
		close;
	}
	end;
}

1@pump,84,105,0	script	Culvert Entrance#i	45,3,3,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("Culvert Entrance#i");
	disablenpc instance_npcname("Culvert Entrance#n");	// disable warp out
	end;
OnTouch:
	if (BaseLevel >= 140)
		warp instance_mapname("2@pump"),38,88;
	else
		warp instance_mapname("1@pump"),74,105;
	end;
}

function	script	F_mal_missing	{
	mes "[Missing, the Cleaner]";
	mes "ฉันจะบอกวิธีทำความสะอาดในไม่ช้าคุณเห็นท่อระบายน้ำรอบตัวไหม";
	next;
	if(select("What culverts??:Yes, I see them.") == 1) {
		mes "[Missing, the Cleaner]";
		mes "หือ? นี่เป็นครั้งแรกที่คุณเห็นท่อระบายน้ำหรือเปล่าคุณจะเห็นเครื่องจักรฝังอยู่ในพื้นมากมายคุณอาจมองไปรอบ ๆ และกลับมา!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "ถูกต้องแล้ว! ท่อระบายน้ำเหล่านั้นสำคัญมาก! เราต้องทำความสะอาดท่อระบายน้ำเพื่อป้องกันไม่ให้สาหร่ายปิดกั้นนี่คือสิ่งที่พวกเราผู้ทำความสะอาดทำ!";
	next;
	mes "[Missing, the Cleaner]";
	mes "ระวังมอนสเตอร์บางตัวจะตามคุณมาในขณะที่คุณกำลังทำความสะอาดท่อระบายน้ำ! แต่อย่ายอมแพ้!";
	next;
	mes "[Missing, the Cleaner]";
	mes "และ ... อย่าปล่อยให้สาหร่าย 6 ตัวซ้อนกัน! ได้ถึง 5 ตัว! ถ้ามี 6 ตัวฉันจะลากคุณออกไปจากที่นี่ !!";
	next;
	mes "[Missing, the Cleaner]";
	mes "ฉันไม่สนใจว่าคุณจะได้รับความช่วยเหลือจากเพื่อนของคุณหรือไม่ฉันต้องการให้คุณทำความสะอาดท่อระบายน้ำทั้งหมดอย่าลืม! ฉันจะคอยดูคุณและให้คำแนะนำคุณแค่ทำตามที่ฉันบอก !!";
	next;
	if (is_party_leader() == false) {
		mes "[Missing, the Cleaner]";
		mes "เมื่อหัวหน้าของคุณเตรียมการเสร็จแล้วก็จะเริ่มดังนั้นเตรียมใจให้พร้อม !!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "ตอนนี้คุณพร้อมที่จะทำความสะอาดแล้วหรือยัง";
	next;
	switch(select("รอ! ยังไม่ได้!: มาเริ่มกันเลย!")) {
	case 1:
		mes "[Missing, the Cleaner]";
		mes "หือยังไม่พร้อมงั้นคุยกับฉันเมื่อคุณพร้อม";
		close;
	case 2:
		mes "[Missing, the Cleaner]";
		mes "ตกลง! ตอนนี้เริ่มแล้ว!";
		return;
	}
}

// Instance: Normal Mode :: in_pump1
//============================================================
1@pump,84,105,4	script	Missing, the Cleaner#nf	545,{
	mes "[Missing, the Cleaner]";
	mes "ฉันทำงานทำความสะอาดมา 40 ปีแล้วไม่เคยเจอทีมไหนแย่ขนาดนี้ !!";
	next;
	mes "[Missing, the Cleaner]";
	mes "ฉันเห็นสาหร่ายทุกที่เพราะคุณกำลังเลื้อย! ท่อน้ำทั้งหมดจะถูกปิดกั้นด้วยสาหร่าย!";
	next;
	if (is_party_leader() == false) {
		mes "[Missing, the Cleaner]";
		mes "เมื่อหัวหน้าของคุณเตรียมการเสร็จสิ้นจะมีการตัดสินข้อยุติดังนั้นเตรียมใจให้พร้อม !!";
		close;
	}
	mes "[Missing, the Cleaner]";
	mes "ถ้าคุณมีความกล้าที่จะท้าทายอีกครั้งฉันจะให้โอกาสคุณอีกครั้ง! คุณจะเลือกอะไร!?";
	next;
	switch(select("รอ! ยังไม่หมด!: เอาล่ะมาเริ่มกันใหม่!")) {
	case 1:
		mes "[Missing, the Cleaner]";
		mes "คุณยังช้าอยู่นะ! คุยกับฉันอีกครั้งเมื่อคุณพร้อม";
		close;
	case 2:
		mes "[Missing, the Cleaner]";
		mes "โอเค! มาเริ่มกันเลย!";
		set .@i$, charat(strnpcinfo(2),0);
		enablenpc instance_npcname("Missing, the Cleaner#"+.@i$);
		donpcevent instance_npcname("Missing, the Cleaner#"+.@i$)+"::OnStart";
		disablenpc instance_npcname("Culvert Entrance#"+.@i$);
		disablenpc instance_npcname("Missing, the Cleaner#"+.@i$+"o");
		donpcevent instance_npcname("Monster Hole#"+.@i$)+"::OnClear";
		disablenpc instance_npcname(strnpcinfo(0));
		close;
	}
	close;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@pump,84,105,4	script	Missing, the Cleaner#n	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnStart";
	close;
OnInstanceInit:
	disablenpc instance_npcname("Missing, the Cleaner#n");
	end;
OnStart:
	killmonster instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	disablenpc instance_npcname("Missing, the Cleaner#n");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("1@pump");
	areamonster .@map$,55,99,61,105,"Contaminated Seaweed",2191,1,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		mapannounce .@map$,"Contaminated Seaweed: "+.@mob_dead_num+" stem",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#n")+"::OnClear";
	set .@map$, instance_mapname("1@pump");
	killmonster .@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead";
	enablenpc instance_npcname("Missing, the Cleaner#nf");
	mapannounce .@map$,"What is this!! Seaweed is all over the culverts! You're done with cleaning! Get out!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("Missing, the Cleaner#n");
	end;
OnTimer100:
	mapannounce instance_mapname("1@pump"),"First culvert will be opened in 5 sec. Cleaner can find the opened drain and click to start cleaning.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("1@pump"),"The one who's in charge of cleaning the culvert cannot move or be attacked until the casting is over.",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer45000:
OnTimer95000:
OnTimer145000:
OnTimer195000:
OnTimer245000:
OnTimer295000:
OnTimer345000:
OnTimer395000:
OnTimer445000:
	mapannounce instance_mapname("1@pump"),"Next culvert will be opened in 5 sec. Please hurry up and find the position of the next culvert.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer50000:
OnTimer100000:
OnTimer150000:
	donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer200000:
OnTimer250000:
OnTimer300000:
OnTimer350000:
OnTimer400000:
OnTimer450000:
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#n")+"::OnSpawn";
	end;
OnTimer515000:
	set .@map$, instance_mapname("1@pump");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	mapannounce .@map$,"Contaminated Seaweed: "+.@mob_dead_num+" stem. Missing will come and inspect the results of the cleaning soon.",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer520000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("1@pump"),instance_npcname("Missing, the Cleaner#n")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#n")+"::OnEnable";
	end;
}

1@pump,1,1,4	script	Monster Hole#n	-1,{
	end;
OnSpawn:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	donpcevent instance_npcname("#Culvert_"+.@i$+rand(1,.@n))+"::OnEnable";
	end;
OnClear:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		set .@n,6;
	else if (.@i$ == "h")
		set .@n,10;
	for(set .@i,1; .@i<=.@n; set .@i,.@i+1)
		donpcevent instance_npcname("#Culvert_"+.@i$+.@n)+"::OnClear";
	end;
}

1@pump,36,111,4	script	#Culvert_n1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",10;
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("1@pump");
	set .@index, atoi(charat(strnpcinfo(2),9));
	switch(.@index) {
		case 1: setarray .@c[0],32,107,40,115; break;
		case 2: setarray .@c[0],64,120,72,128; break;
		case 3: setarray .@c[0],76,110,84,118; break;
		case 4: setarray .@c[0],36,76,44,84; break;
		case 5: setarray .@c[0],71,76,79,84; break;
		case 6: setarray .@c[0],54,97,62,105; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Crab",2176,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Marse",2175,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Vadon",2174,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Shellfish",2178,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Kukre",2179,rand(1,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Cornutus",2177,rand(1,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	getmapxy(.@map$,.@x,.@y,BL_NPC);
	getpartymember 'party_id,2;
	copyarray .@partymemberaid[0],$@partymemberaid[0],$@partymembercount;
	for(set .@i,0; .@i<$@partymembercount; set .@i,.@i+1) {
		if (attachrid(.@partymemberaid[.@i])) {
			if (strcharinfo(3) == .@map$)
				viewpoint 0,.@x,.@y,.@index,0xFFFF00;
			detachrid;
		}
	}
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("1@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer49500:	//OnTimer50000 clashes with the timer in "Missing, the Cleaner#h".
	donpcevent instance_npcname("Missing, the Cleaner#n")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
1@pump,68,124,4	duplicate(#Culvert_n1)	#Culvert_n2	844,14,14
1@pump,80,114,4	duplicate(#Culvert_n1)	#Culvert_n3	844,14,14
1@pump,40,80,4	duplicate(#Culvert_n1)	#Culvert_n4	844,14,14
1@pump,75,80,4	duplicate(#Culvert_n1)	#Culvert_n5	844,14,14
1@pump,58,101,4	duplicate(#Culvert_n1)	#Culvert_n6	844,14,14

1@pump,1,1,4	script	Boss Creation#n	-1,{
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	initnpctimer;
	end;
OnTimer100:
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n")
		mapannounce instance_mapname("1@pump"),"Hmm! That wasn't so bad!",bc_map,"0xff88ff",FW_NORMAL,15;
	else if (.@i$ == "h")
		mapannounce instance_mapname("2@pump"),"Hmm! You guys are pretty good!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer5000:
	mapannounce strnpcinfo(4),"Let's pack up and go... WHAT!!?",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer10000:
	mapannounce strnpcinfo(4),"I sense something strange!! Don't loosen up-- prepare for a fight!!",bc_map,"0xff88ff",FW_NORMAL,15;
	end;
OnTimer20000:
	stopnpctimer;
	set .@i$, charat(strnpcinfo(2),0);
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	if (rand(1,100) > 50) {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"Weird Coelacanth",2188,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"Mutant Coelacanth",2189,1,.@label$;
		mapannounce strnpcinfo(4),"Something big inside the abyss appears with a huge noise.",bc_map,"0x00ffcc",FW_NORMAL,15;
	} else {
		if (.@i$ == "n")
			monster instance_mapname("1@pump"),0,0,"Gloomy Coelacanth",2187,1,.@label$;
		else if (.@i$ == "h")
			monster instance_mapname("2@pump"),0,0,"Violent Coelacanth",2190,1,.@label$;
		mapannounce strnpcinfo(4),"Something savage inside the abyss appears with a huge noise.",bc_map,"0x00ffcc",FW_NORMAL,15;
	}
	end;
OnMyMobDead:
	if (mobcount(strnpcinfo(4),instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		mapannounce strnpcinfo(4),"You've destroyed all monsters in the culverts. There are trophies lying on the ground, please look for them.",bc_map,"0xffff00",FW_NORMAL,15;
		set .@i$, charat(strnpcinfo(2),0);
		set .@map$, strnpcinfo(4);
		enablenpc instance_npcname("Culvert Entrance#"+.@i$);
		enablenpc instance_npcname("Missing, the Cleaner#"+.@i$+"o");
		if (.@i$ == "n") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,6401);
				     if (.@j < 5001) set .@item,12636; //Malang_Sp_Can
				else if (.@j < 5501) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 6001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 6201) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 6401) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		} else if (.@i$ == "h") {
			for(set .@i,0; .@i<10; set .@i,.@i+1) {
				set .@j, rand(1,5001);
				     if (.@j < 2001) set .@item,12615; //Low_Coin_Pocket
				else if (.@j < 3001) set .@item,12621; //Egrade_Pocket
				else if (.@j < 4001) set .@item,12620; //Dgrade_Pocket
				else if (.@j < 4501) set .@item,12619; //Cgrade_Pocket
				else if (.@j < 5001) set .@item,12623; //High_Weapon_Box
				else continue;
				makeitem .@item,1,.@map$,rand(40,77),rand(87,120);
			}
		}
	} else
		mapannounce strnpcinfo(4),"There are still monsters alive.",bc_map,"0x00ff99",FW_NORMAL,20;
	end;
}

1@pump,84,105,4	script	Missing, the Cleaner#no	545,{
	set .@i$, charat(strnpcinfo(2),0);
	if (.@i$ == "n") {
		mes "[Missing, the Cleaner]";
		mes "แปลกใจมาก !!";
		next;
		mes "[Missing, the Cleaner]";
		mes "ปลาหน้าตาน่ารักตัวนั้นน่าจะเป็น Coelacanths ตัวหนึ่งที่อาศัยอยู่แถว ๆ นี้นี่เป็นครั้งแรกที่ฉันเห็นมันมาที่นี่คนเดียว!";
		next;
		mes "[Missing, the Cleaner]";
		mes "ฉันจะทำความสะอาดซากไปแพ็คและออกไป! ทางเข้าอยู่อีกด้านมองหา!";
		next;
	} else if (.@i$ == "h") {
		mes "[Missing, the Cleaner]";
		mes "เจ้าทำลายสัตว์ประหลาดขนาดมหึมา!";
		next;
		mes "[Missing, the Cleaner]";
		mes "คนที่คุณทำลายเป็นหนึ่งใน Coelacanths หลากหลายชนิดคนอื่น ๆ พยายามทำลายพวกมัน แต่ล้มเหลว";
		next;
		mes "[Missing, the Cleaner]";
		mes "ฉันจะทำความสะอาดซากคุณไปรับถ้วยรางวัลและจากไป!";
		next;
	}
	mes "[Missing, the Cleaner]";
	mes "อีกอย่าง! อย่าคุยกับทุกคนเกี่ยวกับสิ่งที่คุณได้เห็นในวันนี้ !!";
	next;
	mes "[Missing, the Cleaner]";
	mes "ถ้าโทมัสปิดตัวลงเราจะตกงาน!";
	close;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
}

1@pump,32,100,0	warp2	Culvert Entrance#n	3,3,mal_in01,161,32

//MD_Putmob "1@pump" 0 0 0 0 20 HYDRA 0 0 2
1@pump,1,1,4	script	Hydra#n	-1,{
OnInstanceInit:
	monster strnpcinfo(4),0,0,"Hydra",1068,20;
	end;
}

// Instance: Hard Mode :: in_pump2
//============================================================
2@pump,39,88,4	duplicate(Missing, the Cleaner#nf)	Missing, the Cleaner#hf	545

2@pump,39,88,4	script	Missing, the Cleaner#h	545,{
	callfunc "F_mal_missing";
	donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnStart";
	close;
OnStart:
	killmonster instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	disablenpc instance_npcname("Missing, the Cleaner#h");
	initnpctimer;
	end;
OnAddSeaweed:
	set .@map$, instance_mapname("2@pump");
	areamonster .@map$,75,78,85,88,"Contaminated Seaweed",2191,1,instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		mapannounce .@map$,"Proliferous Contaminated Seaweed: "+.@mob_dead_num+" stem",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnMyMobDead:
	end;
OnFail:
	stopnpctimer;
	donpcevent instance_npcname("Monster Hole#h")+"::OnClear";
	set .@map$, instance_mapname("2@pump");
	killmonster .@map$, instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead";
	enablenpc instance_npcname("Missing, the Cleaner#hf");
	mapannounce .@map$,"What is this!! Seaweed is all over the culverts! You're done with cleaning! Get out!!",bc_map,"0xff88ff",FW_NORMAL,15;
	disablenpc instance_npcname("Missing, the Cleaner#h");
	end;
OnTimer100:
	mapannounce instance_mapname("2@pump"),"First culvert will be opened in 5 sec. Cleaner can find the opened drain and click to start cleaning.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer5500:
	mapannounce instance_mapname("2@pump"),"The one who's in charge of cleaning the culvert cannot move or be attacked until the casting is over.",bc_map,"0x00ffcc",FW_NORMAL,15;
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer35000:
OnTimer75000:
OnTimer115000:
OnTimer155000:
OnTimer195000:
OnTimer235000:
OnTimer275000:
OnTimer315000:
OnTimer355000:
	mapannounce instance_mapname("2@pump"),"Next culvert will be opened in 5 sec. Please hurry up and find the position of the next culvert.",bc_map,"0x00ffcc",FW_NORMAL,15;
	end;
OnTimer40000:
OnTimer80000:
OnTimer120000:
	donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer160000:
OnTimer200000:
OnTimer240000:
OnTimer280000:
OnTimer320000:
OnTimer360000:
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		donpcevent instance_npcname("Monster Hole#h")+"::OnSpawn";
	end;
OnTimer420000:
	mapannounce instance_mapname("2@pump"),"It seems Missing will come and inspect the results of the cleaning soon. Shall we clean up the mess around here?",bc_map,"0xff3333",FW_NORMAL,20;
	end;
OnTimer425000:
	stopnpctimer;
	set .@mob_dead_num, mobcount(instance_mapname("2@pump"),instance_npcname("Missing, the Cleaner#h")+"::OnMyMobDead");
	if (.@mob_dead_num >= 6)
		donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnFail";
	else
		donpcevent instance_npcname("Boss Creation#h")+"::OnEnable";
	end;
}

2@pump,53,114,4	script	#Culvert_h1	844,14,14,{ //temporary workaround for ALL_SAMEMAP
	progressbar "0xFFFF00",15;
	stopnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	set .@label$, instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	set .@map$, instance_mapname("2@pump");
	set .@index, atoi(substr(strnpcinfo(2),9,getstrlen(strnpcinfo(2))-1));
	switch(.@index) {
		case 1: setarray .@c[0],49,110,57,118; break;
		case 2: setarray .@c[0],75,105,83,113; break;
		case 3: setarray .@c[0],110,110,118,118; break;
		case 4: setarray .@c[0],94,94,102,102; break;
		case 5: setarray .@c[0],58,92,66,100; break;
		case 6: setarray .@c[0],53,66,61,74; break;
		case 7: setarray .@c[0],43,45,51,53; break;
		case 8: setarray .@c[0],77,59,85,67; break;
		case 9: setarray .@c[0],96,70,104,78; break;
		case 10: setarray .@c[0],111,46,119,54; break;
	}
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Marc",2182,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Sword Fish",2181,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Ancient Strouf",2180,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Mutant Anolian",2183,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Abysmal Obeaune",2184,rand(2,3),.@label$;
	areamonster .@map$,.@c[0],.@c[1],.@c[2],.@c[3],"Metamorphous Kapha",2185,rand(2,3),.@label$;
	specialeffect EF_MAPPILLAR2,ALL_SAMEMAP; //currently broken
	initnpctimer;
	end;
OnMyMobDead:
	end;
OnClear:
	stopnpctimer;
	killmonster instance_mapname("2@pump"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;
OnTimer39500:	//OnTimer40000 clashes with the timer in "Missing, the Cleaner#h".
	donpcevent instance_npcname("Missing, the Cleaner#h")+"::OnAddSeaweed";
	donpcevent instance_npcname(strnpcinfo(0))+"::OnClear";
	disablenpc instance_npcname(strnpcinfo(0));
	end;
OnTouch:
	specialeffect EF_MAPPILLAR2;
	end;
}
2@pump,79,109,4	duplicate(#Culvert_h1)	#Culvert_h2	844,14,14
2@pump,114,114,4	duplicate(#Culvert_h1)	#Culvert_h3	844,14,14
2@pump,98,98,4	duplicate(#Culvert_h1)	#Culvert_h4	844,14,14
2@pump,62,96,4	duplicate(#Culvert_h1)	#Culvert_h5	844,14,14
2@pump,57,70,4	duplicate(#Culvert_h1)	#Culvert_h6	844,14,14
2@pump,47,49,4	duplicate(#Culvert_h1)	#Culvert_h7	844,14,14
2@pump,81,63,4	duplicate(#Culvert_h1)	#Culvert_h8	844,14,14
2@pump,100,74,4	duplicate(#Culvert_h1)	#Culvert_h9	844,14,14
2@pump,115,50,4	duplicate(#Culvert_h1)	#Culvert_h10	844,14,14

2@pump,1,1,4	duplicate(Monster Hole#n)	Monster Hole#h	-1
2@pump,1,1,4	duplicate(Boss Creation#n)	Boss Creation#h	-1
2@pump,39,88,4	duplicate(Missing, the Cleaner#no)	Missing, the Cleaner#ho	545
2@pump,38,100,0	duplicate(Culvert Entrance#n)	Culvert Entrance#h	3,3

//MD_Putmob "2@pump" 0 0 0 0 20 HYDRA 0 0 2
2@pump,1,1,4	duplicate(Hydra#n)	Hydra#h	-1
