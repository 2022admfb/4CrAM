//===== rAthena Script =======================================
//= Isle Of Bios
//===== Description:=========================================
//= [Walkthrough Conversion]
//= Isle Of Bios Instance
//===== Changelogs:==========================================
//= 1.0 First version. [Capuche]
//============================================================

moro_cav,50,64,5	script	Yellow Seed#bios	CLEAR_NPC,{
	if (BaseLevel < 160) {
		mes "^ff0000อักขระที่ต่ำกว่าระดับ 160 ไม่สามารถเข้าสู่ดันเจี้ยนนี้ได้ ^000000";
		close;
	}
	if (isbegin_quest(15006) == 0) {
		mes "^ff0000ก่อนอื่นคุณต้องพบกับชายชราพเนจรเพื่อเข้าถึงดันเจี้ยนนี้ ^000000";
		close;
	}
	.@party_id = getcharid(1);
	if (.@party_id < 1) {
		mes "^ff0000ก่อนอื่นคุณต้องจัดตั้ง / เข้าร่วมปาร์ตี้อย่างน้อย 1 คนเพื่อเข้าสู่ดันเจี้ยนนี้ ^000000";
		close;
	}
	switch( checkquest(15005,PLAYTIME) ) {
	case -1:
		break;
	case 0:
	case 1:
		if (checkquest(15007,PLAYTIME) == 2) {// 5 mins to re-enter
			mes "^ff0000This Memorial Dungeon ไม่สามารถเข้าถึงได้เป็นเวลา 23 ชั่วโมงหลังจากการเยี่ยมชมครั้งล่าสุดของคุณ ^000000";
			close;
		}
		switch( checkquest(15008,PLAYTIME) ) {
		case -1:
			break;
		case 0:
		case 1:
			mes "^0000ffThis Memorial Dungeon ไม่สามารถเข้าถึงได้เป็นเวลา 23 ชั่วโมงหลังจากการเยี่ยมชมครั้งล่าสุดของคุณ ^000000";
			close;
		case 2:
			break;
		}
		break;
	case 2:
		erasequest 15005;// Memorial Dungeon:Isle of Bios
		erasequest 15007;// Memorial Dungeon:Isle of Bios
		if (isbegin_quest(15008) > 0)
			erasequest 15008;// Memorial Dungeon:Isle of Bios
		break;
	}
	if (is_party_leader() == true)
		.@create$ = "Create Memorial dungeon.";
	switch( select( .@create$, "Enter Isle of Bios.", "ยกเลิก." ) ) {
	case 1:
		instance_create("Isle of Bios");
		mes "^ff0000สร้างดันเจี้ยนแห่งความทรงจำ ^000000";
		mes "^ff0000กรุณาคลิกแล้วกด Enter อีกครั้ง ^000000";
		close;
	case 2:
		switch( instance_enter("Isle of Bios") ) {
		case IE_NOMEMBER:
		case IE_NOINSTANCE:
		case IE_OTHER:
			if (is_party_leader(.@party_id) == false) {
				mes "^ff0000โปรดทราบว่า This Memorial Dungeon ไม่สามารถเข้าถึงได้เป็นเวลา 23 ชั่วโมงหลังจากการเยี่ยมชมครั้งล่าสุดของคุณ ^ 000000";
				next;
			}
			mes "^ff0000หัวหน้าปาร์ตี้ของคุณยังไม่ได้สร้าง Crack of Time ^ 000000";
			close;
		case IE_OK:
			mapannounce "moro_cav", "" + strcharinfo(0) + " ของปาร์ตี้, " + getpartyname( getcharid(1) ) + ", กำลังเข้าสู่ Isle of Bios.",bc_map,"0x00FF99";
			if (isbegin_quest(15005) == 0) {
				setquest 15005;// Memorial Dungeon:Isle of Bios
				setquest 15007;// Memorial Dungeon:Isle of Bios
			}
			end;
		}
	case 3:
		end;
	}
	end;

OnInit:
	while(true) {
		sleep 500;
		specialeffect EF_ENHANCE;
	}
	end;
}

moro_cav,45,60,5	script	Wandering Old Man#sara	4_M_EINOLD,{
	if (BaseLevel < 160) {
		mes "[Wandering Old Man]";
		mes "...";
		mes "ฉันเกรงว่าคุณจะยังไม่พร้อมสำหรับเรื่องนี้";
		mes "คุณต้องเลเวล 160 เป็นอย่างน้อย";
		close;
	}
	if (isbegin_quest(15006) == 0) {
		mes "[Wandering Old Man]";
		mes "... ฉันอยู่ที่ไหน";
		next;
		mes "[Wandering Old Man]";
		mes "ฉันไม่รู้ว่ามันเริ่มเมื่อไหร่ ... ";
		mes "ฝันร้ายเหมือนกันที่คอยรบกวนฉันมานานแล้ว";
		next;
		mes "[Wandering Old Man]";
		mes "ในฝันร้ายราชาปีศาจโมร็อกโกตื่นขึ้นจากการหลับใหลที่ยาวนานนับศตวรรษและทำให้โลกเต็มไปด้วยความมืดมิดและสยองขวัญ";
		next;
		mes "[Wandering Old Man]";
		mes "ฉันไม่สามารถสลัดภาพที่สดใสที่เห็นในฝันร้ายนั้นได้ฉันเดินเตร่ไปมาเหมือนคนบ้าและในที่สุดก็มาถึงที่นี่";
		next;
		mes "[Wandering Old Man]";
		mes "ฉันเห็นสถานที่นี้ในความฝันเป็นที่ที่ Demon God Morocc ได้ตื่นขึ้นจากการหลับใหลความฝันของฉันแสดงให้ฉันเห็นอนาคตที่เป็นไปได้";
		next;
		mes "[Wandering Old Man]";
		mes "ถ้าความฝันของฉันเป็นลางสังหรณ์จริงๆกองกำลังของ Demon God Morocc ก็อยู่ในเมล็ดสีเหลืองนั้น";
		next;
		mes "[Wandering Old Man]";
		mes "ฉันพยายามเข้าไปในเมล็ดพันธุ์ แต่ก็ไม่มีประโยชน์:ฉันแก่เกินไปและอ่อนแอ แต่บางทีคุณอาจทำได้";
		next;
		mes "[Wandering Old Man]";
		mes "โปรดตรวจสอบเมล็ดสีเหลืองนั่นด้วย";
		mes "การไปคนเดียวมันอันตรายเกินไปควรพาเพื่อนไปด้วยอย่างน้อยหนึ่งคน";
		setquest 15006;// Isle of Bios Exploration
		completequest 15006;// Isle of Bios Exploration
		close;
	}
	mes "[Wandering Old Man]";
	mes "โปรดตรวจสอบเมล็ดสีเหลืองนั่นด้วย";
	mes "การไปคนเดียวมันอันตรายเกินไปควรพาเพื่อนไปด้วยอย่างน้อยหนึ่งคน";
	close;
}

// warps
1@dth1,291,102,0	warp2	warp1#bios1	2,2,1@dth2,17,93
1@dth2,291,102,0	warp2	warp1#bios2	2,2,1@dth3,45,68

// zone 1
1@dth1,74,94,3	script	Grim Reaper Ankou#bios1	3029,{ end; }
1@dth1,71,97,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios1	4_M_GUILLOTINE
1@dth1,67,97,5	duplicate(Grim Reaper Ankou#bios1)	Vrid#bios1	4_M_KNIGHT_BLACK
// 1@dth1,67,97,5	script	Vrid#bios1	4_M_KNIGHT_BLACK,5,7,{// official

// 1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,{// official
1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios1");
	'zeith$ = instance_npcname("Zeith#bios1");
	'vrid$ = instance_npcname("Vrid#bios1");
	'reaper$ = instance_npcname("Grim Reaper Ankou#bios1");
	initnpctimer;
	npctalk "Vrid:คุณมาหยุดการฟื้นคืนชีพของโมร็อกโกด้วยเหรอ", 'vrid$;
	end;
OnTimer2500:
	npctalk "Vrid:คุณมาหยุดการฟื้นคืนชีพของโมร็อกโกด้วยเหรอ", 'vrid$;
	end;
OnTimer5000:
	npctalk "Vrid:ฉันชื่อ Vrid และนี่คือ Zeith จากเมือง Prontera เราเดินทางไปทั่วโลกต่อสู้เพื่อความยุติธรรม", 'vrid$;
	end;
OnTimer7500:
	npctalk "Vrid:เฮ้ Zeith พูดอะไรหน่อย", 'vrid$;
	end;
OnTimer10000:
	npctalk "Zeith:...", 'zeith$;
	end;
OnTimer12500:
	npctalk "Zeith:ฉันคือ Zeith", 'zeith$;
	end;
OnTimer15000:
	npctalk "Zeith:...", 'zeith$;
	end;
OnTimer17500:
	npctalk "Vrid:อย่ารังเกียจเขานะเขาไม่ได้เงียบขนาดนี้เมื่อมีแค่เราสองคน", 'vrid$;
	end;
OnTimer19500:
	npctalk "Zeith:... คุณพูดมากพอแล้วสำหรับเราทั้งคู่", 'zeith$;
	end;
OnTimer21000:
	npctalk "Vrid:พวกเรามาเพียงเพราะผู้เฒ่าที่อยู่ด้านล่างเอาแต่เดินเตร่เรื่องการคืนชีพของโมร็อกโก", 'vrid$;
	end;
OnTimer23500:
	npctalk "Vrid:เอ่อที่นี่คืออะไรฉันเพิ่งสัมผัสเมล็ดพันธุ์ทันใดนั้นทิวทัศน์ก็เปลี่ยนไป", 'vrid$;
	end;
OnTimer26000:
	npctalk "Vrid:วัวศักดิ์สิทธิ์พวกเรากำลังบินอยู่บนฟ้าเหรอ!", 'vrid$;
	end;
OnTimer28500:
	npctalk "???:* Chuckle * คุณอยากรู้ไหมว่าคุณอยู่ที่ไหน", 'reaper$;
	end;
OnTimer31000:
	npctalk "Zeith:มีใคร?!", 'zeith$;
	end;
OnTimer33500:
	enablenpc 'reaper$;
	npctalk "Grim Reaper Ankou:* Chuckle * นี่คือ Isle of Bios สถานที่ที่ฉันคิดขึ้นในใจ", 'reaper$;
	end;
OnTimer36000:
	npctalk "Grim Reaper Ankou:คุณได้เดินเข้าไปในกับดักทำให้ตัวเองเต็มใจเสียสละเพื่อลอร์ดโมร็อกโกของฉันคุณไม่กลัวที่จะตายหรือ", 'reaper$;
	end;
OnTimer38500:
	npctalk "Vrid:ป๊ะ! ถ้าฉันเป็นฉันคงไม่มาด้วยความเต็มใจฉันจะ?", 'vrid$;
	end;
OnTimer41000:
	npctalk "Vrid:คุณเป็นใครเราจะฆ่าคุณก่อนที่คุณจะทำ!", 'vrid$;
	end;
OnTimer43500:
	npctalk "Grim Reaper Ankou:อย่าล่อลวงฉันมนุษย์", 'reaper$;
	end;
OnTimer46000:
	npctalk "Grim Reaper Ankou:ฉันอยากดูคุณตายอย่างช้าๆและเจ็บปวด * หัวเราะเบา ๆ *", 'reaper$;
	end;
OnTimer48500:
	disablenpc 'reaper$;
	end;
OnTimer51000:
	npctalk "Vrid:เขาเป็นใครเขาพยายามจะคืนชีพให้โมรอค?", 'vrid$;
	end;
OnTimer53500:
	npctalk "Vrid:มาไล่เขากันเถอะ Zeith เร็วเข้า!", 'vrid$;
	end;
OnTimer54000:
	disablenpc 'vrid$;
	end;
OnTimer56000:
	npctalk "Zeith:... Ack.", 'zeith$;
	end;
OnTimer58500:
	npctalk "Zeith:ฉันเกลียดการวิ่ง", 'zeith$;
	end;
OnTimer61000:
	disablenpc 'zeith$;
	end;
OnTimer66000:
	stopnpctimer;
	mapannounce 'map_dth1$, "ระบบ:กำจัดศัตรูทั้งหมดบนเกาะและออกทางประตูอีกด้านหนึ่งเพื่อไปยังพื้นที่ถัดไป", bc_map, "0xFFFF00";
	'zeith$ = 'vrid$ = 'reaper$ = "";
	end;
}

1@dth1,1,1,0	script	mobs#bios1	HIDDEN_WARP_NPC,{
	end;
OnStart:
	.@label$ = instance_npcname("mobs#bios1") + "::OnMyMobDead";
	for ( .@mob_id = 3010; .@mob_id < 3013; .@mob_id++ ) {
		switch(.@mob_id) {
		case 3010:		// Corrupt Orc Baby
			setarray .@coord[0],
				90,94, 153,76, 121,53, 135,50, 145,107, 132,126, 101,125, 119,144, 124,164, 192,145, 194,116,
				203,131, 214,142, 196,156, 217,129, 227,150, 215,109, 240,130, 252,105, 249,144, 249,155, 232,87,
				316,103, 245,71, 246,71, 210,62, 214,53, 211,88;
			break;
		case 3011:		// Corrupt Baby Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 166,80, 131,101, 145,107, 108,156, 141,143, 165,130, 203,151,
				203,162, 219,151, 203,162, 206,169, 219,171, 227,150, 238,117, 240,140, 274,104, 306,103,
				237,80, 224,62, 233,57;
			break;
		case 3012:		// Corrupt Familiar
			setarray .@coord[0],
				143,49, 156,46, 170,67, 170,67, 131,101, 144,125, 101,125, 108,156, 135,152, 144,164,
				184,130, 204,112, 227,150, 205,98, 225,103, 228,124, 243,106, 225,89, 297,103, 223,72,
				232,69, 225,54, 211,73;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth1$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;

OnMyMobDead:
	.@count = mobcount( 'map_dth1$,instance_npcname("mobs#bios1") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce 'map_dth1$, "System:" + .@count + " monsters remain on the island.",bc_map,"0xFFFF00";
	else if (.@count == 1)
		mapannounce 'map_dth1$, "System:1 last monster remains on the island.",bc_map,"0xFFFF00";
	else if (.@count == 0) {
		mapannounce 'map_dth1$, "ระบบ:คุณกำจัดมอนสเตอร์ทั้งหมดแล้ว ใช้ทางออก 3 นาฬิกาเพื่อไปยังพื้นที่ถัดไป.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("mobs#bios1");
		enablenpc instance_npcname("warp1#bios1");
		enablenpc instance_npcname("start1#bios2");
		donpcevent instance_npcname("mobs#bios2") + "::OnStart";	// spawn at entrance
	}
	end;
}

// zone 2
1@dth2,67,97,0	script	start1#bios2	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios2");
	mapannounce 'map_dth2$, "System:Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00";
	end;
}

1@dth2,1,1,0	script	mobs#bios2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("mobs#bios2");
	.@label$ = instance_npcname("mobs#bios2") + "::OnMyMobDead";
	for ( .@mob_id = 3013; .@mob_id < 3016; ++.@mob_id ) {
		switch(.@mob_id) {
		case 3013:		// Corrupt Orc Warrior
			setarray .@coord[0],
				90,94, 153,76, 116,66, 133,47, 145,107, 137,134, 101,125, 119,144, 124,164, 194,116,
				203,131, 211,88, 215,109, 214,142, 192,145, 196,156, 217,129, 227,150, 230,89, 210,62,
				214,53, 246,71, 248,81, 252,105, 246,126, 249,144, 249,155, 316,103;
			break;
		case 3014:		// Corrupt Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 165,84, 145,107, 131,101, 108,156, 138,140, 165,130, 203,151,
				203,162, 219,151, 206,169, 203,162, 227,150, 222,164, 238,117, 237,80, 224,62, 233,57,
				236,138, 274,104, 306,103;
			break;
		case 3015:		// Corrupt Phen
			setarray .@coord[0],
				142,54, 156,46, 170,67, 170,67, 131,101, 101,125, 108,156, 135,152, 144,164, 135,131,
				184,130, 204,112, 205,98, 225,103, 225,89, 227,150, 232,69, 223,72, 212,75, 225,54,
				247,110, 232,126, 297,103;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth2$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;

OnMyMobDead:
	.@count = mobcount( 'map_dth2$,instance_npcname("mobs#bios2") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce 'map_dth2$, "System:" + .@count + " มอนสเตอร์ยังคงอยู่บนเกาะ.",bc_map,"0xFFFF00";
	else if (.@count == 1)
		mapannounce 'map_dth2$, "System:สัตว์ประหลาดตัวสุดท้าย 1 ตัวยังคงอยู่บนเกาะ.",bc_map,"0xFFFF00";
	else if (.@count == 0) {
		mapannounce 'map_dth2$, "System:คุณกำจัดมอนสเตอร์ทั้งหมดแล้ว ใช้ทางออก 3 นาฬิกาเพื่อไปยังพื้นที่ถัดไป.",bc_map,"0xFFFF00";
		disablenpc instance_npcname("mobs#bios2");
		enablenpc instance_npcname("warp1#bios2");
		enablenpc instance_npcname("Zeith#bios3");
		enablenpc instance_npcname("Vrid#bios3");
		enablenpc instance_npcname("event_boss#bios3");
	}
	end;
}

// zone 3
// 1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,5,7,{// official
1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,{
	if ('reward == 1) {
		mes "[Vrid]";
		mes "ฉันมาด้วย";
		mes "Hero Token โดยบังเอิญ";
		mes "อยากจะมอบให้คุณสมควรได้รับ";
		next;
		mes "[Vrid]";
		mes "ออกไปจากที่น่าขนลุกนี้กันเถอะ!";
		close2;
		getitem 6684,1;// Token Of Hero
		if (ep14_3_isle == 0)
			ep14_3_isle = 1;
		if (checkquest(15007,PLAYTIME) == 0 || checkquest(15007,PLAYTIME) == 1)
			setquest 15008;// Memorial Dungeon:Isle of Bios
		warp "moro_cav",45,63;
	}
	end;
}

1@dth3,64,74,5	script	event_boss#bios3	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("event_boss#bios3");
	'party_id = getcharid(1);
	donpcevent instance_npcname("freeze#bios3") + "::OnStart";
	end;
}

// 1@dth3,69,67,0	script	freeze#bios3	HIDDEN_WARP_NPC,30,30,{// official
1@dth3,1,1,0	script	freeze#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("freeze#bios3");
	'reaper3$ = instance_npcname("Grim Reaper Ankou#bios3");
	'zeith3$ = instance_npcname("Zeith#bios3");
	'vrid3$ = instance_npcname("Vrid#bios3");
	initnpctimer;
	npctalk "Vrid:คุณโชคดีที่มาที่นี่ ฉันหลงทางโดยสิ้นเชิงในตอนที่เข้าไปในเขาวงกตนั้น", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Vrid:ถ้าไม่ใช่ Zeith ฉันก็ยังคงอยู่ที่นั่นเดินเตร่ ขอบคุณ Zeith", 'vrid3$;
	end;
OnTimer5000:
	npctalk "Zeith:... มีบางอย่างรบกวนฉัน", 'zeith3$;
	end;
OnTimer7500:
	npctalk "Vrid:มีอะไรเหรอ?", 'vrid3$;
	end;
OnTimer10000:
	npctalk "Zeith:ฉันรู้สึกว่ามอนสเตอร์แข็งแกร่งขึ้นเรื่อย ๆ ตามกาลเวลา", 'zeith3$;
	end;
OnTimer12500:
	npctalk "Zeith:เหมือนกับเด็ก ๆ ที่เติบโตเป็นผู้ใหญ่", 'zeith3$;
	end;
OnTimer15000:
	npctalk "Vrid:อืมฉันคิดว่าคุณพูดถูก", 'vrid3$;
	end;
OnTimer17500:
	npctalk "Vrid:พวกมันเล็กไปหมดในตอนแรกจากนั้นเราก็ต้องเจอกับสิ่งที่ใหญ่กว่าและแข็งแกร่งกว่าเมื่อเวลาผ่านไป", 'vrid3$;
	end;
OnTimer20000:
	npctalk "Vrid:สำหรับฉันมันฟังดูเป็นธรรมชาติ กังวลเรื่องอะไร", 'vrid3$;
	end;
OnTimer22500:
	npctalk "Zeith:ตอนนี้ฉันอยากรู้เกี่ยวกับสัตว์ประหลาดที่เราจะเจอ", 'zeith3$;
	end;
OnTimer25000:
	npctalk "Zeith:ไม่ว่าจะเป็นตัวละครดั้งเดิมที่แข็งแกร่งกว่าหรืออะไรที่แตกต่างไปจากเดิมอย่างสิ้นเชิง", 'zeith3$;
	end;
OnTimer27000:
	enablenpc 'reaper3$;
	end;
OnTimer27500:
	npctalk "Grim Reaper Ankou:* หัวเราะเบา ๆ * อย่างน้อยพวกคุณก็มีหัวที่ดีบนไหล่ของเขา", 'reaper3$;
	end;
OnTimer30000:
	npctalk "Vrid:คุณอีกแล้วเหรอ?", 'vrid3$;
	end;
OnTimer32500:
	npctalk "Grim Reaper Ankou:คุณอยากรู้เกี่ยวกับศัตรูคนต่อไปของคุณหรือไม่?", 'reaper3$;
	end;
OnTimer35000:
	npctalk "Grim Reaper Ankou:ข้างหน้าของคุณคือกองทัพอมตะ", 'reaper3$;
	end;
OnTimer37500:
	npctalk "Grim Reaper Ankou:* หัวเราะเบา ๆ * ออกมาทหารของฉัน!", 'reaper3$;
	end;
OnTimer37700:
	specialeffect EF_READYPORTAL,AREA,'reaper3$;
	end;
OnTimer40000:
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			enablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
OnTimer42000:
	npctalk "Vrid:หุบปากเจ้าปีศาจ! เราไม่มีเวลาให้ลูกรักของคุณ!", 'vrid3$;
	end;
OnTimer45000:
	npctalk "Vrid:ฉันจะกำจัดเธอเอง!", 'vrid3$;
	end;
OnTimer47500:
	npctalk "Grim Reaper Ankou:กลับมาหาคุณ bigmouth", 'reaper3$;
	end;
OnTimer50000:
	npctalk "Grim Reaper Ankou:ฉันจะสอนคุณว่ามันน่ากลัวแค่ไหนที่ต้องเผชิญกับความสยองขวัญ!", 'reaper3$;
	end;
OnTimer52500:
	specialeffect EF_STORMGUST,AREA,'reaper3$;
	end;
OnTimer52700:
	specialeffect EF_FREEZED,AREA,'zeith3$;
	specialeffect EF_FREEZED,AREA,'vrid3$;
	specialeffect EF_FIREHIT,AREA,'zeith3$;
	specialeffect EF_FIREHIT,AREA,'vrid3$;
	setnpcdisplay( 'vrid3$, 4_M_FROZEN_KN );
	setnpcdisplay( 'zeith3$, 4_M_FROZEN_GC );

	getpartymember 'party_id, 0, .@player_name$;
	getpartymember 'party_id, 1, .@char_id;
	getpartymember 'party_id, 2, .@account_id;
	for ( .@i = 0; .@i <$@partymembercount; .@i++ ) {
		if (isloggedin(.@account_id[.@i],.@char_id[.@i]) == true) {
			getmapxy .@map$,.@x,.@y, BL_PC, .@player_name$[.@i];
			if (.@map$ == 'map_dth3$ && distance(.@x,.@y,69,67) < 30)
				sc_start SC_FREEZE,17500,0,10000, (SCSTART_NOAVOID|SCSTART_NOTICKDEF|SCSTART_NORATEDEF), .@account_id[.@i];
		}
	}
	end;
OnTimer55000:
	npctalk "Vrid:ฮึ! พ - เกิดอะไรขึ้น?! II ขยับไม่ได้!", 'vrid3$;
	end;
OnTimer57500:
	npctalk "Zeith:ไม่ - ฉันก็ไม่ได้! ฉันขยับเท้าไม่ได้!", 'zeith3$;
	end;
OnTimer60000:
	npctalk "Grim Reaper Ankou:บนเกาะ Bios แต่ละวันเป็นช่วงชีวิต", 'reaper3$;
	end;
OnTimer62500:
	npctalk "Grim Reaper Ankou:คุณต้องสังเกตว่าทหารของฉันโตขึ้นก่อนพระอาทิตย์ตก", 'reaper3$;
	end;
OnTimer65000:
	npctalk "Grim Reaper Ankou:หลังจากตกค่ำพวกเขามาถึงขั้นตอนสุดท้ายของการพัฒนานั่นคือชีวิตที่อยู่เหนือความตาย", 'reaper3$;
	end;
OnTimer67500:
	npctalk "Grim Reaper Ankou:ถูกต้อง ถึงเวลาที่คุณต้องพบกับความตาย", 'reaper3$;
	end;
OnTimer70000:
	npctalk "Grim Reaper Ankou:เตรียมพร้อมที่จะรู้สึกกลัวตาย! *ซิกซี้*", 'reaper3$;
	// note:unfreeze player
	end;
OnTimer72000:
	disablenpc 'reaper3$;
	end;
OnTimer72500:
	npctalk "Vrid:ให้ตายเถอะพวกอันเด ธ กำลังจะมา แต่ฉันสู้ไม่ได้เพราะฉันขยับเท้าไม่ได้!", 'vrid3$;
	end;
OnTimer75000:
	npctalk "Vrid:อ๊ากฉันไม่อยากตาย!", 'vrid3$;
	end;
OnTimer77500:
	npctalk "Zeith:... ฮึ ...", 'zeith3$;
	end;
OnTimer80000:
	stopnpctimer;
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	.@label$ = instance_npcname("freeze#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"คอรัปชั่น Orc Zombie",3016,1, .@label$;// EP14_3_DEATH_C_MOB1
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("freeze#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("freeze#bios3");
		donpcevent instance_npcname("verit#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	verit#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("verit#bios3");
	initnpctimer;
	npctalk "Vrid:ว้าวคุณวิเศษมาก! คุณฆ่าพวกเขาทั้งหมดหรือไม่?", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Zeith:ยังไม่ได้ กำลังจะมาอีก!", 'zeith3$;
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("verit#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Verit เสียหาย",3017,1, .@label$;// EP14_3_DEATH_C_MOB2
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("verit#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("verit#bios3");
		donpcevent instance_npcname("megalodon#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	megalodon#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("megalodon#bios3");
	initnpctimer;
	npctalk "Vrid:ฉันหวังว่าจะเป็นคนสุดท้ายจริงๆ", 'vrid3$;
	end;
OnTimer2500:
	npctalk "Zeith:ไม่เราไม่ได้โชคดีขนาดนี้ - กำลังจะมีขึ้น!", 'zeith3$;
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("megalodon#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Megalodon ที่ทุจริต",3018,1, .@label$;// EP14_3_DEATH_C_MOB3
	end;

OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("megalodon#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("megalodon#bios3");
		donpcevent instance_npcname("boss_prelude#bios3") + "::OnStart";
	}
	end;
}

1@dth3,1,1,0	script	boss_prelude#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc 'reaper3$;
	initnpctimer;
	end;
OnTimer500:
	npctalk "Grim Reaper Ankou:ฉันประเมินคุณต่ำเกินไปหรือทหารของฉันไม่แข็งแกร่งอย่างที่ฉันคิด", 'reaper3$;
	end;
OnTimer3000:
	npctalk "Vrid:คุณกำลังโกง! ปล่อยพวกเราและต่อสู้อย่างยุติธรรม!", 'vrid3$;
	end;
OnTimer5500:
	npctalk "Grim Reaper Ankou:คุณยังพูดอยู่ ฉันจะฆ่าคุณเองและเอาชนะมัน!", 'reaper3$;
	end;
OnTimer7500:
	disablenpc 'reaper3$;
	end;
OnTimer8000:
	stopnpctimer;
	donpcevent instance_npcname("boss#bios3") + "::OnStart";
	end;
}

1@dth3,1,1,0	script	boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("boss#bios3");
	monster 'map_dth3$,70,67,"Grim Reaper Ankou",3029,1, instance_npcname("boss#bios3") + "::OnMyBossDead";
	'boss_id =$@mobid[0];
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStart";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStart";
	end;
OnMyBossDead:
	'boss_id = 0;
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStop";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStop";
	enablenpc 'reaper3$;
	enablenpc 'zeith3$;
	enablenpc 'vrid3$;
	initnpctimer;
	end;
OnTimer3000:
	npctalk "Grim Reaper Ankou:เจ้าแข็งแกร่ง ...", 'reaper3$;
	end;
OnTimer6500:
	npctalk "Grim Reaper Ankou:แต่เป้าหมายของฉันสำเร็จแล้ว ... อย่าคิดว่ามันจบแล้ว", 'reaper3$;
	end;
OnTimer8000:
	npctalk "Grim Reaper Ankou:อีกไม่นานลอร์ดโมร็อกโกจะกลับมา * หัวเราะเบา ๆ * ความกลัวตายจะติดตามคุณไปทุกที่", 'reaper3$;
	end;
OnTimer10500:
	disablenpc 'reaper3$;
	specialeffect EF_ICECRASH,AREA,'vrid3$;
	specialeffect EF_ICECRASH,AREA,'zeith3$;
	setnpcdisplay( 'vrid3$, 4_M_KNIGHT_BLACK );
	setnpcdisplay( 'zeith3$, 4_M_GUILLOTINE );
	end;
OnTimer13000:
	npctalk "Vrid:อ่าในที่สุดฉันก็ขยับได้!", 'vrid3$;
	end;
OnTimer16500:
	npctalk "Zeith:อืมฉันละอายใจตัวเองที่อ่อนแอมาก ...", 'zeith3$;
	end;
OnTimer18000:
	npctalk "Vrid:ต๊ายคนเกี่ยวคนนั้นแข็งแกร่งกว่าฉันมากและ Zeith รวมกัน", 'vrid3$;
	end;
OnTimer20500:
	npctalk "Vrid:เราอาจตายได้ถ้าไม่ใช่เพื่อคุณ", 'vrid3$;
	end;
OnTimer23000:
	stopnpctimer;
	npctalk "Vrid:ฉันอยากจะให้อะไรคุณเพื่อช่วยชีวิตพวกเรา คุยกับฉันอีกครั้งเมื่อคุณพร้อมที่จะยอมรับ", 'vrid3$;
	'reward = 1;
	disablenpc instance_npcname("boss#bios3");
	end;
}

1@dth3,1,1,0	script	talk_friends#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer15000:
	.@r = rand(7);
	if (.@r == 0)
		npctalk "Zeith:ฮึ ... ฉันรู้สึกหมดหนทาง ...", 'zeith3$;
	else if (.@r == 1)
		npctalk "Zeith:ผู้ชายคนนั้น Ankou มีกลิ่นอันตราย คุณควรเคลื่อนไหวได้ดีขึ้นเมื่อเขาเริ่มใช้ทักษะ", 'zeith3$;
	else if (.@r == 2)
		npctalk "Vrid:ใช่ฉันขอโทษที่เราช่วยไม่ได้", 'vrid3$;
	else if (.@r == 3)
		npctalk "Vrid:คุณสิบแปดมงกุฎสู้อย่างยุติธรรม!", 'vrid3$;
	else if (.@r == 4)
		npctalk "Vrid:ให้ตายเถอะฉันขยับไม่ได้ ...", 'vrid3$;
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}

1@dth3,1,1,0	script	talk_boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer13000:
	.@r = rand(6);
	if (.@r == 0)
		unittalk 'boss_id, "Grim Reaper Ankou:* หัวเราะเบา ๆ *";
	else if (.@r == 1)
		unittalk 'boss_id, "Grim Reaper Ankou:รู้สึกกลัวตาย!";
	else if (.@r == 2)
		unittalk 'boss_id, "Grim Reaper Ankou:Lord Morocc จะกลับมาในไม่ช้า";
	else if (.@r == 3)
		unittalk 'boss_id, "Grim Reaper Ankou:นี่เป็นสิ่งที่ดีที่สุดที่คุณทำได้หรือไม่? สิ่งมีชีวิตที่อ่อนแอเช่นนี้คุณมนุษย์";
	initnpctimer;
	end;;
OnStop:
	stopnpctimer;
	end;

OnInstanceInit:
	'reward = 'boss_id = 0;
	'map_dth1$ = instance_mapname("1@dth1");
	'map_dth2$ = instance_mapname("1@dth2");
	'map_dth3$ = instance_mapname("1@dth3");

	// warps
	disablenpc instance_npcname("warp1#bios1");
	disablenpc instance_npcname("warp1#bios2");

	// zone 1
	disablenpc instance_npcname("Grim Reaper Ankou#bios1");
	donpcevent instance_npcname("mobs#bios1") + "::OnStart";	// spawn at entrance

	// zone 2
	disablenpc instance_npcname("start1#bios2");
	disablenpc instance_npcname("mobs#bios2");

	// zone 3
	disablenpc instance_npcname("event_boss#bios3");
	disablenpc instance_npcname("freeze#bios3");
	disablenpc instance_npcname("verit#bios3");
	disablenpc instance_npcname("megalodon#bios3");
	disablenpc instance_npcname("boss_prelude#bios3");
	disablenpc instance_npcname("boss#bios3");
	disablenpc instance_npcname("talk_friends#bios3");
	disablenpc instance_npcname("talk_boss#bios3");
	disablenpc instance_npcname("Vrid#bios3");
	disablenpc instance_npcname("Zeith#bios3");
	disablenpc instance_npcname("Grim Reaper Ankou#bios3");
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
}

1@dth3,72,74,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios3	4_M_GUILLOTINE
1@dth3,70,67,3	duplicate(Grim Reaper Ankou#bios1)	Grim Reaper Ankou#bios3	3029

1@dth3,62,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio1	3016
1@dth3,64,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio1	3016
1@dth3,66,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio1	3016
1@dth3,68,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio1	3016
1@dth3,72,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio1	3016
1@dth3,74,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio1	3016
1@dth3,76,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio1	3016
1@dth3,78,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio1	3016
1@dth3,62,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio2	3016
1@dth3,64,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio2	3016
1@dth3,66,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio2	3016
1@dth3,68,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio2	3016
1@dth3,72,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio2	3016
1@dth3,74,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio2	3016
1@dth3,76,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio2	3016
1@dth3,78,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio2	3016
1@dth3,62,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio3	3016
1@dth3,64,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio3	3016
1@dth3,66,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio3	3016
1@dth3,68,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio3	3016
1@dth3,72,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio3	3016
1@dth3,74,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio3	3016
1@dth3,76,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio3	3016
1@dth3,78,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio3	3016
