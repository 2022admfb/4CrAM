//===== rAthena Script =======================================
//= Instance Ritual of Blessing.
//===== Description:=========================================
//- [Walkthrough conversion].
//- Required in Banquet main quest.
//===== Changelogs:==========================================
//= 1.0 First version. [Capuche]
//============================================================

2@mir,100,17,5	script	Skia#skia01	4_EP16_SKIA,{
	mes "[Skia]";
	mes "ยินดีต้อนรับ!";
	mes "ฉันสงสัยว่าคุณจะมาถึงเมื่อไหร่";
	next;
	mes "[Nyhill]";
	mes "คุณอยู่ที่นี่!";
	mes "ผู้คนจากครอบครัวอื่น ๆ ต่างจับจองที่นั่งกันหมดแล้ว";
	next;
	mes "[Nyhill]";
	mes "ยังไงซะ.........";
	mes "หลังจากฉันออกจากห้องจัดเลี้ยง";
	mes ".... ไม่เป็นไร";
	npctalk "... ?", instance_npcname("Skia#skia01");
	next;
	mes "[Nyhill]";
	mes "ไม่มีอะไรดีที่จะได้ยินถ้าเราให้พวกเขารออีกต่อไป เริ่มกันเลย";
	next;
	mes "[Skia]";
	mes "^4d4dff มีที่นั่งด้านซ้ายถัดจากลอร์ดRichard ต้องเป็นที่นั่งที่จองไว้สำหรับผู้สังเกตการณ์ ^000000";
	next;
	mes "[Skia]";
	mes "ไปกันเลยดีกว่า";
	close2;
	enablenpc instance_npcname("Skia Nerius#skia02");
	enablenpc instance_npcname("Nyhill M. Heine#nihil02");
	disablenpc instance_npcname("Skia#skia01");
	disablenpc instance_npcname("Nyhill#nihil01");
	end;
}
2@mir,103,17,3	duplicate(Skia#skia01)	Nyhill#nihil01	4_EP16_NIHIL

2@mir,107,77,0	script	#royal_evt02	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch_:
	disablenpc instance_npcname("#royal_evt02");
	donpcevent instance_npcname("#royal_evt02") + "::OnEvent";// player can move
	end;
OnEvent:
	disablenpc instance_npcname("Skia#skia01");	// extra
	disablenpc instance_npcname("Nyhill#nihil01");
	enablenpc instance_npcname("Skia Nerius#skia02");
	enablenpc instance_npcname("Nyhill M. Heine#nihil02");

	mapannounce 'map_2mir$, "Kronecker:แม้ว่าจะมีความล่าช้าเล็กน้อย แต่ให้ฉันดำเนินการต่อ", bc_map,0xFF99;
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker:ตามที่ประชุมใหญ่ซึ่งตกทอดมาในพระราชวัง", bc_map,0xFF99;
	sleep 3000;
	npctalk "Heine", instance_npcname("Friedrich S. Heine#stol_1");
	npctalk "Heine", instance_npcname("Nyhill M. Heine#nihil02");
	sleep 3000;
	npctalk "Nerius", instance_npcname("Skia Nerius#skia02");
	npctalk "Nerius", instance_npcname("Spica Nerius#spica01");
	sleep 3000;
	npctalk "Walther", instance_npcname("Isenhonor Walther#eisen");
	npctalk "Walther", instance_npcname("Ian Walther#ian01");
	sleep 3000;
	npctalk "Wigner", instance_npcname("Levuiere Wigner#louvier1");
	npctalk "Wigner", instance_npcname("Jurgen Wigner#jurgen01");
	npctalk "Wigner", instance_npcname("Isaac Wigner#isaac01");
	npctalk "Wigner", instance_npcname("Katrin Wigner#kath01");
	sleep 3000;
	npctalk "Geoborg", instance_npcname("Catherine D. Geoborg#ca1");
	npctalk "Geoborg", instance_npcname("Ernst Geoborg#ernst01");
	sleep 3000;
	npctalk "Richard", instance_npcname("Po Richard#po01");
	sleep 3000;
	npctalk "Roegenburg", instance_npcname("Maximilian Roegenburg#m1");
	npctalk "Roegenburg", instance_npcname("Wolf Roegenburg#wolf01");
	npctalk "Roegenburg", instance_npcname("Helmut Roegenburg#helmu");
	npctalk "Roegenburg", instance_npcname("Agnes Roegenburg#agnes0_1");
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker:ในนามของราชวงศ์ทั้งเจ็ดที่สนับสนุน Rune-Midgarts", bc_map,0xFF99;
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker:นี่คือตัวแทนคนใหม่ที่ได้รับการแต่งตั้ง", bc_map,0xFF99;
	sleep 3000;
	mapannounce 'map_2mir$, "โครเนคเคอร์:แสงดาวพรอาจส่องถึงความรุ่งเรืองของโลก ...", bc_map,0xFF99;
	sleep 3000;
	npctalk "โปรดให้พลังจากแหล่งที่ได้รับสำหรับสิ่งมีชีวิตทั้งหมด", instance_npcname("Friedrich S. Heine#stol_1");
	npctalk "โปรดให้พลังจากแหล่งที่ได้รับสำหรับสิ่งมีชีวิตทั้งหมด", instance_npcname("Nyhill M. Heine#nihil02");
	sleep 3000;
	npctalk "ความกล้าหาญที่จะก้าวไปข้างหน้าโดยไม่หันหลังกลับและแทงศัตรูด้วยกริช", instance_npcname("Catherine D. Geoborg#ca1");
	npctalk "ความกล้าหาญที่จะก้าวไปข้างหน้าโดยไม่หันหลังกลับและแทงศัตรูด้วยกริช", instance_npcname("Ernst Geoborg#ernst01");
	sleep 3000;
	npctalk "หัวใจที่โอบรับทุกสิ่งในโลกนี้", instance_npcname("Skia Nerius#skia02");
	npctalk "หัวใจที่โอบรับทุกสิ่งในโลกนี้", instance_npcname("Spica Nerius#spica01");
	sleep 3000;
	npctalk "เจตจำนงอันแข็งแกร่งที่จะปกป้องทุกสิ่งในอ้อมแขน", instance_npcname("Po Richard#po01");
	sleep 3000;
	npctalk "ความภาคภูมิใจที่ยังคงอยู่ซึ่งไม่เคยโค้งงอในความยุติธรรมและศรัทธาไม่ว่าจะเกิดอะไรขึ้น", instance_npcname("Isenhonor Walther#eisen");
	npctalk "ความภาคภูมิใจที่ยังคงอยู่ซึ่งไม่เคยโค้งงอในความยุติธรรมและศรัทธาไม่ว่าจะเกิดอะไรขึ้น", instance_npcname("Ian Walther#ian01");
	sleep 3000;
	npctalk "คุณธรรมของการมองผู้อื่นด้วยจิตใจที่ถ่อมตน", instance_npcname("Maximilian Roegenburg#m1");
	npctalk "คุณธรรมของการมองผู้อื่นด้วยจิตใจที่ถ่อมตน", instance_npcname("Wolf Roegenburg#wolf01");
	npctalk "คุณธรรมของการมองผู้อื่นด้วยจิตใจที่ถ่อมตน", instance_npcname("Helmut Roegenburg#helmu");
	npctalk "คุณธรรมของการมองผู้อื่นด้วยจิตใจที่ถ่อมตน", instance_npcname("Agnes Roegenburg#agnes0_1");
	sleep 3000;
	npctalk "และปัญญาในการมองความจริง", instance_npcname("Levuiere Wigner#louvier1");
	npctalk "และปัญญาในการมองความจริง", instance_npcname("Jurgen Wigner#jurgen01");
	npctalk "และปัญญาในการมองความจริง", instance_npcname("Isaac Wigner#isaac01");
	npctalk "และปัญญาในการมองความจริง", instance_npcname("Katrin Wigner#kath01");
	sleep 3000;
	mapannounce 'map_2mir$, "Kronecker:แด่ราชาองค์ใหม่ของเรา ...", bc_map,0xFF99;
	sleep 1500;
	mapannounce 'map_2mir$, "ฉันขอโทษที่ขัดจังหวะความสนุก!", bc_map,0xDDDDDD;
	sleep 500;
	specialeffect EF_SCREEN_QUAKE, AREA, instance_npcname("Peter Heine#peter01");
	disablenpc instance_npcname("Peter Heine#peter01");
	enablenpc instance_npcname("Fallen Peter#peter02");
	sleep 500;
	mapannounce 'map_2mir$, "Kronecker:ปีเตอร์ !!!", bc_map,0xFF99;
	sleep 500;
	disablenpc instance_npcname("Kronecker G. Heine#gran1");
	.@kronecker_2$ = instance_npcname("Kronecker G. Heine#gran2");
	enablenpc .@kronecker_2$;
	sleep 500;
	.@intruder$ = instance_npcname("Intruder#hunter01");
	enablenpc .@intruder$;
	npctalk "โอ้สวัสดี! คุณต้องเป็น Archduke Grantz the Sage ที่มีชื่อเสียง!", .@intruder$;
	sleep 3000;
	npctalk "คุณกำลังทำอะไร!!!", .@kronecker_2$;
	sleep 3000;
	npctalk "เปล่าไม่มีอะไรมาก ไม่มีอะไรคุ้มค่าที่จะทำที่นี่อีกต่อไป", .@intruder$;
	sleep 3000;
	npctalk "ได้ยินมาว่ามีอะไรดีๆซ่อนอยู่ที่นี่ ...", .@intruder$;
	sleep 3000;
	npctalk "แต่นี่มันอะไรกัน? เป็นของปลอม!", .@intruder$;
	specialeffect EF_BEGINSPELL,AREA,.@kronecker_2$;
	sleep 500;
	specialeffect EF_BEGINSPELL6,AREA, .@intruder$;
	specialeffect EF_LEVEL99,AREA, .@intruder$;
	sleep 2000;
	npctalk "ท่านผู้เฒ่าและผู้ดีที่อยู่เบื้องหลังเขา ...", .@intruder$;
	sleep 3000;
	npctalk "ถือไว้ที่นั่น มันไม่สนุก ฉันอาจต้องการทำลายบางสิ่ง", .@intruder$;
	sleep 3000;
	npctalk "ตัวอย่างเช่น...", .@intruder$;
	sleep 3000;
	npctalk "ของปลอมที่คล้าย Ymir's Heart!", .@intruder$;
	sleep 3000;
	npctalk "...คุณต้องการอะไร?", .@kronecker_2$;
	sleep 3000;
	npctalk "หัวใจของ Ymir ฉันเคยได้ยินว่ามันอยู่ที่นี่ แต่ฉันไม่เห็นเลยใช่ไหม?", .@intruder$;
	sleep 3000;
	npctalk "... คุณกำลังพูดถึงอะไร?", .@kronecker_2$;
	sleep 3000;
	npctalk "โอ้คุณไม่รู้เหรอ?", .@intruder$;
	sleep 3000;
	npctalk "ฉันเห็น หากคุณรู้ว่าคุณจะไม่ได้เก็บมันไว้ในพื้นดินเสียเวลาไปกับพิธีกรรมนี้และทั้งหมด", .@intruder$;
	sleep 3000;
	npctalk "แต่ฉันไม่สามารถช่วยได้ชายชรา นี่คือของปลอม", .@intruder$;
	sleep 2000;
	npctalk "...กล้าดียังไง...", .@kronecker_2$;
	sleep 2000;
	npctalk "อย่าให้ฉันดู ฉันเสียเวลาที่นี่มาแล้วเช่นกันรู้ไหม", .@intruder$;
	sleep 3000;
	npctalk "คราวนี้ฉันจะไปโดยไม่จัดฉาก", .@intruder$;
	sleep 3000;
	npctalk "ฮา! พลังทั้งหมดทำอะไรได้ดีเมื่อคุณไม่รู้ว่าจะใช้มันอย่างไร? ฉันเดาว่าคุณไม่ต้องการสิ่งนี้อีกต่อไป", .@intruder$;
	sleep 3000;
	npctalk "ถึงเวลาแล้วที่หัวใจของ Ymir ปลอมจะไป!", .@intruder$;
	sleep 500;
	specialeffect EF_BEGINSPELL3,AREA, .@intruder$;
	sleep 500;
	specialeffect EF_SCREEN_QUAKE,AREA, instance_npcname("Peter Heine#peter01");
	specialeffect EF_LORD,AREA, .@kronecker_2$;
	specialeffect EF_LORD,AREA, instance_npcname("Levuiere Wigner#louvier1");
	specialeffect EF_LORD,AREA, instance_npcname("Catherine D. Geoborg#ca1");

	disablenpc instance_npcname("Ian Walther#ian01");
	disablenpc instance_npcname("Levuiere Wigner#louvier1");
	disablenpc instance_npcname("Jurgen Wigner#jurgen01");
	disablenpc instance_npcname("Catherine D. Geoborg#ca1");
	disablenpc instance_npcname("Katrin Wigner#kath01");
	disablenpc instance_npcname("Isaac Wigner#isaac01");

	enablenpc instance_npcname("Ian Walther#ian02");
	enablenpc instance_npcname("Levuiere Wigner#louvier2");
	enablenpc instance_npcname("Jurgen Wigner#jurgen02");
	enablenpc instance_npcname("Catherine D. Geoborg#ca2");
	enablenpc instance_npcname("Katrin Wigner#kath02");
	enablenpc instance_npcname("Isaac Wigner#isaac02");
	sleep 500;
	specialeffect EF_LORD,AREA, instance_npcname("Maximilian Roegenburg#m1");
	disablenpc instance_npcname("Ernst Geoborg#ernst01");
	disablenpc instance_npcname("Maximilian Roegenburg#m1");
	disablenpc instance_npcname("Wolf Roegenburg#wolf01");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_1");

	enablenpc instance_npcname("Maximilian Roegenburg#m2");
	enablenpc instance_npcname("Ernst Geoborg#ernst02");
	enablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	enablenpc instance_npcname("Wolf Roegenburg#wolf02");

	specialeffect EF_LORD,AREA, instance_npcname("Agnes Roegenburg#agnes0_2");

	mapannounce 'map_2mir$, "Intruder:ถ้าอย่างนั้นท่านสุภาพบุรุษและสุภาพสตรีทั้งหลายจงมีความสุข!", bc_map,0xDDDDDD;
	disablenpc .@intruder$;
	enablenpc instance_npcname("Intruder#hunter02");
	enablenpc instance_npcname("Intruder#hunter03");
	enablenpc instance_npcname("Intruder#hunter04");
	enablenpc instance_npcname("Intruder#hunter05");
	enablenpc instance_npcname("Intruder#hunter06");

	sleep 500;
	donpcevent instance_npcname("Intruder_main") + "::OnEvent";

	disablenpc instance_npcname("Po Richard#po01");
	disablenpc instance_npcname("Spica Nerius#spica01");
	disablenpc instance_npcname("Isenhonor Walther#eisen");
	disablenpc instance_npcname("Friedrich S. Heine#stol_1");
	disablenpc instance_npcname("Helmut Roegenburg#helmu");
	disablenpc instance_npcname("Skia Nerius#skia02");
	disablenpc instance_npcname("Nyhill M. Heine#nihil02");

	enablenpc instance_npcname("Skia Nerius#skia03");
	enablenpc instance_npcname("Nyhill M. Heine#nihil03");
	enablenpc instance_npcname("Spica Nerius#spica02");
	enablenpc instance_npcname("Po Richard#po02");
	enablenpc instance_npcname("Isenhonor Walther#eisen2");
	enablenpc instance_npcname("Friedrich S. Heine#stol_2");
	enablenpc instance_npcname("Helmut Roegenburg#helmu2");
	end;
}

2@mir,1,1,0	script	Intruder_main	-1,{
	end;
OnEvent:
	enablenpc instance_npcname("Intruder_main");
	.@label$ = instance_npcname("Intruder_main") + "::OnMobDead";
	monster 'map_2mir$,107,73,"Suspicious Intruder",3454,1, .@label$;	// HEART_HUNTER_N
	setunitdata $@mobid[0], UMOB_HP, 10000;
	monster 'map_2mir$, 98,73,"Suspicious Intruder",3454,1, .@label$;
	setunitdata $@mobid[0], UMOB_HP, 10000;
	monster 'map_2mir$,101,83,"Suspicious Intruder",3454,1, .@label$;
	setunitdata $@mobid[0], UMOB_HP, 10000;
	'intruder_count = 0;

	donpcevent instance_npcname("Intruder#hunter02") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter03") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter04") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter05") + "::OnStart";
	donpcevent instance_npcname("Intruder#hunter06") + "::OnStart";
	end;
OnMobDead:
	'intruder_count++;
	switch( 'intruder_count ) {
	case 1:
		donpcevent instance_npcname("Intruder#hunter02") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter05") + "::OnLeave";
		break;
	case 2:
		donpcevent instance_npcname("Intruder#hunter03") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter06") + "::OnLeave";
		break;
	case 3:
		donpcevent instance_npcname("Intruder#hunter04") + "::OnLeave";
		break;
	}
	sleep 100;	// it seems there is a slight delay

	switch( 'intruder_count ) {
	case 3:
		mapannounce 'map_2mir$, "เฟล! ออกจากที่นี้กันเถอะ!", bc_map,0xDDDDDD;
		// display the effect a second time
		donpcevent instance_npcname("Intruder#hunter02") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter03") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter04") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter05") + "::OnLeave";
		donpcevent instance_npcname("Intruder#hunter06") + "::OnLeave";

		disablenpc instance_npcname("Intruder#hunter02");
		disablenpc instance_npcname("Intruder#hunter03");
		disablenpc instance_npcname("Intruder#hunter04");
		disablenpc instance_npcname("Intruder#hunter05");
		disablenpc instance_npcname("Intruder#hunter06");
		break;
	default:
		break;
	}
	end;
}

2@mir,87,79,5	script	Intruder#hunter02	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter02");	// officially disabled, changed to hideonnpc to display the effect a second time
	end;
OnTimer1000:
	specialeffect EF_PIERCE;
	end;
OnTimer2000:
	specialeffect EF_CONCENTRATION, AREA, instance_npcname("Isenhonor Walther#eisen2");
	end;
OnTimer4000:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer5000:
	npctalk "มันเป็นสิ่งที่ไม่มีประโยชน์!";
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Isenhonor Walther#eisen2");
	end;
OnTimer7000:
	specialeffect EF_SHIELDCHARGE;
	end;
OnTimer8000:
	specialeffect EF_CHAINCOMBO, AREA, instance_npcname("Isenhonor Walther#eisen2");
	end;
OnTimer10000:
	specialeffect EF_PIERCE;
	end;
OnTimer11000:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Isenhonor Walther#eisen2");
	npctalk "เราไม่มีเวลาแชท!", instance_npcname("Isenhonor Walther#eisen2");
	initnpctimer;
	end;
}

2@mir,91,84,5	script	Intruder#hunter03	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter03");
	end;
OnTimer1500:
	specialeffect EF_MAGICALATTHIT;
	end;
OnTimer2500:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Skia Nerius#skia03");
	end;
OnTimer4500:
	specialeffect EF_HIT5;
	end;
OnTimer5500:
	specialeffect EF_HIT6, AREA, instance_npcname("Skia Nerius#skia03");
	end;
OnTimer7500:
	specialeffect EF_HIT2;
	end;
OnTimer8500:
	npctalk "มันเป็นสิ่งที่ไม่มีประโยชน์!";
	specialeffect EF_HIT1, AREA, instance_npcname("Skia Nerius#skia03");
	end;
OnTimer11100:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer12000:
	specialeffect EF_HIT1, AREA, instance_npcname("Skia Nerius#skia03");
	initnpctimer;
	end;
}

2@mir,94,86,5	script	Intruder#hunter04	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter04");
	end;
OnTimer1200:	// weird timer but ~correct
	specialeffect EF_MAGICALATTHIT;
	end;
OnTimer2200:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	end;
OnTimer3800:
	specialeffect EF_HIT5;
	end;
OnTimer4800:
	npctalk "หึ!";
	specialeffect EF_HIT6, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	end;
OnTimer8200:
	specialeffect EF_HIT2;
	end;
OnTimer9200:
	specialeffect EF_HIT1, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	end;
OnTimer11500:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer12500:
	specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Friedrich S. Heine#stol_2");
	npctalk "... ไอ้พวกนี้มาจากไหน?", instance_npcname("Friedrich S. Heine#stol_2");
	initnpctimer;
	end;
}

2@mir,107,86,5	script	Intruder#hunter05	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter05");
	end;
OnTimer1000:
	specialeffect EF_MAGICALATTHIT;
	end;
OnTimer2000:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	end;
OnTimer4000:
	npctalk "ตาย!!!";
	specialeffect EF_HIT5;
	end;
OnTimer4500:
	npctalk "ดูสิใครเอ่ย !!!", instance_npcname("Helmut Roegenburg#helmu2");
	specialeffect EF_HIT6, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	end;
OnTimer7000:
	specialeffect EF_HIT2;
	end;
OnTimer8000:
	specialeffect EF_HIT1, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	end;
OnTimer10000:
	specialeffect EF_HOLYCROSS;
	end;
OnTimer11000:
	specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Helmut Roegenburg#helmu2");
	npctalk "อ๊าก !!!", instance_npcname("Helmut Roegenburg#helmu2");
	initnpctimer;
	end;
}

2@mir,110,85,5	script	Intruder#hunter06	3454,{
	end;
OnStart:
	initnpctimer;
	end;
OnLeave:
	stopnpctimer;
	specialeffect EF_TEIHIT3;
	hideonnpc instance_npcname("Intruder#hunter06");
	end;
OnTimer1000:
	specialeffect EF_HITDARK;
	end;
OnTimer2000:
	specialeffect EF_TEIHIT1X, AREA, instance_npcname("Po Richard#po02");
	end;
OnTimer4000:
	npctalk "ตาย!!!";
	specialeffect EF_LIGHTBOLT;
	end;
OnTimer5000:
	specialeffect EF_HIT6, AREA, instance_npcname("Po Richard#po02");
	end;
OnTimer7000:
	specialeffect EF_COMBOATTACK1;
	end;
OnTimer8000:
	specialeffect EF_HIT1, AREA, instance_npcname("Po Richard#po02");
	end;
OnTimer10000:
	specialeffect EF_NAPALMBEAT;
	end;
OnTimer12000:
	npctalk "โอ้นี่ดูไม่ดีเลย", instance_npcname("Po Richard#po02");
	specialeffect EF_AUTOCOUNTER, AREA, instance_npcname("Po Richard#po02");
	initnpctimer;
	end;
}

2@mir,97,80,7	script	Friedrich S. Heine#stol_1	4_EP16_STOLZ,{
	npctalk "... เงียบ.";
	end;
}

2@mir,88,78,2	script	Isenhonor Walther#eisen2	4_M_1STPRIN1,{
	if ('intruder_count < 3) {
		npctalk "เราไม่มีเวลาแชท!";
		end;
	}
	mes "[Isenhonor]";
	mes "เกิดอะไรขึ้น...";
	mes "เราจะออกไปจากที่นี่ก่อน";
	next;
	mes "[Isenhonor]";
	mes "ทุกคนใช่มั้ย ...";
	close2;
	disablenpc instance_npcname("Isenhonor Walther#eisen2");
	disablenpc instance_npcname("Ian Walther#ian02");
	end;
}

2@mir,106,85,7	script	Helmut Roegenburg#helmu2	4_EP16_HELMUT,{
	if ('intruder_count < 3) {
		npctalk "หืม!";
		end;
	}
	mes "[เฮลมุท]";
	mes "ทุกอย่างดูเหมือนจะดี";
	mes "... ฉันจะไป พี่ ๆ ดูไม่ค่อยดีเลย";
	close2;
	disablenpc instance_npcname("Helmut Roegenburg#helmu2");
	disablenpc instance_npcname("Wolf Roegenburg#wolf02");
	disablenpc instance_npcname("Maximilian Roegenburg#m2");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	end;
}

2@mir,92,83,2	script	Skia Nerius#skia03	4_EP16_SKIA,{
	if ('intruder_count < 3) {
		npctalk "... เอ๊ะ ... มันมาจากไหน?";
		end;
	}
	mes "[Skia]";
	mes "ว้าออกไปจากที่นี่กันเถอะ";
	mes "ซิสออกไปกันเถอะ!";
	mes "Nyhill คุณควรมากับเรา!";
	next;
	mes "[Skia]";
	mes "คนอื่นจะได้ออกไปเอง!";
	close2;
	disablenpc instance_npcname("Skia Nerius#skia03");
	disablenpc instance_npcname("Spica Nerius#spica02");
	end;
}

2@mir,109,84,7	script	Po Richard#po02	4_M_6THPRIN1,{
	if ('intruder_count < 3) {
		npctalk "... ฮ่าฮ่าฉันไม่ได้คาดหวังว่าจะได้ต่อสู้ที่นี่!";
		end;
	}
	npctalk "เนื่องจากห้องโถงดูเหมือนจะถูกเคลียร์หมดแล้วฉันจึงออกไปตอนนี้ คราวนี้ช่างยุ่งยากอะไรเช่นนี้!";
	sleep 2000;
	disablenpc instance_npcname("Po Richard#po02");
	end;
}

2@mir,95,85,1	script	Friedrich S. Heine#stol_2	4_EP16_STOLZ,{
	if ('intruder_count < 3) {
		npctalk "กรุณาดูแลคนอื่น ๆ";
		end;
	}
	mes "[ฟรีดริช]";
	mes "... ดูเหมือนจะไม่มีใครตาย";
	npctalk "คุณกำลังรออะไรอยู่! ออกไปจากที่นี่พร้อมกับผู้บาดเจ็บ!";
	close2;
OnDisable:
	disablenpc instance_npcname("Skia Nerius#skia03");
	disablenpc instance_npcname("Spica Nerius#spica02");
	disablenpc instance_npcname("Isenhonor Walther#eisen2");
	disablenpc instance_npcname("Ian Walther#ian02");
	disablenpc instance_npcname("Levuiere Wigner#louvier2");
	disablenpc instance_npcname("Jurgen Wigner#jurgen02");
	disablenpc instance_npcname("Katrin Wigner#kath02");
	disablenpc instance_npcname("Ernst Geoborg#ernst02");
	disablenpc instance_npcname("Catherine D. Geoborg#ca2");
	disablenpc instance_npcname("Po Richard#po02");
	disablenpc instance_npcname("Maximilian Roegenburg#m2");
	disablenpc instance_npcname("Wolf Roegenburg#wolf02");
	disablenpc instance_npcname("Helmut Roegenburg#helmu2");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	disablenpc instance_npcname("Isaac Wigner#isaac02");
	end;
}

2@mir,101,93,7	script	Kronecker G. Heine#gran2	4_EP16_GRANZ,{
	if ('intruder_count < 3) {
		mes "[Kronecker]";
		mes "ปีเตอร์อยู่ในอาการวิกฤต";
		mes "... คุณคิดจะต่อสู้กับผู้บุกรุกเพื่อพวกเราได้หรือไม่?";
		close;
	}
	if (isbegin_quest(7692) == 1) {
		mes "[Kronecker]";
		mes "....";
		mes ".... ^4d4dffTerra Gloria ^000000 หายไป ...";
		next;
		select("เทอร์รากลอเรีย?");
		mes "[Kronecker]";
		mes "เป็นมงกุฎที่ตกทอดในพระราชวังรูน - มิดการ์ท";
		mes "มันมีความหมายมากกว่ามงกุฎธรรมดา";
		next;
		mes "[Kronecker]";
		mes "มันถูกพวกเขายึดไปหรือไม่?";
		mes "อะไรบนโลก ... !";
		next;
		mes "[Kronecker]";
		mes "สิ่งที่ซ่อนอยู่ที่นี่ไม่มีใครรู้ ... ยกเว้นราชวงศ์ทั้งเจ็ด ... !";
		next;
		mes "[Kronecker]";
		mes "มันหมายความว่า...";
		mes "... สิ่งแรกก่อนอื่นมีสิ่งที่ต้องค้นหา";
		next;
		mes "[Kronecker]";
		mes "ออกไปจากที่นี่กันเถอะ";
		mes "มีเหลืออีกมากสำหรับการใช้เพื่อค้นหา";
		erasequest 7692;
		setquest 7693;// Surprise Attack
		banquet_main_quest = 9;
		next;
		mes "[Kronecker]";
		mes "ใครที่ออกจากที่นี่จงเคลียร์กับผู้บาดเจ็บที่อยู่ใกล้ตัวคุณ!";
		mes "ห้องแห่งสติจะปิดให้บริการชั่วขณะ!";
		donpcevent instance_npcname("Friedrich S. Heine#stol_2") + "::OnDisable";
		disablenpc instance_npcname("Nyhill M. Heine#nihil03");
		close;
	}
	mes "[Kronecker]";
	mes "ปีเตอร์ได้รับบาดเจ็บ";
	mes "เราจำเป็นต้องย้ายเขาอย่างรวดเร็ว";
	next;
	mes "[Kronecker]";
	mes "ฉันจะพบคุณที่นั่น";
	mes "คุณสามารถออกทางประตูที่คุณเข้ามาได้";
	mes "มาพบกันที่ Past Memories of the Royal Family";
	close;
}

2@mir,93,80,2	script	Nyhill M. Heine#nihil03	4_EP16_NIHIL,4,4,{
	if ('intruder_count < 3) {
		npctalk "...พุทโธ่...";
		end;
	}
	mes "[Nyhill]";
	mes "... นี่ ... ทั้งหมด ... ความผิดของฉัน ...";
	close;
OnTouch_:
	npctalk "... ฉันจะเป็นตัวช่วยไหม?";
	npcskill "AL_HEAL",11,99,60;
	npcskill "PR_KYRIE",10,99,60;
	end;
}

2@mir,97,65,5	script	Jurgen Wigner#jurgen02	4_M_4THPRIN1,{
	npctalk "ฉันไว้วางใจคุณ!";
	npcskill "AL_INCAGI",10,99,60;
	end;
}

2@mir,111,66,4	script	Catherine D. Geoborg#ca2	4_EP16_EGEO,{
	npctalk "... ฉันหวังว่ามันจะช่วยคุณได้";
	npcskill "AL_HEAL",10,130,115;
	end;
}

2@mir,107,61,6	script	Isaac Wigner#isaac02	4_EP16_ISAAC,{
	npctalk "เป็นกำลังใจให้!";
	npcskill "AL_BLESSING",10,99,60;
	end;
}

2@mir,112,71,1	script	Agnes Roegenburg#agnes0_2	4_EP16_AGNES,{
	npctalk "อ๊าก! ระวัง!";
	end;
}

2@mir,96,83,2	script	Spica Nerius#spica02	4_EP16_SPICA,{
	if ('intruder_count < 3)
		npctalk "... ส ... คิ ... ก ...";
	else
		npctalk "Skia ทำไมผู้ชายคนนี้ถึงช่วย Lord Nyhill ไม่ใช่ฉัน?";
	end;
}


2@mir,102,97,3	script	Intruder#hunter01	3454,{
	end;
OnInstanceInit:
	'map_2mir$ = instance_mapname("2@mir");
	'intruder_count = 0;

	disablenpc instance_npcname("Spica Nerius#spica02");
	disablenpc instance_npcname("Agnes Roegenburg#agnes0_2");
	disablenpc instance_npcname("Isaac Wigner#isaac02");
	disablenpc instance_npcname("Catherine D. Geoborg#ca2");
	disablenpc instance_npcname("Jurgen Wigner#jurgen02");
	disablenpc instance_npcname("Kronecker G. Heine#gran2");
	disablenpc instance_npcname("Isenhonor Walther#eisen2");
	disablenpc instance_npcname("Helmut Roegenburg#helmu2");
	disablenpc instance_npcname("Po Richard#po02");
	disablenpc instance_npcname("Friedrich S. Heine#stol_2");

	disablenpc instance_npcname("Intruder#hunter01");
	disablenpc instance_npcname("Intruder#hunter02");
	disablenpc instance_npcname("Intruder#hunter03");
	disablenpc instance_npcname("Intruder#hunter04");
	disablenpc instance_npcname("Intruder#hunter05");
	disablenpc instance_npcname("Intruder#hunter06");

	disablenpc instance_npcname("Skia Nerius#skia03");
	disablenpc instance_npcname("Nyhill M. Heine#nihil03");
	end;
}

2@mir,96,83,0	script	dummy_npc_rituel_disabled	-1,{
	end;
OnInstanceInit:
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
2@mir,88,74,3	duplicate(dummy_npc_rituel_disabled)	Ian Walther#ian02	1_M_JOBTESTER
2@mir,86,70,4	duplicate(dummy_npc_rituel_disabled)	Levuiere Wigner#louvier2	4_EP16_LOUVIERE
2@mir,94,72,3	duplicate(dummy_npc_rituel_disabled)	Katrin Wigner#kath02	4_EP16_POE
2@mir,112,66,3	duplicate(dummy_npc_rituel_disabled)	Ernst Geoborg#ernst02	4_M_2NDPRIN1
2@mir,91,69,7	duplicate(dummy_npc_rituel_disabled)	Maximilian Roegenburg#m2	4_EP16_MAX
2@mir,94,66,3	duplicate(dummy_npc_rituel_disabled)	Wolf Roegenburg#wolf02	4_EP16_WOLF
2@mir,100,80,7	duplicate(dummy_npc_rituel_disabled)	Nyhill M. Heine#nihil02	4_EP16_NIHIL
2@mir,101,95,3	duplicate(dummy_npc_rituel_disabled)	Fallen Peter#peter02	4_EP16_PETER
2@mir,97,77,7	duplicate(dummy_npc_rituel_disabled)	Skia Nerius#skia02	4_EP16_SKIA


2@mir,96,83,0	script	dummy_npc_rituel	-1,{ end; }
2@mir,101,88,4	duplicate(dummy_npc_rituel)	Kronecker G. Heine#gran1	4_EP16_GRANZ
2@mir,97,71,7	duplicate(dummy_npc_rituel)	Levuiere Wigner#louvier1	4_EP16_LOUVIERE
2@mir,100,71,7	duplicate(dummy_npc_rituel)	Jurgen Wigner#jurgen01	4_M_4THPRIN1
2@mir,97,68,7	duplicate(dummy_npc_rituel)	Isaac Wigner#isaac01	4_EP16_ISAAC
2@mir,100,68,7	duplicate(dummy_npc_rituel)	Katrin Wigner#kath01	4_EP16_POE
2@mir,103,80,2	duplicate(dummy_npc_rituel)	Catherine D. Geoborg#ca1	4_EP16_EGEO
2@mir,106,80,2	duplicate(dummy_npc_rituel)	Ernst Geoborg#ernst01	4_M_2NDPRIN1
2@mir,103,77,2	duplicate(dummy_npc_rituel)	Po Richard#po01	4_M_6THPRIN1
2@mir,103,74,2	duplicate(dummy_npc_rituel)	Maximilian Roegenburg#m1	4_EP16_MAX
2@mir,106,74,2	duplicate(dummy_npc_rituel)	Wolf Roegenburg#wolf01	4_EP16_WOLF
2@mir,101,94,0	duplicate(dummy_npc_rituel)	Peter Heine#peter01	4_M_3RDPRIN1
2@mir,100,77,7	duplicate(dummy_npc_rituel)	Spica Nerius#spica01	4_EP16_SPICA
2@mir,106,71,2	duplicate(dummy_npc_rituel)	Agnes Roegenburg#agnes0_1	4_EP16_AGNES
2@mir,100,74,7	duplicate(dummy_npc_rituel)	Ian Walther#ian01	1_M_JOBTESTER
2@mir,97,74,7	duplicate(dummy_npc_rituel)	Isenhonor Walther#eisen	4_M_1STPRIN1
2@mir,103,71,2	duplicate(dummy_npc_rituel)	Helmut Roegenburg#helmu	4_EP16_HELMUT

2@mir,101,10,0	script	to_prt_lib	WARPNPC,1,1,{
	if (isbegin_quest(7693) == 1) {
		warp "prt_lib",88,80;
		end;
	}
	mes "[" + strcharinfo(0) + "]";
	mes "เราควรดูพิธีกรรมขอพร";
	mes "อย่าเพิ่งออกไปตอนนี้";
	close;
}

