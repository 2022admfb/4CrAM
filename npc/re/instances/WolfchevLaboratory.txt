//===== rAthena Script =======================================
//= Wolfchev's Laboratory
//===== By: ==================================================
//= Kisuka
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Defeat Wolfchev's human experimentations.
//===== Additional Comments: =================================
//= 1.0 First version, edited. [Euphy]
//= 1.1 Moved the Enchant (Sorcerer#Bio4Reward) and Reward
//=		(Weird old man#Bio4Reward) NPC in merchant folder. [Capuche]
//============================================================

// Giacomo Girolam
//============================================================
lighthalzen,303,303,6	script	Giacomo Girolamo#kiup2	4_M_KHMAN,3,3,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 ตรวจสอบสินค้าคงคลังของคุณคุณมีไอเท็มมากเกินไปที่จะทำเควส ^000000";
		close;
	}
	if (lhz_curse > 30) {
		if (!lght_duk01) {
			if (Sex == SEX_MALE) {
				emotion ET_HNG;
				mes "[Giacomo Girolamo]";
				mes ".... ผู้ชาย .... ?";
				next;
				mes "[Giacomo Girolamo]";
				mes "อ๊ะ! เฮ้ผู้ชาย!";
				next;
				select("What's up?");
				emotion ET_QUESTION, playerattached();
				mes "[Giacomo Girolamo]";
				mes "คุณเคยได้ยินเกี่ยวกับการทดลองกับมนุษย์ที่น่ากลัวใน Rekenber Corporation ซึ่งมีสำนักงานใหญ่อยู่ที่ Lighthalzen หรือไม่";
				next;
				select("Whaaat?! human experimentation?");
				emotion ET_HUK, playerattached();
				mes "[Giacomo Girolamo]";
				mes "เออ! มนุษย์ทดลอง !!";
				mes "มีข่าวลือมากมายว่ามีการทดลองกับมนุษย์ที่น่าสยดสยองจริง ๆ แต่คนเหล่านี้ในสลัม .... ไม่อยากจะเชื่อว่าเป็นเพราะขบวนการสวัสดิการจาก Rekenber";
				next;
				emotion ET_PROFUSELY_SWEAT;
				mes "[Giacomo Girolamo]";
				mes "อืม! และคนรวยเหล่านั้นดูเหมือนจะรู้อะไรบางอย่าง แต่พวกเขาไม่เคยพูดถึงเรื่องนั้น ... ";
				next;
				mes "[Giacomo Girolamo]";
				mes "อ๊ะ !! และนี่เป็นความลับที่มีแค่บางคนรู้ฉันได้ยินมาจาก Rekenber Lab., .. คนชื่อ ... อืม ... Wol .... ";
				next;
				emotion ET_SCRATCH;
				mes "[Giacomo Girolamo]";
				mes "ยังไงก็ตาม !! นักวิทยาศาสตร์คนหนึ่งชื่อ Wolf .. จู่ๆมีบางอย่างถูกไล่ออกและเขาต้องเกี่ยวข้องกับข่าวลือนี้แน่ ๆ ... ";
				next;
				select("You mean... Wolfchev?");
				emotion ET_SURPRISE;
				mes "[Giacomo Girolamo]";
				mes "Ohhhh ?! ใช่ Wolfchev !! ใช่แล้วชื่อ Wolfchev .. นักวิทยาศาสตร์บ้า .. ";
				next;
				emotion ET_OK;
				mes "[Giacomo Girolamo]";
				mes "ฉันคิดว่าคุณคงจะเข้าใจเรื่องราวได้เร็วขึ้น ... ฉันแน่ใจว่าเขามีส่วนเกี่ยวข้องกับข่าวลือนี้ฉันสาบาน";
				next;
				mes "[Giacomo Girolamo]";
				mes "ว่าแล้ว.. แล้วคุณล่ะมีอะไรที่คุณรู้เกี่ยวกับเขาไหม";
				next;
				if (select("แสร้งทำเป็นว่าคุณไม่รู้อะไรเลย "," บอกเขาในสิ่งที่คุณรู้") == 1) {
					mes "["+ strcharinfo(0) +"]";
					mes "ไม่จริงฉันเคยได้ยินชื่อของเขา แต่ฉันไม่รู้อะไรเกี่ยวกับเขาเลย .. ";
					next;
					mes "[Giacomo Girolamo]";
					mes "หืม .... นายฟังดูน่าสงสัยเหรอ!";
					next;
				} else {
					mes "["+ strcharinfo(0) +"]";
					mes "เขาบังเอิญไปค้นคว้าเรื่อง 'โฮมุนคูลัส' ของมนุษย์โดยไม่บอกเรื่องนั้นกับ Rekenber Corp. และมีผู้แจ้งเบาะแสไปยังคอร์ปดังนั้นเขาจึงถูกไล่ออก";
					next;
					emotion ET_BEST;
					mes "[Giacomo Girolamo]";
					mes "โอ้ผู้ชาย .. ! ดูเหมือนเธอจะรู้ดีนะ! ถ้าบังเอิญคุณเกี่ยวข้องกับ Rekenber หรือเปล่า ??";
					next;
					emotion ET_THINK;
					mes "["+ strcharinfo(0) +"]";
					mes "... .... ...";
					next;
				}
				emotion ET_SCRATCH;
				mes "[Giacomo Girolamo]";
				mes "ไม่ต้องกังวลฉันไม่ได้หมายถึงอะไร";
				next;
				mes "[Giacomo Girolamo]";
				mes "อืม! ฉันแค่อยากรู้อยากเห็นเพราะบางคนในสลัมและนักผจญภัยบางคนหายไป";
				mes "และฉันได้ยินมาว่า Rekenber กำลังทำการทดลองกับมนุษย์กับคนที่หายไปหวังว่าคุณจะไม่เข้าใจฉันผิด";
				next;
				lght_duk01 = 1;
				setquest 5109;
				mes "ที่จริงมันไม่ใช่ข่าวลือที่น่าขำ";
				mes "ควรตรวจสอบห้องแล็บที่ทำการทดลอง";
				close;
			} else {
				emotion ET_THROB;
				mes "[Giacomo Girolamo]";
				mes "โอ้! ช่างสวยอะไรอย่างนี้ !!";
				next;
				emotion ET_QUESTION;
				mes "[Giacomo Girolamo]";
				mes "ความงามมาอยู่ที่นี่ในสถานที่ที่มีข่าวลืออันตรายได้อย่างไร";
				next;
				switch(select("เดิน ... ", "ไปเที่ยว ... ", "ช็อปปิ้ง ... ")) {
				case 1:
					mes "[Giacomo Girolamo]";
					mes "Ahh.. Lady, you are so graceful even when you are just walking.....";
					next;
					break;
				case 2:
					mes "[Giacomo Girolamo]";
					mes "คุณผู้หญิงคุณเป็นผู้หญิงที่สง่างามแม้ว่าคุณจะออกไปเที่ยวในเมืองที่ยากจนนี้ ... ";
					next;
					break;
				case 3:
					mes "[Giacomo Girolamo]";
					mes "Ahh.. Lady.. Shopping in the slum!!.. Are you a type who needs to look around every single shop?";
					next;
					break;
				}
			}
			select("... .... ... ....");
			mes "[Giacomo Girolamo]";
			mes "ฉันกล้าถามเธอได้ยังไงและสนใจในสิ่งที่ผู้หญิงคนนั้นทำ ... ";
			next;
			emotion ET_OK;
			mes "[Giacomo Girolamo]";
			mes "ฉันแค่หวังว่าผู้หญิงคนนั้นจะออกไปจากเมืองนี้พร้อมกับข่าวลือที่อันตราย .. ";
			next;
			select("Dangerous rumors?");
			emotion ET_QUESTION, playerattached();
			mes "[Giacomo Girolamo]";
			mes "อา ... ท่านหญิงอาจไม่เคยได้ยินข่าวลือ";
			next;
			mes "[Giacomo Girolamo]";
			mes "เมื่อเร็ว ๆ นี้มีบางคนจากเมืองนี้มาจากสลัม ... และนักผจญภัยบางคนหายไป ... ";
			next;
			mes "[Giacomo Girolamo]";
			mes "แล้วข่าวลือก็เกิดขึ้นเนื่องจากกรณีการหายตัวไปเกิดขึ้นบ่อยครั้งว่า Rekenber กำลังทำการทดลองกับมนุษย์ที่น่าสยดสยองในห้องทดลองลับของพวกเขา";
			next;
			select("Excuse me?! human experimentation?");
			emotion ET_HUK, playerattached();
			mes "[Giacomo Girolamo]";
			mes "เออ! มนุษย์ทดลอง !!";
			mes "มีข่าวลือมากมายว่ามีการทดลองกับมนุษย์ที่น่าสยดสยองจริง ๆ แต่คนเหล่านี้ในสลัม .... ไม่อยากจะเชื่อว่าเป็นเพราะขบวนการสวัสดิการจาก Rekenber";
			next;
			mes "[Giacomo Girolamo]";
			mes "อืม! คนรวยก็อยากจะเชื่อ.. ฉันคิดว่า .. อ๊ะ !! แล้วสองสามอาทิตย์ที่ผ่านมาอืมเขาชื่ออะไร Wolfchev ??";
			next;
			mes "[Giacomo Girolamo]";
			mes "อย่างไรก็ตาม !! งานวิจัยนั้นถูกไล่ออกจากห้องทดลองฉันคิดว่าเขาต้องมีส่วนเกี่ยวข้องกับข่าวลือนี้";
			next;
			mes "[Giacomo Girolamo]";
			mes "ได้โปรดหญิงโปรดอย่าอยู่ในสถานที่เช่นนี้และอยู่อย่างปลอดภัย";
			next;
			lght_duk01 = 1;
			setquest 5109;
			mes "............ ดูเหมือนจะเป็นเรื่องร้ายแรงนะ";
			mes "ควรตรวจสอบห้องแล็บที่ทำการทดลอง";
			close;
		} else {
			mes "[Giacomo Girolamo]";
			if (Sex == SEX_MALE) {
				emotion ET_WRAP;
				mes "ดูแลตัวเองไม่ให้หาย!";
			} else {
				emotion ET_BIGTHROB;
				mes "Be careful, Lady.";
			}
			close;
		}
	} else {
		if (Sex == SEX_MALE) {
			emotion ET_DELIGHT;
			mes "[Giacomo Girolamo]";
			mes "ผู้หญิงสวยเหล่านั้นเท่านั้นที่เป็นเหตุผลในชีวิตของฉัน ~!";
			mes "ฉัน, Giacomo Girolamo Casanova de Seingalt ฉันไม่เคยสนใจผู้ชายอย่างเธอเลย !!";
			next;
			emotion ET_FRET;
			mes "[Giacomo Girolamo]";
			mes "แต่ฉันฟังคุณได้ ... คุณอยากจะพูดอะไรกับฉันไหม";
			next;
			switch(select("ไม่ .. ?", "เกี่ยวกับผู้หญิง .. ", "คุณ Womanizer !!", "อิจฉาคุณ ... ", "ข่าวลือเมื่อเร็ว ๆ นี้คืออะไร?")) {
			case 1:
				emotion ET_SCRATCH;
				mes "[Giacomo Girolamo]";
				mes "เฮ้คุณออกไปจากทางของฉันแล้ว .. ";
				close;
			case 2:
				emotion ET_FRET;
				mes "[Giacomo Girolamo]";
				mes "ฉันรู้สึกไม่อยากพูดถึงผู้หญิงที่มีค่าของฉันเพื่อสนุกกับพวกเธอคุณทำกับเพื่อนของคุณ";
				close;
			case 3:
				mes "[Giacomo Girolamo]";
				mes "เฮ้ .. ขอโทษนะ !!! แต่ฉันไม่ใช่ 'Womanizer' ฉันเป็นแค่คนที่รักผู้หญิง";
				next;
				emotion ET_OK;
				mes "[Giacomo Girolamo]";
				mes "แน่นอน ..... เออ ... ถ้าฉันเป็นผู้หญิงเจ้าชู้ผู้ชายทุกคนในโลกก็เหมือนกัน! ฉันก็แค่คาสโนว่าอืม 55555";
				close;
			case 4:
				emotion ET_QUESTION;
				mes "[Giacomo Girolamo]";
				mes "Haa.. Do you....? not have any woman but your sister and mother around you?";
				next;
				mes "[Giacomo Girolamo]";
				mes "... .... ...";
				next;
				emotion ET_PROFUSELY_SWEAT;
				mes "[Giacomo Girolamo]";
				mes "โอ้ ...... ขอโทษ.. ฉันเข้าใจแล้ว .... ";
				close;
			case 5:
				mes "[Giacomo Girolamo]";
				mes "You.. are not like how you seem? Interesting..";
				next;
				mes "[Giacomo Girolamo]";
				mes "เออเมื่อไม่นานมานี้ผู้คนจำนวนมากจากสลัมและนักผจญภัยพเนจรบางคนก็หายตัวไปและฉันไม่รู้ว่าทำไม";
				next;
				mes "[Giacomo Girolamo]";
				mes "คุณก็ต้องระวังด้วย .. ";
				close;
			}
		} else {
			emotion ET_QUESTION;
			mes "[Giacomo Girolamo]";
			mes "เฮ้ผู้หญิง?";
			next;
			mes "[Giacomo Girolamo]";
			switch(rand(1,10)) {
			case 1:
				mes "ตอนที่คุณตกลงมาจากสวรรค์เทวดาของฉันไม่เจ็บปวดเหรอ";
				next;
				break;
			case 2:
				mes "คุณคงเหนื่อยเพราะคุณมักจะทำให้ฉันหลงทาง";
				next;
				break;
			case 3:
				mes "เมื่อคืนคุณไม่เหนื่อยกับการเดินไปมาในความฝันของฉันเหรอ";
				next;
				break;
			case 4:
				mes "Excuse me. But I think I lost my heart. Could you please borrow me yours?";
				next;
				break;
			case 5:
				mes "คุณเชื่อในรักแรกพบ ?? หรือเราจะเริ่มใหม่ได้ .. ?";
				next;
				break;
			case 6:
				mes "ขอโทษนะ แต่ขอจูบกับคนที่เพิ่งเจอได้มั้ยโอเคงั้นให้ฉันแนะนำตัวนะ";
				next;
				break;
			case 7:
				mes "ฉันหลงทางที่นี่ตั้งแต่ครั้งแรกช่วยบอกให้ฉันรู้ว่าทางไหนคือหัวใจของคุณ";
				next;
				break;
			case 8:
				mes "ขอแผนที่หน่อย.. ขอทาง .... ฉันตกหลุมตาคุณ";
				next;
				break;
			case 9:
				mes "ขอแผนที่หน่อย.. ขอทาง .... ฉันตกหลุมตาคุณ";
				next;
				break;
			case 10:
				mes "ฉันสัญญาว่าฉันจะเป็นทุกอย่างของคุณในทุกกรณีในสถานการณ์ที่ยากลำบาก";
				next;
				break;
			}
			emotion ET_BIGTHROB;
			mes "[Giacomo Girolamo]";
			mes "วันนี้เป็นวันแรกที่เราเจอกัน แต่ฉันรักเธอนะเลดี้ <3";
			next;
			mes "[Giacomo Girolamo]";
			mes "และฉันบอกคุณเรื่องนี้เพราะฉันแค่เป็นห่วงคุณคุณต้องออกไปจากเมืองนี้ซึ่งเต็มไปด้วยคดีการหายตัวไป";
			close;
		}
	}

OnTouch:
	if (!lght_duk01)
		emotion (Sex == SEX_MALE? ET_HNG : ET_BIGTHROB), "Giacomo Girolamo#kiup2";
	end;
}

// Warp portal to the 4th Fl.
//============================================================
lhz_dun03,239,78,1	script	lhz_dun03_lhz_dun04	WARPNPC,1,1,{
	end;

OnTouch:
	if (lght_duk01 > 0 && lght_duk01 < 6) {
		mes "เพื่อที่จะตรวจสอบการทดลองในมนุษย์ฉันต้องลงไปในขณะที่ฉันไม่ต้องการเพราะเสียงที่น่ากลัว";
		close2;
		if (lght_duk01 < 3) {
			warp "que_lhz", 245, 56;
		} else if (lght_duk01 == 3) {
			warp "que_lhz", 96, 136;
		} else {
			warp "que_lhz", 148, 215;
		}
	} else {
		if (lhz_curse < 31) {
			mes "- Whistling sound -";
			mes "จากด้านล่างมีเสียงที่น่าสยดสยองปนกับสายลม";
			close2;
		}
		warp "lhz_dun04", 245, 56;
	}
	end;
}

// Warp Portal to the 3rd Fl.
//============================================================
lhz_dun04,244,61,1	warp	lhz_dun04_lhz_dun03	1,1,lhz_dun03,240,75

// Inside the Lab
//============================================================
que_lhz,242,50,0	script	#01Startpoint	-1,3,3,{
	end;

OnInit:
	initnpctimer;
	end;

OnTimer60000:
	mapannounce "que_lhz", "Man's whisper: Pl.. please.. save me.. I don't wanna die.. wouaaaa..", bc_map, "0x7DCBF0";
	end;

OnTimer120000:
	mapannounce "que_lhz", "Woman's whisper: .. Save.. aaaah.. us...", bc_map, "0x7DCBF0";
	end;

OnTimer180000:
	mapannounce "que_lhz", "Kid's whisper: Bawwww~ Where am I? Mama......aaaa....", bc_map, "0x7DCBF0";
	end;

OnTimer240000:
	mapannounce "que_lhz", "Oldman's whisper: Kirk...hel...help......", bc_map, "0x7DCBF0";
	end;

OnTimer300000:
	stopnpctimer;
	initnpctimer;
	end;

OnTouch:
	if (lght_duk01 == 1) {
		mes "ฉันมาถึงศูนย์วิจัยแห่งหนึ่งซึ่งดูเหมือนว่าจะถูกใช้มาจนถึงทุกวันนี้";
		next;
		mes "There are some destroyed part, but the basic facility seem fine.";
		next;
		lght_duk01 = 2;
		mes "Get around a little more...";
		close;
	}
	end;
}

que_lhz,206,74,4	script	Researcher#02	4_LGTSCIENCE,3,3,{
	end;

OnTouch:
	if (lght_duk01 == 2) {
		emotion ET_HUK, playerattached();
		mes "["+ strcharinfo(0) +"]";
		mes "แก !!!! คือ Wolfchev?";
		next;
		emotion ET_KIK;
		mes "[Wolfchev]";
		mes "A Human! kaahaha. คนมักจะเกลียดที่นี่ทำไมคุณถึงมาที่นี่?";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "คุณ !!! ฉันได้ยินมาว่าคุณกำลังทดลองกับมนุษย์ Wolfchev !!";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "และฉันเห็นว่าคุณยังคงทำสิ่งนั้นอยู่ !!!";
		next;
		emotion ET_KIK;
		mes "[Wolfchev]";
		mes "คิฮิฮิฉันเห็นว่าคุณรู้อะไรเกี่ยวกับฉันคุณเป็นคนกล่าวหาฉันเหรอ";
		mes "คุณกล้ากลับมาหาฉันได้ยังไงหลังจากที่คุณทำแบบนั้น Kihihih";
		next;
		mes "[Wolfchev]";
		mes "ฉันรู้สึกดีที่ได้พบมนุษย์";
		mes "อีกนาน แต่ฉันต้องไปคิฮิฮิฮิ";
		next;
		mes "- Wolfchev วิ่งหนีไปด้วยเสียงหัวเราะที่น่าอึดอัด";
		next;
		erasequest 5109;
		setquest 5110;
		lght_duk01 = 3;
		emotion ET_HUK, playerattached();
		mes "["+ strcharinfo(0) +"]";
		mes "โอ้ว! Wolfchev หยุด!";
		close2;
		warp "que_lhz", 96, 136;
	}
	end;
}

que_lhz,94,119,4	script	Researcher#03	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 ตรวจสอบสินค้าคงคลังของคุณคุณมีไอเท็มมากเกินไปที่จะทำเควส ^000000";
		close;
	}
	if (lght_duk01 == 3) {
		emotion ET_PROFUSELY_SWEAT;
		mes "[Wolfchev]";
		mes "(Gasping)  Pshaw ... คุณยังตามฉันอยู่เฮ้ทำไมคุณยังติดตามฉัน";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Wolfchev .. คุณเคยบอกไว้ก่อนหน้านี้ว่าคุณกำลังทำวิจัยสำหรับทุกคน";
		next;
		mes "[Wolfchev]";
		mes "ฉันอาจจะบอกว่า .... แล้วไง";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "แต่การทดลองของคุณดูอันตรายมากทำไมทุกคนถึงต้องการ";
		next;
		emotion ET_KIK;
		mes "[Wolfchev]";
		mes "ทุกคนมีความปรารถนาที่จะแข็งแกร่งขึ้นและต้องการที่จะครอบงำผู้อื่นเพื่อให้คนอื่น ๆ เชื่อฟังและชื่นชมคุณคุณไม่มีความปรารถนาอย่างนั้นหรือ";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "... .... ...";
		next;
		mes "[Wolfchev]";
		mes "ถ้าคุณอยากจะช่วยตัวเองควรเลิกติดตามฉันซะเถอะ Bugger off";
		next;
		mes "วูล์ฟชอฟวิ่งหนีอีกครั้งพร้อมเสียงแปลก ๆ ";
		next;
		lght_duk01 = 4;
		mes "["+ strcharinfo(0) +"]";
		mes "อ้าว !! แก !! วิ่งหนีอีกแล้ว ..... หยุด!";
		close2;
		warp "que_lhz", 148, 215;
	}
	end;
}

que_lhz,147,224,4	script	Researcher#04	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 ตรวจสอบสินค้าคงคลังของคุณคุณมีไอเท็มมากเกินไปที่จะทำเควส ^000000";
		close;
	}
	if (lght_duk01 == 4) {
		mes "[Wolfchev]";
		mes "You are pretty persistent, aren't you? Or I think.......that you like me, huh? Kihihii.";
		next;
		emotion ET_THINK;
		mes "["+ strcharinfo(0) +"]";
		mes "... .... ...";
		next;
		specialeffect EF_SIGHTRASHER;
		mes "Wolfchev ล้อเลียนคุณเมื่อคุณเงียบและกดปุ่มในมือจากนั้นหนูตะเภาก็ตื่นขึ้นมารอบ ๆ ตัวเขา";
		next;
		for(.@i = 1; .@i < 9; .@i++)
			donpcevent "Human Guinea pig#0"+.@i+"::OnEnable";
		mes "[Wolfchev]";
		mes "อีกแล้ว .. ถึงเวลาบอกลา";
		next;
		emotion ET_SURPRISE;
		mes "[Wolfchev]";
		mes "อืม! อาจจะ .... ";
		next;
		mes "[Wolfchev]";
		mes "If you come to me for the experimentation, I can treat you better.";
		next;
		donpcevent "#Eventctrl::OnEnable";
		progressbar "FF00FF", 5;
		emotion ET_HUK;
		mes "[Wolfchev]";
		mes "Oh my,... What happened?";
		next;
		donpcevent "#Eventctrl::OnEnable2";
		for(.@i = 1; .@i < 9; .@i++)
			donpcevent "Human Guinea pig#0"+.@i+"::OnDisable";
		mes "[Wolfchev]";
		mes "ไม่! อย่ามาหาฉันอย่า !!";
		next;
		lght_duk01 = 5;
		mes "First of all, I gotta handle those guinea pigs!";
		close;
	} else if (lght_duk01 == 5) {
		.@i = rand(1,3);
		if (.@i == 2) {
			mes "Wolfchev สับสนและไม่รู้ว่าตอนนี้มอนสเตอร์พวกนั้นโจมตีเขาด้วย";
			next;
			specialeffect EF_STUNATTACK;
			specialeffect EF_STUNATTACK;
			mes "- ตีเขาเพื่อปลุกเขา -";
			next;
			specialeffect EF_DARKBREATH;
			emotion ET_STARE_ABOUT;
			mes "["+ strcharinfo(0) +"]";
			mes "เฮ้เพื่อน! ยังไงก็ออกไปจากที่นี่ก่อนเถอะไม่มีที่ไหนที่พวกเขาไปไม่ถึง?";
			next;
			mes "[Wolfchev]";
			mes "Oooops.. then my....my lab....to my lab...";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "ห้องแล็บของคุณโอเคที่ไหนล่ะ";
			next;
			mes "[Wolfchev]";
			mes "to the north....... . Not so far from here..";
			next;
			erasequest 5110;
			setquest 5111;
			lght_duk01 = 6;
			mes "ไปที่ '^0000FFWolfchev's Lab ^000000' ที่ทิศทาง 12 นาฬิกา ... ";
			close;
		} else {
			emotion ET_CONFUSE;
			mes "Wolfchev สับสนและไม่รู้ว่าตอนนี้มอนสเตอร์พวกนั้นโจมตีเขาด้วย";
			close;
		}
	} else if (lght_duk01 == 6) {
		mes "ไปที่ '^0000FFWolfchev's Lab ^000000' ที่ทิศทาง 12 นาฬิกา ... ";
		close;
	}
	end;
}

// Warp to lhz_dun04 from Wolfchev's Lab
//============================================================
que_lhz,148,251,0	script	#Move2lab	-1,12,0,{
	end;

OnTouch:
	if (lght_duk01 == 6) {
		mes "ฉันมาถึง '^0000FFWolfchev's Lab ^000000'.";
		close2;
		warp "lhz_dun04", 148, 269;
		end;
	} else {
		mes "ฉันยังมีบางอย่างที่ต้องทำกับ Wolfchev";
		close;
	}
}

// Event Controller
//============================================================
que_lhz,1,1,0	script	#Eventctrl	-1,{
	end;

OnEnable:
	initnpctimer;
	end;

OnEnable2:
	monster "que_lhz", 139, 232, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 147, 232, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 232, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 224, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 156, 215, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 147, 215, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 139, 215, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	monster "que_lhz", 139, 224, "Human Guinea pig", 2244, 1, "#Eventctrl::OnMyMobDead";
	end;

OnTimer1000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#01";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#01";
	end;

OnTimer2000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#02";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#02";
	end;

OnTimer3000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#03";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#03";
	end;

OnTimer4000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#04";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#04";
	end;

OnTimer5000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#05";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#05";
	end;

OnTimer6000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#06";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#06";
	end;

OnTimer7000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#07";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#07";
	end;

OnTimer8000:
	specialeffect EF_SUI_EXPLOSION,AREA, "#08";
	specialeffect EF_BEGINASURA, AREA, "Human Guinea pig#08";
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount("que_lhz", "#Eventctrl::OnMyMobDead") < 4)
		killmonster "que_lhz", "#Eventctrl::OnMyMobDead";
}
que_lhz,139,232,0	script	#01	-1,{ end; }
que_lhz,147,232,0	script	#02	-1,{ end; }
que_lhz,156,232,0	script	#03	-1,{ end; }
que_lhz,156,224,0	script	#04	-1,{ end; }
que_lhz,156,215,0	script	#05	-1,{ end; }
que_lhz,147,215,0	script	#06	-1,{ end; }
que_lhz,139,215,0	script	#07	-1,{ end; }
que_lhz,139,224,0	script	#08	-1,{ end; }

// Human Guinea pigs
//============================================================
que_lhz,140,231,1	script	Human Guinea pig#01	2224,{
	end;

OnInit:
	disablenpc strnpcinfo(0);
	end;

OnEnable:
	enablenpc strnpcinfo(0);
	end;

OnDisable:
	specialeffect EF_FLASHER;
	disablenpc strnpcinfo(0);
	end;
}
que_lhz,147,231,0	duplicate(Human Guinea pig#01)	Human Guinea pig#02	2224
que_lhz,155,231,7	duplicate(Human Guinea pig#01)	Human Guinea pig#03	2224
que_lhz,155,224,6	duplicate(Human Guinea pig#01)	Human Guinea pig#04	2224
que_lhz,155,216,5	duplicate(Human Guinea pig#01)	Human Guinea pig#05	2224
que_lhz,147,216,4	duplicate(Human Guinea pig#01)	Human Guinea pig#06	2224
que_lhz,140,216,3	duplicate(Human Guinea pig#01)	Human Guinea pig#07	2224
que_lhz,140,224,2	duplicate(Human Guinea pig#01)	Human Guinea pig#08	2224

// Instance Creation
//============================================================
lhz_dun04,151,276,3	script	Researcher#memo	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000You cannot continue the process due to possessing too many belongings.^000000";
		close;
	}
	.@quest_time = checkquest(5112, PLAYTIME);
	.@killed_bosses = true;
	for(.@quest_id = 5113; .@quest_id <= 5125; .@quest_id++) {
		if (checkquest(.@quest_id, HUNTING) <= 1)
			.@killed_bosses = false;
	}
	if (lght_duk01 < 6) {
		mes "[Researcher]";
		mes "คุณมีธุระอะไรที่นี่?";
		next;
		if (select("Yes I do!!", "Umm.. No..") == 1) {
			mes "[Researcher]";
			mes "ทำไมไม่หยุดจ้องที่ห้องทดลองของคนอื่นแล้วก็ไปกันเถอะ";
			close;
		}
		mes "[Researcher]";
		mes "Stop wondering here, and be on your way. Will you? Khh Khh.";
		close;
	} else if (lght_duk01 == 6) {
		mes "It seems experimental creatures cannot come near the laboratory, just like Wolfchev said.";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "Hey, is there any way to stop those creatures? You should know something, you made those things!";
		next;
		mes "[Wolfchev]";
		mes "Urrgg .. ฉันไม่ได้คาดหวังว่าพวกเขาจะหลุดการควบคุมแบบนั้น ... ";
		next;
		mes "[Wolfchev]";
		mes "นี่มันอะไรกันเนี่ย";
		next;
		mes "[Wolfchev]";
		mes "Urr.. I think they will ruin my whole laboratory!";
		next;
		emotion ET_BLABLA, playerattached();
		mes "["+ strcharinfo(0) +"]";
		mes "เฮ้! Wolfchev !!";
		next;
		mes "[Wolfchev]";
		mes "คุณ!? เฮ้โปรดหยุดพวกเขา!";
		mes "สิ่งมีชีวิต ^9f6077 มีความสัมพันธ์ทางธรรมชาติกับสาขางานเดียวกัน ^000000";
		next;
		mes "[Wolfchev]";
		mes "มีสิ่งมีชีวิตพิเศษอยู่ตัวหนึ่งคือ ^46B951 ตำแหน่งบอส ^000000 ... ";
		mes "ถ้าคุณสามารถปิดการใช้งานสิ่งมีชีวิตนั้น ^ A1BB44slave ทั้งหมด ^000000 ที่เดินไปมาที่นี่จะหยุดเคลื่อนไหวทันที";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "บอสครี?";
		mes "เฮ้! Wolfchev! นั่นอยู่ที่ไหน ^46B951 บอสสัตว์ ^000000?";
		next;
		mes "[Wolfchev]";
		mes "ข้างใน .. ของห้องทดลอง .. ตอนนี้มันควรจะตื่นแล้วมันน่าเศร้าที่จะทำลายชีวิตการทำงานของฉัน แต่ฉันไม่สามารถทำมันได้อีกต่อไป .. ... ";
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "How can I get into the Wolfchev's laboratory?";
		next;
		mes "[Wolfchev]";
		mes "โอ้.. เดี๋ยว!";
		mes "ฉันจะช่วยคุณไปที่นั่น";
		next;
		mes "[Wolfchev]";
		mes "Good news is that boss creatures are not set up to wake up all at once, even though slaves are.";
		next;
		erasequest 5111;
		lght_duk01 = 7;
		for(.@i = 5113; .@i <= 5125; .@i++)
			setquest .@i;
		mes "[Wolfchev]";
		mes "แต่ถึงกระนั้นมันก็อันตรายมากเพราะเราไม่รู้ว่าพวกเขาจะหนีออกจากที่นั่นเมื่อไหร่";
		mes "ดังนั้นโปรดเตรียมสมาชิกในปาร์ตี้ของคุณและดูแลสิ่งมีชีวิตระดับหัวหน้าเหล่านั้นฉันไม่แน่ใจว่าสถานที่นี้จะอยู่ได้นานแค่ไหน";
		close;
	} else if (lght_duk01 > 6) {
		if (getcharid(1) > 0) {
			mes "[Wolfchev]";
			mes "พวกคุณพร้อมหรือยังฉันจะเตรียมทางเข้าถ้าคุณเป็นหัวหน้าปาร์ตี้นั้น";
			next;
			switch(select("คำเตือนใด ๆ ", "เข้าไปในห้องปฏิบัติการ", "ห้ามเข้า")) {
			case 1:
				mes "[Wolfchev]";
				mes "ฉันไม่รู้ว่าการกดสัญญาณเตือนฉุกเฉินจะทำให้เกิดปัญหาดังกล่าวดูเหมือนระบบการจัดการจะเปลี่ยนเป็นโหมดฉุกเฉิน";
				next;
				mes "[Wolfchev]";
				mes "มีระบบมากมายที่ติดตั้งอยู่ภายในห้องปฏิบัติการดังนั้นฉันไม่สามารถแน่ใจได้ว่าตอนนี้ระบบต่างๆจะบิดเบี้ยวอย่างไร";
				next;
				mes "[Wolfchev]";
				mes "อ๊ะ! ถ้าระบบฉุกเฉินเปิดใช้งานมีเพียงหัวหน้าปาร์ตี้เท่านั้นที่จะควบคุมทุกอย่างได้มันกดดันมากฉันเข้าใจ";
				next;
				mes "[Wolfchev]";
				mes "และเพื่อให้ทราบโปรดอย่าแตะต้องของสุ่มด้วยความอยากรู้อยากเห็นฉันไม่ต้องการให้คุณบาดเจ็บ";
				close;
			case 2:
				if (.@quest_time == 0) {
					mes "[Wolfchev]";
					mes "ฉันขอโทษ แต่ดูเหมือนว่าคุณจะยังเข้าไปในห้องทดลองไม่ได้คุณจะกลับมาใหม่ในภายหลังหรือไม่ระบบมันยุ่งยากมากที่คุณรู้";
					close;
				} else {
					//if (.@quest_time == 2)
						//recall_completequest 5112;
					if (isbegin_quest(5112))
						erasequest 5112;
					if (!.@killed_bosses) {
						if (lght_duk01 == 9) {
							mes "[Wolfchev]";
							mes "You came back, Ka Ha Ha.";
							next;
							mes "[Wolfchev]";
							mes "หลังจากที่คุณจากไปฉันกำลังซ่อมบางส่วนของระบบแล้วจู่ๆก็มีเรื่องแปลก ๆ เกิดขึ้น";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "What was that?";
							next;
							mes "[Wolfchev]";
							mes "บอสทุกตัวที่คุณดูแลเริ่มสร้างตัวเองใหม่ .. ";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "อะไร.. เป็นไปได้ยังไง ... ?!";
							next;
							mes "[Wolfchev]";
							mes "I am also completely lost here.. But, this should not be happening..";
							next;
							mes "["+ strcharinfo(0) +"]";
							mes "... .... ...";
							next;
							mes "[Wolfchev]";
							mes "อย่างที่คุณเห็นคุณเป็นคนเดียวที่ฉันไว้ใจคุณและพรรคพวกของคุณจะช่วยฉันอีกครั้งในการปิดการใช้งานสิ่งมีชีวิตเหล่านั้นได้หรือไม่";
							next;
							if (select("I am not sure", "Why not") == 1) {
								mes "[Wolfchev]";
								mes "I see. I understand why you are hesitating, and don't want to press you any more. But, still I'm not sure why you want go there anyways,";
								next;
							} else {
								for(.@i = 5113; .@i <= 5125; .@i++) {
									setquest .@i;
								}
								lght_duk01 = 10;
								mes "[Wolfchev]";
								mes "ขอบคุณมาก." + strcharinfo (0) + "";
								mes "ฉันจะไปช่วยคุณในห้องปฏิบัติการทันที";
								next;
							}
						} else {
							mes "[Wolfchev]";
							mes "อืม .. สิ่งมีชีวิตระดับบอสเหล่านั้นฟื้นคืนชีพอีกครั้ง .. ";
							next;
							mes "[Wolfchev]";
							mes "ช่วยอีกครั้งได้ไหม";
							next;
							if (select("ฉันไม่แน่ใจ", "ทำไมไม่") == 1) {
								mes "[Wolfchev]";
								mes "ฉันเข้าใจแล้วฉันเข้าใจว่าทำไมคุณถึงลังเลและไม่ต้องการกดดันคุณอีกต่อไป แต่ฉันก็ยังไม่แน่ใจว่าทำไมคุณถึงต้องการไปที่นั่นต่อไป";
								next;
							} else {
								for(.@i = 5113; .@i <= 5125; .@i++) {
									setquest .@i;
								}
								lght_duk01 = 10;
								mes "[Wolfchev]";
								mes "Thank you. "+strcharinfo(0)+"";
								mes "คุณอยากไปที่นั่นเร็ว ๆ ใช่ไหม";
								next;
							}
						}
					}
				}
				if (is_party_leader() == true) {
					if (instance_create("Wolfchev's Laboratory") < 0) {
						mes "[Wolfchev]";
						mes "อืม .. ระบบเข้าแล็บแปลกไปหน่อยจะรอให้ฉันตรวจสอบระบบ?";
						close;
					}
					mes "[Wolfchev]";
					mes "โปรดรอ ... ";
					next;
					mes "Wolfchev เริ่มควบคุมเกียร์บางอย่าง";
					next;
					progressbar "FF00FF", 2;
					mes "[Wolfchev]";
					mes "เอาล่ะฉันได้ทำเครื่องหมาย lan ที่กำลังป้อนรหัสระบบเป็น";
					mes "^0000FF" + strcharinfo (1) + "^000000 ชื่อหัวหน้าพรรค ^0000FF" + strcharinfo (0) + "^000000 !!.";
					next;
					mes "[Wolfchev]";
					mes "ตอนนี้ปาร์ตี้ของคุณมีอิสระที่จะผ่านทางเข้า";
					close;
				} else {
					mes "[Wolfchev]";
					mes "คุณไม่ใช่ ^0000FFLeader ของปาร์ตี้ ^000000 ใช่หรือไม่โปรดไปรับหัวหน้า";
					close;
				}
			case 3:
				mes "[Wolfchev]";
				mes "ต้องการการเตรียมตัวมากกว่านี้ได้โปรดเราไม่มีเวลาทั้งวัน";
				close;
			}
		} else {
			mes "[Wolfchev]";
			mes "คุณกำลังพยายามไปที่นั่นนั่นคือการฆ่าตัวตาย! ระบบการเข้าห้องแล็บของเราไม่ง่ายอย่างที่คิด z`";
			close;
		}
	} else {
		mes "[Wolfchev]";
		mes "Woot! มีบางอย่างแปลก ๆ เกิดขึ้นถ้ายังคงเกิดขึ้นเราควรลองทางอื่น .. ";
		close;
	}
}

lhz_dun04,147,279,0	script	Laboratory Entrance#memo	CLEAR_NPC,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000You cannot continue the process due to possessing too many belongings.^000000";
		close;
	}
	.@lhz_time = checkquest(5112, PLAYTIME);
	if (lght_duk01 < 7) {
		mes "It says,";
		mes "'^0000FF****'s laboratory member only^000000'";
		close;
	}
	if (.@lhz_time == -1) {
		mes "This is an entrance towards Wolfchev's laboratory. Something is howling over the entrance.";
		next;
		if (select("Go inside", "Think one more time") == 2) {
			mes "คุณหยุดเข้าห้องทดลองของ Wolfchev แล้ว";
			close;
		}
		if (instance_enter("Wolfchev's Laboratory") != IE_OK) {  // probably missing failure cases
			mes "^FF0000Warning^000000";
			mes ""+ strcharinfo(0) +". . .";
			mes "^FF0000Unregistered personnel^000000";
			next;
			pushpc 0, 10;
			percentheal -5,0;
			specialeffect EF_AUTOCOUNTER;
			specialeffect2 EF_BASH;
			mes "You have been wounded by laboratory entrance system attack.";
			close;
		}
		mapannounce "lhz_dun04","Laboratory entrance system ["+ strcharinfo(1) +"] member ["+ strcharinfo(0) +"] access granted..",bc_map,"0x0DF297";
		setquest 5112;
		//warp "1@lhz", 45, 148;
		close;
	} else if (.@lhz_time == 0) {
		mes "Di Rit- Di- Di- Dit-";
		next;
		mes ""+ strcharinfo(0) +". . .";
		mes "^FF0000Forbidden personnel^000000";
		next;
		mes "The system denied your entrance.";
		close;
	} else if (.@lhz_time >= 1) {
		mes "Di Rit- Di- Di- Dit-";
		next;
		//if (.@lhz_time == 2)
			//recall_completequest 5112;
		erasequest 5112;
		mes ""+ strcharinfo(0) +". . .";
		mes "^FF0000Access denial has been deactivated.^000000";
		close;
	} else {
		mes "^FF0000Error! Error!^000000";
		mes "^FF0000Please try again.^000000";
		close;
	}
}

// The Lab No.1 : System 1_1
//============================================================
1@lhz,35,108,4	script	Lab1#1_1	CLEAR_NPC,{
	end;

OnInstanceInit:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnType_02:
	donpcevent instance_npcname("Manual Sheet#1_1")+"::OnEnable";
	donpcevent instance_npcname("Valve#1_2")+"::OnEnable";
	donpcevent instance_npcname("Valve#1_3")+"::OnEnable";
	mapannounce instance_mapname("1@lhz"),"[Security System] the persons concerned, access with the emergency access method.",bc_map,"0xff9977";
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Security System] This is a warning notice! Warning notice! Intruder Detected.",bc_map,"0xff9977";
	end;

OnTimer2000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, The Lab No.1's security system will be activated.",bc_map,"0xff9977";
	end;

OnTimer7000:
	donpcevent instance_npcname("MobCtrl1#mob01")+"::OnEnable";
	end;
}

// The Lab No.1 Monster Control
//============================================================
1@lhz,37,108,4	script	MobCtrl1#mob01	CLEAR_NPC,{
	end;

OnEnable:
	.@map$ = instance_mapname("1@lhz");
	mapannounce .@map$,"[Security System] Security system Activated !! Experimental animals are released. The persons concerned have to evacuate.",bc_map,"0xff9977";
	areamonster .@map$,35,159,50,169,"Starving Lab animal",2242,rand(10,15),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	initnpctimer;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	end;

OnTimer180000:
	stopnpctimer;
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("Lab1#1_1")+"::OnType_02";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Lab No.1's security system has been stopped.",bc_map,"0xff9977";
	} else {
		initnpctimer;
	}
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("Lab1#1_1")+"::OnType_02";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Lab No.1's security system has been stopped.",bc_map,"0xff9977";
		stopnpctimer;
	}
	end;
}

// Emergency Access Valve Manual
//============================================================
1@lhz,39,168,0	script	Manual Sheet#1_1	CLEAR_NPC,{
	mes "มีแผ่นคู่มือวาล์วเกลื่อนพื้น";
	next;
	if (is_party_leader() == true) {
		mes "คุณหัวหน้าปาร์ตี้เริ่มอ่านคู่มือ";
		next;
		mes "[Emergency Access Valve Manual]";
		switch(lght_duk02) {
		case 0:
			switch(rand(1,6)) {
			case 1:
				lght_duk02 = 1;
				mes "หมุนวาล์วทางซ้ายตามเข็มนาฬิกาสองครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง, ตามเข็มนาฬิกาครั้งเดียว";
				break;
			case 2:
				lght_duk02 = 2;
				mes "หมุนวาล์วซ้ายตามเข็มนาฬิกาหนึ่งครั้ง, ทวนเข็มนาฬิกาสองครั้ง, ตามเข็มนาฬิกาครั้งเดียว";
				break;
			case 3:
				lght_duk02 = 3;
				mes "หมุนวาล์วทางซ้ายตามเข็มนาฬิกาหนึ่งครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง, ตามเข็มนาฬิกาสองครั้ง";
				break;
			case 4:
				lght_duk02 = 4;
				mes "หมุนวาล์วทางขวาทวนเข็มนาฬิกาสองครั้ง, ตามเข็มนาฬิกาหนึ่งครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง";
				break;
			case 5:
				lght_duk02 = 5;
				mes "หมุนวาล์วทางขวาทวนเข็มนาฬิกาหนึ่งครั้ง, ตามเข็มนาฬิกาสองครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง"; 
				break;
			case 6:
				lght_duk02 = 6;
				mes "Turn the right valve Counterclockwise once, Clockwise once, Counterclockwise twice";
				break;
			default:
				mes "This is a unknown Error. If you get the same problem after trying one more time, contact GM.";
				break;
			}
			break;
		case 1:
			mes "หมุนวาล์วทางซ้ายตามเข็มนาฬิกาสองครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง, ตามเข็มนาฬิกาครั้งเดียว";
			break;
		case 2:
			mes "หมุนวาล์วซ้ายตามเข็มนาฬิกาหนึ่งครั้ง, ทวนเข็มนาฬิกาสองครั้ง, ตามเข็มนาฬิกาครั้งเดียว";
			break;
		case 3:
			mes "หมุนวาล์วทางซ้ายตามเข็มนาฬิกาหนึ่งครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง, ตามเข็มนาฬิกาสองครั้ง";
			break;
		case 4:
			mes "หมุนวาล์วทางขวาทวนเข็มนาฬิกาสองครั้ง, ตามเข็มนาฬิกาหนึ่งครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง";
			break;
		case 5:
			mes "หมุนวาล์วทางขวาทวนเข็มนาฬิกาหนึ่งครั้ง, ตามเข็มนาฬิกาสองครั้ง, ทวนเข็มนาฬิกาหนึ่งครั้ง";
			break;
		case 6:
			mes "หมุนวาล์วทางขวาทวนเข็มนาฬิกาหนึ่งครั้งตามเข็มนาฬิกาหนึ่งครั้งทวนเข็มนาฬิกาสองครั้ง";
			break;
		default:
			mes "นี่เป็นข้อผิดพลาดที่ไม่ทราบสาเหตุหากคุณพบปัญหาเดียวกันหลังจากลองอีกครั้งโปรดติดต่อ GM";
			break;
		}
	} else {
		mes "Let your party leader handle the valve.";
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("Manual Sheet#1_1");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("Manual Sheet#1_1");
	end; 
}

// Left Valve
//============================================================
1@lhz,41,172,0	script	Valve#1_2	CLEAR_NPC,{
	.@open_portal = 0;
	mes "นี่คือวาล์วทางซ้ายสำหรับทางเข้าฉุกเฉิน";
	if (is_party_leader() == true) {
		next;
		mes "คุณต้องการหันไปทางไหน";
		next;
		if (lght_duk02 < 1 || lght_duk02 > 3) {
			for(.@i = 1; .@i <= 4; .@i++) {
				select("ตามเข็มนาฬิกา", "ทวนเข็มนาฬิกา");
				mes "-A metal sound-";
				if (.@i != 4)
					mes "and, to which direction then?";
				next;
			}
		} else {
			// lght_duk02 == 1 : Clockwise, Clockwise, Counterclockwise, Clockwise
			// lght_duk02 == 2 : Clockwise, Counterclockwise, Counterclockwise, Clockwise
			// lght_duk02 == 3 : Clockwise, Counterclockwise, Clockwise, Clockwise
			for(.@i = 1; .@i <= 4; .@i++) {
				switch(select("Clockwise", "Counterclockwise")) {
				case 1:
					if ((.@i == 1 || .@i == 4) || (.@i == 2 && lght_duk02 == 1) || (.@i == 3 && lght_duk02 == 3)) {
						.@open_portal += 1;
					}
				case 2:
					if ((.@i == 2 && lght_duk02 != 1) || (.@i == 3 && lght_duk02 != 3)) {
						.@open_portal += 1;
					}
				}
				mes "-A metal sound-";
				if (.@i != 4)
					mes "แล้วไปทางไหน";
				next;
			}
		}
		if (.@open_portal == 4) {
			donpcevent instance_npcname("#potal_01")+"::OnEnable";
			lght_duk02 = 0;
			mes "- ประตูเปิด -";
			next;
			mes "ประตูที่เชื่อมกับห้องทดลองหมายเลข 2 เปิดอยู่แล้ว";
		} else {
			lght_duk02 = 0;
			mes "ไม่มีอะไรเกิดขึ้น";
			next;
			mes "คุณต้องอ่านคู่มือ";
		}
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("Valve#1_2");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("Valve#1_2");
	end;
}

// Right Valve
//============================================================
1@lhz,52,172,0	script	Valve#1_3	CLEAR_NPC,{
	.@open_portal = 0;
	mes "นี่คือวาล์วทางซ้ายสำหรับทางเข้าฉุกเฉิน";
	if (is_party_leader() == true) {
		next;
		mes "คุณต้องการหันไปทางไหน";
		next;
		if (lght_duk02 < 4 || lght_duk02 > 6) {
			for(.@i = 1; .@i <= 4; .@i++) {
				select("Clockwise", "Counterclockwise");
				mes "- เสียงโลหะ -";
				if (.@i != 4)
					mes "แล้วไปทางไหน";
				next;
			}
		} else {
			// lght_duk02 == 4 : Counterclockwise, Counterclockwise, Clockwise, Counterclockwise
			// lght_duk02 == 5 : Counterclockwise, Clockwise, Clockwise, Counterclockwise
			// lght_duk02 == 6 : Counterclockwise, Clockwise, Counterclockwise, Counterclockwise
			for(.@i = 1; .@i <= 4; .@i++) {
				switch(select("Clockwise", "Counterclockwise")) {
				case 1:
					if ((.@i == 2 && lght_duk02 != 4) || (.@i == 3 && lght_duk02 != 6))
						.@open_portal += 1;
					break;
				case 2:
					if ((.@i == 1 || .@i == 4) || (.@i == 2 && lght_duk02 == 4) || (.@i == 3 && lght_duk02 == 6))
						.@open_portal += 1;
					break;
				}
				mes "-A metal sound-";
				if (.@i != 4)
					mes "แล้วไปทางไหน";
				next;
			}
		}
		if (.@open_portal == 4) {
			donpcevent instance_npcname("#potal_01")+"::OnEnable";
			lght_duk02 = 0;
			mes "- ประตูเปิด -";
			next;
			mes "ประตูที่เชื่อมกับห้องทดลองหมายเลข 2 เปิดอยู่แล้ว";
		} else {
			lght_duk02 = 0;
			mes "ไม่มีอะไรเกิดขึ้น";
			next;
			mes "คุณต้องอ่านคู่มือ";
		}
	}
	close;

OnInstanceInit:
	disablenpc instance_npcname("Valve#1_3");
	end;

OnEnable:
	specialeffect EF_LIGHTSPHERE;
	enablenpc instance_npcname("Valve#1_3");
	end;
}


// The Lab No.1 >> The Lab No.2 Warp portal
//============================================================
1@lhz,45,173,0	script	#potal_01	WARPNPC,2,2,{
	end;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#potal_01");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[Management System] The door which connects to the Lab No.2 is now open.",bc_map,"0xff9977";
	enablenpc instance_npcname("#potal_01");
	end;

OnTouch_:
	warp instance_mapname("1@lhz"), 151, 29;
	end;
}

// The Lab No.2
//============================================================
1@lhz,151,29,0	script	#2_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#2_0");
	end;

OnDisable:
	disablenpc instance_npcname("#2_0");
	end;

OnTouch_:
	donpcevent instance_npcname("Lab2#2_1")+"::OnEnable";
	donpcevent instance_npcname("#2_0")+"::OnDisable";
	end;
}

// The Lab No.2 2_1 System - Control of Monster Wave and Pipe right before exploding
//============================================================
1@lhz,35,106,4	script	Lab2#2_1	CLEAR_NPC,{
	end;

OnInstanceInit:
OnDisable:
	stopnpctimer;
	end;

OnEnable:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Security System] This is a warning notice! Warning notice! Intruder Detected in The Lab No.2!!",bc_map,"0xff9977";
	end;

OnTimer3000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, The Lab No.2 Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure in the Lab No.2 is too high.",bc_map,"0xff99ff";
	end;

OnTimer8000:
	mapannounce instance_mapname("1@lhz"),"[Security System] First security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On01";
	end;

OnTimer10000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer190000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer303000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Second Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer308000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Second security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On02";
	end;

OnTimer370000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer550000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer603000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Third Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer608000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Third security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On03";
	end;

OnTimer730000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer903000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Fourth Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer908000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fourth security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On04";
	end;

OnTimer910000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer1090000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	end;

OnTimer1203000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, Fifth Security system will start working.",bc_map,"0xff9977";
	end;

OnTimer1208000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fifth security system activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl2#mob02")+"::On05";
	end;

OnTimer1270000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Warning! The pipe pressure is too high. Open the valve.",bc_map,"0xff99ff";
	donpcevent instance_npcname("Lab2#2_2")+"::OnPipePressure";
	stopnpctimer;
	end;
}

// The Lab No.2 2_2 System - Pipe Operation Control
// When 3 pipes explode, system will shut down.
//============================================================
1@lhz,37,106,4	script	Lab2#2_2	CLEAR_NPC,{
	end;

OnInstanceInit:
	'broken_pipes = 0;
	stopnpctimer;
	end;

OnPipePressure:
	switch(rand(1,6)) {
	case 1:
		donpcevent instance_npcname("Valve#2_1")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_01";
	case 2:
		donpcevent instance_npcname("Valve#2_2")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_02";
	case 3:
		donpcevent instance_npcname("Valve#2_3")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_03";
	case 4:
		donpcevent instance_npcname("Valve#2_4")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_04";
	case 5:
		donpcevent instance_npcname("Valve#2_5")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_05";
	case 6:
		donpcevent instance_npcname("Valve#2_6")+"::OnEnable";
		donpcevent instance_npcname("MobCtrl2#mob02")+"::On2_06";
	}
	initnpctimer;
	end;

OnDisableValve1:
	disablenpc instance_npcname("Valve#2_1");
	stopnpctimer;
	end;

OnDisableValve2:
	disablenpc instance_npcname("Valve#2_2");
	stopnpctimer;
	end;

OnDisableValve3:
	disablenpc instance_npcname("Valve#2_3");
	stopnpctimer;
	end;

OnDisableValve4:
	disablenpc instance_npcname("Valve#2_4");
	stopnpctimer;
	end;

OnDisableValve5:
	disablenpc instance_npcname("Valve#2_5");
	stopnpctimer;
	end;

OnDisableValve6:
	disablenpc instance_npcname("Valve#2_6");
	stopnpctimer;
	end;

OnTimer63000:
	for(.@i = 1; .@i < 7; .@i++) {
		specialeffect EF_SUI_EXPLOSION, AREA, instance_npcname("Valve#2_"+.@i+"");
		disablenpc instance_npcname("Valve#2_"+.@i+"");
	}
	'broken_pipes += 1;
	if ('broken_pipes == 3) {
		donpcevent instance_npcname("#2_3")+"::OnEnable";
		stopnpctimer;
	} else {
		mapannounce instance_mapname("1@lhz"),"[Management System] Pipe explosion detected! When "+ (3 - 'broken_pipes) +" more break, the system will stop.",bc_map,"0xff99ff";
	}
	end;
}

// The Lab No.2 2_3 System
//============================================================
1@lhz,151,48,0	script	#2_3	-1,20,20,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2_3");
	end;

OnEnable:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Management System] System Freezes! System Freezes! Evacuate from the Lab in 10 seconds.",bc_map,"0xff99ff";
	end;

OnTimer11000:
	mapannounce instance_mapname("1@lhz"),"[Management System] Evacuate!!",bc_map,"0xff99ff";
	enablenpc instance_npcname("#2_3");
	end;

OnTouch:
	warp "lhz_dun04", 147, 273;
	end;
}

// The Lab No.2 Monster Control
//============================================================
1@lhz,39,106,4	script	MobCtrl2#mob02	CLEAR_NPC,{
	end;

On01:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,10,.@label$;
	end;

On02:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,20,.@label$;
	end;

On03:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,16,.@label$;
	end;

On04:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,19,.@label$;
	end;

On05:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster .@map$,139,37,164,58,"Starving Lab animal",2242,20,.@label$;
	end;

On2_01:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),155,43,161,49,"Starving Lab animal",2243,5,.@label$;
	end;

On2_02:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),137,40,143,46,"Starving Lab animal",2243,5,.@label$;
	end;

On2_03:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),137,51,143,57,"Starving Lab animal",2243,5,.@label$;
	end;

On2_04:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),143,56,149,62,"Starving Lab animal",2243,5,.@label$;
	end;

On2_05:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),160,48,166,54,"Starving Lab animal",2243,5,.@label$;
	end;

On2_06:
	.@label$ = instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	areamonster instance_mapname("1@lhz"),135,44,141,50,"Starving Lab animal",2243,5,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"),instance_npcname("MobCtrl2#mob02")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#potal_02")+"::OnEnable";
		donpcevent instance_npcname("Lab2#2_1")+"::OnDisable";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Security system of The Lab No.2 has been stopped.",bc_map,"0xff9977";
	}
	end;
}

// The Lab No.2 Valves
//============================================================
-	script	bio4FLab2Valve	-1,{
	if (is_party_leader() == true) {
		progressbar "0xffff00", 20;
		stopnpctimer;
		donpcevent instance_npcname("Lab2#2_2")+"::OnDisableValve1";
		disablenpc instance_npcname(strnpcinfo(0));
		end;
	} else {
		mes "Let your party leader handle the valve.";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	initnpctimer;
	end;

OnTimer3000:
	specialeffect EF_DRAGONSMOKE,AREA,instance_npcname(strnpcinfo(0));
	stopnpctimer;
	initnpctimer;
	end;
}
1@lhz,158,46,0	duplicate(bio4FLab2Valve)	Valve#2_1	CLEAR_NPC
1@lhz,138,43,0	duplicate(bio4FLab2Valve)	Valve#2_2	CLEAR_NPC
1@lhz,139,54,0	duplicate(bio4FLab2Valve)	Valve#2_3	CLEAR_NPC
1@lhz,145,60,0	duplicate(bio4FLab2Valve)	Valve#2_4	CLEAR_NPC
1@lhz,165,51,0	duplicate(bio4FLab2Valve)	Valve#2_5	CLEAR_NPC
1@lhz,137,47,0	duplicate(bio4FLab2Valve)	Valve#2_6	CLEAR_NPC

// The Lab No.2 >> The Lab No.3 Warp portal(General)
//============================================================
1@lhz,151,64,0	script	#potal_02	WARPNPC,2,2,{
	end;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#potal_02");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[Management System] The door which connects to the Lab No.3 is now open.",bc_map,"0xff99ff";
	enablenpc instance_npcname("#potal_02");
	end;

OnTouch_:
	warp instance_mapname("1@lhz"), 84, 28;
	end;
}

// The Lab No.3
//============================================================
1@lhz,84,28,0	script	#3_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#3_0");
	end;

OnDisable:
	disablenpc instance_npcname("#3_0");
	end;

OnTouch_:
	donpcevent instance_npcname("Lab3#3_1")+"::OnEnable";
	donpcevent instance_npcname("#3_0")+"::OnDisable";
	end;
}

// The Lab No.3 3_1 System - Monster wave control
//============================================================
1@lhz,35,104,4	script	Lab3#3_1	CLEAR_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"[Security System] This is a warning notice! Warning notice!",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::OnDisable";
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Intruder Detected in The Lab No.3!!",bc_map,"0xff9977";
	end;

OnTimer7000:
	mapannounce instance_mapname("1@lhz"),"[Security System] In 5 seconds, The Security system of The Lab No.3 will start working.",bc_map,"0xff9977";
	end;

OnTimer12000:
	mapannounce instance_mapname("1@lhz"),"[Security System] First security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On01";
	end;

OnTimer192000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Second security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On02";
	end;

OnTimer372000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Third security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On03";
	end;

OnTimer552000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fourth security system of The Lab No.3activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On04";
	end;

OnTimer732000:
	mapannounce instance_mapname("1@lhz"),"[Security System] Fifth security system of The Lab No.3 activated !! The persons concerned have to evacuate.",bc_map,"0xff9977";
	donpcevent instance_npcname("MobCtrl3#mob03")+"::On05";
	stopnpctimer;
	end;
}

// The Lab No.3 Monster Control
//============================================================
1@lhz,37,104,4	script	MobCtrl3#mob03	CLEAR_NPC,{
	end;

On01:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2243,1,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2243,1,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2243,1,.@label$;		
	monster .@map$,86,60,"Starving Lab animal",2243,1,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2243,1,.@label$;
	end;

On02:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2243,.@amount2,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2242,.@amount2,.@label$;
	end;

On03:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2243,.@amount2,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2242,.@amount1,.@label$;
	end;

On04:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@amount4 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2242,.@amount4,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2242,.@amount4,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2242,.@amount2,.@label$;
	end;

On05:
	.@map$ = instance_mapname("1@lhz");
	.@amount1 = rand(1,5);
	.@amount2 = rand(1,5);
	.@amount3 = rand(1,5);
	.@amount4 = rand(1,5);
	.@amount5 = rand(1,5);
	.@label$ = instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	monster .@map$,80,60,"Starving Lab animal",2243,.@amount1,.@label$;
	monster .@map$,82,60,"Starving Lab animal",2242,.@amount2,.@label$;
	monster .@map$,84,60,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,86,60,"Starving Lab animal",2243,.@amount4,.@label$;
	monster .@map$,81,58,"Starving Lab animal",2242,.@amount5,.@label$;
	monster .@map$,83,58,"Starving Lab animal",2242,.@amount1,.@label$;
	monster .@map$,85,58,"Starving Lab animal",2243,.@amount2,.@label$;
	monster .@map$,87,58,"Starving Lab animal",2242,.@amount3,.@label$;
	monster .@map$,82,56,"Starving Lab animal",2242,.@amount4,.@label$;
	monster .@map$,85,56,"Starving Lab animal",2243,.@amount5,.@label$;
	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead";
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"),instance_npcname("MobCtrl3#mob03")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#potal_03")+"::OnEnable";
		donpcevent instance_npcname("Lab3#3_1")+"::OnDisable";
		mapannounce instance_mapname("1@lhz"),"[Security System] The Security system of The Lab No.3 has been stopped.",bc_map,"0xff9977";
	}
	end;
}

// The Lab No.3 >> Boss's Room Warp portal
//============================================================
1@lhz,83,62,0	script	#potal_03	WARPNPC,2,2,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#potal_03");
	end;

OnEnable:
	mapannounce instance_mapname("1@lhz"),"[Management System] The door which connects to the Lab No.4 is now open.",bc_map,"0x7799ff";
	enablenpc instance_npcname("#potal_03");
	end;

OnDisable:
	disablenpc instance_npcname("#potal_03");
	end;

OnTouch_:
	warp instance_mapname("1@lhz"), 137, 100;
	end;
}

// The Lab No.4
//============================================================
1@lhz,137,100,0	script	#4_0	-1,1,1,{
	end;

OnEnable:
	enablenpc instance_npcname("#4_0");
	end;

OnDisable:
	disablenpc instance_npcname("#4_0");
	end;

OnTouch_:
	donpcevent instance_npcname("Lab4#4_1")+"::OnEnable";
	donpcevent instance_npcname("#4_0")+"::OnDisable";
	end;
}

// The Lab No.4 4_1 System - Summon of Boss Monster
//============================================================
1@lhz,35,102,4	script	Lab4#4_1	CLEAR_NPC,{
	end;

OnEnable:
	initnpctimer;
	end;

OnDisable:
	stopnpctimer;
	end;

OnTimer1000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Who are you....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Seyren Windsor#boss01");
	end;

OnTimer2000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Cecile Damon#boss02");
	end;

OnTimer3000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Do not approach more...",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Gertie Wie#boss03");
	end;

OnTimer4000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Eremes Guile#boss04");
	end;

OnTimer5000:
	mapannounce instance_mapname("1@lhz"),"Whisper: This is a very dangerous place..",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Trentini#boss05");
	end;

OnTimer6000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Chen Liu#boss06");
	end;

OnTimer7000:
	mapannounce instance_mapname("1@lhz"),"Whisper: You won't go out alive....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Alphochio Basil#boss07");
	end;

OnTimer8000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Flamel Emul#boss08");
	end;

OnTimer9000:
	mapannounce instance_mapname("1@lhz"),"Whisper: If you don't want to be a part of us....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Randel Lawrence#boss09");
	end;

OnTimer10000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Celia Alde#boss10");
	end;

OnTimer11000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Leave now....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Kathryne Keyron#boss11");
	end;

OnTimer12000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Margaretha Sorin#boss12");
	end;

OnTimer13000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Before this place is thoroughly filled with sorrow and pain....",bc_map,"0x7DCBF0";
	specialeffect EF_SPHERE,AREA,instance_npcname("Howard Alt-Eisen#boss13");
	end;

OnTimer14000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Gren#boss14");
	end;

OnTimer15000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Roke#boss15");
	end;

OnTimer16000:
	specialeffect EF_SPHERE,AREA,instance_npcname("Type_**Dree#boss16");
	end;

OnTimer17000:
	mapannounce instance_mapname("1@lhz"),"Whisper: Ah....it....it's too late......",bc_map,"0x7DCBF0";
	donpcevent instance_npcname("MobCtrl4#mob04")+"::OnEnable";
	stopnpctimer;
	end;
}

// Area No. 4 - Boss Summon Control
//============================================================
1@lhz,37,102,4	script	MobCtrl4#mob04	CLEAR_NPC,{
	end;

OnEnable:
	switch(rand(1,13)) {
	case 1:
		donpcevent instance_npcname("Seyren Windsor#boss01")+"::OnEnable";
		end;
	case 2:
		donpcevent instance_npcname("Cecile Damon#boss02")+"::OnEnable";
		end;
	case 3:
		donpcevent instance_npcname("Gertie Wie#boss03")+"::OnEnable";
		end;
	case 4:
		donpcevent instance_npcname("Eremes Guile#boss04")+"::OnEnable";
		end;
	case 5:
		donpcevent instance_npcname("Trentini#boss05")+"::OnEnable";
		end;
	case 6:
		donpcevent instance_npcname("Chen Liu#boss06")+"::OnEnable";
		end;
	case 7:
		donpcevent instance_npcname("Alphochio Basil#boss07")+"::OnEnable";
		end;
	case 8:
		donpcevent instance_npcname("Flamel Emul#boss08")+"::OnEnable";
		end;
	case 9:
		donpcevent instance_npcname("Randel Lawrence#boss09")+"::OnEnable";
		end;
	case 10:
		donpcevent instance_npcname("Celia Alde#boss10")+"::OnEnable";
		end;
	case 11:
		donpcevent instance_npcname("Kathryne Keyron#boss11")+"::OnEnable";
		end;
	case 12:
		donpcevent instance_npcname("Margaretha Sorin#boss12")+"::OnEnable";
		end;
	case 13:
		donpcevent instance_npcname("Howard Alt-Eisen#boss13")+"::OnEnable";
		end; 
	}

OnDisable:
	//stopnpctimer;
	end;
}

// Random Experiments in Tubes
//============================================================
1@lhz,156,148,3	script	Type_**Gren#boss14	4_M_03,{ end; }
1@lhz,152,152,3	script	Type_**Roke#boss15	4_M_HUMAN_01,{ end; }
1@lhz,148,156,3	script	Type_**Dree#boss16	4_M_DWARF,{ end; }

// Bosses
//============================================================
-	script	bio4FBoss	-1,{
	end;

OnEnable:
	.@map$ = instance_mapname("1@lhz");
	.@label$ = instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	specialeffect EF_SUI_EXPLOSION;

	if (strnpcinfo(0) == "Seyren Windsor#boss01")
		monster .@map$,129,154,"Load Knight Seyren",1646,1,.@label$;
	else if (strnpcinfo(0) == "Cecile Damon#boss02")
		monster .@map$,125,150,"Sniper Cecile",1650,1,.@label$;
	else if (strnpcinfo(0) == "Gertie Wie#boss03")
		monster .@map$,121,146,"Stalker Gertie",2239,1,.@label$;
	else if (strnpcinfo(0) == "Eremes Guile#boss04")
		monster .@map$,117,142,"Assassin Cross Eremes",1647,1,.@label$;
	else if (strnpcinfo(0) == "Trentini#boss05")
		monster .@map$,117,125,"Gypsy Trentini",2241,1,.@label$;
	else if (strnpcinfo(0) == "Chen Liu#boss06")
		monster .@map$,121,121,"Champion Chen",2238,1,.@label$;
	else if (strnpcinfo(0) == "Alphochio Basil#boss07")
		monster .@map$,125,117,"Crown Alphochio",2240,1,.@label$;
	else if (strnpcinfo(0) == "Flamel Emul#boss08")
		monster .@map$,129,113,"Creator Flamel",2236,1,.@label$;
	else if (strnpcinfo(0) == "Randel Lawrence#boss09")
		monster .@map$,146,113,"Paladin Randel",2235,1,.@label$;
	else if (strnpcinfo(0) == "Celia Alde#boss10")
		monster .@map$,150,117,"Professor Celia",2237,1,.@label$;
	else if (strnpcinfo(0) == "Kathryne Keyron#boss11")
		monster .@map$,154,121,"High Wizard Kathryne",1651,1,.@label$;
	else if (strnpcinfo(0) == "Margaretha Sorin#boss12")
		monster .@map$,158,125,"High Priest Margaretha",1649,1,.@label$;
	else if (strnpcinfo(0) == "Howard Alt-Eisen#boss13")
		monster .@map$,158,142,"White Smith Howard",1648,1,.@label$;

	end;

OnDisable:
	killmonster instance_mapname("1@lhz"),instance_npcname(strnpcinfo(0))+"::OnMyMobDead";
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@lhz"), instance_npcname(strnpcinfo(0))+"::OnMyMobDead") < 1) {
		.@map$ = instance_mapname("1@lhz");

		if (strnpcinfo(0) == "Seyren Windsor#boss01")
			mapannounce .@map$,"Awww... Where are all my fellows....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Cecile Damon#boss02")
			mapannounce .@map$,"Any..one, please help.. please.....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Gertie Wie#boss03")
			mapannounce .@map$,".. did I act too.. rashly...?",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Eremes Guile#boss04")
			mapannounce .@map$,"Ah..... I am absolutely defeated....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Trentini#boss05")
			mapannounce .@map$,"ah.. If I.. ever can ... dance and.... sing...happily....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Chen Liu#boss06")
			mapannounce .@map$,"I.... am so .. sorry....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Alphochio Basil#boss07")
			mapannounce .@map$,"...the new song......was ....for.....her....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Flamel Emul#boss08")
			mapannounce .@map$,"Ha...I wanted to show.... the new potion....",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Randel Lawrence#boss09")
			mapannounce .@map$,"Wooooo.. everyone got defeated.....??",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Celia Alde#boss10")
			mapannounce .@map$,"No..... this experiment is.....dang...",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Kathryne Keyron#boss11")
			mapannounce .@map$,"awww.. is everyone Ok....?",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Margaretha Sorin#boss12")
			mapannounce .@map$,"My good boy.. Don't cry... ah.... our Great Odin.. please save us...",bc_map,"0xFD3B02";
		else if (strnpcinfo(0) == "Howard Alt-Eisen#boss13")
			mapannounce .@map$,"oh...no.... can't breath.... and my sister...?? sister!!",bc_map,"0xFD3B02";

		donpcevent instance_npcname("Wolfchev#last")+"::OnEnable";
		donpcevent instance_npcname(strnpcinfo(0))+"::OnDisable";
	}
	end;
}
1@lhz,127,156,5	duplicate(bio4FBoss)	Seyren Windsor#boss01	1646
1@lhz,123,152,5	duplicate(bio4FBoss)	Cecile Damon#boss02	1650
1@lhz,119,148,5	duplicate(bio4FBoss)	Gertie Wie#boss03	2239
1@lhz,115,144,5	duplicate(bio4FBoss)	Eremes Guile#boss04	1647
1@lhz,115,123,7	duplicate(bio4FBoss)	Trentini#boss05	2241
1@lhz,119,119,7	duplicate(bio4FBoss)	Chen Liu#boss06	2238
1@lhz,123,115,7	duplicate(bio4FBoss)	Alphochio Basil#boss07	2240
1@lhz,127,111,7	duplicate(bio4FBoss)	Flamel Emul#boss08	2236
1@lhz,148,111,1	duplicate(bio4FBoss)	Randel Lawrence#boss09	2235
1@lhz,152,115,1	duplicate(bio4FBoss)	Celia Alde#boss10	2237
1@lhz,156,119,1	duplicate(bio4FBoss)	Kathryne Keyron#boss11	1651
1@lhz,160,123,1	duplicate(bio4FBoss)	Margaretha Sorin#boss12	1649
1@lhz,160,144,3	duplicate(bio4FBoss)	Howard Alt-Eisen#boss13	1648

// Wolfchev - Final encounter
//============================================================
1@lhz,137,156,7	script	Wolfchev#last	4_LGTSCIENCE,{
	if ((MaxWeight - Weight) < 1000 || !checkweight(1201, 1)) {
		mes "^FF0000 ตรวจสอบสินค้าคงคลังของคุณคุณมีไอเท็มมากเกินไปที่จะทำเควส ^000000";
		close;
	}
	for(.@i = 5113; .@i <= 5125; .@i++) {
		switch( checkquest(.@i,HUNTING) ) {
		case -1:
			break;
		case 0:
		case 1:
			.@all_quests_complete += 1;
			break;
		case 2:
			.@all_quests_complete += 2;
			break;
		}
	}
	if (.@all_quests_complete == 26) {
		// Quests Complete. Get Rewards.
		if (lght_duk01 == 8) {
			// First time beating the instance.
			mes "[Wolfchev]";
			mes "ในที่สุดเจ้าก็เอาชนะบอสของข้าได้";
			next;
			mes "[Wolfchev]";
			mes "สุดท้ายนี่คือผลลัพธ์ ?? ฉันทดลองเพื่อใครพวกเขาโกงฉันหรือเปล่า";
			next;
			mes "[Wolfchev]";
			mes "ปุ่มนี้ควรจะกดสำหรับกรณีฉุกเฉิน ... แต่ .. ";
			next;
			mes "[Wolfchev]";
			mes "เฮ้ ... คุณกับฉันเจอสายสัมพันธ์ที่ไม่ดี .... ";
			next;
			mes "[Wolfchev]";
			mes "แต่ .... คุณช่วยฉันด้วย ... คุณทำอย่างนั้นได้อย่างไร";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "เมื่อฉันพบคุณเป็นครั้งแรกคุณดูเหมือนจะบ้า แต่ก็เศร้าแม้ว่าคุณจะทำการทดลองที่โหดร้ายและน่ากลัวเหล่านั้นได้ก็ตาม";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "แต่ที่จริงคุณทำแบบนั้นเพราะคุณเหงา, .. คุณแค่ตั้งอกตั้งใจกับเหตุผลนี้เหรอ?";
			next;
			mes "[Wolfchev]";
			mes "HAHAHAHA. เหงา ... พูดมั้ย?";
			next;
			mes "[Wolfchev]";
			mes "อาจจะเห็นแบบนั้นกับคุณ แต่มันไม่ใช่ความเหงาเป็นเพียงข้ออ้างที่ดีและง่ายสำหรับคนขี้แพ้";
			next;
			mes "[Wolfchev]";
			mes "ฉันมีความสุขมากที่ได้ทำการทดลองเพื่อความสมบูรณ์แบบฉันไม่เคยรู้สึกเหงาเลย";
			next;
			mes "[Wolfchev]";
			mes "อย่างไรก็ตามตั้งแต่ฉันรู้ว่า Rekenber ทรยศฉันฉันไม่สามารถปล่อยให้พวกเขาทำในสิ่งที่ต้องการได้";
			next;
			mes "[Wolfchev]";
			mes "สำหรับตอนนี้ฉันจะปล่อยให้พวกเขาอยู่คนเดียว แต่ฉันจะทำให้พวกเขาร้องไห้และเสียใจที่พยายามกำจัด Wolfchev";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "... .... ... ....";
			next;
			lght_duk01 = 9;
			callsub L_Reward;
			mes "[Wolfchev]";
			mes "aaaaaah," + strcharinfo (0) + "ขอบคุณมากที่ลบเด็กผู้ชายของฉันที่บ้าไปแล้วไม่ใช่ของขวัญชิ้นใหญ่ แต่ฉันขอให้คุณไม่ปฏิเสธฉันไม่ต้องการสิ่งนี้อีกต่อไป";
			next;
			mes "[Wolfchev]";
			mes "และถ้าคุณทำได้ ... หวังว่าคุณจะกลับมาบ่อยๆโดยเฉพาะกับเพื่อน ๆ ของคุณฉันอยากให้ที่นี่อยู่กับหัวใจของมนุษย์อีกครั้งเพื่อให้ความเศร้าที่นี่หายไป";
			next;
			mes "ฉันมองไปที่ Wolfchev อีกครั้งก่อนออกจากห้องทดลองของเขาและฉันรู้สึกได้ว่าเขาไม่ได้หมกมุ่นอีกต่อไป .. ";
			close2;
			warp "lhz_dun04", 147, 273;
			end;
		} else {
			mes "[Wolfchev]";
			mes "คุณปราบสิ่งมีชีวิตบอสได้หมดแล้วขอบคุณอีกครั้ง";
			next;
			callsub L_Reward;
			mes "[Wolfchev]";
			mes "ฉันพูดอีกครั้ง," + strcharinfo (0) + "ขอบคุณมากที่ลบเด็กผู้ชายของฉันที่บ้าไปแล้วไม่ใช่ของขวัญชิ้นใหญ่ แต่ฉันขอให้คุณไม่ปฏิเสธฉันไม่ต้องการสิ่งนี้อีกต่อไป";
			next;
			mes "[Wolfchev]";
			mes "Khahaha เจอกันใหม่คุณอยากออกไปจากที่นี่ตอนนี้ไหม";
			next;
			if (select("Not yet", "Yes") == 1) {
				mes "[Wolfchev]";
				mes "แล้วบอกฉันเมื่อคุณต้องการ";
				close;
			}
			mes "[Wolfchev]";
			mes "แล้วฉันจะส่งคุณออกไป";
			close2;
			warp "lhz_dun04", 147, 273;
			end;
		}
	} else {
		if (.@all_quests_complete == 0) {
			mes "[Wolfchev]";
			mes "ฉันกำลังดูระบบ .... ไม่รู้ว่าใครเป็นคนทำ ..... แต่มันซับซ้อนมากที่จะได้.. ไม่มีความคิด ... ";
			next;
		} else {
			mes "[Wolfchev]";
			mes "kakakaah .. ขอบคุณครับคุณจัดการสัตว์ Boss พวกนั้นได้ดี ... ";
			next;
		}
		if (lght_duk01 == 7) {
			lght_duk01 = 8;
			mes "[Wolfchev]";
			mes "ในขณะที่คุณกำลังดิ้นรน ... ";
			next;
			mes "[Wolfchev]";
			mes "ฉันคิดบางอย่างออก ... และมีข้อผิดพลาดของระบบที่ไม่ยอมให้เราไปไหนมาไหนได้อย่างอิสระดังนั้นคุณสามารถช่วยฉันได้ภายใน 72 ชั่วโมง ... ไปพักผ่อนก่อนที่คุณจะกลับมา";
			next;
			mes "[Wolfchev]";
			mes "อ๊ะ ... แล้วฉันจะส่งคุณออกจากห้องทดลองของฉันได้ไหม";
			next;
		} else {
			mes "[Wolfchev]";
			mes "คุณต้องการออกไปจากที่นี่เดี๋ยวนี้หรือไม่";
			next;
		}
		if (select("Not yet", "Yes") == 1) {
			mes "[Wolfchev]";
			mes "แล้วบอกฉันเมื่อคุณต้องการ";
			close;
		}
		mes "[Wolfchev]";
		mes "แล้วฉันจะส่งคุณออกไป";
		close2;
		warp "lhz_dun04", 147, 273;
		end;
	}

L_Reward:
	for(.@i = 5113; .@i <= 5125; .@i++) {
		erasequest .@i;
	}
	.@r = rand(1, 1000);
	if      (.@r < 84)  getitem 2582,  1; //Salvage_Cape
	else if (.@r < 167) getitem 18570, 1; //Ancient_Gold_Deco
	else if (.@r < 250) getitem 1490,  1; //Giant_Lance
	else if (.@r < 333) getitem 16017, 1; //Bloody_Cross
	else if (.@r < 416) getitem 1291,  1; //Guillotine_Katar
	else if (.@r < 499) getitem 1584,  1; //Chilly_Spell_Book
	else if (.@r < 582) getitem 6471, 10; //Goast_Chill
	else if (.@r < 665) getitem 6470, 10; //Blood_Thirst
	else if (.@r < 748) getitem 6469, 10; //Will_Of_Warrior
	else if (.@r < 831) getitem 6471, 20; //Goast_Chill
	else if (.@r < 914) getitem 6470, 20; //Blood_Thirst
	else                getitem 6469, 20; //Will_Of_Warrior
	return;

OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("Wolfchev#last");
	end;

OnEnable:
	enablenpc instance_npcname("Wolfchev#last");
	end;
}
