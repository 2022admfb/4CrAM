//===== rAthena Script ======================================= 
//= Bakonawa Lake
//===== By:================================================== 
//= Euphy
//===== Current Version:===================================== 
//= 1.2
//===== Compatible With:===================================== 
//= rAthena Project
//===== Description:========================================= 
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments:================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//= 1.2 Added VIP features. [Euphy]
//============================================================ 

// Instance Creation::ma_baku
//============================================================
ma_scene01,174,179,4	script	Taho	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "คุณบรรทุกของมากเกินไปกลับมาหลังจากถอดบางชิ้นออก";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "คุณแบกน้ำหนักมากเกินไปกลับมาหลังจากลดน้ำหนัก";
		close;
	}
	if (BaseLevel < 140) {
		mes "[Taho]";
		mes "เฮ้นี่เป็นสถานที่ที่อันตรายเกินไปสำหรับคุณคุณไม่สามารถรักษาตัวให้ปลอดภัยได้ออกจากที่นี่แล้วไปที่หมู่บ้านเดี๋ยวนี้!";
		close;
	}
	//disable
	/*if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
		mes "[Taho]";
		mes "พวกเราจะต้องถูกบาโคนาวะคนนั้นฆ่าหมดอ่าเราต้องฆ่าเขา แต่น่าเสียดายที่เราไม่ได้เตรียมตัว";
		close;
	}*/

	set .@party_id,getcharid(1);
	set .@md_name$, "Bakonawa Lake";

	set .@baku_time, checkquest(12278,PLAYTIME);
	if (.@baku_time == -1) {
		if (checkquest(12279) == -1) setquest 12279;
	}
	if (!.@party_id) {
		mes "[Taho]";
		mes "ไม่มี บริษัท ไหนที่จะไปกับคุณได้หรือถึงคุณอยากไปคนเดียวก็มาหลังจากจัดงานเลี้ยง";
		close;
	}
	if (.@baku_time == -1) {
		if (is_party_leader() == true) {
			mes "[Taho]";
			mes "คุณเป็นหัวหน้าของผู้สังหารบาโกนาวะหรือเปล่าถนนไม่ค่อยดีเราเลยต้องเอาเชือกลงจากหน้าผาใช่ไหม";
			next;
			set .@i, select("ได้โปรดสานเชือก:ตอนนี้ฉันจะลงไปข้างล่าง:ยกเลิก");
		} else {
			mes "[Taho]";
			mes "คุณมาเพื่อฆ่าบาโกนาวะหัวหน้าพรรคของคุณต้องช่วยฉันทอเชือกเพื่อลงไป";
			next;
			set .@i, select(":ตอนนี้ฉันจะลงไป:ยกเลิก.");
		}
		switch(.@i) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[Taho]";
				mes "โอ้เชือกหลุดต้องทำใหม่";
				close;
			}
			mes "[Taho]";
			mes "ตอนนี้ฉันกำลังทอผ้าอยู่ดังนั้นคุณสามารถลงไปเมื่อฉันทำเสร็จแล้ว";
			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "[Taho]";
				mes "เกิดข้อผิดพลาดที่ไม่รู้จัก";
				close;
			case IE_NOINSTANCE:
				mes "[Taho]";
				mes "เชือกยังไม่ได้ทอรอหน่อย";
				close;
			case IE_NOMEMBER:
				mes "[Taho]";
				mes "พลเรือนเข้าไม่ได้";
				close;
			case IE_OK:
				mapannounce "ma_scene01","สมาชิกปาร์ตี้, "+strcharinfo(0)+" ของปาร์ตี้ "+getpartyname(.@party_id)+" กำลังเข้าสู่ดันเจี้ยน, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
				setquest 12278;
				//warp "1@ma_b",64,51;
				close;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 0 || .@baku_time == 1) {
		mes "[Taho]";
		mes "เชือกที่ใช้ในการเดินทางไปทะเลสาบขาดแล้วตอนนี้เรากำลังทำความสะอาดบริเวณโดยรอบแล้วค่อยกลับมาใหม่";
		close;
	} else if (.@baku_time == 2) {
		mes "[Taho]";
		mes "ทุกอย่างสะอาดหมดแล้วตอนนี้คุณสามารถลงไปได้อีกครั้งหลังจากได้รับเชือก";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	Bakonawa's Rage	541,{
	callfunc "F_GM_NPC";
	mes "[Taho]";
	mes "ฉันชื่อมิงกี้";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		set malaya_bakona1,7;
		set malaya_bakona2,15;
	}
	close;
}

// Instance Scripts::in_baku01
//============================================================
1@ma_b,62,52,4	script	Taho#nf	541,{
	mes "[Taho]";
	mes "ฮิฮิฉันสามารถเข้าร่วมการล่าบาโกนาวะได้ แต่ฉันอยากจะซ่อนตัวอยู่หลังก้อนหินนั้นและเป็นกำลังใจให้พวกคุณ";
	next;
	mes "[Taho]";
	mes "Bakonawa กลายเป็นสิ่งที่อยู่ยงคงกระพันในบางครั้งเมื่อถึงเวลานั้นโปรดฟังคำแนะนำของฉันอย่างระมัดระวังเชื่อใจฉันแล้วทุกอย่างจะดี";
	next;
	if (is_party_leader() == true) {
		mes "[Taho]";
		mes "ตอนนี้ฉันจะทำให้ Bakonawa ผุดขึ้นมาจากทะเลสาบ";
		next;
		switch(select("เดี๋ยวก่อนฉันยังไม่พร้อม!:มาทำกัน!")) {
		case 1:
			mes "[Taho]";
			mes "โอ ... ได้เลย ... บอกฉันว่าพร้อมเมื่อไร";
			close;
		case 2:
			mes "[Taho]";
			mes "แล้วฉันจะทิ้ง Albopal สักหยดลงที่ทะเลสาบนี้";
			next;
			mes "[Taho]";
			mes "เมื่อ Bakonawa ได้ลิ้มรสสิ่งนี้เขาก็จะปรากฏตัวขึ้น ...";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("Taho#nf");
			close;
		}
	} else {
		mes "[Taho]";
		mes "ฉันทิ้งอัลโปปาลเพื่อทำให้บาโกนาวะบ้าคลั่ง Bakonawa สามารถเป็นแหล่งโปรตีนที่ดีเมื่อเราได้รับเขา .";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Taho:ฉันสามารถเห็นเขาที่นั่นบนพื้นผิว! โจมตี !!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	monster .@map$,78,83,"Bakonawa",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"10 minutes";
OnTimer60000:	callsub OnAnnounce,"9 minutes";
OnTimer120000:	callsub OnAnnounce,"8 minutes";
OnTimer180000:	callsub OnAnnounce,"7 minutes";
OnTimer240000:	callsub OnAnnounce,"6 minutes";
OnTimer300000:	callsub OnAnnounce,"5 minutes";
OnTimer360000:	callsub OnAnnounce,"4 minutes";
OnTimer420000:	callsub OnAnnounce,"3 minutes";
OnTimer480000:	callsub OnAnnounce,"2 minutes";
OnTimer540000:	callsub OnAnnounce,"1 minute";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"จำกัด เวลา: 30 วินาที ",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa ได้หนีลึกเข้าไปในทะเลสาบ .",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"เวลาที่ จำกัด:"+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	set .@map$, instance_mapname("1@ma_b");
	for(set .@i,0; .@i<9; set .@i,.@i+1) {
		set .@rand, rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"Bakonawa's Will",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"Bakonawa's Will",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"Taho:Good! Bakonawa ซ่อนอยู่ใต้น้ำ.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"Taho:เราต้องส่งเสียงดังด้วยหม้อลมและฆ้องเพื่อที่เขาจะไม่จดจ่อกับดวงจันทร์ !",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Taho:แม้ว่าเขาจะขึ้นมาเหนือผิวน้ำก็ตาม!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"Mission - ทำลาย 2 caldrons และ 2 gongs ทางด้านซ้ายและขวาของทะเลสาบ.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	monster .@map$,95,98,"Caldron",2328,1,.@label$;
	monster .@map$,60,98,"Caldron",2328,1,.@label$;
	monster .@map$,97,104,"Gong",2328,1,.@label$;
	monster .@map$,58,104,"Gong",2328,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"Taho:Good! "+.@mob_dead_num+" left to go!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 end;
OnTimer1000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer60000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer120000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer180000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer240000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer270000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer280000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer290000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer295000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer296000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer297000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer298000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer299000:	callsub OnAnnounce,"1 second",20,0;
OnTimer300000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa has escaped deep into the lake.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Time Limit:"+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,93,"Enraged Bakonawa",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"Taho:เขากลับลงไปในทะเลสาบอีกครั้ง มันจบหรือยัง?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"Taho:UHM! มีบางอย่างเคลื่อนไหวอย่างรวดเร็วใต้น้ำ! บะ ... Bakonawa กำลังจะมา !!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Mission - กำจัด Bakonawa ที่ดุร้าย.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,83,"Enraged Bakonawa",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"Taho:ฉันเห็นเขาโยนของที่ดูเหมือนกล่องออกมา.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("Taho#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10 minutes",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer360000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer420000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer480000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer540000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer570000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer580000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer590000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer595000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer596000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer597000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer598000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer599000:	callsub OnAnnounce,"1 second",20,0;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"Bakonawa หนีลึกลงไปในทะเลสาบ .",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("Taho#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Time Limit:"+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	set .@mob_dead_num, mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		set .@mob_rg, 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			set .@mob_rg, getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"Bakonawa's Puppet",2334,1,.@label$;
			set .@mob_dead_num, mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	Taho#Completed	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "ดูเหมือนว่าคุณจะถือสิ่งของประเภทต่างๆมากเกินไปโปรดลองอีกครั้งหลังจากลดแล้ว";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "ดูเหมือนว่าคุณจะแบกของที่มีน้ำหนักมากเกินไปแล้วโปรดลองอีกครั้งหลังจากลดน้ำหนักลง";
		close;
              }
	if (checkquest(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[Taho]";
		mes "สิ่งนี้พบจากศพของบาโกนาวะมันจะดีกว่าถ้าคุณรับสิ่งนี้";
		if (!VIP_SCRIPT || vip_status(VIP_STATUS_ACTIVE))
			getitem 6499,7; //Ancient_Grudge
		else
			getitem 6499,5; //Ancient_Grudge
		getexp 0,100000;
		next;
	}
	mes "[Taho]";
	mes "เรารีบออกไปจากที่นี่กันดีกว่าสถานที่แห่งนี้ยังคงอันตรายแม้จะไม่มีบาโกนาวะ ... ";
	next;
	mes "[Taho]";
	mes "งั้นใช้เชือกขึ้นไปกันเถอะ!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("Taho#Completed");
	end;
OnStart:
	enablenpc instance_npcname("Taho#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"Bakonawa Treasure Box",2335,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"Taho:หลังจากเปิดกล่องสมบัติแล้วโปรดไปเยี่ยมฉันที่ด้านบนสุดของเนินเขา ฉันมีบางอย่างจะมอบให้กับคุณ.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	Taho#Fail	541,{
	mes "[Taho]";
	mes "อืมเราเกือบจะได้เขามาแล้ว แต่เขาเป็นสัตว์ที่รวดเร็วตัวหนึ่ง ... ";
	next;
	if (is_party_leader() == true) {
		mes "[Taho]";
		mes "เราจะลองอีกครั้งไหม";
		next;
		switch(select("รอสักครู่!:แน่นอน! เราไม่สามารถถอยได้แล้ว!")) {
		case 1:
			mes "[Taho]";
			mes "AWW ... ฉันเกือบจะพาเขาออกไปแล้ว";
			close;
		case 2:
			mes "[Taho]";
			mes "แล้วฉันจะใช้ Albopal ไปที่ทะเลสาบอีกครั้ง";
			next;
			mes "[Taho]";
			mes "เรากำลังจะเห็นบาโกนาวะด้วยความโกรธที่รุนแรงขึ้น ... ";
			disablenpc instance_npcname("Taho#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[Taho]";
		mes "ฉันทิ้ง Albopal เหมือนที่เคยทำเมื่อสักครู่ฉันหวังว่ามันจะทำให้เขาโกรธ ... ";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Taho#Fail");
	end;
OnStart:
	enablenpc instance_npcname("Taho#Fail");
	end;
}
