//===== rAthena Script =======================================
//= Central Laboratory
//===== Description: =========================================
//= [Official Conversion]
//= Central Laboratory Instance
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//= 1.1 Updated to match official script. [Capuche]
//============================================================

verus01,153,155,5	script	Temporary Dimension-mov#pa0829_01	PORTAL,{
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[Temporary Dimension-mover]";
		mes "คุณไม่มีเพื่อนร่วมทางหรือจัดปาร์ตี้ได้แม้จะเดินทางคนเดียวก็ตาม";
		close;
	}
	switch( checkquest(12347,PLAYTIME) ) {
	case -1:
		switch( instance_enter("Central Laboratory") ) {
		case IE_NOMEMBER:
		case IE_NOINSTANCE:
			mes "[Temporary Dimension-mover]";
			mes "ข้อความสำหรับการเคลื่อนย้ายมิติยังไม่ถูกทำให้มีชีวิต";
			close;
		case IE_OTHER:
			mes "[Temporary Dimension-mov]";
			mes "ข้อผิดพลาดที่ไม่รู้จัก";
			close;
		case IE_OK:
			mapannounce "verus01", "" + strcharinfo(0) + " ของปาร์ตี้, " + getpartyname(.@party_id) + ", กำลังเข้าสู่ Central Laboratory.",bc_map,"0x00FF99";
			setquest 12347;// Trace of Laboratory Access
			end;
		}
		end;
	case 0:
	case 1:
		mes "[Temporary Dimension-mover]";
		mes "มีร่องรอยของเหตุการณ์สุดท้ายเหลืออยู่การเข้าถึงถูกปฏิเสธ";
		close;
	case 2:
		mes "^0000ffUse of Dimension-mover น่าจะเป็นไปได้ ^000000";
		erasequest 12347;// Trace of Laboratory Access
		close;
	}
}

verus01,149,155,5	script	Civilization Explorer#pa0829_01	4_LGTSCIENCE,{
	if (BaseLevel < 135) {
		mes "[Civilization Explorer]";
		mes "อืม ... ฉันไม่รู้ว่าคุณสามารถช่วยได้ไหมดูเหมือนว่ายังมีช่องว่างอยู่!";
		next;
		mes "[Civilization Explorer]";
		mes "เมื่อคุณแข็งแกร่งขึ้นกลับมาหาฉันคุณควรจะเลเวล 135 เกือบ!";
		close;
	}
	mes "^ff0000 การฝึกมอนสเตอร์จะขัดขวางความคืบหน้าในดันเจี้ยนนี้โปรดสังเกตสิ่งนี้ด้วย ^000000";
	next;
	mes "[Civilization Explorer]";
	switch( isbegin_quest(12346) ) {
	case 0:
		mes "มีหลายสิ่งที่น่าสนใจเกี่ยวกับอารยธรรมโบราณในพื้นที่นี้";
		next;
		mes "[Civilization Explorer]";
		mes "การสังเกตชั้นดินคาดว่ามีอายุ 500 ถึง 600 ปี!";
		mes "แต่วัตถุที่ขุดพบหลายชิ้นที่นี่เหนือกว่าเทคโนโลยีปัจจุบันของเราไปมากแล้ว";
		next;
		mes "[Civilization Explorer]";
		mes "พบคำศัพท์ที่ไม่ได้ระบุจำนวนมากในบันทึกที่กู้คืนด้วยเช่นกัน";
		next;
		select("What kind of unknown terms?");
		mes "[Civilization Explorer]";
		mes "^0000ffMother Computer^000000 or, ^0000ff LSI Chip^000000 Or, and what else. ^0000ffChicken^000000?";
		next;
		mes "[Civilization Explorer]";
		mes "อืมถูกต้องมีคำว่าไก่แน่ ๆ ฉันไม่รู้ว่ามันจะเป็นเทคโนโลยีแบบไหน ... ";
		next;
		mes "[Civilization Explorer]";
		mes "มีการอ้างอิงในบันทึกของผู้คนที่ดูเหมือนจะ 'หยั่งรากลึก' ท่ามกลางร่องรอยของเทคโนโลยีมากมาย";
		next;
		if (select( "ไม่ใช่หัวข้อที่ฉันสนใจ", "น่าสนใจมีผลหรือไม่?" ) == 1) {
			mes "[Civilization Explorer]";
			mes "โชคร้าย.";
			close;
		}
		mes "[Civilization Explorer]";
		mes "เป้าหมายของฉันคือการค้นคว้าเกี่ยวกับการใช้เทคโนโลยีที่เกี่ยวข้องกับช่วงเวลาที่เกี่ยวข้องแม้ว่าสิ่งที่ฉันได้พบมาจะไม่มีนัยสำคัญก็ตาม";
		next;
		mes "[Civilization Explorer]";
		mes "ถ้าฉันมีวิธีของฉันฉันจะใช้ผู้เสนอญัตติมิติเพื่อดูตัวเอง แต่มีงานมากเกินไปนอกจากฉันไม่ใช่นักผจญภัย ... ";
		next;
		mes "[Civilization Explorer]";
		mes "บังเอิญคุณสนใจที่จะเดินทางไปยังอดีตหรือไม่";
		next;
		mes "[Civilization Explorer]";
		mes "จะดีกว่านี้ถ้าคุณพาฉันไปรอบ ๆ ถ้าคุณพบวัตถุที่น่าสนใจ";
		next;
		if (select( "ไม่สนใจ", "ตกลง") == 1) {
			mes "[Civilization Explorer]";
			mes "โชคร้าย.";
			close;
		}
		mes "[Civilization Explorer]";
		mes "คุณเป็นนักผจญภัยบอกฉันทีว่าคุณตัดสินใจจะทำอะไร";
		if (isbegin_quest(12346) == 0)
			setquest 12346;// Strange Ancient Science
		break;
	case 1:
		mes "ฉันจะช่วยคุณได้อย่างไร";
		break;
	case 2:
		mes "คราวนี้อยากทำอะไร";
		break;
	}
	next;
	if (isbegin_quest(12346) == 1)
		completequest 12346;
	.@party_id = getcharid(1);
	if (.@party_id == 0) {
		mes "[Civilization Explorer]";
		mes "กรุณาคุยกับฉันหลังจากแต่งปาร์ตี้ตั้งแต่ 1 คนขึ้นไปหรือหลังจากเข้าร่วมปาร์ตี้";
		close;
	}
	switch( checkquest(12347,PLAYTIME) ) {
	case -1:
		if (is_party_leader() == false) {
			mes "[Civilization Explorer]";
			mes "อ่าคุณไม่ใช่หัวหน้าปาร์ตี้พาฉันไปหาหัวหน้าของคุณ! หรืออาจจะพาหัวหน้ามาหาฉันก็ได้";
			close;
		}
		if (select( "Produce a crack of time", "Cancel" ) == 2)
			close;
		if (instance_create("Central Laboratory") < 1) {
			mes "Party: " + getpartyname( getcharid(1) ) + "";
			mes "Leader: " + getpartyleader( getcharid(1) ) + "";
			mes "^0000ffCentral Laboratory ^000000 - Unknown Error";
			close;
		}
		mes "[Civilization Explorer]";
		mes "เมื่อกาลเวลาเปิดออกหายใจแล้วพูดอีกครั้ง";
		close;
	case 0:
	case 1:
		mes "[Civilization Explorer]";
		mes "อ๊ะไม่นะ";
		mes "ผลจากการเดินทางต่างมิติยังเหลืออยู่เราไม่สามารถเดินทางได้อีกในสภาพนี้";
		close;
	case 2:
		mes "^0000ff ร่องรอยของการเข้าไปในห้องปฏิบัติการกลางทั้งหมดถูกลบออกการพูดกับ Civilization Explorer ดูเหมือนจะเป็นไปได้ ^000000";
		erasequest 12347;// Trace of Laboratory Access
		close;
	}
}

// Entrance
1@lab,104,34,4	script	Probationary researcher#pa0829	4_LGTSCIENCE,{
	if ('switch_enabled == 1) {
		mes "[Probationary researcher]";
		mes "ประตูทางเข้าเปิดอยู่แล้ว";
		close;
	}
	if (is_party_leader() == false) {
		mes "[Probationary researcher]";
		mes "กรุณารอสักครู่ฉันกำลังคุยกับไกด์ว่าคุณได้รับอนุญาตหรือไม่";
		close;
	}
	mes "[Probationary researcher]";
	mes "คุณเข้าร่วมเป็นผู้ช่วยในการทดลองของโครงการวิจัยว 32001 หรือไม่";
	next;
	if (select( "Yes.", "No." ) == 2) {
		mes "[Probationary researcher]";
		mes "ดูเหมือนว่าคุณอยู่ในห้องปฏิบัติการที่ไม่ถูกต้องโปรดออกและขอความช่วยเหลือ";
		close;
	}
	mes "[Probationary researcher]";
	mes "เราต้องใช้เวลาประมาณ 5 วินาทีเพื่อยืนยันรหัสการเข้าถึง";
	close2;
	sleep 3000;
	npctalk "ผู้วิจัยภาคทัณฑ์: รหัสการเข้าถึงตรงกันฉันจะเปิดประตู";
	sleep 6000;
	npctalk "ผู้วิจัยคุมประพฤติ: ตอนนี้คุณสามารถเข้าไปได้แล้วโปรดทราบว่าคุณไม่สามารถเปิดประตูจากอีกด้านหนึ่งได้";
	if ('switch_enabled == 0) {
		'switch_enabled = 1;
		enablenpc instance_npcname("lab_warp#1");
		enablenpc instance_npcname("lab_warp#2");
		enablenpc instance_npcname("lab_warp#3");
		enablenpc instance_npcname("Main Switch#");
		enablenpc instance_npcname("Senior researcher#pa082");
		enablenpc instance_npcname("#eff_nc");
		for ( .@i = 1; .@i <= 8; ++.@i )
			enablenpc instance_npcname( "Switch" + .@i + "#" );
	}
	end;

OnInstanceInit:
	'map_name$ = instance_mapname("1@lab");
	'code_found = 'bool_event = 'switch_enabled = 0;
	for ( .@i = 1; .@i <= 8; .@i++ )
		'switch_position[.@i] = 0;

	// Warps
	disablenpc instance_npcname("lab_warp#1");
	disablenpc instance_npcname("lab_warp#2");
	disablenpc instance_npcname("lab_warp#3");
	disablenpc instance_npcname("lab_warp#4");
	disablenpc instance_npcname("lab_warp#5");

	// Switchs
	disablenpc instance_npcname("Main Switch#");
	disablenpc instance_npcname("Senior researcher#pa082");
	disablenpc instance_npcname("#eff_nc");
	for ( .@i = 1; .@i <= 8; ++.@i )
		disablenpc instance_npcname( "Switch" + .@i + "#" );

	// Experiments
	disablenpc instance_npcname("DR Silvia#pa0829");
	disablenpc instance_npcname("DR Federico#pa0829");
	disablenpc instance_npcname("DR Artemia#pa0829");
	disablenpc instance_npcname("DR Vincent#pa0829");
	disablenpc instance_npcname("#wp01");
	disablenpc instance_npcname("#npc_con01");
	disablenpc instance_npcname("#npc_con03");
	disablenpc instance_npcname("#npc_con05");
	disablenpc instance_npcname("#npc_con06");
	disablenpc instance_npcname("#npc_con07");
	disablenpc instance_npcname("#npc_con08");
	disablenpc instance_npcname("#npc_con10");
	disablenpc instance_npcname("#npc_con11");
	for ( .@i = 1; .@i <= 14; ++.@i )
		hideonnpc instance_npcname("#gen_" + (.@i < 10 ? "0" : "") + .@i);
	disablenpc instance_npcname("lab_warp#end");
	end;
}
// 1@lab,104,34,4	script	Probationary researcher#pa0829_2	4_LGTSCIENCE

// Switchs
1@lab,37,37,3	script	Switch1#	CLEAR_NPC,{
	.@num = charat( strnpcinfo(1),6 );
	setarray .@position$[0], "OFF", "ON";
	mes "[Switch " + .@num + "]";
	mes "The switch is currently in the " + .@position$[ 'switch_position[.@num] ] + " position. What will you do?";
	next;
	'switch_position[.@num] = !( select( "ON","OFF" ) - 1 );
	close;
}
1@lab,39,37,3	duplicate(Switch1#)	Switch2#	CLEAR_NPC
1@lab,41,37,3	duplicate(Switch1#)	Switch3#	CLEAR_NPC
1@lab,43,37,3	duplicate(Switch1#)	Switch4#	CLEAR_NPC
1@lab,46,37,3	duplicate(Switch1#)	Switch5#	CLEAR_NPC
1@lab,48,37,3	duplicate(Switch1#)	Switch6#	CLEAR_NPC
1@lab,50,37,3	duplicate(Switch1#)	Switch7#	CLEAR_NPC
1@lab,52,37,3	duplicate(Switch1#)	Switch8#	CLEAR_NPC

1@lab,34,37,3	script	Main Switch#	CLEAR_NPC,{
	.@string$ = 'switch_position[1] + "" + 'switch_position[2] + "" + 'switch_position[3] + "" + 'switch_position[4] + "" +
		'switch_position[5] + "" + 'switch_position[6] + "" + 'switch_position[7] + "" + 'switch_position[8];
	.@current_code = strtol( .@string$,2 );
	.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
	npctalk "Today's access code " + .@code + ", currently entered code " + .@current_code + ".";
	sleep 6000;
	if ('code_found > 0)
		npctalk "ประตูเปิดอยู่แล้ว.";
	else if (.@current_code != .@code)
		npctalk "รหัสไม่ถูกต้องโปรดลองอีกครั้ง.";
	else {
		'code_found = 1;
		npctalk "The code is correct. Opening door.";
		enablenpc instance_npcname("lab_warp#4");
		enablenpc instance_npcname("lab_warp#5");
		enablenpc instance_npcname("DR Silvia#pa0829");
		enablenpc instance_npcname("DR Federico#pa0829");
		enablenpc instance_npcname("DR Artemia#pa0829");
		enablenpc instance_npcname("DR Vincent#pa0829");
		enablenpc instance_npcname("#wp01");
		donpcevent instance_npcname("#eff_nc") + "::OnStop";
		for ( .@i = 1; .@i <= 8; ++.@i )
			disablenpc instance_npcname( "Switch" + .@i + "#" );
	}
	end;
}

1@lab,45,32,3	script	Senior researcher#pa082	4_LGTSCIENCE,{
	if ('code_found > 0) {
		mes "[Senior Researcher]";
		mes "ประตูเปิดอยู่ดังนั้นตอนนี้คุณสามารถเข้าไปได้โปรดทราบว่าคุณไม่สามารถเปิดประตูจากอีกด้านหนึ่งได้";
		close;
	}
	.@code = (gettime(DT_MONTH) + gettime(DT_DAYOFMONTH)) * 5;
	mes "[Senior Researcher]";
	mes "ดูเหมือนว่าคุณจะมาที่นี่เพื่อเข้าร่วมการทดลองการเร่งอนุภาค";
	next;
	mes "[Senior Researcher]";
	mes "รหัสเข้าวันนี้คือ ^0000ff" +.@code+ "^000000.";
	mes "ต้องป้อนรหัสเป็นเลขฐานสองใช้สวิตช์ข้างหลังฉันเพื่อป้อน";
	next;
	mes "[Senior Researcher]";
	mes "หลังจากป้อนรหัสแล้วให้กดปุ่มหลักใกล้ทางเข้า";
	for ( .@i = 1; .@i <= 8; ++.@i )
		specialeffect EF_STEAL, AREA, instance_npcname("Switch" + .@i + "#");
	close2;
	donpcevent instance_npcname("#eff_nc") + "::OnStart";
	end;
}

1@lab,1,1,0	script	#eff_nc	-1,{
	end;
OnStart:
OnTimer2000:
	specialeffect 203,AREA, instance_npcname("Main Switch#");
	for ( .@i = 1; .@i <= 8; ++.@i )
		specialeffect (203+.@i),AREA, instance_npcname("Switch" + .@i + "#");
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}


// Experiments
1@lab,81,84,7	script	DR Silvia#pa0829	4_F_SCIENCE,{
	mes "[DR Silvia]";
	mes "หลังการทดลองฉันอยากกลับบ้านและเพลิดเพลินกับเบียร์เย็น ๆ และไก่ทอด";
	next;
	mes "[DR Silvia]";
	mes "ไม่ได้อยู่บ้านมาเกือบ 2 ปีแล้ว";
	next;
	mes "^0000ffSilvia กำลังดูอุปกรณ์เร่งอนุภาคด้วยท่าทางเหนื่อยล้ามาก ^000000";
	close;
}

1@lab,80,87,7	script	DR Federico#pa0829	4_LGTSCIENCE,{
	mes "[DR Federico]";
	mes "อุปกรณ์ชิ้นนี้น่าทึ่งมากยิ่งมองมากขึ้นไม่เคยมีเครื่องเร่งอนุภาคขนาดนี้มาก่อน";
	next;
	mes "[DR Federico]";
	mes "ด้วยขนาดนี้ฉันสงสัยว่าจะสร้างหลุมดำขนาดเล็กได้ไหม ... ";
	next;
	mes "[DR Federico]";
	mes "อ๊ะฉันแค่พูดเหตุการณ์แบบนี้ใกล้จะหายนะมากกว่าการทดลองฮ่า ๆ ";
	close;
}

1@lab,80,90,6	script	DR Artemia#pa0829	4_F_SCIENCE,{
	mes "[DR Artemia]";
	mes "ดูสิความเป็นจริงไม่ชอบเมื่อคุณจับเชือกแล้วลากมัน";
	next;
	mes "[DR Artemia]";
	mes "การบิดเบือนมิติเกิดขึ้นได้ด้วยการเร่งอนุภาคที่ทรงพลังเช่นนี้";
	next;
	mes "[DR Artemia]";
	mes "ฉันรู้สึกแย่กับเรื่องนี้เพราะ ... ธรรมชาติหาทาง";
	close;
}

1@lab,81,93,6	script	DR Vincent#pa0829	4_LGTSCIENCE,{
	mes "[DR Vincent]";
	mes "เครื่องสมบูรณ์ไม่มีอะไรผิดพลาด!";
	next;
	mes "[DR Vincent]";
	mes "อาร์ทีเมียกังวลมากยังไงก็ตามฉันจะไปเที่ยวหลังจากการทดลอง";
	close;
}

1@lab,80,89,0	script	#wp01	HIDDEN_WARP_NPC,9,9,{
	end;
OnTouch:
	donpcevent instance_npcname("#npc_con01") + "::OnStart";
	disablenpc instance_npcname("#wp01");
	end;
}

// Round 1
1@lab,4,1,3	script	#npc_con01	CLEAR_NPC,{
	end;
OnStart:
	'vincent$ = instance_npcname("DR Vincent#pa0829");
	'federico$ = instance_npcname("DR Federico#pa0829");
	'silvia$ = instance_npcname("DR Silvia#pa0829");
	'artemia$ = instance_npcname("DR Artemia#pa0829");
	'gen_01$ = instance_npcname("#gen_01");
	'gen_02$ = instance_npcname("#gen_02");
	'gen_03$ = instance_npcname("#gen_03");
	'gen_04$ = instance_npcname("#gen_04");
	'gen_05$ = instance_npcname("#gen_05");
	'gen_06$ = instance_npcname("#gen_06");
	'gen_07$ = instance_npcname("#gen_07");
	'gen_08$ = instance_npcname("#gen_08");
	'gen_09$ = instance_npcname("#gen_09");
	'gen_10$ = instance_npcname("#gen_10");
	'gen_11$ = instance_npcname("#gen_11");
	'gen_12$ = instance_npcname("#gen_12");
	'gen_13$ = instance_npcname("#gen_13");
	'gen_14$ = instance_npcname("#gen_14");

	npctalk "Federico: มาเริ่มกันเลยหืม ... คุณพร้อมไหม Dr. Fresa?", 'federico$;
	sleep 5000;
	npctalk "Artemia : อาจทำการตรวจยืนยันขั้นสุดท้ายการทดลองในขอบเขตนี้จำเป็นต้องใช้ความระมัดระวังเช่นนี้.", 'artemia$;
	sleep 6000;
	npctalk "Silvia : จำเป็นต้องใช้ความระมัดระวังในการใช้ผลิตภัณฑ์สำเร็จรูปสำหรับการทดลองครั้งแรก.", 'silvia$;
	sleep 4000;
	npctalk "Vincent : อ่าฉันหวังว่าเราจะเสร็จเร็ว ๆ เพื่อที่ฉันจะได้ไปกินไก่.", 'vincent$;
	sleep 3000;
	npctalk "Silvia : อ๊ะ! ฉันก็ชอบเหมือนกันโทรหาฉันเมื่อคุณไป.", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "?????: อา, อา, หนึ่งสองหนึ่งสอง, การทดสอบไมค์.", bc_map,0x00ff77;
	sleep 2000;
	npctalk "Federico : เธออยู่ที่นี่.", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "?????: หัวหน้าผู้อำนวยการ Lona Fresa ของเครื่องเร่งอนุภาคโครงการ.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa :  เวลาปัจจุบัน " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day 25 minutes after 10.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa :  การทดสอบผลกระทบของการเร่งอนุภาคที่มีต่อสภาพแวดล้อมในพื้นที่เปิดโล่ง.", bc_map,0x00ff77;
	sleep 5000;
	npctalk "Artemia :  อ๊า ... ฉันไม่ชอบการทดลองนี้จริงๆ.", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa :  เริ่มต้นการทดลองหัวหน้านักวิจัย Dr. Federico, Artemia, Silvia, Vincent and assistant researchers.", bc_map,0x00ff77;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	npctalk "Artemia : ให้เราเริ่ม ~", 'artemia$;
	sleep 3000;
	npctalk "Vincent : การเริ่มต้นการหมุนของเครื่องกำเนิดไฟฟ้า.", 'vincent$;
	sleep 3000;
	npctalk "Federico :  การหมุนของเครื่องกำเนิดไฟฟ้าคงที่ที่ 10,000 RPM", 'federico$;
	sleep 2500;
	npctalk "Silvia :แรงดันไฟฟ้ายังคงอยู่ในแถบความคลาดเคลื่อน.", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : การทดลองนี้มีไว้สำหรับการสร้างวัสดุใหม่ที่ไม่เคยมีมาก่อน.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa : ผู้บันทึกตรวจสอบให้แน่ใจว่าได้บันทึกรายละเอียดเกี่ยวกับปฏิกิริยาของตัวเร่งปฏิกิริยาเมื่อติดตั้งเข้ากับตัวเร่ง.", bc_map,0x00ff77;
	sleep 6000;
	npctalk "Artemia : Contacting catalyst bed number 1.", 'artemia$;
	sleep 9000;
	npctalk "Silvia : ไม่เสถียรยังอยู่ในขอบเขตความคลาดเคลื่อน.", 'silvia$;
	sleep 3000;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	sleep 2000;
	npctalk "Silvia : ตรวจจับความร้อนใกล้กับแถบความคลาดเคลื่อนระหว่างเตียงตัวเร่งปฏิกิริยาและตัวเร่ง!", 'silvia$;
	sleep 5000;
	npctalk "Vincent : Accelerator กำลังร้อน.", 'vincent$;
	sleep 5000;
	npctalk "Vincent: 320c.", 'vincent$;
	sleep 5000;
	npctalk "Vincent : 670c มันร้อนเร็วเกินไป!", 'vincent$;
	sleep 3000;
	npctalk "Vincent :  ทะลุ 1000c !!!! Dr. Fresa! เราต้องหยุดเครื่องชั่วขณะ!", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : It is fine.", bc_map,0x00ff77;
	sleep 5000;
	npctalk "Silvia: มันไม่ใช่ปัญหาง่ายๆอุณหภูมิสูงเกินกว่าที่คาดการณ์ไว้มาก!", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa: คาดการณ์ปัญหานี้รักษาสถานะปัจจุบัน", bc_map, 0x00ff77;
	sleep 3000;
	npctalk "Artemia :  แต่อย่างน้อย.. ฉันจะมีไก่ทีหลัง.", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: การรับสถานการณ์ที่เปลี่ยนแปลงผ่านเซิร์ฟเวอร์หลักการตรวจจับการบิดเบือนมิติระหว่างเครื่องเร่งอนุภาคและตัวเร่งปฏิกิริยา.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "Artemia : Dr. Fresa!มันเป็นสถานการณ์อันตรายของ Mother Computer ปิดไฟหลัก!", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: การแจ้งเตือนสิ่งมีชีวิตใหม่ที่ไม่ปรากฏชื่อใกล้เข้ามาจากเครื่องบินเดมิ.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "Vincent : เกิดอะไรขึ้นใครบางคนปิดคอมพิวเตอร์เครื่องนั้น!", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: สิ่งมีชีวิตใหม่จะถูกส่งจากเครื่องบินเดมิเร็ว ๆ นี้", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: 5 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 4 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 3 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 2 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: 1 seconds remaining", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	donpcevent instance_npcname("#npc_con05") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con05	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con05");
	killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
	.@boss_call = rand(1,105);
	if (.@boss_call <= 7)			.@mob_id = 1087;	// ORK_HERO
	else if (.@boss_call <= 14)		.@mob_id = 1147;	// MAYA
	else if (.@boss_call <= 21)		.@mob_id = 1190;	// ORC_LORD
	else if (.@boss_call <= 28)		.@mob_id = 1115;	// EDDGA
	else if (.@boss_call <= 35)		.@mob_id = 1086;	// GOLDEN_BUG
	else if (.@boss_call <= 42)		.@mob_id = 1038;	// OSIRIS
	else if (.@boss_call <= 49)		.@mob_id = 1159;	// PHREEONI
	else if (.@boss_call <= 56)		.@mob_id = 1389;	// DRACULA
	else if (.@boss_call <= 63)		.@mob_id = 1046;	// DOPPELGANGER
	else if (.@boss_call <= 70)		.@mob_id = 1059;	// MISTRESS
	else if (.@boss_call <= 77)		.@mob_id = 1150;	// MOONLIGHT
	else if (.@boss_call <= 84)		.@mob_id = 1688;	// LADY_TANEE
	else if (.@boss_call <= 91)		.@mob_id = 1039;	// BAPHOMET
	else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con05") + "::OnMyMobDead";
	sleep 3000;
	npctalk "Artemia : อาร์ทีเมีย: อะไร ... นั่นอะไร?!", 'artemia$;
	sleep 3000;
	npctalk "Silvia : มันไม่ตอบสนองเราบางทีมันอาจมองไม่เห็นเรา", 'silvia$;
	sleep 5000;
	npctalk "Vincent : แต่มันเป็นศัตรูกับผู้ช่วยโครงการ! สิ่งมีชีวิตนั้นคืออะไร!?", 'vincent$;
	sleep 5000;
	npctalk " ฉันขยับร่างกายไม่ได้! ฉันคิดว่าอวกาศถูกแช่แข็ง!", 'vincent$;
	end;

OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Restarting device.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con05") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con06") + "::OnStart";
	disablenpc instance_npcname("#npc_con05");
	stopnpctimer;
	end;
}

// Round 2
1@lab,4,1,3	script	#npc_con06	CLEAR_NPC,{
	end;
OnStart:
	sleep 9000;
	npctalk "Artemia: MA-4885!! On who's order are you restarting! Stop!!", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885: Unrecognized command.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 6000;
	npctalk "Artemia: Dr. Fresa!! เราต้องหยุด! นี่มันอันตรายเกินไป!", 'artemia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
	sleep 3000;
	npctalk "Vincent: Fresa! เราไม่มีเวลา! ปิดคอม !!", 'vincent$;
	sleep 3000;
	mapannounce 'map_name$, "MA-4885:  เริ่มขั้นตอนการเร่งอนุภาคใหม่.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	npctalk "Silvia : Fresa! ตัดสินใจ!!", 'silvia$;
	sleep 3000;
	mapannounce 'map_name$, "Lona Fresa : ......", bc_map,0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa : เวลาปัจจุบัน 20 นาทีที่ผ่านมา 11 เลื่อนการทดลองไปชั่วขณะ", bc_map,0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: การเข้าถึงถูกปฏิเสธไม่เข้าใจคำสั่ง.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	sleep 2000;
	mapannounce 'map_name$, "Lona Fresa : Mother Comp MA-4885.ฉันเป็นหัวหน้าผู้อำนวยการของคุณ Dr. Fresa. Cease current progress.", bc_map,0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Access denied.  การเข้าถึงถูกปฏิเสธไม่เข้าใจคำสั่ง.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	npctalk "Federico : อ้บ้า! อย่างอื่นกำลังจะมา!!", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "MA-4885: Alert. การแจ้งเตือนสิ่งมีชีวิตใหม่ที่ไม่ปรากฏชื่อใกล้เข้ามาจากเครื่องบินเดมิ.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	npctalk "Vincent :  โอ้ ... ไอ้นี่คือเหตุผลที่คุณไม่ควรติดตั้ง CPU ตัวใหม่ล่าสุด.", 'vincent$;
	sleep 3000;
	npctalk "Federico : อ่า ... ร่างกายของฉัน ... ขยับไม่ได้ไอ้บ้า", 'federico$;
	sleep 4000;
	mapannounce 'map_name$, "MA-4885:สิ่งมีชีวิตใหม่จะถูกส่งจากเครื่องบินเดมิในไม่ช้า.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885:เหลืออีก 5 วินาที", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885:เหลืออีก 4 วินาที", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885:เหลืออีก 3 วินาที", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885:เหลืออีก 2 วินาที", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885:1 วินาทีที่เหลือ", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 1000;
	donpcevent instance_npcname("#npc_con07") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: Unidentifiable creature discovered. Temporarily shutting down main power.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con07	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con07");
	killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
	.@boss_call = rand(1,98);
	if (.@boss_call <= 7)		.@mob_id = 1980;	// KUBLIN
	else if (.@boss_call <= 14)	.@mob_id = 1157;	// PHARAOH
	else if (.@boss_call <= 21)	.@mob_id = 1112;	// DRAKE
	else if (.@boss_call <= 28)	.@mob_id = 1251;	// KNIGHT_OF_WINDSTORM
	else if (.@boss_call <= 35)	.@mob_id = 2068;	// BOITATA
	else if (.@boss_call <= 42)	.@mob_id = 1373;	// LORD_OF_DEATH
	else if (.@boss_call <= 49)	.@mob_id = 2156;	// LEAK
	else if (.@boss_call <= 56)	.@mob_id = 1272;	// DARK_LORD
	else if (.@boss_call <= 63)	.@mob_id = 1630;	// BACSOJIN_
	else if (.@boss_call <= 70)	.@mob_id = 1252;	// GARM
	else if (.@boss_call <= 77)	.@mob_id = 1779;	// KTULLANUX
	else if (.@boss_call <= 84)	.@mob_id = 1708;	// THANATOS
	else if (.@boss_call <= 86)	.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 88)	.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 90)	.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 92)	.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 94)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 96)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 98)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con07") + "::OnMyMobDead";
	sleep 6000;
	npctalk "Silvia: เหมือนครั้งที่แล้วดูเหมือนว่าจะไม่สนใจเราเลย.", 'silvia$;
	sleep 4000;
	npctalk "Vincent: ขอปิดคอมแม่โดยสมบูรณ์ในขณะที่ไฟหลักดับชั่วคราว!!", 'vincent$;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: กำลังตอบคำถามพื้นที่นี้ติดอยู่ในระยะที่ผิดเพี้ยน.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: ปฏิสัมพันธ์โดยตรงระหว่างสิ่งมีชีวิตเดมีระนาบกับสิ่งมีชีวิตในโลกนี้จะเกิดขึ้นได้หลังจากเฟสปรับสมดุลแล้ว.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: กำลังทำการคำนวณเพื่อคืนสมดุลให้กับเฟสที่ผิดเพี้ยนโปรดรอสักครู่.", bc_map,0xFFFF00,FW_NORMAL,13;
	sleep 5000;
	npctalk "Artemia: ก้อนโลหะบ้านั่นมันพูดอะไรกันมันบอกว่าเราไม่ได้อยู่ในช่วงเดียวกับสัตว์ประหลาดพวกนั้นหรอกเหรอ?", 'artemia$;
	sleep 4000;
	npctalk "อ่าฉันคิดว่าเราน่าจะหวังว่าคอมพิวเตอร์ของแม่จะคำนวณไม่ถูก.", 'federico$;
	end;

OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: Unidentified creature eliminated. Phase adjustment failed. Restarting relevant procedure.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con07") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con08") + "::OnStart";
	disablenpc instance_npcname("#npc_con07");
	stopnpctimer;
	end;
}

// Round 3
1@lab,4,1,3	script	#npc_con08	CLEAR_NPC,{
	end;
OnStart:
	sleep 13000;
	mapannounce 'map_name$, "Lona Fresa: Mother Computer MA-4885.ดูเหมือนว่าคุณกำลังพยายามประสานมิติอื่นกับโลกนี้.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: That is correct.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 6000;
	npctalk "Artemia: Dr. Fresa!!  เหตุการณ์ไร้สาระนี้ไม่เกี่ยวข้องกับการทดลองเดิมของเราเลย!", 'artemia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: ฉันได้ตระหนักถึงสิ่งนั้นแล้ว.", bc_map, 0x00ff77;
	sleep 6000;
	npctalk "Silvia: เราไม่สามารถปิดระบบไฟฟ้าหลักโดยสมบูรณ์ได้หรือ?", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: เริ่มขั้นตอนการเร่งอนุภาคใหม่.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	mapannounce 'map_name$, "Lona Fresa: " + gettime(DT_MONTH) + " Month " + gettime(DT_DAYOFMONTH) + " Day. Current time 38 minutes past 11 temporarily terminating particle accelerator project.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa: การสิ้นสุดบันทึกด้วยสาเหตุของความผิดปกติของคอมพิวเตอร์แม่และการเร่งความเร็วเกินไป.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "Silvia : เอาล่ะ! ตอนนี้ Dr. Fresa เพิ่งจะปิดเครื่อง.", 'silvia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa:ตัดกระแสไฟไปที่  Mother Computer MA-4885.", bc_map, 0x00ff77;
	sleep 6000;
	mapannounce 'map_name$, "MA-4885: ระงับการทำงานของอุปกรณ์ทั้งหมดตามคำขอของผู้ดูแลระบบ.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: .....command... ----!!!  Zizip... ---!! -- following command ----- !! Zizip.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	'bool_event = false;
	npctalk "Artemia : ต๊าย ... จบแล้วเหรอ?", 'artemia$;
	sleep 6000;
	mapannounce 'map_name$, "Lona Fresa:เสร็จสิ้นการปิดเครื่องคอมพิวเตอร์แม่ MA-4885 ยืนยันการระงับอุปกรณ์.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "ดูเหมือนจะจบแล้ว.", 'silvia$;
	sleep 4000;
	npctalk "Federico: Plant initialization is the best.", 'federico$;
	sleep 4000;
	mapannounce 'map_name$, "Lona Fresa:กำลังบันทึกความคืบหน้าการทดลองทั้งหมดและสิ้นสุด ณ จุดปัจจุบัน.", bc_map, 0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: ความล้มเหลวในการทดสอบขั้นสุดท้ายเป็นเรื่องที่น่าเสียดาย แต่ฉันจะสัญญาว่าจะเป็นการทดลองที่สมบูรณ์แบบยิ่งขึ้นในครั้งต่อไป.", bc_map, 0x00ff77;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: Good work everybody.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "Artemia: ดูเหมือนจะจบแล้ว.", 'artemia$;
	sleep 5000;
	npctalk "Federico: ฉันคิดว่ามันเป็นการทดลองแบบดันทุรังของ Fresa แต่มันเป็นปัญหากับคอมพ์ของแม่.", 'federico$;
	sleep 5500;
	npctalk "Silvia:  ใจเย็น ๆ กันเถอะแล้วออกไปจากห้องทดลองที่น่ากลัวนี้.", 'silvia$;
	sleep 5000;
	npctalk "Vincent : ยังไงก็ตาม ... ฉันยังขยับไม่ได้แล้วเกิดอะไรขึ้น?", 'vincent$;
	sleep 5000;
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	donpcevent instance_npcname("#npc_con03") + "::OnStart3";
	sleep 2000;
	mapannounce 'map_name$, "MA-4885: CODE NAME - DESCENT", bc_map, 0xff0000,FW_NORMAL,16,0;
	sleep 5000;
	npctalk "Federico : อะไรนะนี่ไม่ใช่ขั้นตอนการเริ่มต้น?", 'federico$;
	sleep 2500;
	mapannounce 'map_name$, "MA-4885: เริ่มการแปลงเฟสสุดท้าย.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 3000;
	npctalk "Silvia :มันจบแล้ว.", 'silvia$;
	sleep 5500;
	mapannounce 'map_name$, "MA-4885:สิ่งมีชีวิตใหม่จะถูกส่งจากเครื่องบินเดมิเร็ว ๆ นี้.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: เหลืออีก 5 วินาที", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: เหลืออีก 4 วินาที", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: เหลืออีก 3 วินาที", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: เหลืออีก 2 วินาที", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	mapannounce 'map_name$, "MA-4885: เหลืออีก 1 วินาที", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 1000;
	donpcevent instance_npcname("#npc_con10") + "::OnStart";
	mapannounce 'map_name$, "MA-4885: ปิดเครื่องชั่วคราวสำหรับการสังเคราะห์ด้วยเฟสที่เปลี่ยนรูป.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 3000;
	'bool_event = false;
	end;
}

1@lab,4,1,3	script	#npc_con10	CLEAR_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#npc_con10");
	killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
	.@boss_call = rand(1,105);
	if (.@boss_call <= 7)			.@mob_id = 1623;	// RSX_0806
	else if (.@boss_call <= 14)		.@mob_id = 1418;	// DARK_SNAKE_LORD
	else if (.@boss_call <= 21)		.@mob_id = 1312;	// TURTLE_GENERAL
	else if (.@boss_call <= 28)		.@mob_id = 1785;	// ATROCE
	else if (.@boss_call <= 35)		.@mob_id = 1734;	// KIEL_
	else if (.@boss_call <= 42)		.@mob_id = 1719;	// DETALE
	else if (.@boss_call <= 49)		.@mob_id = 1768;	// GLOOMUNDERNIGHT
	else if (.@boss_call <= 56)		.@mob_id = 2087;	// QUEEN_SCARABA
	else if (.@boss_call <= 63)		.@mob_id = 1751;	// RANDGRIS
	else if (.@boss_call <= 70)		.@mob_id = 2253;	// DAEHYON
	else if (.@boss_call <= 77)		.@mob_id = 2255;	// KADES
	else if (.@boss_call <= 84)		.@mob_id = 1832;	// IFRIT
	else if (.@boss_call <= 91)		.@mob_id = 1874;	// BEELZEBUB_
	else if (.@boss_call <= 93)		.@mob_id = 1089;	// TOAD
	else if (.@boss_call <= 95)		.@mob_id = 1092;	// VAGABOND_WOLF
	else if (.@boss_call <= 97)		.@mob_id = 1088;	// VOCAL
	else if (.@boss_call <= 99)		.@mob_id = 1096;	// ANGELING
	else if (.@boss_call <= 101)	.@mob_id = 1093;	// ECLIPSE
	else if (.@boss_call <= 103)	.@mob_id = 1120;	// GHOSTRING
	else if (.@boss_call <= 105)	.@mob_id = 1090;	// MASTERING

	monster 'map_name$,90,88, "Unidentified creature",.@mob_id,1, instance_npcname("#npc_con10") + "::OnMyMobDead";
	mapannounce 'map_name$, "MA-4885: ขั้นตอนการแปลงเฟสสุดท้าย.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: โลกนี้จะถูกเรียกขึ้นสู่ระนาบเดมิหลังจากปรากฏการณ์การรบกวนเฟสกับสิ่งมีชีวิตจางหายไป.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	end;
OnMyMobDead:
	mapannounce 'map_name$, "MA-4885: การซิงโครไนซ์เฟสเสร็จสิ้นก่อนที่สิ่งมีชีวิตจะถูกกำจัดการเริ่มต้นการเดินทางแบบมิติ.", bc_map, 0xffff00,FW_NORMAL,13,0;
	if (mobcount( 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead" ) < 1)
		initnpctimer;
	end;
OnTimer1000:
	killmonster 'map_name$, instance_npcname("#npc_con10") + "::OnMyMobDead";
	donpcevent instance_npcname("#npc_con11") + "::OnStart";
	disablenpc instance_npcname("#npc_con10");
	stopnpctimer;
	end;
}

1@lab,4,1,3	script	#npc_con11	CLEAR_NPC,{
	end;
OnStart:
	'bool_event = true;
	donpcevent instance_npcname("#npc_con03") + "::OnStart";
	sleep 12000;
	mapannounce 'map_name$, "MA-4885: กำลังทำการคำนวณเพื่อคืนสมดุลให้กับเฟสที่ผิดเพี้ยนโปรดรอสักครู่.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	npctalk "Federico : Mother comp is conducting a tremendous experiment going beyond what we thought of.", 'federico$;
	sleep 2500;
	npctalk "Vincent : Seems more magnanimous than we are. Fresa!!! Are you listening? Sound the alarm!", 'vincent$;
	sleep 5000;
	npctalk "Vincent : We should at least evacuate the people in the other labs.", 'vincent$;
	sleep 5000;
	mapannounce 'map_name$, "Lona Fresa: But ... you.", bc_map, 0x00ff77;
	sleep 5000;
	npctalk "Silvia : Gee, I don't know what will happen but I am curious about shifting dimensions. Don't mind me!", 'silvia$;
	sleep 5000;
	npctalk "Artemia : Dimension moving while experimenting on new material production. Got caught up in a different level of an experiment.", 'artemia$;
	sleep 5000;
	mapannounce 'map_name$, "Alarm System : Alert! Alert! Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "Alarm System : I repeat. Class 1 alert issued from the Particle Laboratory. All researchers evacuate to the bunker.", bc_map, 0xff0000,FW_NORMAL,13,0;
	npctalk "Federico : Is this how it feels to move dimensions. How strange...", 'federico$;
	sleep 4000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'federico$;
	disablenpc 'federico$;
	mapannounce 'map_name$, "MA-4885: Dimension move at 20% progress. There could be dimensional interference with certain creatures. Please be aware.", bc_map, 0xffff00,FW_NORMAL,13,0;
	donpcevent instance_npcname("#npc_con03") + "::OnStart2";
	npctalk "Vincent : I wonder if the others evacuated?", 'vincent$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'vincent$;
	disablenpc 'vincent$;
	npctalk "Silvia : Ah...... I should be seeing this while having chicken and beer...", 'silvia$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'silvia$;
	disablenpc 'silvia$;
	mapannounce 'map_name$, "MA-4885: Dimension move at 78% progress. Most of the dimension encroached by demi-plane.", bc_map, 0xffff00,FW_NORMAL,13,0;
	npctalk "Artemia : Ha... whose going to eat the chicken now?", 'artemia$;
	sleep 5000;
	specialeffect EF_SUI_EXPLOSION, AREA, 'artemia$;
	disablenpc 'artemia$;
	donpcevent instance_npcname("#npc_con03") + "::OnStart3";
	sleep 10000;
	'bool_event = false;
	mapannounce 'map_name$, "MA-4885: Dimension move 100% completed. Welcome to the demi-plane dimension.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: A gateway possible for contact with the demi-plane open at 3 o'clock of the laboratory.", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: Cannot guarantee what kind of dimension will be met due to the instability of the gateway...", bc_map, 0xffff00,FW_NORMAL,13,0;
	sleep 5000;
	mapannounce 'map_name$, "MA-4885: If there are any surviving organisms I wish you luck. I hope to see you again.", bc_map, 0xffff00,FW_NORMAL,13,0;
	enablenpc instance_npcname("lab_warp#end");
	end;
}


// Display effects
1@lab,4,1,5	script	#npc_con03	CLEAR_NPC,{
	end;

function effect_gen {
	.@effect = getarg(0);
	.@count = getargcount();
	for ( .@i = 1; .@i < .@count; .@i++ )
		specialeffect .@effect, AREA, getd( "'gen_" + ( getarg(.@i) < 10 ? "0" : "" ) + getarg(.@i) + "$" );
	return;
}

function effect_1_10_gen { effect_gen( getarg(0), 1,2,3,4,5,6,7,8,9,10 ); }

OnStart:
	effect_gen( EF_MAGICALATTHIT, 1 );
	sleep 2000;
	effect_gen( EF_MAGICALATTHIT, 1,3 );
	sleep 1900;
	effect_gen( EF_MAGICALATTHIT, 1,3,8 );
	sleep 1700;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4 );
	sleep 1600;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6 );
	sleep 1500;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2 );
	sleep 1400;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7 );
	sleep 1300;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9 );
	sleep 1200;
	effect_gen( EF_MAGICALATTHIT, 1,3,8,4,6,2,7,9,5 );
	sleep 1100;
	while( 'bool_event ) {
		effect_1_10_gen( EF_MAGICALATTHIT );
		sleep 1000;
	}
	end;
OnStart2:
	effect_gen( EF_BLASTMINEBOMB, 1 );
	sleep 2000;
	effect_gen( EF_BLASTMINEBOMB, 1,3 );
	sleep 1900;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8 );
	sleep 1800;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4 );
	sleep 1700;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6 );
	sleep 1600;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2 );
	sleep 1500;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7 );
	sleep 1400;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9 );
	sleep 1300;
	effect_gen( EF_BLASTMINEBOMB, 1,3,8,4,6,2,7,9,5 );
	sleep 1200;
	while( 'bool_event ) {
		effect_1_10_gen( EF_BLASTMINEBOMB );
		sleep 1100;
	}
	end;

OnStart3:
	while( 'bool_event ) {
		specialeffect EF_LORD, AREA, 'gen_11$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_12$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_13$;
		sleep 2000;
		specialeffect EF_LORD, AREA, 'gen_14$;
		sleep 2000;
	}
	end;
}

1@lab,91,80,5	script	#gen_01	CLEAR_NPC,{ end; }
1@lab,96,81,5	duplicate(#gen_01)	#gen_02	CLEAR_NPC
1@lab,99,85,5	duplicate(#gen_01)	#gen_03	CLEAR_NPC
1@lab,99,91,5	duplicate(#gen_01)	#gen_04	CLEAR_NPC
1@lab,96,95,5	duplicate(#gen_01)	#gen_05	CLEAR_NPC
1@lab,91,97,5	duplicate(#gen_01)	#gen_06	CLEAR_NPC
1@lab,85,96,5	duplicate(#gen_01)	#gen_07	CLEAR_NPC
1@lab,82,91,5	duplicate(#gen_01)	#gen_08	CLEAR_NPC
1@lab,82,86,5	duplicate(#gen_01)	#gen_09	CLEAR_NPC
1@lab,85,81,5	duplicate(#gen_01)	#gen_10	CLEAR_NPC
1@lab,90,97,5	duplicate(#gen_01)	#gen_11	CLEAR_NPC
1@lab,82,89,5	duplicate(#gen_01)	#gen_12	CLEAR_NPC
1@lab,91,80,5	duplicate(#gen_01)	#gen_13	CLEAR_NPC
1@lab,99,88,5	duplicate(#gen_01)	#gen_14	CLEAR_NPC


// Warps
1@lab,102,28,0	warp2	lab_warp#1	2,2,1@lab,97,28
1@lab,80,28,0	warp2	lab_warp#2	2,2,1@lab,75,28
1@lab,58,28,0	warp2	lab_warp#3	2,2,1@lab,53,28
1@lab,29,38,0	warp2	lab_warp#4	2,2,1@lab,29,43
1@lab,48,82,0	warp2	lab_warp#5	2,2,1@lab,58,82
1@lab,123,88,0	warp2	lab_warp#end	2,2,verus01,152,150


//-- GM NPCs
1@lab,1,1,3	script	#adsw_	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0,0,2000) < 1)
		end;
	mes "[Time Administrator]";
	mes "Which time do you want to go?";
	next;
	switch( select( "Cancel", "1 start", "2 start", "3 start", "4 start" ) ) {
	case 1:
		close;
	case 2:
		donpcevent instance_npcname("#npc_con01") + "::OnStart";
		break;
	case 3:
		donpcevent instance_npcname("#npc_con06") + "::OnStart";
		break;
	case 4:
		donpcevent instance_npcname("#npc_con08") + "::OnStart";
		break;
	case 5:
		end;
	}
	warp instance_mapname("1@lab"), 84,90;
	end;
}

verus01,1,5,5	script	Laboratory Administrator#pa0829_01	CLEAR_NPC,{
	if (callfunc("F_GM_NPC",1854,0,0,2000) < 1) {
		warp "prontera",100,100;
		end;
	}
	mes "[Laboratory Administrator]";
	mes "All handled well!";
	close2;
	if (isbegin_quest(12347) > 0)
		erasequest 12347;
	end;
}
