//===== rAthena Script =======================================
//= Instances Werner Laboratory central room.
//===== Description: =========================================
//- [Walkthrough conversion]
//- Require Terra Gloria main quest.
//- Note: For now there are 2 instances (officially one) to
//  avoid exploit, the warp entry being the same. The first
//  (main quest) is a solo instance and the 2nd (daily) a
//  a party instance. The instance is a basic version,
//  KRO added Seyren boss fight in the next updates.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

slabw01,232,91,5	duplicate(dummy_cloaked_npc)	Skia#ep162_06	4_EP16_SKIA
slabw01,233,92,3	duplicate(dummy_cloaked_npc)	Scared researcher#ep162	4_M_ALCHE_A

// Werner Laboratory central room (solo instance)
slabw01,236,91,3	script	Rookie#ep162_04	4_M_ROOKIE,{
	if (terra_gloria_main == 20) {
		mes "[Rookie]";
		mes "มาดูที่นี้.";
		mes "ตามแบบแปลนส่วนนี้เชื่อมต่อกับห้องกลาง ... แต่ฉันเห็นประตูที่น่าสงสัยเท่านั้น";
		next;
		mes "[Rookie]";
		mes "ไม่น่าแปลกใจที่ดูเหมือนว่าพวกเขามีอะไรสำรองไว้";
		mes "เราจะใช้ระเบิดและระเบิดได้ไหม?";
		next;
		cloakoffnpc( "Skia#ep162_06", getcharid(0) );
		cloakoffnpc( "Scared researcher#ep162", getcharid(0) );
		npctalk "รอ!", "Skia#ep162_06", bc_self;
		mes "[Skia]";
		mes "ไม่ได้หยาบคาย!";
		mes "ฉันตัดสินใจได้ดีมากเมื่อมาที่นี่";
		mes "บอกให้เขาเปิดสิ่งนี้";
		next;
		npctalk "ว้าว! ผู้หญิงคนนี้ฉลาดทีเดียว!", "", bc_self;
		npctalk "ทำไม ... คุณทำกับฉันแบบนี้!", "Scared researcher#ep162", bc_self;
		mes "[Skia]";
		mes "ฉันถามเขาว่าเขารู้เส้นทางตรงนี้ไหมและคำตอบของเขาคือใช่ ฉันจึงพาเขามาที่นี่";
		mes "เขาจะเปิดใช้งานประตูสู่ห้องกลาง";
		next;
		npctalk "นี่ ... คือวิธีทำ", "Scared researcher#ep162", bc_self;
		mes "[Rookie]";
		mes "อืมฉันเห็น นั่นคือวิธีการใช้งาน";
		mes "เยี่ยมมากตอนนี้ฉันเข้าใจแล้ว";
		mes "และเราไม่ต้องการเขาอีกต่อไป.";
		next;
		npctalk "ทะ ... อะไรนะ! ได้โปรดอย่าฆ่าฉัน!", "Scared researcher#ep162", bc_self;
		mes "[Skia]";
		mes "จริงๆ? จากนั้นฉันจะนำนักวิจัยคนนี้ไปยังฝูงบินด้านนอก";
		mes "เกี่ยวกับห้องกลาง " + strcharinfo(0) + " ฉันไว้วางใจคุณ!";
		next;
		cloakonnpc( "Skia#ep162_06", getcharid(0) );
		cloakonnpc( "Scared researcher#ep162", getcharid(0) );
		mes "[Rookie]";
		mes "ฮ่าฮ่าพวกเขาให้เราเปิดประตูห้องกลาง!";
		mes "โปรดพูดกับฉันอีกครั้งเมื่อคุณพร้อมที่จะเข้าไปที่นี่";
		next;
		mes "[Rookie]";
		mes "หลังจากเปิดใช้งานประตูเราต้องป้องกันจากด้านนอก";
		mes "ฉันไม่สามารถไปที่นั่นกับคุณได้";
		terra_gloria_main = 21;
		close;
	}
	if (terra_gloria_main == 21) {
		if (is_party_leader() == false) {
			mes "^4d4dff กิจกรรมนี้ดำเนินต่อไปยังอนุสรณ์สถาน";
			mes "โปรดลองอีกครั้งหลังจากจัดงานปาร์ตี้ ^000000";
			close;
		}
		mes "[Rookie]";
		mes "คุณพร้อมไหม?";
		mes "ถ้าคุณเข้ามาตอนนี้ฉันจะเปิดใช้งานประตู";
		next;
		if (select( "เข้าเลย.", "ห้ามเข้า." ) == 2) {
			mes "[Rookie]";
			mes "เราทั้งคู่รู้ดีว่าเราไม่มีเวลาสำหรับเรื่องนี้ใช่ไหม";
			close;
		}
		mes "[Rookie]";
		mes "ว้าว! นี่ไง! ฉันเรียนรู้สิ่งต่างๆได้เร็วเสมอ!";
		mes "เข้าไปได้เลย!";
		cloakoffnpc( "Central_room#evt_gate01", getcharid(0) );
		instance_create("Werner Laboratory central room#1");	// officially there is only one 'Werner Laboratory central room' instance
		close;
	}
	if (terra_gloria_main == 22) {
		mes "[Rookie]";
		mes "คุณเรียกคืนสิ่งของหรือไม่?";
		close;
	}
	switch( checkquest(7742,HUNTING) ) {
	case -1:
		mes "[Rookie]";
		mes "เราได้พบปรากฏการณ์ประหลาดในห้องกลาง เราไม่ได้ตรวจสอบ";
		mes "บางทีอาจมีเรื่องการทดลองที่แข็งแกร่งหลงเหลืออยู่ข้างใน";
		next;
		mes "[Rookie]";
		mes "สิ่งอำนวยความสะดวกประเภทนี้คืออะไร?";
		mes "Ssss.";
		mes "เมื่อมีการระบุอย่างเหมาะสมฉันอาจขอให้คุณหาข้อยุติ ...";
		mes "ฉันจะต้องเฝ้าประตูห้องกลางอีกครั้ง";
		close;
	case 0:
	case 1:
		if (getcharid(1) < 1) {
			mes "^4d4dff กิจกรรมนี้ดำเนินต่อไปยังอนุสรณ์สถาน";
			mes "โปรดลองอีกครั้งหลังจากจัดงานปาร์ตี้ ^000000";
			close;
		}
		if (is_party_leader() == false) {
			mes "[Rookie]";
			mes "หากมีประตูที่เปิดใช้งานแล้วคุณสามารถดูได้";
			next;
			mes "[Rookie]";
			mes "มิฉะนั้นโปรดขอทางเข้าหัวหน้าพรรคของคุณ";
			close;
		}
		mes "[Rookie]";
		mes "คุณพร้อมไหม?";
		mes "ถ้าคุณเข้ามาตอนนี้ฉันจะเปิดใช้งานประตู";
		next;
		if (select( "เข้าเลย.", "ห้ามเข้า." ) == 2) {
			mes "[Rookie]";
			mes "เราทั้งคู่รู้ดีว่าเราไม่มีเวลาสำหรับเรื่องนี้ใช่ไหม";
			close;
		}
		mes "[Rookie]";
		mes "ว้าว! นี่ไง! ฉันเรียนรู้สิ่งต่างๆได้เร็วเสมอ!";
		mes "เข้าไปได้เลย!";
		cloakoffnpc( "Central_room#evt_gate01", getcharid(0) );
		instance_create("Werner Laboratory central room#2");
		close;
	case 2:
		mes "[Rookie]";
		mes "เราไม่แน่ใจว่ามีผู้ทดลองอีกกี่คนที่ไอเซนเวอร์เนอร์สร้างและละทิ้งไป";
		next;
		mes "[Rookie]";
		mes "แต่ขอเรียกว่าวัน";
		close;
	}
	end;

OnInit:
	questinfo( QTYPE_QUEST, QMARK_YELLOW, " checkquest (7742, HUNTING) == 0 || checkquest (7742, HUNTING) == 1" );
	end;
}

slabw01,246,88,3	script	Central_room#evt_gate01	PORTAL,{
	if (terra_gloria_main != 21 && terra_gloria_main < 23)
		end;
	if (getcharid(1) < 1 || (terra_gloria_main == 21 && is_party_leader() == false)) {
		mes "^4d4dffMD เฉพาะหัวหน้าปาร์ตี้เท่านั้นที่สามารถเข้าสู่ [ห้องกลาง] ได้ในขณะนี้ ^000000";
		close;
	}
	mes "คุณสามารถย้ายไปที่ห้องกลาง";
	next;
	if (select( "เข้าไป.", "อย่าเข้าไป." ) == 2) {
		mes "[Rookie]";
		mes "อ้อ ... ต้องเปิดทุกครั้ง มันไม่สะดวก!";
		close;
	}
	if (terra_gloria_main == 21)
		.@md_name$ = "Werner Laboratory central room#1";
	else
		.@md_name$ = "Werner Laboratory central room#2";	// officially player without the daily hunting quest can enter in the instance, allowing to spam the instance without delay
	switch( instance_enter(.@md_name$) ) {
	case IE_NOMEMBER:
		end;
	case IE_NOINSTANCE:
		mes "[Rookie]";
		mes "ประตูทำงานผิดปกติในขณะนี้ มันอันตรายแบบนี้ ...";
		mes "กรุณารอสักครู่";
		close;
	case IE_OTHER:
		mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น.";
		close;
	case IE_OK:
		mapannounce "slabw01", "" + strcharinfo(0) + " ของงานปาร์ตี้ " + getpartyname( getcharid(1) ) + "กำลังเข้าสู่ " + .@md_name$ + ".", bc_map, 0xFF99;
		// warp "1@slw",187,24;
		end;
	}
	end;

OnInit:
	cloakonnpc( strnpcinfo(0) );
	end;
}

// warps
1@slw,187,170,0	warp	#slwwarp01	1,1,1@slw,71,32
1@slw,71,77,0	warp	#slwwarp02	1,1,1@slw,54,134
1@slw,55,150,0	warp	Emergency exit#exit_war	1,1,que_swat,155,58

// Entrance - event type
1@slw,187,25,0	script	#sl_evt01	HIDDEN_WARP_NPC,2,2,{
	end;
OnTouch:
	if (is_party_leader() == false)
		end;
	disablenpc instance_npcname("#sl_evt01");
	if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")	// terra_gloria_main == 21
		enablenpc instance_npcname("Eisen Werner#werner01");
	else {
		enablenpc instance_npcname("Guard#gard01");
		enablenpc instance_npcname("Guard#gard02");
		enablenpc instance_npcname("Guard#gard03");
		enablenpc instance_npcname("Guard#gard04");
	}
	end;

OnInstanceInit:
	'device_state[0] = 'device_state[1] = 0;
	'event_final = 0;

	'map_slw$ = instance_mapname("1@slw");

	disablenpc instance_npcname("#slwwarp01");
	disablenpc instance_npcname("#slwwarp02");
	disablenpc instance_npcname("Central Entrance#door");
	disablenpc instance_npcname("Emergency exit#exit_war");

	disablenpc instance_npcname("Eisen Werner#werner01");
	disablenpc instance_npcname("Eisen Werner#werner02");
	disablenpc instance_npcname("Eisen Werner#werner03");
	disablenpc instance_npcname("Eisen Werner#werner04");
	disablenpc instance_npcname("Eisen Werner#werner05");

	disablenpc instance_npcname("Security device#switch01");
	disablenpc instance_npcname("Security device#switch02");
	disablenpc instance_npcname("Security device#switch11");
	disablenpc instance_npcname("Security device#switch12");

	disablenpc instance_npcname("Guard#gard01");
	disablenpc instance_npcname("Guard#gard02");
	disablenpc instance_npcname("Guard#gard03");
	disablenpc instance_npcname("Guard#gard04");
	disablenpc instance_npcname("Guard#gard05");
	disablenpc instance_npcname("Guard#gard06");
	disablenpc instance_npcname("Guard#gard07");

	disablenpc instance_npcname("Seyren Windsor#seiren");
	disablenpc instance_npcname("Pet child#boss_master");
	disablenpc instance_npcname("#boss_master_dummy");
	end;
}

// Room 1
// * Samples and devices
1@slw,174,145,3	script	Displayed sample#01	CLEAR_NPC,{
	setarray .@sample$[0],
		"^ff0000 ความบริสุทธิ์ ^000000", "MT-Wxx0s57b",
		"^4d4dffRose ^000000", "MT-Wxx00b21",
		"^4d4dff การเปรียบเทียบ ^000000", "MT-Wxx023-f1",
		"^4d4dffJoy ^000000", "OT-Wxx01- ค",
		"^4d4dffSea ^000000", "MT-Wxc1c1b",
		"^ff0000Eternity ^000000", "MT-Wx267b",
		"^4d4dff กลับบ้านเลย ^000000", "OTM-Wxff01",
		"^4d4dff ความเหงา ^000000", "MT-Wxx00c46",
		"^4d4dffGlow ^000000", "OT-Wxx4d4dff",
		"^ff0000Dawn ^000000", "MT-Wff_01v",
		"^4d4dffTwilight ^000000", "OT-Wxx00c3b",
		"^4d4dffGirl ^000000 ของ ^ff0000dream ^000000", "MT-Wxx0ax2-1";
	.@num = (atoi(strnpcinfo(2)) - 1) * 2;
	mes "[" + .@sample$[.@num] + "]";
	mes "Experiment_number: " + .@sample$[.@num+1] + "";
	close;
}
1@slw,201,145,3	duplicate(Displayed sample#01)	Displayed sample#02	CLEAR_NPC
1@slw,174,132,3	duplicate(Displayed sample#01)	Displayed sample#03	CLEAR_NPC
1@slw,201,132,3	duplicate(Displayed sample#01)	Displayed sample#04	CLEAR_NPC
1@slw,174,118,3	duplicate(Displayed sample#01)	Displayed sample#05	CLEAR_NPC
1@slw,201,118,3	duplicate(Displayed sample#01)	Displayed sample#06	CLEAR_NPC
1@slw,174,104,3	duplicate(Displayed sample#01)	Displayed sample#07	CLEAR_NPC
1@slw,201,104,3	duplicate(Displayed sample#01)	Displayed sample#08	CLEAR_NPC
1@slw,174,90,3	duplicate(Displayed sample#01)	Displayed sample#09	CLEAR_NPC
1@slw,201,90,3	duplicate(Displayed sample#01)	Displayed sample#10	CLEAR_NPC
1@slw,174,76,3	duplicate(Displayed sample#01)	Displayed sample#11	CLEAR_NPC
1@slw,201,76,3	duplicate(Displayed sample#01)	Displayed sample#12	CLEAR_NPC

// * Events - First entrance
1@slw,188,42,3	script	Eisen Werner#werner01	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "มาที่นี่ตัวละครหลัก";
	mes "บอกตามตรงว่าฉันประทับใจ ฉันไม่เคยคาดหวังว่าคุณจะมาที่นี่";
	next;
	mes "[Eisen Werner]";
	mes "ดีอะไรก็ได้";
	mes "ยินดีต้อนรับ.";
	mes "ยินดีต้อนรับสู่โลกของ Eisen Werner";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "ฉันไม่เคยคิดมาก่อนว่ามันจะเสร็จสิ้นลงในทันทีเช่นนี้";
	mes "ก็ตาม. ใครสน?";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "ฉันเลิกช่วยห้องทดลองนี้แล้ว";
	mes "คุณต้องการที่จะดู?";
	next;
	mes "[Eisen Werner]";
	mes "มีหลายสิ่งหลายอย่างรวมถึงรอยแตกในมิติ ฯลฯ";
	mes "ในบางโลกกระแสของเวลาก็บิดเบี้ยว";
	next;
	mes "[Eisen Werner]";
	mes "มันคือโลกคู่ขนาน?";
	mes "หรือเคยมีใครมาถึงจุดหนึ่งในอดีต?";
	next;
	mes "[Eisen Werner]";
	mes "เวลาหมายถึงอะไร? คุณเคยคิดบ้างไหม?";
	mes "การไหลของมันสัมพันธ์กัน";
	next;
	mes "[Eisen Werner]";
	mes "ผู้ชายคนนั้นหยุดเดี๋ยวนี้";
	mes "ถ้าเวลาผ่านไปทางชีวภาพก็หมายความว่ามีคนแก่ขึ้น";
	next;
	mes "[Eisen Werner]";
	mes "ที่ถูกหยุดโดยการบังคับ";
	mes "ในกรณีนั้นเราใช้นิพจน์เช่นนี้";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "เวลาได้หยุดลง";
	next;
	mes "[Eisen Werner]";
	mes "....";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "คุณต้องการที่จะดูภายใน?";
	mes "ใช้เวลาของคุณและมองผ่านทางเดิน";
	next;
	mes "[Eisen Werner]";
	mes "ฉันจะแนะนำคุณ";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner01");
		enablenpc instance_npcname("Eisen Werner#werner02");
	}
	end;
}

1@slw,189,164,3	script	Eisen Werner#werner02	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "คุณสนุกกับงานของฉันไหม";
	mes "ฉันส่งพวกโง่ ๆ กลับไปเพื่อเริ่มต้นใหม่";
	next;
	mes "[Eisen Werner]";
	mes "มันไม่ง่ายเลย";
	mes "......";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "ถ้าแค่เด็กคนนั้นกลับไปเขาคงไม่จบแบบนั้น ...";
	next;
	mes "[Eisen Werner]";
	mes "ฉันหมายถึงถ้าเขาหยุด ...";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "ประธาน ... ฉันหมายถึงคนนั้นหมกมุ่นอยู่กับอดีต";
	mes "ขอบคุณประธานที่ทำให้ฉันค้นคว้าได้ตามที่ต้องการ";
	next;
	mes "[Eisen Werner]";
	mes "คุณไม่อยากรู้เรื่องนี้เหรอ?";
	mes "ทำไมเขาถึงหมกมุ่นกับอดีตมากขนาดนี้?";
	next;
	mes "[Eisen Werner]";
	mes "ยังไงก็ตามคุณต้องใช้เครื่องมือทางด้านขวาและด้านซ้ายถ้าคุณต้องการที่จะไปไกลกว่านี้ .... คุณสามารถทำได้หรือไม่?";
	next;
	mes "[Eisen Werner]";
	mes "เนื่องจากเป็นอุปกรณ์ที่เรียบง่ายฉันเดาว่าคุณทำได้";
	mes "เปลี่ยน ^ff0000red ^000000 เป็น ^4d4dffblue ^000000";
	mes "และเปลี่ยน ^4d4dffblue ^000000 เป็น ^ff0000red ^000000 แค่นั้นแหละ.";
	next;
	mes "[Eisen Werner]";
	mes "ถ้าคุณดูงานของฉันอย่างถูกต้องคุณจะเข้าใจสิ่งที่ฉันพูด";
	mes "แล้วเจอกันโซนถัดไป";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner02");
		enablenpc instance_npcname("Security device#switch01");
		enablenpc instance_npcname("Security device#switch02");
	}
	end;
}

1@slw,155,191,3	script	Security device#switch01	CLEAR_NPC,{
	if (is_party_leader() == false)
		end;
	.@number = atoi( replacestr( strnpcinfo(2), "switch", "" ) );
	.@type = (.@number < 10) ? 0 : 1;
	.@npc_num = (.@number < 10) ? .@number : (.@number-10);
	if ('device_state[.@type] & .@npc_num) {
		mes "เปิดใช้งานแล้ว";
		close;
	}
	callsub S_Code, .@string$, .@answer;
	mes "มีรหัสความปลอดภัยเขียนลงไป";
	mes "[" + .@string$ + "]";
	mes "คุณจะป้อนข้อมูลอะไร";
	next;
	if (select( "^ff0000" + .@string$ + "^000000", "^4d4dff" + .@string$ + "^000000" ) != .@answer) {
		'device_state[.@type] = 0;
		mes "เริ่มต้นอุปกรณ์แล้ว";
		close;
	}
	if (is_party_leader() == false)
		end;
	'device_state[.@type] = 'device_state[.@type] | .@npc_num;
	if ('device_state[.@type] != 3) {
		mes "ใช้งานอุปกรณ์รักษาความปลอดภัย";
		mes "ดูเหมือนว่ามีอุปกรณ์รักษาความปลอดภัยอีกหนึ่งอย่าง";
		close;
	}
	mes "ใช้งานอุปกรณ์รักษาความปลอดภัย";
	mes "ตอนนี้ประตูเปิดใช้งานแล้ว";
	switch( .@type ) {
	case 0:
		enablenpc instance_npcname("#slwwarp01");
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")	// terra_gloria_main == 21
			enablenpc instance_npcname("Eisen Werner#werner03");
		else {
			enablenpc instance_npcname("Guard#gard05");
			enablenpc instance_npcname("Guard#gard06");
			enablenpc instance_npcname("Guard#gard07");
		}
		break;
	case 1:
		if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1")
			enablenpc instance_npcname("Eisen Werner#werner04");
		else
			enablenpc instance_npcname("Central Entrance#door");
		enablenpc instance_npcname("#slwwarp02");
		break;
	}
	close;

S_Code:
	setarray .@sample$[0],
		"Purity", 2,
		"Eternity", 2,
		"Dawn", 2,
		"Rose", 1,
		"Contradiction", 1,
		"Joy", 1,
		"Sea", 1,
		"Way back home", 1,
		"Loneliness", 1,
		"Glow", 1,
		"Twilight", 1;
	.@size = getarraysize(.@sample$) / 2;
	.@r = rand(.@size) * 2;
	set getarg(0), .@sample$[.@r];
	set getarg(1), .@sample$[.@r+1];
	return;
}
1@slw,220,191,3	duplicate(Security device#switch01)	Security device#switch02	CLEAR_NPC
1@slw,22,61,3	duplicate(Security device#switch01)	Security device#switch11	CLEAR_NPC
1@slw,122,61,3	duplicate(Security device#switch01)	Security device#switch12	CLEAR_NPC


// * Events - Daily
1@slw,188,58,3	script	Guard#gard01	3622,4,4,{
	end;
OnTouch:
	.@num = atoi( replacestr( strnpcinfo(2), "gard0", "" ) );
	getmapxy .@map$,.@x,.@y, BL_NPC;
	switch( .@num ) {
	case 1:	// 2 spawn area
		npctalk ".. แดงเข้ามาได้ยังไง?";
		areamonster .@map$, 186,92,190,94, "Special Guard", 3622,3;	// EP16_2_MM_S_GUARDS
		.@mob_count = 3;
		break;
	case 2:	// 3 spawn area
		npctalk ".. แดงเข้ามาได้ยังไง?";
		areamonster .@map$, 186,142,190,143, "Special Guard", 3622,3;	// EP16_2_MM_S_GUARDS
		areamonster .@map$, 186,160,190,162, "Special Guard", 3622,3;
		.@mob_count = 3;
		break;
	case 5:
		npctalk ".. แดงเข้ามาได้ยังไง?";
		.@mob_count = 3;
		break;
	case 3:
		npctalk ".. แดงเข้ามาได้ยังไง?";
		.@mob_count = 1;
		.@label$ = instance_npcname("#security_device_lab") + ":: OnLeft1";
		break;
	case 4:
		npctalk ".. แดงเข้ามาได้ยังไง?";
		.@mob_count = 3;
		.@label$ = instance_npcname("#security_device_lab") + ":: OnRight1";
		break;
	case 6:
		npctalk "ใครอยู่ตรงนั้นน่ะ?";
		.@mob_count = 3;
		.@label$ = instance_npcname("#security_device_lab") + ":: OnLeft2";
		break;
	case 7:
		npctalk "ให้ฉันเสร็จคุณ!";
		.@mob_count = 3;
		.@label$ = instance_npcname("#security_device_lab") + ":: OnRight2";
		break;
	}
	disablenpc instance_npcname( strnpcinfo(0) );
	areamonster .@map$, (.@x-2), (.@y-2), (.@x+2), (.@y+2), "Special Guard", 3622, .@mob_count, .@label$;	// EP16_2_MM_S_GUARDS
	end;
}
1@slw,189,117,3	duplicate(Guard#gard01)	Guard#gard02	3622,4,4
1@slw,171,167,3	duplicate(Guard#gard01)	Guard#gard03	3622,4,4
1@slw,206,167,3	duplicate(Guard#gard01)	Guard#gard04	3622,4,4
1@slw,70,38,5	duplicate(Guard#gard01)	Guard#gard05	3622,4,4
1@slw,33,52,3	duplicate(Guard#gard01)	Guard#gard06	3622,4,4
1@slw,111,52,3	duplicate(Guard#gard01)	Guard#gard07	3622,4,4

1@slw,1,1,3	script	#security_device_lab	HIDDEN_WARP_NPC,{
	end;
OnLeft1:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + ":: OnLeft1" ) < 1) {
		mapannounce 'map_slw$, "เปิดใช้งานอุปกรณ์รักษาความปลอดภัย L-01 แล้ว", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch01");
	}
	end;
OnRight1:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + ":: OnRight1" ) < 1) {
		mapannounce 'map_slw$, "เปิดใช้งานอุปกรณ์รักษาความปลอดภัย R-01 แล้ว", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch02");
	}
	end;
OnLeft2:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + ":: OnLeft2" ) < 1) {
		mapannounce 'map_slw$, "เปิดใช้งานอุปกรณ์รักษาความปลอดภัย L-02 แล้ว", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch11");
	}
	end;
OnRight2:
	if (mobcount( 'map_slw$, instance_npcname("#security_device_lab") + ":: OnRight2" ) < 1) {
		mapannounce 'map_slw$, "เปิดใช้งานอุปกรณ์รักษาความปลอดภัย R-02 แล้ว", bc_map,0xFF99;
		enablenpc instance_npcname("Security device#switch12");
	}
	end;
}


// Room 2
// * Events - First entrance
1@slw,72,70,3	script	Eisen Werner#werner03	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "ง่ายกว่าที่คุณคิด?";
	mes "อุปกรณ์รักษาความปลอดภัยรุ่นต่อไปก็คล้าย ๆ กันคงไม่ต้องอธิบายอีก";
	next;
	mes "[Eisen Werner]";
	mes "หลังจากดูรอบ ๆ ที่นี่คุณคิดอย่างไร?";
	mes "มันไม่น่าทึ่งเหรอ?";
	mes "คุณเคยไปที่ ^4d4dffVerus ^000000 หรือไม่";
	next;
	mes "[Eisen Werner]";
	mes "นั่นคือห้องทดลองที่ฉันสร้างขึ้นโดยอ้างถึง";
	mes "มันเป็นประเภทของฉัน";
	mes "......";
	next;
	mes "[Eisen Werner]";
	mes "เทคโนโลยีนี้แตกต่างอย่างสิ้นเชิงกับสิ่งที่เรามีจนถึงตอนนั้น";
	mes "เราใช้ทุกอย่างที่มี";
	next;
	mes "[Eisen Werner]";
	mes "พูดว่าอะไรนะ?";
	mes "มันดูดีไหม?";
	next;
	select("คุณสงสัยไหมว่าทำไมฉันถึงพูดแบบนี้?");
	mes "[Eisen Werner]";
	mes "อืมนั่นเป็นเพราะเธอกำลังจะตายที่นี่อยู่ดี";
	mes "ดังนั้นฉันจึงอยากจะส่งเสริมความสำเร็จของฉัน";
	next;
	select("ตายที่นี่? ผม?");
	mes "[Eisen Werner]";
	mes "แน่นอน. คุณจะตายที่นี่";
	mes "คุณคิดว่าฉันโง่พอที่จะถูกปล่อยให้อยู่ที่นี่คนเดียวหรือไม่?";
	next;
	mes "[Eisen Werner]";
	mes "ฉันรู้ว่าคุณมาที่นี่เพื่อเรื่องอาณาจักรรูน - มิดการ์ท";
	mes "ฉันได้เตรียมบางอย่างแล้ว";
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "ด้วยความรู้สึกที่ยิ่งใหญ่";
	mes "คุณจะตาย ที่นี่.";
	next;
	mes "[Eisen Werner]";
	mes "ดังนั้นเพียงแค่เป็นแขกของฉัน";
	mes "ไปโซนถัดไปกันเถอะ";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner03");
		enablenpc instance_npcname("Security device#switch11");
		enablenpc instance_npcname("Security device#switch12");
	}
	end;
}


// Room 3
// * Events - First entrance
1@slw,57,141,3	script	Eisen Werner#werner04	4_M_EISEN,{
	if (is_party_leader() == false)
		end;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "นอกเหนือจากกำแพงนี้จะมีบางสิ่งที่คุณได้รับ";
	next;
	mes "[Eisen Werner]";
	mes "ขวา. สิ่งนั้นที่คุณสลักเป็นอัญมณีและใช้เป็นเครื่องประดับบนมงกุฎ";
	mes "คุณรู้หรือไม่ว่ามันคืออะไร?";
	next;
	mes "[Eisen Werner]";
	mes "แม้ว่ามันจะเป็นเรื่องที่สนใจ แต่ก็เป็นเศษหัวใจของ Ymir";
	mes "เทียบไม่ได้กับการเลียนแบบ";
	next;
	mes "[Eisen Werner]";
	mes "... คนโง่ในอาณาจักรไม่สามารถใช้พลังที่มีอยู่แล้วได้";
	mes "ช่างน่าเสียดาย";
	mes "เลยตัดสินใจใช้ ...";
	next;
	mes "[Eisen Werner]";
	mes "คุณต้องการเรียกคืนหรือไม่? ทุกอย่างขึ้นอยู่กับความสามารถของคุณ";
	mes "ฉันไม่ได้รอด้วยมือเปล่า";
	next;
	mes "[Eisen Werner]";
	mes "ให้ฉันบอกคุณอีกครั้ง ไม่ต้องแปลกใจ.";
	mes "ไม่ฉันจะดีใจถ้าคุณประหลาดใจ";
	mes "หุหุหุหุหุ ....";
	next;
	cutin "ep16_eisen03.bmp",1;
	mes "[Eisen Werner]";
	mes "ตอนนี้มาดูของขวัญที่ฉันเตรียมไว้";
	close2;
	cutin "",255;
	if (is_party_leader() == true) {
		disablenpc instance_npcname("Eisen Werner#werner04");
		enablenpc instance_npcname("Central Entrance#door");
	}
	end;
}

1@slw,54,146,0	script	Central Entrance#door	WARPNPC,1,1,{
	end;
OnTouch:
	if (is_party_leader() == false) {
		warp 'map_slw$,55,153;
		end;
	}
	if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1") {
		if ('event_final == 0) {
			'event_final = 1;
			enablenpc instance_npcname("Eisen Werner#werner05");
		}
		warp 'map_slw$,55,153;
		end;
	}
	switch( 'event_final ) {
	case 1:
		mes "เล่นซ้ำเรื่องราว";
		close2;
		warp 'map_slw$,55,153;
		end;
	case 2:
		warp 'map_slw$,55,153;
		end;
	}
	mes "คุณมีสองทางเลือกในโซนถัดไป";
	mes "หากคุณต้องการเล่นเรื่องราวของ Seyren Windsor อีกครั้งโปรดเลือก ^4d4dff ดำเนินเรื่องต่อไป ^000000 หากคุณต้องการชมผลงานชิ้นเอกของ Eisen Werner โปรดเลือก ^4d4dff ดำเนินการต่อสู้ ^000000";
	next;
	if (select( "ดำเนินเรื่อง.", "ดำเนินการต่อสู้" ) == 1) {
		mes "คุณได้เลือกเรื่องราวแล้ว";
		close2;
		if ('event_final == 0) {
			'event_final = 1;
			enablenpc instance_npcname("Eisen Werner#werner05");
		}
		warp 'map_slw$,55,153;
		end;
	}
	mes "คุณได้เลือกการต่อสู้";
	close2;
	if ('event_final == 0) {
		'event_final = 2;
		enablenpc instance_npcname("Pet child#boss_master");
		enablenpc instance_npcname("#boss_master_dummy");
	}
	warp 'map_slw$,55,153;
	end;
}


// Room Boss
// * Story
1@slw,56,171,3	duplicate(dummy_npc)	Seyren Windsor#seiren	4_M_SEIREN_UC

1@slw,53,171,5	script	Eisen Werner#werner05	4_M_EISEN,{
	if (is_party_leader() == false) {
		mes "^4d4dff มีเพียงหัวหน้าปาร์ตี้เท่านั้นที่สามารถดำเนินกิจกรรมนี้ได้ ^000000";
		close;
	}
	if (checkweight(501,1) == 0) {
		mes "- ไม่สามารถดำเนินการตามภารกิจได้เนื่องจากคุณมีไอเทมในครอบครองมากเกินไป -";
		close;
	}
	.@seyren$ = instance_npcname("Seyren Windsor#seiren");
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "สถานที่แห่งนี้เป็นหัวใจหลักของห้องปฏิบัติการและมีงานวิจัยของฉันทุกอย่าง";
	npctalk "สถานที่แห่งนี้เป็นหัวใจหลักของห้องปฏิบัติการและมีงานวิจัยของฉันทุกอย่าง";
	next;
	mes "[Eisen Werner]";
	mes "มันเป็นส่วนหนึ่งของหัวใจของ Ymir ที่กุมอำนาจพื้นฐานในการประกอบเป็นโลก นอกจากนี้ยังถือเป็นต้นกำเนิดของชีวิต";
	npctalk "มันเป็นส่วนหนึ่งของหัวใจของ Ymir ที่กุมอำนาจพื้นฐานในการประกอบเป็นโลก นอกจากนี้ยังถือเป็นต้นกำเนิดของชีวิต";
	next;
	mes "[Eisen Werner]";
	mes "ดูสิ่งนี้อย่างรอบคอบ";
	mes "สิ่งที่คุณได้รับกลับมาคือการทำสิ่งที่ยอดเยี่ยม!";
	npctalk "ดูสิ่งนี้อย่างรอบคอบ สิ่งที่คุณได้รับกลับมาคือการทำสิ่งที่ยอดเยี่ยม!";
	next;
	mes "[Eisen Werner]";
	mes "พลังงานที่สกัดจากสิ่งนี้ไหลเข้าสู่วงเวทย์ที่พื้น ... ฮ่าฮ่าฮ่า แน่นอนว่าวงเวทย์นี้ถูกตีความใหม่อย่างน่าอัศจรรย์";
	npctalk "พลังงานที่สกัดจากสิ่งนี้ไหลเข้าสู่วงเวทย์ที่พื้น ... ฮ่าฮ่าฮ่า แน่นอนว่าวงเวทย์นี้ถูกตีความใหม่อย่างน่าอัศจรรย์";
	next;
	mes "[Eisen Werner]";
	mes "ไม่ว่าในกรณีใดพลังงานจะเข้าสู่วัตถุทดลอง ... เมื่อใช้พลังนั้นฉันได้ปรับเวลาแล้ว";
	next;
	cutin "ep16_eisen03.bmp",1;
	mes "[Eisen Werner]";
	mes "... แน่นอนว่าบางคนถูกฆ่าตายเพราะเซลล์แตก ฮ่า";
	next;
	select("นั่นคือทั้งหมดที่คุณต้องการจะพูด?");
	unittalk getcharid(3), "" + strcharinfo(0) + " : นั่นคือทั้งหมดที่คุณต้องการจะพูด?";
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "ไม่! ไม่เคย!";
	mes "คุณลืมสิ่งที่ฉันบอกคุณไปก่อนหน้านี้แล้วหรือยัง?";
	mes "ฉันบอกคุณแล้วว่าฉันเตรียมของขวัญ";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "บางอย่าง ...";
	mes "ที่จะทำให้คุณประหลาดใจ";
	close2;
	setpcblock PCBLOCK_NPC, true;
	cutin "",255;
	sleep2 3000;
	enablenpc .@seyren$;
	npctalk "......", .@seyren$;
	sleep2 1000;
	setpcblock PCBLOCK_NPC, false;
	unittalk getcharid(3), "" + strcharinfo(0) + " : ... เซย์เรน?!";
	mes "[Eisen Werner]";
	mes "คุณชอบของขวัญที่ฉันเตรียมไว้อย่างไร?!";
	mes "พอใจมั้ย?";
	cutin "ep16_eisen01.bmp",1;
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "เพื่อนคนนี้เป็นหนึ่งในคณะผู้แทนของราชวงศ์ที่โง่เขลา";
	mes "ขอบคุณการทดลองของผู้กล้าบางคนฉันได้รับสิ่งที่ยอดเยี่ยมนี้";
	next;
	select("คุณกำลังพูดถึง In Vivo Experiment หรือไม่?");
	unittalk getcharid(3), "" + strcharinfo(0) + " : คุณกำลังพูดถึง In Vivo Experiment หรือไม่?";
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "ใช่แน่นอน";
	mes "มีคนมากมายที่ทำใน Vivo Experiments นอกเหนือจากเพื่อนคนนั้น แต่เขาเป็นคนแรกที่กล้าพอที่จะพัฒนาสิ่งมีชีวิต";
	next;
	cutin "ep16_eisen01.bmp",1;
	mes "[Eisen Werner]";
	mes "ใช้ชิ้นส่วนหัวใจเลียนแบบของ Ymir";
	mes "นับเป็นจุดเปลี่ยนที่ยิ่งใหญ่สำหรับ In Vivo Experiments";
	next;
	mes "[Eisen Werner]";
	mes "ฉันพยายามนำมันไปใช้กับสิ่งของหลาย ๆ อย่าง";
	next;
	mes "[Eisen Werner]";
	mes "ยังไงซะ....";
	mes "คุณควรทักทายมัน ขวา?";
	next;
	mes "[Eisen Werner]";
	mes "อา ~. ถูกตัอง.";
	mes "คุณไม่จำเป็นต้องทักทายเพราะคุณทั้งคู่กำลังจะตาย!";
	cutin "ep16_eisen03.bmp",1;
	next;
	cutin "ep16_eisen02.bmp",1;
	mes "[Eisen Werner]";
	mes "เซย์เรนพาแขกเหล่านี้ไปนรก";
	mes "คุณเข้าใจที่ฉันพูดไหม?";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[Seyren]";
	mes ".......";
	npctalk "......", .@seyren$;
	next;
	cutin "ep16_evt_ws.bmp",4;
	mes "[Eisen Werner]";
	mes "หืม?";
	mes "นี่คืออะไรเซย์เรน? ตอนนี้ติดลบหรือเปล่า";
	next;
	mes "[Seyren]";
	mes "ฉันรู้สึกเหมือนได้ตื่นขึ้นจากการหลับใหลอันยาวนาน";
	mes "บางทีมันอาจจะเป็นวันนี้";
	npctalk "ฉันรู้สึกเหมือนได้ตื่นขึ้นจากการหลับใหลอันยาวนาน บางทีมันอาจจะเป็นวันนี้", .@seyren$;
	next;
	mes "[Eisen Werner]";
	mes "อย่าลืมหน้าที่ของคุณ Seyren";
	mes "คุณคืออาวุธ คุณไม่มีอะไรมากไปกว่าหุ่นเชิดในชุดของอัศวินที่เรียกว่า Seyren Windsor";
	next;
	mes "[Eisen Werner]";
	mes "คุณต้องการให้ฉันอธิบายทีละขั้นตอนหรือไม่?";
	mes "หลังจากปลูกถ่ายหุ่นของ Ymir's Heart แล้วร่างกายไม่สามารถทนต่อพลังได้และพังทลายลงในที่สุด";
	next;
	mes "[Eisen Werner]";
	mes "จากนั้นวิญญาณของมันก็ทำซ้ำร่างหลักและแบ่งออกเรื่อย ๆ";
	mes "ในระหว่างกระบวนการนั้นความทรงจำสติจิตวิญญาณความคิดและอัตตาของมันกระจัดกระจายไปหมดเหมือนทราย พวกเขากลายเป็นคนไร้ความหมาย";
	next;
	mes "[Eisen Werner]";
	mes "เมื่อเราปลูกถ่ายหุ่นจำลองของ Ymir's Heart ให้กับคนที่มีจิตใจและร่างกายที่แข็งแกร่งเราคาดหวังว่าบุคคลนั้นจะกลายเป็นอาวุธชีวภาพที่แข็งแกร่งกว่า";
	next;
	mes "[Eisen Werner]";
	mes "เราไม่เคยคาดคิดมาก่อนว่ามันจะซ้ำกันและแบ่งออกอย่างไม่มีที่สิ้นสุด เป็นผลให้การทดลองนั้นถือว่าล้มเหลว";
	next;
	mes "[Eisen Werner]";
	mes "มันเป็นสิ่งมีชีวิตที่ไม่สามารถควบคุมได้ซึ่งเต็มไปด้วยความโกรธเท่านั้น";
	next;
	mes "[Seyren]";
	mes "ฉันอยู่ที่นี่.";
	npctalk "ฉันอยู่ที่นี่.", .@seyren$;
	next;
	mes "[Eisen Werner]";
	mes "คุณรู้อะไรไหม? ตอนนี้ยังแบ่งกันอยู่";
	mes "เพียงแค่เลียนแบบตัวละครหลักพวกมันยังเติบโตอยู่ภายใน";
	next;
	mes "[Eisen Werner]";
	mes "เนื่องจากตอนนี้พวกเขาเป็นสิ่งมีชีวิตที่ไม่สมบูรณ์พวกเขาจึงต้องการที่จะสมบูรณ์แบบ";
	mes "ตอนนี้พวกเขาเป็นสิ่งมีชีวิตที่ไม่สมบูรณ์ดังนั้นพวกเขาจึงต้องการที่จะสมบูรณ์แบบแทนที่จะเลียนแบบร่างกายหลักและใช้ชีวิตเป็นภาพเสมือนจริง";
	next;
	mes "[Eisen Werner]";
	mes "พวกเขาต้องการมีอยู่ในรูปแบบเอกพจน์";
	next;
	mes "[Eisen Werner]";
	mes "และการทดลองเดียวที่ประสบความสำเร็จคือคุณ ตอนนี้เราเรียกคุณว่าเซย์เรน";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[Seyren]";
	mes "...... จุดจบของเจตจำนงของคุณหรือไม่?";
	npctalk "...... จุดจบของเจตจำนงของคุณหรือไม่?", .@seyren$;
	next;
	cutin "ep16_eisen03.bmp",0;
	mes "[Eisen Werner]";
	mes "ไงที่รัก? คุณทำกับฉันไม่ได้";
	mes "คุณต้องเชื่อฟังฉัน!";
	next;
	cutin "ep16_eisen02.bmp",0;
	mes "[Eisen Werner]";
	mes "คุณจำไม่ได้ว่าฉันพยายามมากแค่ไหนที่จะพาคุณออกมาจากนรกนั่น?";
	mes "อย่าหวั่นไหวกับสติของคนที่ตายไปแล้ว";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[Seyren]";
	mes "... ไอเซนเวอร์เนอร์.";
	mes "ผมขอถามคุณอีกครั้ง จุดจบของเจตจำนงของคุณหรือไม่?";
	npctalk "... ไอเซนเวอร์เนอร์. ผมขอถามคุณอีกครั้ง จุดจบของเจตจำนงของคุณหรือไม่?", .@seyren$;
	next;
	cutin "ep16_eisen01.bmp",0;
	mes "[Eisen Werner]";
	mes "ประณาม!";
	mes "ฉันไม่เคยคาดหวังสิ่งนี้มาก่อน!";
	mes "โลกนี้เป็นสถานที่ตลกไม่ใช่หรือ? ฮ่าฮ่าฮ่า";
	next;
	mes "[Eisen Werner]";
	mes "ไม่เคยคิดว่าฉันจะเสร็จจากที่นี่";
	mes "ฉันไม่ต้องการผู้แพ้เช่นกัน";
	close2;
	setpcblock PCBLOCK_NPC, true;
	cutin "",255;
	specialeffect EF_BEGINSPELL3;
	sleep2 1000;
	specialeffect EF_SCREEN_QUAKE;
	sleep2 1000;
	specialeffect EF_LORD;
	disablenpc instance_npcname("Eisen Werner#werner05");
	sleep2 2000;
	setpcblock PCBLOCK_NPC, false;
	npctalk "โอ้ ....", .@seyren$;
	mes "[Seyren]";
	mes "... ไอเซ็น. คุณละทิ้งทุกอย่างแบบนี้";
	npctalk "... ไอเซ็น. คุณละทิ้งทุกอย่างแบบนี้", .@seyren$;
	specialeffect2 EF_DEVIL3;
	cutin "ep16_seiren01.bmp",2;
	next;
	mes "[Seyren]";
	mes ".. อา...";
	npctalk ".. อา...", .@seyren$;
	cutin "ep16_seiren02.bmp",2;
	next;
	mes "[Seyren]";
	mes "ความแข็งกำลังอ่อนลงอีกครั้ง";
	mes "ตอนนี้ฉันมีข้อความด่วนกว่านี้สำหรับคุณมากกว่าสำหรับ Eisen";
	npctalk "ความแข็งกำลังอ่อนลงอีกครั้ง ตอนนี้ฉันมีข้อความด่วนกว่านี้สำหรับคุณมากกว่าสำหรับ Eisen", .@seyren$;
	next;
	mes "[Seyren]";
	mes "ฉันรอคุณมานานแล้ว";
	mes "อย่างที่ Eisen พูดมันไม่สำคัญอีกต่อไปว่าฉันคือ Seyren หรือไม่";
	npctalk "ฉันรอคุณมานานแล้ว อย่างที่ Eisen พูดมันไม่สำคัญอีกต่อไปว่าฉันคือ Seyren หรือไม่", .@seyren$;
	next;
	mes "[Seyren]";
	mes "เนื่องจากฉันมีความสามารถในการคิดฉันไม่รู้ว่าฉันเป็นใครและทำไมฉันถึงมีตัวตน";
	next;
	specialeffect2 EF_DEVIL4;
	mes "[Seyren]";
	mes "การแบ่งส่วนต่อเนื่องสร้างสำเนาของตัวเองนับไม่ถ้วน";
	next;
	mes "[Seyren]";
	mes "พลังที่โอบล้อมด้วยหุ่นจำลองของหัวใจของ Ymir ซึ่งตอบสนองต่อเจตจำนงของฉันและของเพื่อนของฉัน พวกเขาต้องการที่จะรวมตัวกัน แต่ก็แบ่งตัวกันเป็นวรรคเป็นเวร";
	next;
	specialeffect2 EF_DEVIL5;
	cutin "ep16_seiren01.bmp",2;
	mes "[Seyren]";
	mes "จนถึงตอนนี้มีการสร้างสำเนาของตัวเองกี่ชุด?";
	next;
	cutin "ep16_seiren02.bmp",2;
	mes "[Seyren]";
	mes "ฉันสูญเสียอัตตาหลังจากการแบ่งอย่างต่อเนื่อง แต่ถูกสร้างขึ้นใหม่เป็นรูปร่างก่อนหน้า ฉันไม่คิดว่ามันเป็นเรื่องบังเอิญ";
	next;
	mes "[Seyren]";
	mes "ฉันคิดว่าต้องมีเหตุผล";
	next;
	mes "[Seyren]";
	mes "และฉันก็รู้หลังจากดู ^4d4dffTerra Gloria ^000000";
	mes "เหตุผลที่พระเจ้าให้โอกาสนี้แก่ฉัน";
	next;
	specialeffect2 EF_DEVIL6;
	cutin "ep16_seiren01.bmp",2;
	mes "[Seyren]";
	mes "คุณมาเรียกคืนนี้หรือไม่?";
	mes "... แผ่นดินหายไปและเหลือเพียงดาว";
	next;
	specialeffect2 EF_DEVIL7;
	cutin "ep16_seiren02.bmp",2;
	mes "[Seyren]";
	mes "ไอเซนพูดถูก ^4d4dff เซย์เรนวินด์เซอร์ไม่อยู่ในโลกนี้อีกต่อไป ^000000";
	mes "สิ่งที่ฉันมีในตอนนี้คือความตั้งใจอันแรงกล้าที่จะถ่ายทอดบางสิ่งไปเรื่อย ๆ";
	next;
	specialeffect2 EF_SCREEN_QUAKE;
	specialeffect2 EF_DEVIL8;
	cutin "ep16_seiren01.bmp",2;
	mes "[Seyren]";
	mes "บางทีอาจจะทำให้ฉันมีตัวตน";
	mes "แต่ตอนนี้มันจบแล้ว";
	cutin "ep16_seiren02.bmp",2;
	next;
	specialeffect2 EF_DEVIL9;
	cutin "ep16_seiren01.bmp",2;
	mes "[Seyren]";
	mes ".....";
	next;
	mes "[Seyren]";
	mes "ฉันไม่มีเวลามากกว่านี้จริงๆ";
	mes "หลังจากช่วงเวลานี้ ^4d4dffI จะถูกแบ่งและกระจัดกระจาย ^000000 อีกครั้ง";
	next;
	mes "[Seyren]";
	mes "ในขณะที่ฉันยังมีความตั้งใจตอนนี้ฉันมีหนึ่งข้อความที่จะส่งให้คุณ";
	next;
	if (instance_live_info(ILI_NAME) == "Werner Laboratory central room#1") {
		mes "[Seyren]";
		mes "ได้โปรดไปข้างหน้า";
		mes "Seyren Windsor อัศวินแห่ง Prontera ล้มเหลวในงาน";
		next;
		mes "[Seyren]";
		mes "^4d4dffMe และเพื่อนทั้งหมดของฉันเสียชีวิตไปนานแล้ว";
		mes "โปรดอย่าเสียความรักที่มีต่อศพที่ตายไปแล้ว ^000000";
		next;
		mes "[Seyren]";
		mes "พวกเขาก็เหมือนกับผีที่มีเปลือกหุ้ม";
		mes "ฉันโชคดีที่ได้แสดงเจตจำนงสุดท้ายต่อหน้าคุณ";
		next;
		mes "[Seyren]";
		mes "พวกเขาคือ ^4d4dffrelics ^000000 ของฉันและเพื่อน ๆ";
		mes "โปรดมอบให้กับครอบครัวของเรา";
		if (is_party_leader() == true) {
			getitem 25179,1;	// Blessing_Star
			getitem 23087,1;	// Small_Leather_Bag
			erasequest 7739;// Find Star of Blessing!
			setquest 7740;// Star of Blessing obtained
			terra_gloria_main = 22;
		}
		close2;
		setpcblock PCBLOCK_NPC, true;
		cutin "",255;
		specialeffect EF_ICECRASH, AREA, .@seyren$;
		disablenpc .@seyren$;
		// monster 'map_slw$,56,171, "เซย์เรน", SEYREN, 1;
		sleep2 1000;
		specialeffect2 EF_SCREEN_QUAKE;
		sleep2 1000;
		setpcblock PCBLOCK_NPC, false;
		specialeffect2 EF_SCREEN_QUAKE;
		specialeffect2 EF_RAIN_PARTICLE2;
		warp "que_swat",155,50;
		end;
	}
	// daily
	mes "[Seyren]";
	mes "ดีใจมากที่ได้พบคุณในตอนท้าย";
	mes "ขอบคุณ ... บาย.";
	close2;
	setpcblock PCBLOCK_NPC, true;
	cutin "",255;
	specialeffect EF_ICECRASH, AREA, .@seyren$;
	cloakonnpc .@seyren$;
	// monster 'map_slw$,56,171, "เซย์เรน", SEYREN, 1;
	sleep2 1000;
	specialeffect2 EF_SCREEN_QUAKE;
	sleep2 1000;
	specialeffect2 EF_SCREEN_QUAKE;
	specialeffect2 EF_RAIN_PARTICLE2;
	setpcblock PCBLOCK_NPC, false;
	if (is_party_leader() == true)
		'event_final = 0;
	mapannounce 'map_slw$, "เริ่มต้นการตั้งค่าของห้องกลางแล้ว", bc_map,0xFF99;
	warpparty 'map_slw$,54,134,getcharid(1),'map_slw$,2,2;
	disablenpc .@seyren$;
	end;
}

// * Battle
1@slw,55,170,3	duplicate(dummy_npc)	Pet child#boss_master	3621

1@slw,55,170,3	script	#boss_master_dummy	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch:
	if (is_party_leader() == false)	// officially player can spawn multiple boss changing party leader
		end;
	setpcblock PCBLOCK_NPC, true;
	.@npc_name$ = instance_npcname("Pet child#boss_master");
	npctalk "กล้าดียังไงเข้ามา! คุณกล้ามาก!", .@npc_name$;
	sleep2 3000;
	npctalk "คุณต้องเป็นเกียรติที่จะต่อสู้กับฉัน!", .@npc_name$;
	sleep2 3000;
	npctalk ".......", .@npc_name$;
	sleep2 3000;
	npctalk ".. เหี้ยเวอร์. ทำไมคุณถึงผสมฉันกับหมาป่า?", .@npc_name$;
	sleep2 3000;
	npctalk "เอ ... ยังไงคุณก็เป็นเนื้อตาย", .@npc_name$;
	sleep2 3000;
	setpcblock PCBLOCK_NPC, false;
	disablenpc instance_npcname("#boss_master_dummy");
	npctalk "Beeee เตรียม !!!", .@npc_name$;
	specialeffect EF_DANCE_BLADE_ATK, AREA, instance_npcname("Pet child#boss_master");
	specialeffect EF_ALL_FULL_THROTTLE, AREA, instance_npcname("Pet child#boss_master");
	donpcevent instance_npcname("#boss_master_lab") + ":: OnStart";
	mapannounce 'map_slw$, "เปิดใช้งานสนามเวทมนตร์แล้ว", bc_map,0xFF99;
	cloakonnpc instance_npcname("Pet child#boss_master");
	end;
}

1@slw,1,1,3	script	#boss_master_lab	HIDDEN_WARP_NPC,5,5,{
	end;
OnStart:
	enablenpc instance_npcname("#boss_master_lab");
	if (mobcount( 'map_slw$, instance_npcname("#boss_master_lab") + ":: OnMobDead" ) < 1)
		monster 'map_slw$,55,170, "สัตว์เลี้ยงเด็ก", 3621, 1, instance_npcname("#boss_master_lab") + ":: OnMobDead";	// EP16_2_MM_CUTIE
	end;
OnMobDead:
	if (mobcount( 'map_slw$, instance_npcname("#boss_master_lab") + ":: OnMobDead" ) < 1) {
		mapannounce 'map_slw$, "สนามเวทมนตร์ถูกกำจัด สร้างทางเข้าแล้ว", bc_map,0xFF99;
		enablenpc instance_npcname("Emergency exit#exit_war");
		disablenpc instance_npcname("#boss_master_lab");
	}
	end;
}

