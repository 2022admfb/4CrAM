//===== rAthena Script ======================================= 
//= Orc's Memory
//===== By:================================================== 
//= L0ne_W0lf
//===== Current Version:===================================== 
//= 1.7
//===== Compatible With:===================================== 
//= rAthena Project
//===== Description:========================================= 
//= [Official Conversion]
//= Relive the memory of an orc.
//===== Additional Comments:================================= 
//= 1.0 First version. [L0ne_W0lf]
//= 1.1 First round of bugfixes. (bugreport:3928) [L0ne_W0lf]
//=     Fixed some npcs calling wrong events. 
//=     Fixed Depraved Orc Spirit spawn point
//=     Fixed an NPC that was never being enabled.
//= 1.1a Second round from bugreport. (bugreport:3928) [L0ne_W0lf]
//=     Fixed first warp never enabling, issue not seen
//=     because of a local source mod.
//=     Corrected the IDs for the Vengeful Orc Spirit and
//=     Shaman Cargalache, copy/paste fail.
//=     Changed spawn point for dungeon according to spawn
//=     point on iRO.
//= 1.2 More bugfixes, mostly typos. :O (bugreport:3944) [L0ne_W0lf]
//=     Fixed some varaibles (types/values) as well.
//= 1.3 Disabled an exploit related to the orc hero spawn. [L0ne_W0lf]
//=     Corrected the variable type for party leader name.
//= 1.4 Fixed donpcevent calling non-existant npc. (bugreport:4039) [L0ne_W0lf]
//= 1.5 Fixed a checkquest condition never setting quest. [L0ne_W0lf]
//= 1.6 Added 'instance_check_party' command to the script for proper checking if
//=     the invoking party meets the requirements to join the Memorial Dungeon.
//= 1.7 Instance system rewrite. [Euphy]
//============================================================ 

// Entrance
//============================================================
gef_fild10,242,202,0	script	Dimensional Gorge Piece	406,{

	set .@party_id,getcharid(1);
	set .@md_name$,"Orc's Memory";

	if (!instance_check_party(.@party_id,2,30,80)) {
		mes "เฉพาะผู้ใช้ระหว่างเลเวล ^ff00030 ~ 80 ^000000 เท่านั้นที่สามารถเข้าสู่ดันเจี้ยนนี้ได้ ";
		close;
	}

	set .@orctime,checkquest(12059,PLAYTIME);
	if (.@orctime == -1) {
		if (is_party_leader() == true) {
			mes "ยืนยันสถานะปาร์ตี้แล้ว คุณต้องการจองทางเข้า "+.@md_name$+" หรือไม่ ";
			next;
			switch(select("สร้าง "+.@md_name$+":เข้าสู่ดันเจี้ยน:ยกเลิก")) {
			case 1:
				if (instance_create(.@md_name$) < 0) {
					mes "ชื่อปาร์ตี้:" +getpartyname (.@party_id);
					mes "หัวหน้าปาร์ตี้:" +strcharinfo(0);
					mes "^0000ff" +.@md_name$ + "^000000 - การจองล้มเหลว";
					close;
				}
				mes "^0000ff" +.@md_name$ + "^000000- พยายามจองทางเข้า";
				mes "หลังจากทำการจองคุณต้องเลือก 'เข้าดันเจี้ยน' จากเมนูหากคุณต้องการเข้าสู่" +.@md_name$+" ";
				close;
			case 2:
				callsub L_Enter,0;
			case 3:
				close;
			}
		}
		if(select(.@md_name$ + " เข้าสู่ Memorial Dungeon:ยกเลิก") == 2)
			end;
		callsub L_Enter,1;
	} else if (.@orctime == 0 || .@orctime == 1) {
		mes "คุณสามารถเข้าสู่ดันเจี้ยนได้หากสร้างขึ้น ";
		next;
		if(select("เข้าสู่ Dungeon "+.@md_name$+":ยกเลิก") == 2)
			close;
		callsub L_Enter,0;
	} else if (.@orctime == 2) {
		mes "^0000ff บันทึกและเอฟเฟกต์ทั้งหมดที่เกี่ยวข้องกับดันเจี้ยนความทรงจำของออร์คจะถูกลบ ตอนนี้คุณสามารถสร้างใหม่ หรือ เข้าดันเจี้ยนใหม่ได้แล้ว ^000000 ";
		erasequest 12059;
		close;
	}
	mes "ในการสร้างดันเจี้ยนคุณต้องเป็นหัวหน้าปาร์ตี้และมีสมาชิกอย่างน้อย 2 คนในปาร์ตี้ ";
	close;
L_Enter:
	switch(instance_enter("Orc's Memory")) {
	case IE_OTHER:
		mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น.";
		close;
	case IE_NOINSTANCE:
		mes "ความทรงจำของออร์คดันเจี้ยนแห่งอนุสรณ์ไม่มีอยู่";
		mes "ดันเจี้ยนแห่งความทรงจำถูกหัวหน้าปาร์ตี้ทำลายหรือเพราะเวลาที่กำหนดโปรดลองอีกครั้งหลังจากผ่านไป 2 ชั่วโมง";
		close;
	case IE_NOMEMBER:
		mes "เฉพาะสมาชิกในปาร์ตี้เท่านั้นที่สามารถเข้าสู่ Memorial Dungeon ได้ ";
		close;
	case IE_OK:
		mapannounce "gef_fild10", getpartyname( getcharid(1) ) +" สมาชิก ปาร์ตี้ "+strcharinfo(0)+" เข้าสู่ Orc's Memory.",bc_map,"0x00ff99";
		if (checkquest(12059) == -1) setquest 12059;
		//warp "1@orcs",179,15;
		if (getarg(0) == 0) close;
		else end;
	}

}

gef_fild10,238,202,6	script	Mad Scientist#orc	865,{
	if (BaseLevel < 50) {
		mes "Mad Scientist ดูเหมือนจะไม่สังเกตเห็นคุณและเอาแต่พึมพำกับตัวเอง ... ";
		close;
	}
	if (mad == 0) {
		mes "[ Mad Scientist ]";
		mes "ฮ่าฮ่าไม่มีพวกออร์คมาใกล้ฉัน!";
		mes "พลังแห่งช่องเขามิติไร้พ่าย !!";
		next;
		switch(select("Orcs ไม่โจมตีคุณ?:หืมม!")) {
		case 1:
			mes "[ Mad Scientist ]";
			mes "ใช่ใช่ .. ฉันรู้ว่ามันยากที่จะเชื่อฉันไม่โทษเธอ";
			mes "ฉันเคยศึกษาช่องเขามิติที่ค้นพบใกล้กับโมร็อค";
			mes "จากการวิจัยของฉันพบว่าแร่ธาตุมีพลังมหาศาลอยู่ในตัว!";
			next;
			break;
		case 2:
			mes "[ Mad Scientist ]";
			mes "คุณเป็นคนขี้ระแวงใช่มั้ย";
			next;
			break;
		}
		select("Oh? Like what, travel to alternate space?");
		mes "[ Mad Scientist ]";
		mes "ฉลาดมาก! ฉันทำไปแล้วจริงๆเห็นรูปปั้นนั่นไหมนี่ไม่ใช่ Monolith ธรรมดาของคุณ!";
		next;
		mes "[ Mad Scientist ]";
		mes "ฉันสร้างมันขึ้นมาด้วยการผสมผสานระหว่างเทคโนโลยีของเราและพลังงานที่ไร้ขีด จำกัด ที่ฉันค้นพบจากช่องเขา! อยากลองไหม";
		next;
		select("...Um, that doesn't sound legal?");
		mes "[ Mad Scientist ]";
		mes "เฮ้สิ่งที่น่าสนใจมันต้องถูกกฎหมายตั้งแต่เมื่อไหร่!? แล้วไง! คุณสามารถคุยกับออร์คได้! C'mon!";
		next;
		mes "[ Mad Scientist ]";
		mes "มีโอกาสที่พวกเขาจะให้คำแนะนำมากมายเมื่อพวกเขาเห็นคุณดังนั้นเพียงแค่ปีกมัน! พวกเขามักจะพูดในสิ่งเดียวกันจนถึงจุดที่ฉันจำได้!";
		next;
		switch(select("Alright... What do I do?:Umm, maybe not.")) {
		case 1:
			mes "[ Mad Scientist ]";
			mes "ดีมาก! นั่งพนมมือแล้วพยายามทำตัวให้เป็นธรรมชาติเมื่ออยู่ที่นั่น!";
			set mad,1;
			close;
		case 2:
			mes "[ Mad Scientist ]";
			mes "โอเคเป็นอย่างนั้น!";
			close;
		}
	} else if (mad == 1) {
		mes "[ Mad Scientist ]";
		mes "หืม? เดินทางผ่านมิติ โดยรูปปั้นนั้น ได้เลย!";
		close;
	} else {
		set mad,1;
		end;
	}
}

// Orc's Memory Floor 1
//============================================================
1@orcs,180,30,0	script	#Resurrect Monsters1	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Resurrect Monsters1");
	end;

OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnEnable:
	enablenpc instance_npcname("#Resurrect Monsters1");
	monster instance_mapname("1@orcs"),0,0,"Orc Warrior",1023,30,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 30 - mobcount(.@map$,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		set .@mob_ran,rand(1,30);
		if (.@mob_ran > 29) {
			monster .@map$,0,0,"Orc Warrior",1023,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
		}
		else if ((.@mob_ran > 28) && (.@mob_ran < 30)) {
			monster .@map$,0,0,"High Orc",1213,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,10) == 9) {
				mapannounce .@map$, "High Orc:เราต้องการการป้องกันเพิ่มเติม! รับคนเพิ่มที่นี่!",bc_map,"0xff4444";
			}
		}
		else if ((.@mob_ran > 26) && (.@mob_ran < 29)) {
			areamonster .@map$,41,91,51,81,"High Orc",1213,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,10) == 9) {
				mapannounce .@map$, "ออร์คชั้นสูงอยู่ไหน!? รับพวกเขาเพื่อหยุดศัตรู!",bc_map,"0xff4444";
			}
		}
		else {
			areamonster .@map$,17,187,27,177,"High Orc",1213,.@mob_dead_num,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,5) == 3) {
				mapannounce .@map$, "ข้อควรระวัง: กองทัพเริ่มรวมตัวกันที่โซนหมายเลข 4.",bc_map,"0x77ff77";
			}
			if (rand(1,100) == 50) {
				initnpctimer;
			}
		}
	}
	end;

OnTimer10:
	mapannounce instance_mapname("1@orcs"), "เสียงตะโกนของหัวหน้า Orc of Safeguards: ดูเหมือนว่าจะใช้เวลานานกว่าที่คิด เรียกโกเลมสตาแลกติก!",bc_map,"0xff4444";
	end;

OnTimer4010:
	set .@map$, instance_mapname("1@orcs");
	mapannounce .@map$, "Stalactic Golems กำลังขุดออกมาจากใต้ดินลึก.",bc_map,"0x77ff77";
	areamonster .@map$,17,187,27,177,"Stalactic Golem",1278,20,instance_npcname("#Resurrect Monsters1")+"::OnMyMobDead";
	stopnpctimer;
	end;
}

1@orcs,180,30,0	script	#Resurrect Monsters2	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Resurrect Monsters2");
	end;

OnDisable:
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
	end;

OnEnable:
	enablenpc instance_npcname("#Resurrect Monsters2");
	monster instance_mapname("1@orcs"),0,0,"Trained Wolf",1106,15,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 15 - mobcount(.@map$,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead");
	if (rand(1,30) > 15) {
		if (.@mob_dead_num > 0) {
			monster .@map$,0,0,"Trained Wolf",1106,.@mob_dead_num,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
		}
	}
	else {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,17,187,27,177,"Trained Wolf",1106,.@mob_dead_num,instance_npcname("#Resurrect Monsters2")+"::OnMyMobDead";
		}
	}
	end;
}

1@orcs,180,30,0	script	#Resurrect Monsters3	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Resurrect Monsters3");
	end;

OnDisable:
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
	end;

OnEnable:
	enablenpc instance_npcname("#Resurrect Monsters3");
	monster instance_mapname("1@orcs"),0,0,"Orc Archer",1189,15,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 15 - mobcount(.@map$,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead");
	set .@mob_ran,rand(1,30);
	if (.@mob_ran > 29) {
		if (.@mob_dead_num > 0) {
			monster .@map$,0,0,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	else if ((.@mob_ran > 26) && (.@mob_ran < 30)) {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,43,155,47,159,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
			if (rand(1,3) == 3) {
				mapannounce .@map$, "High Orc:โจมตีพวกเขาจากด้านหลัง! ตัดการสนับสนุนของพวกเขา!",bc_map,"0xff4444";
			}
		}
	}
	else {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,17,187,27,177,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	end;
}

1@orcs,180,30,0	script	#Resurrect Monsters4	844,{
	end;

OnInstanceInit:
	enablenpc instance_npcname("#Resurrect Monsters4");
	areamonster instance_mapname("1@orcs"),98,35,178,115,"Anopheles",1627,10,instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("1@orcs");
	set .@mob_dead_num, 10 - mobcount(.@map$,instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		monster .@map$,0,0,"Anopheles",1627,.@mob_dead_num,instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead";
	}
	end;

OnDisable:
	killmonster instance_mapname("1@orcs"),instance_npcname("#Resurrect Monsters4")+"::OnMyMobDead";
	end;
}

1@orcs,180,29,4	script	Kruger#1-1	433,{
	donpcevent instance_npcname("Kruger#1-2")+"::OnEnable";
	end;
}

1@orcs,180,29,4	script	Kruger#1-2	433,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("Kruger#1-2");
	end;

OnEnable:
	disablenpc instance_npcname("Kruger#1-1");
	enablenpc instance_npcname("Kruger#1-2");
	initnpctimer;
	end;

OnTimer10:
	mapannounce instance_mapname("1@orcs"), "Kruger:ให้ตายเถอะ ... ใช้เวลานานขนาดนี้ !! ฉันไม่มีทั้งวัน!!",bc_map,"0xffff00";
	end;

OnTimer5710:
	mapannounce instance_mapname("1@orcs"), "Kruger:แผนของฉันคือปล่อยให้สหายของเราเปิดประตู แต่มันก็พังทลายไปหมดตั้งแต่ฉันถูกจับโดย Orc Shaman.",bc_map,"0xffff00";
	end;

OnTimer14610:
	mapannounce instance_mapname("1@orcs"), "Kruger:เสียงตะโกนของหัวหน้า Orc of Safeguards: ฉันได้กลิ่นหนู .. ส่งหน่วยลาดตระเวนไปที่ทางเข้า!!",bc_map,"0xff4444";
	end;

OnTimer20210:
	mapannounce instance_mapname("1@orcs"), "Kruger:ยี้ .. พวกเขาจะอยู่ที่นี่ทุกนาที ตกลง. Listen to ฉัน now.",bc_map,"0xffff00";
	end;

OnTimer24910:
	mapannounce instance_mapname("1@orcs"), "Kruger:Orc Shaman ได้ปิดผนึกชั้นใต้ดินที่ 1 โดยแบ่งออกเป็น 4 โซน แต่ละโซนมี Enchanted Orc หนึ่งตัวที่มีพลังในการปลดผนึกโซนถัดไป.",bc_map,"0xffff00";
	end;

OnTimer34310:
	mapannounce instance_mapname("1@orcs"), "Kruger:ค้นหา Orcs Enchanted เหล่านั้นและกำจัดพวกมันเพื่อย้ายไปยังโซนถัดไป",bc_map,"0xffff00";
	end;

OnTimer39710:
	mapannounce instance_mapname("1@orcs"), "Kruger:พยายามหลีกเลี่ยงการเผชิญหน้ากับออร์คอื่น ๆ จากนั้นเป็นพวกที่หลงเสน่ห์ ทุกครั้งที่คุณฆ่าออร์คปกติออร์คชั้นสูงจะมารวมตัวกันที่เส้นทางสุดท้ายไปยังชั้น 2.",bc_map,"0xffff00";
	end;

OnTimer49210:
	mapannounce instance_mapname("1@orcs"), "Kruger:ในกรณีที่เลวร้ายที่สุดอาจปิดกั้นทางเดินไปยังชั้น 2 ได้ทั้งหมด เพื่อประโยชน์ของคุณเองคุณควรส่อเสียดให้มากที่สุด.",bc_map,"0xffff00";
	end;

OnTimer56310:
	mapannounce instance_mapname("1@orcs"), "Mission:แอบเข้ามาและกำจัด 'ออร์คผู้หลงใหล' การหลีกเลี่ยงการต่อสู้กับออร์คตัวอื่นเป็นวิธีที่ดีที่สุดในการ ขึ้นสู่ชั้น 2.",bc_map,"0x44ffff";
	donpcevent instance_npcname("#Resurrect Monsters1")+"::OnEnable";
	donpcevent instance_npcname("#Resurrect Monsters2")+"::OnEnable";
	donpcevent instance_npcname("#Resurrect Monsters3")+"::OnEnable";
	disablenpc instance_npcname("Kruger#1-2");
	end;

OnTimer60000:
	areamonster instance_mapname("1@orcs"),137,83,143,89,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead";
	stopnpctimer;
	end;

}

1@orcs,168,125,0	script	B1 Area 1	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 1");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 1");
	areamonster instance_mapname("1@orcs"),103,105,109,111,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead1";
	end;

OnTouch_:
	warp instance_mapname("1@orcs"),168,130;
	end;

OnContinue:
	donpcevent instance_npcname("B1 Area 2")+"::OnEnable";
	initnpctimer;
	end;

OnTimer10300:
	mapannounce instance_mapname("1@orcs"), "Kruger's Whisper:พวกออร์คที่นี่เคยเป็นเพื่อนของฉัน พวกเขาสูญเสียเจตจำนงนับตั้งแต่ Orc Shaman เริ่มควบคุมพวกเขาด้วยเวทมนตร์ของเธอ.",bc_map,"0xff4499";
	end;

OnTimer18700:
	mapannounce instance_mapname("1@orcs"), "Kruger's Whisper:ไม่มีอะไรที่เราสามารถทำได้นอกจากเอาชนะ Orc Shaman หากเราต้องการช่วยเผ่าที่เหลือ.",bc_map,"0xff4499";
	stopnpctimer;
	end;
}

1@orcs,89,94,0	script	B1 Area 2	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 2");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 2");
	areamonster instance_mapname("1@orcs"),32,40,38,46,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead2";
	end;

OnTouch_:
	warp instance_mapname("1@orcs"),85,85;
	end;

OnContinue:
	donpcevent instance_npcname("B1 Area 3")+"::OnEnable";
	initnpctimer;
	end;

OnTimer30300:
	mapannounce instance_mapname("1@orcs"), "Kruger's Whisper:ฉันเห็นศพของเผ่าเรา ดูเหมือนว่าหมอผีออร์คจะใช้ออร์คเหล่านั้นในพิธีกรรมของเธอ.",bc_map,"0xff4499";
	end;

OnTimer37600:
	mapannounce instance_mapname("1@orcs"), "Kruger's Whisper:... ทุกอย่างเกี่ยวข้องกับฉัน ฉันต้องรับผิดชอบต่อความชั่วร้ายนี้.",bc_map,"0xff4499";
	stopnpctimer;
	end;
}

1@orcs,38,105,0	script	B1 Area 3	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 3");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 3");
	areamonster instance_mapname("1@orcs"),19,177,25,183,"Enchanted Orc",1023,1,instance_npcname("B1 Area Mobs")+"::OnMyMobDead3";
	end;

OnTouch_:
	warp instance_mapname("1@orcs"),38,110;
	end;

OnContinue:
	donpcevent instance_npcname("B1 Area 4")+"::OnEnable";
	initnpctimer;
	end;

OnTimer30300:
	mapannounce instance_mapname("1@orcs"), "ได้โปรดแขวนไว้ที่นั่น!",bc_map,"0xff4499";
	end;

OnTimer32700:
	mapannounce instance_mapname("1@orcs"), "เราจะได้พักผ่อนเมื่อไปถึงชั้นใต้ดินที่ 2 หลังจากผ่านที่นี่.",bc_map,"0xff4499";
	stopnpctimer;
	end;
}

1@orcs,21,189,0	script	B1 Area 4	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("B1 Area 4");
	end;

OnEnable:
	enablenpc instance_npcname("B1 Area 4");
	donpcevent instance_npcname("#Resurrect Monsters1")+"::OnDisable";
	donpcevent instance_npcname("#Resurrect Monsters2")+"::OnDisable";
	donpcevent instance_npcname("#Resurrect Monsters3")+"::OnDisable";
	donpcevent instance_npcname("#Resurrect Monsters4")+"::OnDisable";
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),32,171;
	end;
}

1@orcs,1,1,0	script	B1 Area Mobs	-1,{
	end;

OnMyMobDead:
	donpcevent instance_npcname("B1 Area 1")+"::OnEnable";
	end;

OnMyMobDead1:
	donpcevent instance_npcname("B1 Area 1")+"::OnContinue";
	end;

OnMyMobDead2:
	donpcevent instance_npcname("B1 Area 2")+"::OnContinue";
	end;

OnMyMobDead3:
	donpcevent instance_npcname("B1 Area 3")+"::OnContinue";
	end;
}

// Orc's Memory Floor 2
//============================================================
2@orcs,36,171,0	script	#2Resurrect Monsters1	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2Resurrect Monsters1");
	end;

OnEnable:
	enablenpc instance_npcname("#2Resurrect Monsters1");
	monster instance_mapname("2@orcs"),0,0,"Vengeful Orc",1152,30,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnDisable:
	stopnpctimer;
	killmonster instance_mapname("2@orcs"),instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@orcs");
	set .@mob_dead_num, 30 - mobcount(.@map$,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		set .@mob_ran,rand(1,30);
		if (.@mob_ran > 29) {
			monster .@map$,0,0,"Vengeful Orc",1152,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
		}
		else if ((.@mob_ran > 28) && (.@mob_ran < 30)) {
			monster .@map$,0,0,"High Orc",1213,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
		}
		else if ((.@mob_ran > 26) && (.@mob_ran < 29)) {
			areamonster .@map$,157,112,167,122,"High Orc",1213,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,10) == 9) {
				mapannounce .@map$, "Warning:High Orcs กำลังรวมตัวกันใกล้พื้นที่ 3.",bc_map,"0xff4444";
			}
		}
		else {
			areamonster .@map$,173,13,183,23,"High Orc",1213,.@mob_dead_num,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
			if (rand(1,5) == 3) {
				mapannounce .@map$, "Caution:กองกำลังเริ่มมีสมาธิที่แท่นบูชาของหมอผี.",bc_map,"0x77ff77";
			}
			if (rand(1,70) == 50) {
				initnpctimer;
			}
		}
	}
	end;

OnTimer10:
	mapannounce instance_mapname("2@orcs"), "Voice from somewhere:เขลา ... คุณคิดว่าแท่นบูชาจะล้มลงอย่างนั้นจริงๆหรือ?",bc_map,"0xff4444";
	end;

OnTimer4010:
	set .@map$, instance_mapname("2@orcs");
	mapannounce .@map$, "[ Wraiths were summoned by an unknown power ]",bc_map,"0x77ff77";
	areamonster .@map$,167,25,177,35,"Wraith",1475,30,instance_npcname("#2Resurrect Monsters1")+"::OnMyMobDead";
	stopnpctimer;
	end;
}

2@orcs,36,171,0	script	#2Resurrect Monsters3	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#2Resurrect Monsters3");
	end;

OnEnable:
	enablenpc instance_npcname("#2Resurrect Monsters3");
	monster instance_mapname("2@orcs"),0,0,"Orc Zombie",1153,15,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@orcs");
	set .@mob_dead_num, 15 - mobcount(.@map$,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead");
	set .@mob_ran,rand(1,30);
	if (.@mob_ran > 29) {
		if (.@mob_dead_num > 0) {
			monster .@map$,0,0,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	else if ((.@mob_ran > 6) && (.@mob_ran < 30)) {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,168,10,184,26,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
			if (rand(1,15) == 3) {
				mapannounce .@map$, "Warning:ทีม Orc Archer กำลังรวมตัวกันใกล้แท่นบูชา.",bc_map,"0xff4444";
			}
		}
	}
	else {
		if (.@mob_dead_num > 0) {
			areamonster .@map$,168,21,184,21,"Orc Archer",1189,.@mob_dead_num,instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
		}
	}
	end;

OnDisable:
	killmonster instance_mapname("2@orcs"),instance_npcname("#2Resurrect Monsters3")+"::OnMyMobDead";
	end;
}

2@orcs,180,30,0	script	#2Resurrect Monsters4	844,{
	end;

OnInstanceInit:
	enablenpc instance_npcname("#2Resurrect Monsters4");
	monster instance_mapname("2@orcs"),0,0,"Anopheles",1627,10,instance_npcname("#2Resurrect Monsters4")+"::OnMyMobDead";
	end;

OnMyMobDead:
	set .@map$, instance_mapname("2@orcs");
	set .@mob_dead_num, 10 - mobcount(.@map$,instance_npcname("#2Resurrect Monsters4")+"::OnMyMobDead");
	if (.@mob_dead_num > 0) {
		monster .@map$,0,0,"Anopheles",1627,1,instance_npcname("#2Resurrect Monsters4")+"::OnMyMobDead";
	}
	end;
}

2@orcs,35,169,4	script	Kruger#2-1	433,{
	donpcevent instance_npcname("Kruger#2-2")+"::OnEnable";
	end;
}

2@orcs,35,169,4	script	Kruger#2-2	433,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("Kruger#2-2");
	end;

OnEnable:
	disablenpc instance_npcname("Kruger#2-1");
	enablenpc instance_npcname("Kruger#2-2");
	initnpctimer;
	end;

OnTimer10:
	mapannounce instance_mapname("2@orcs"), "Kruger's Whisper:ฉันจะบอกวิธีไปที่แท่นบูชาของหมอผี.",bc_map,"0xffff00";
	end;

OnTimer3510:
	mapannounce instance_mapname("2@orcs"), "Kruger's Whisper:คุณเห็นเตาอั้งโล่ที่จุดไฟทางไหม? ปลดปล่อยโซนถัดไปด้วยการเสริมสร้างเปลวไฟs.",bc_map,"0xffff00";
	end;

OnTimer10710:
	mapannounce instance_mapname("2@orcs"), "Kruger's Whisper:แน่นอนว่าสัตว์ประหลาดเหล่านั้นจะไม่ยอมให้คุณสัมผัสเตาอั้งโล่นั้นได้ง่ายๆ.",bc_map,"0xffff00";
	end;

OnTimer16310:
	mapannounce instance_mapname("2@orcs"), "Kruger's Whisper:แต่ถึงกระนั้นพยายามอย่าให้การต่อสู้เป็นที่สังเกตมากเกินไปเพื่อที่หมอผีจะไม่เฝ้าแท่นบูชาร่วมกับทีมกองทัพของเธอ.",bc_map,"0xffff00";
	end;

OnTimer21910:
	mapannounce instance_mapname("2@orcs"), "Kruger's Whisper:มีเพียงหัวหน้าพรรคเท่านั้นที่สามารถเสริมสร้างเปลวไฟได้ดังนั้นจงปกป้องหัวหน้าของคุณ.",bc_map,"0xffff00";
	end;

OnTimer23910:
	mapannounce instance_mapname("2@orcs"), "Mission:คลายโซนด้วยการจุดเตาอั้งโล่ พวกเขาสามารถสว่างได้ตามลำดับที่กำหนดเท่านั้นดังนั้นโปรดใช้ความระมัดระวัง.",bc_map,"0x4444ff";
	donpcevent instance_npcname("#2Resurrect Monsters1")+"::OnEnable";
	donpcevent instance_npcname("#2Resurrect Monsters3")+"::OnEnable";
	donpcevent instance_npcname("Torch#1-1")+"::OnEnable";
	disablenpc instance_npcname("Kruger#2-2");
	end;
}

2@orcs,26,164,0	script	Torch#1-1	844,{
	if (is_party_leader() == false) end;
	progressbar "ffff00",5;
	setarray .@id[0], atoi(charat(strnpcinfo(2),0)), atoi(charat(strnpcinfo(2),2));
	if (.@id[1] == 4)
		donpcevent instance_npcname("#Warp2-"+.@id[0])+"::OnEnable";
	else
		donpcevent instance_npcname("Torch#"+.@id[0]+"-"+(.@id[1]+1))+"::OnEnable";
	initnpctimer;
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnInstanceInit:
	if (strnpcinfo(0) != "Torch#2-1" && strnpcinfo(0) != "Torch#3-1")
		disablenpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	enablenpc instance_npcname(strnpcinfo(0));
	end;

OnTimer100:
	specialeffect EF_FIREPILLAR;
	end;

OnTimer2000:
	specialeffect EF_FIREPILLARBOMB;
	stopnpctimer;
	end;
}
2@orcs,55,155,0	duplicate(Torch#1-1)	Torch#1-2	844
2@orcs,108,146,0	duplicate(Torch#1-1)	Torch#1-3	844
2@orcs,98,171,0	duplicate(Torch#1-1)	Torch#1-4	844

2@orcs,48,100,0	script	#Warp2-1	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp2-1");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,109,156,"Safeguard Chief",1981,1,instance_npcname("#Mobs Control")+"::OnMyMobDead1";
	mapannounce .@map$, "The Chief Orc of Safeguards:โอ้ย !! ดูเหมือนว่าฉันจะมี พวก เอาชนะฉันถ้าคุณทำได้!!",bc_map,"0xff8888";
	end;

OnContinue:
	enablenpc instance_npcname("#Warp2-1");
	initnpctimer;
	end;

OnTimer10000:
	areamonster instance_mapname("2@orcs"),28,158,40,170,"Safeguard Chief",1981,1,instance_npcname("#Mobs Control")+"::OnMyMobDead1";
	stopnpctimer;
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),47,93;
	end;
}

2@orcs,35,92,0	duplicate(Torch#1-1)	Torch#2-1	844
2@orcs,32,70,0	duplicate(Torch#1-1)	Torch#2-2	844
2@orcs,70,31,0	duplicate(Torch#1-1)	Torch#2-3	844
2@orcs,84,51,0	duplicate(Torch#1-1)	Torch#2-4	844

2@orcs,101,55,0	script	#Warp2-2	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp2-2");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,67,64,"Orc Sniper",1982,1,instance_npcname("#Mobs Control")+"::OnMyMobDead2";
	mapannounce .@map$, "Orc Sniper:ห๊ะ! ค่อนข้างน่าประทับใจที่คุณมาไกลขนาดนี้ แต่ทริปเล็ก ๆ ที่โง่เขลาของคุณจบลงที่นี่...",bc_map,"0xff8888";
	end;

OnContinue:
	enablenpc instance_npcname("#Warp2-2");
	initnpctimer;
	end;

OnTimer10000:
	areamonster instance_mapname("2@orcs"),40,91,52,103,"Orc Sniper",1982,1,instance_npcname("#Mobs Control")+"::OnMyMobDead2";
	stopnpctimer;
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),107,55;
	end;
}

2@orcs,142,145,0	duplicate(Torch#1-1)	Torch#3-1	844
2@orcs,162,134,0	duplicate(Torch#1-1)	Torch#3-2	844
2@orcs,144,117,0	duplicate(Torch#1-1)	Torch#3-3	844
2@orcs,136,98,0	duplicate(Torch#1-1)	Torch#3-4	844

2@orcs,167,104,0	script	#Warp2-3	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp2-3");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,152,147,"Depraved Orc Spirit",1983,1,instance_npcname("#Mobs Control")+"::OnMyMobDead3";
	mapannounce .@map$, "วิญญาณออร์คที่ต่ำช้า:ฉันได้กลิ่นเนื้อ! หิว! อยากลองกินเนื้อมนุษย์!!",bc_map,"0xff8888";
	end;

OnContinue:
	areamonster instance_mapname("2@orcs"),117,61,129,73,"Depraved Orc Spirit",1983,1;
	donpcevent instance_npcname("#Boss Control")+"::OnEnable";
	enablenpc instance_npcname("#Warp2-3");
	initnpctimer;
	end;

OnTimer10:
	mapannounce instance_mapname("2@orcs"), "Shaman Cargalache:ฮามาก !! ในที่สุดคุณก็มาถึงที่นี่ มือสังหารที่คุณส่งมานั้นแย่มาก ออร์คโง่ตัวนั้นเริ่มเย็นลงใต้เท้าของฉัน.",bc_map,"0xffff00";
	end;

OnTimer6810:
	mapannounce instance_mapname("2@orcs"), "Shaman Cargalache:ทาสผู้ภักดีของฉันไปรับผู้บุกรุกเหล่านั้น!",bc_map,"0xffff00";
	end;

OnTimer10310:
	mapannounce instance_mapname("2@orcs"), "Depraved Orc Hero:ไม่ว่าคุณจะพูดอะไรเจ้านายของฉัน.",bc_map,"0xff7777";
	end;

OnTimer13110:
	mapannounce instance_mapname("2@orcs"), "Caution:คุณถูกค้นพบโดย Shaman Cargalache แผนการลอบสังหาร Shaman ของครูเกอร์ล้มเหลว คุณต้องกำจัด Cargalache และค้นหาร่องรอยของ Kruger.",bc_map,"0x8888ff";
	stopnpctimer;
	end;

OnTouch_:
	warp instance_mapname("2@orcs"),167,95;
	end;
}

2@orcs,36,171,0	script	#Boss Control	844,{
	end;

OnInstanceInit:
	disablenpc instance_npcname("#Boss Control");
	end;

OnEnable:
	set .@map$, instance_mapname("2@orcs");
	monster .@map$,185,8,"Shaman Cargalache",1984,1,instance_npcname("#Boss Control")+"::OnMyMobDead";
	monster .@map$,179,15,"Depraved Orc Hero",1087,1;
	enablenpc instance_npcname("#Boss Control");
	end;

OnMyMobDead:
	donpcevent instance_npcname("Kruger#")+"::OnEnable";
	set .@map$, instance_mapname("2@orcs");
	set .@mob_ran,rand(1,5);
	if (.@mob_ran == 1) {
		mapannounce .@map$, "Shaman Cargalache:เป็นไปได้ยังไง ... คนอย่างเธอ...!!",bc_map,"0xffff00";
	}
	else if (.@mob_ran == 2) {
		mapannounce .@map$, "Shaman Cargalache:เป็นยังไงบ้างที่ฉันถูกครอบงำโดยมนุษย์!",bc_map,"0xffff00";
	}
	else if (.@mob_ran == 3) {
		mapannounce .@map$, "Shaman Cargalache:นี่ ... มันไม่สามารถสิ้นสุดได้..",bc_map,"0xffff00";
	}
	else if (.@mob_ran == 4) {
		mapannounce .@map$, "Shaman Cargalache:ฉัน ... ตายไม่ได้ ... ยัง ...!",bc_map,"0xffff00";
	}
	else {
		mapannounce .@map$, "Shaman Cargalache:พ่ายแพ้ให้กับคนเขลาเหล่านี้ ... มันไม่สามารถเกิดขึ้นได้...!",bc_map,"0xffff00";
	}
	donpcevent instance_npcname("#2Resurrect Monsters1")+"::OnDisable";
	donpcevent instance_npcname("#2Resurrect Monsters3")+"::OnDisable";
	donpcevent instance_npcname("#Warp Outside Orc Dun")+"::OnEnable";
	end;
}

2@orcs,182,8,0	script	#Warp Outside Orc Dun	45,3,3,{
OnInstanceInit:
	disablenpc instance_npcname("#Warp Outside Orc Dun");
	end;

OnEnable:
	enablenpc instance_npcname("#Warp Outside Orc Dun");
	end;

OnTouch_:
	warp "gef_fild10",240,197;
	end;
}

2@orcs,172,13,0	script	Kruger#	434,{
	if (yong_odun < 2) {
		mes "[ Kruger ]";
		mes "* ไอ *";
		mes "" +strcharinfo(0)+ "ก็คือคุณ ... ";
		next;
		mes "["+strcharinfo(0)+ "]";
		mes "อย่าขยับ! คุณบาดเจ็บ!";
		next;
		mes "[ Kruger ]";
		mes "มัน ... พอดี.. ฉันกำลังจะตาย ... ";
		mes ".... ";
		mes "หมอผีหรือหมอผีล่ะ";
		next;
		mes "["+strcharinfo(0)+ "]";
		mes "หมอผีตายไปแล้วชาแมนคนนั้นคือใคร";
		next;
		mes "ครูเกอร์ดูเหมือนจะโล่งใจเมื่อได้ยินการตายของหมอผี แต่คุณสังเกตเห็นสีหน้าขมขื่นของเขา";
		next;
		mes "[ Kruger ]";
		mes "ฉัน .. ฉันฆ่าลูกสาวตัวเองไม่ได้ ... ";
		mes "ขอบคุณฉันแน่ใจว่าในที่สุดเธอก็หลุดพ้นจากฝันร้ายที่เคยทำให้วิญญาณของเธอหายใจไม่ออก";
		next;
		mes "ครูเกอร์กำลังจะพูดอะไรบางอย่างมากกว่านี้ แต่เขาก็สูดลมหายใจเฮือกสุดท้ายก่อนจะทำได้ ... ";
		set yong_odun,2;
		close;
	}
	else {
		mes "คุณสามารถเห็นศพของครูเกอร์นอนอยู่บนพื้นอย่างสงบ ";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("Kruger#");
	end;

OnEnable:
	enablenpc instance_npcname("Kruger#");
	end;
}

2@orcs,36,171,0	script	#Mobs Control	844,{
	end;

OnMyMobDead1:
	donpcevent instance_npcname("#Warp2-1")+"::OnContinue";
	end;

OnMyMobDead2:
	donpcevent instance_npcname("#Warp2-2")+"::OnContinue";
	end;

OnMyMobDead3:
	donpcevent instance_npcname("#Warp2-3")+"::OnContinue";
	end;
}
