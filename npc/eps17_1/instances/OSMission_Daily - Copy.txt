//===== Crazyarashi Services =====================================|
//= Episode 17.1 Illusion:OS Mission Daily                     =|
//===== By:======================================================|
//= crazyarashi                                                  =|
//===== Current Version:=========================================|
//= 1.0 Initial Version                                          =|
//================================================================|
//= OS Mission Daily Instance                                    =|
//================================================================|
sp_cor,160,55,5	script	Operation Officer#os_mission	HIDDEN_WARP_NPC,7,7,{
	.@md_name$ = "OS Mission 2";
	.@party_id = getcharid(1);
	.@party_name$ = getpartyname(.@party_id);
	if(checkquest(12454) != 1){
	//if(checkquest(12454)){
		mes "[ Operation Officer ]";
		mes "แม้ว่าการปฏิบัติการครั้งแรกจะทำโดยทีมของเรา แต่มนุษย์กลายพันธุ์ก็ยังคงออกมาต่อไป";
		next;
		mes "[ Operation Officer ]";
		mes "หากสิ่งอำนวยความสะดวกถูกทำลายที่แหล่งที่มามันจะล้างออก แต่จะไม่ได้รับการอนุมัติจากระดับบนดังนั้นเราจะต้องส่งทีมค้นหาที่สองไปแทน";
		setquest 12454;
		
	} else {
		mes "[ Operation Officer ]";
		mes "ฉันหวังว่าการค้นหาจะเสร็จสิ้นอย่างปลอดภัย";
	}
	next;
	mes "[ Operation Officer ]";
	mes "คุณเตรียมพร้อมหรือยัง?";
	next;
	//switch(checkquest(12455,PLAYTIME)){
		switch(checkquest(12455,2)){
		case 0:case 1:
			mes "[ Operation Officer ]";
			mes "ฉันยังไม่มีเวลาขอพักก่อน";
			end;
			
		case 2:
			erasequest 12455;
			break;
			
		default:
			break;
	}
	if(!.@party_id){
		mes "[ Operation Officer ]";
		mes "คุณกำลังไปคนเดียว? มันอาจจะยาก เมื่อคุณตั้งพรรคแล้วมาคุยกับฉันอีกครั้ง";
		close;
	}
	.@s = select((!instance_id(IM_PARTY)?"สร้าง OS Mission 2.":""),(!instance_id(IM_PARTY)?"":"เข้าสู่ OS Mission 2"));
	switch(.@s){
		case 1:
			switch(instance_create(.@md_name$,IM_PARTY)){
				case -1:
					mes "^0000ff"+.@md_name$+" ^000000 - การจองล้มเหลว!";
					mes "เกิดข้อผิดพลาดที่ไม่รู้จัก";
					end;
				case -2:
					mes "[ Operation Officer ]";
					mes "คุณกำลังไปคนเดียว? มันอาจจะยาก เมื่อคุณตั้งพรรคแล้วมาคุยกับฉันอีกครั้ง";
					close;
				case -3:
					mes "[ Operation Officer ]";
					mes "คุณมีอินสแตนซ์ที่ใช้งานอยู่แล้ว กรุณายกเลิกก่อนดำเนินการต่อ";
					close;
					
				case -4:
					mes "^0000ff"+.@md_name$+" ^000000 - การจองล้มเหลว!";
					mes "^0000ff"+.@md_name$+" ^000000 เต็มแล้ว";
					end;
					
				default:
					mes "[ Operation Officer ]";
					mes "ก่อนไปอย่าลืมจัดปาร์ตี้ก่อน";
					end;
			}

		case 2:
			switch(instance_enter(.@md_name$)){
				case 3:
					mes "ข้อผิดพลาดที่ไม่รู้จักได้เกิดขึ้น.";
					end;
					
				case 2:
				case 1:
					mes "^ff0000Character ไม่มีอินสแตนซ์ที่สงวนไว้ ^000000";
					end;
					
				case 0:
					mapannounce "sp_cor", strcharinfo(0) + " ของงานปาร์ตี้ "+ .@party_name$ +"กำลังเข้าสู่ " + .@md_name$ + ".", bc_map, "0x00FF99";
					setquest 12455;
					end;
			}	
	}
	end;
	
OnTouch:
	if(checkquest(12452) == 2){
		classchange 4_M_ILYA,"",bc_self;
	}
	end;
}

1@os_a,335,33,0	script	#os_mission_daily_start	HIDDEN_WARP_NPC,1,1,{
	end;
	
OnTouch:
	if(is_party_leader() == false)
		end;
		
	F_InstanceHide(strnpcinfo(0),5);
	F_InstanceHide("#os_mob_phase_1",4);
	doevent instance_npcname("#os_mob_phase_1")+"::OnMobSpawn";
	end;
	
OnInstanceInit:
	'instance_id = instance_id();
	'os_a_map$ = instance_mapname("1@os_a");
	setcell 'os_a_map$,248,110,258,110,CELL_SHOOTABLE,0;
	setcell 'os_a_map$,248,110,258,110,CELL_WALKABLE,0;
	setcell 'os_a_map$,198,110,204,110,CELL_SHOOTABLE,0;
	setcell 'os_a_map$,198,110,204,110,CELL_WALKABLE,0;
	end;
}

1@os_a,1,1,0	script	#os_mob_phase_1	HIDDEN_WARP_NPC,{
	end;
	
OnMobSpawn:
	.@map$ = 'os_a_map$;
	monster .@map$,300,94,"CP1 Twin Caput",20348,1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,300,94,"CP1 Dolor",20349,1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,300,94,"CP1 Bellare",20350,1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,280,96,"CP1 Twin Caput",20348,2,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,270,96,"CP1 Dolor",20349,3,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,260,76,"CP1 Bellare",20350,2,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,260,76,"CP1 Twin Caput",20348,1,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	monster .@map$,260,76,"CP1 Dolor",20349,4,instance_npcname(strnpcinfo(0))+"::OnMobKill";	
	sleep2 2000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ทีมอัลฟ่าควรเตรียมพร้อมสำหรับการต่อสู้ทันทีที่เห็นมนุษย์กลายพันธุ์ใกล้ ๆ",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:พื้นที่นี้ไม่มีพลเรือนเราสามารถปราบสัตว์ประหลาดได้อย่างปลอดภัย",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ชาร์ลีทีมเดลต้าจะต่อสู้ในสนามรบหากพวกเขามีส่วนร่วม",bc_blue|bc_map;
	do {
		sleep 20000;
		.@mob_count = mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill");
		if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0){
			mapannounce .@map$,"ผู้รอดชีวิตจากการกลายพันธุ์ที่เหลืออยู่ - " + .@mob_count,bc_map;
		}
	} while(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0);
	end;
	
OnMobKill:
	.@map$ = 'os_a_map$;
	if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") < 1){
		F_InstanceHide(strnpcinfo(0),3);
		mapannounce .@map$,"ฐานแรกปลอดภัย",bc_map;
		navigateto(.@map$,198,80);
		F_InstanceHide("#os_mob_phase_2",4);
		doevent instance_npcname("#os_mob_phase_2")+"::OnMobSpawn";
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_a,1,1,0	script	#os_mob_phase_2	HIDDEN_WARP_NPC,{
OnMobSpawn:
	.@map$ = 'os_a_map$;
	areamonster .@map$,198,80,214,88,"CP1 Twin Caput",20348,5,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,198,80,214,88,"CP1 Dolor",20349,7,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,198,80,214,88,"CP1 Bellare",20350,3,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	sleep2 2000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:เราได้รับข้อมูลเกี่ยวกับพื้นที่ค้นหานี้",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:มนุษย์กลายพันธุ์ที่มีเครื่องหมาย A013 นั้นมีลักษณะเฉพาะและไม่ขึ้นกับฐานนี้",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:หลีกเลี่ยงการต่อสู้เว้นแต่คุณจะเต็มใจที่จะต่อสู้กับมนุษย์กลายพันธุ์มากกว่าปกติ",bc_blue|bc_map;
	do {
		sleep 20000;
		.@mob_count = mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill");
		if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0){
			mapannounce .@map$,"ผู้รอดชีวิตจากการกลายพันธุ์ที่เหลืออยู่ - " + .@mob_count,bc_map;
		}
	} while(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0);
	end;
	
OnMobKill:
	.@map$ = 'os_a_map$;
	if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") < 1){
		F_InstanceHide(strnpcinfo(0),3);
		mapannounce .@map$,"ฐานที่สองปลอดภัย",bc_map;
		navigateto(.@map$,207,125);
		F_InstanceHide("#os_mob_phase_3",4);
		doevent instance_npcname("#os_mob_phase_3")+"::OnMobSpawn";
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_a,1,1,0	script	#os_mob_phase_3	HIDDEN_WARP_NPC,{
OnMobSpawn:
	.@map$ = 'os_a_map$;
	areamonster .@map$,207,125,222,138,"CP1 Twin Caput",20348,5,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,207,125,222,138,"CP1 Dolor",20349,5,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,207,125,222,138,"CP1 Bellare",20350,5,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	for(.@i = 1; .@i < 33; .@i++){
		F_InstanceHide("#os_poison_daily_"+.@i,3);
	}
	setcell 'os_a_map$,248,110,258,110,CELL_SHOOTABLE,1;
	setcell 'os_a_map$,248,110,258,110,CELL_WALKABLE,1;
	setcell 'os_a_map$,198,110,204,110,CELL_SHOOTABLE,1;
	setcell 'os_a_map$,198,110,204,110,CELL_WALKABLE,1;
	sleep2 2000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ระมัดระวังในการจัดการกับมิวแทนต์คลาส CP4 หรือสูงกว่า",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:เป็นประเภทเดียวกับ CP1 แต่พลังทำลายล้างจะเพิ่มขึ้นตามระดับ ระวังอย่าถูกล้อม",bc_blue|bc_map;
	do {
		sleep 20000;
		.@mob_count = mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill");
		if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0){
			mapannounce .@map$,"ผู้รอดชีวิตจากการกลายพันธุ์ที่เหลืออยู่ - " + .@mob_count,bc_map;
		}
	} while(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0);
	end;
	
OnMobKill:
	.@map$ = 'os_a_map$;
	if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") < 1){
		F_InstanceHide(strnpcinfo(0),3);
		mapannounce .@map$,"ฐานที่สามปลอดภัย",bc_map;
		navigateto(.@map$,239,152);
		F_InstanceHide("#os_mob_phase_4",4);
		doevent instance_npcname("#os_mob_phase_4")+"::OnMobSpawn";
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_a,1,1,0	script	#os_mob_phase_4	HIDDEN_WARP_NPC,{
OnMobSpawn:
	.@map$ = 'os_a_map$;
	areamonster .@map$,239,152,258,161,"CP1 Twin Caput",20348,10,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,239,152,258,161,"CP1 Bellare",20350,5,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	sleep2 2000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:พื้นที่ที่สี่มีเอกสารลับจากห้องเก็บของ",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ฉันต้องการให้คุณมุ่งเน้นไปที่การต่อสู้เพราะทีมแรกได้ค้นหาสถานที่สำหรับข้อมูลสำคัญแล้ว",bc_blue|bc_map;
	do {
		sleep 20000;
		.@mob_count = mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill");
		if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0){
			mapannounce .@map$,"ผู้รอดชีวิตจากการกลายพันธุ์ที่เหลืออยู่ - " + .@mob_count,bc_map;
		}
	} while(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0);
	end;
	
OnMobKill:
	.@map$ = 'os_a_map$;
	if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") < 1){
		F_InstanceHide(strnpcinfo(0),3);
		mapannounce .@map$,"ฐานที่สี่ปลอดภัย",bc_map;
		navigateto(.@map$,211,183);
		F_InstanceHide("#os_mob_phase_5",4);
		doevent instance_npcname("#os_mob_phase_5")+"::OnMobSpawn";
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_a,1,1,0	script	#os_mob_phase_5	HIDDEN_WARP_NPC,{
OnMobSpawn:
	.@map$ = 'os_a_map$;
	areamonster .@map$,211,183,228,191,"CP1 Twin Caput",20348,11,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,211,183,228,191,"CP1 Dolor",20349,11,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	areamonster .@map$,211,183,228,191,"CP1 Bellare",20350,8,instance_npcname(strnpcinfo(0))+"::OnMobKill";
	sleep2 2000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ทีมไชโย มนุษย์กลายพันธุ์ CP6 มองเห็น พลังโจมตีของพวกเขาแข็งแกร่งมาก",bc_blue|bc_map;
	sleep2 3000;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ทีมอัลฟ่าชาร์ลีและเดลต้าระวังให้ดี อย่าอยู่ท่ามกลางมนุษย์กลายพันธุ์เกรด CP6",bc_blue|bc_map;
	do {
		.@map$ = 'os_a_map$;
		sleep 20000;
		.@mob_count = mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill");
		if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0){
			mapannounce .@map$,"ผู้รอดชีวิตจากการกลายพันธุ์ที่เหลืออยู่ - " + .@mob_count,bc_map;
		}
	} while(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") > 0);
	end;
	
OnMobKill:
	.@map$ = 'os_a_map$;
	if(mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMobKill") < 1){
		F_InstanceHide(strnpcinfo(0),3);
		mapannounce .@map$,"ฐานที่ห้าปลอดภัย",bc_map;
		navigateto(.@map$,194,193);
		F_InstanceHide("#os_mob_phase_boss",4);
		doevent instance_npcname("#os_mob_phase_boss")+"::OnMobSpawn";
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_a,1,1,0	script	#os_mob_phase_boss	HIDDEN_WARP_NPC,{
OnMobSpawn:
	.@map$ = 'os_a_map$;
	monster .@map$,194,193,"CP24 Miguel",20346,1,instance_npcname(strnpcinfo(0))+"::OnBossKill";
	'boss_id = $@mobid[0];
	'boss_alive = 1;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ถึงทุกทีม หากคุณพบมิวแทนต์ S-Class ให้เข้าร่วมการต่อสู้ทันที",bc_blue|bc_map;
	sleep2 1500;
	mapannounce .@map$,"????:อ๊ากกก ...",bc_map,0xFF0000;
	sleep2 1500;
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:มนุษย์กลายพันธุ์ที่มี CP สูงนั้นยากที่จะรับมือดังนั้นโปรดระวัง!",bc_blue|bc_map;
	freeloop(true);
	do {
		sleep 100;
		if(unitexists('boss_id)){
			getunitdata 'boss_id,.@boss_data;
			.@HP = .@boss_data[2];
			.@MAX_HP = .@boss_data[3];
			.@FIRST_SUMMON = (.@MAX_HP/10)*7;
			.@SECOND_SUMMON = (.@MAX_HP/10)*3;
			if(.@HP < .@FIRST_SUMMON && 'first_summon < 1){
				F_InstanceEvent(strnpcinfo(0),"OnFirstSummon");
			}
			if(.@HP < .@SECOND_SUMMON && 'second_summon < 1){
				F_InstanceEvent(strnpcinfo(0),"OnSecondSummon");
			}
		}
	} while('boss_alive);
	freeloop(false);
	end;
	
OnFirstSummon:
	'first_summon = 1;
	.@map$ = 'os_a_map$;
	freeloop(true);
	while('boss_alive){
		getunitdata 'boss_id,.@xy;
		'x = .@xy[6];
		'y = .@xy[7];
		'summon[0] = monster(.@map$,'x+7,'y,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		'summon[1] = monster(.@map$,'x-7,'y,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		'summon[2] = monster(.@map$,'x,'y+7,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		'summon[3] = monster(.@map$,'x,'y-7,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		setunitdata 'summon[0],UMOB_DMGIMMUNE,1;
		setunitdata 'summon[1],UMOB_DMGIMMUNE,1;
		setunitdata 'summon[2],UMOB_DMGIMMUNE,1;
		setunitdata 'summon[3],UMOB_DMGIMMUNE,1;
		sleep 150;
		if(unitexists('summon[0]))
			unitkill 'summon[0];
		if(unitexists('summon[1]))
			unitkill 'summon[1];
		if(unitexists('summon[2]))
			unitkill 'summon[2];
		if(unitexists('summon[3]))
			unitkill 'summon[3];
	}
	freeloop(false);
	end;
	
OnSecondSummon:
	'second_summon = 1;
	.@map$ = 'os_a_map$;
	freeloop(true);
	while('boss_alive){
		getunitdata 'boss_id,.@xy;
		'x = .@xy[6];
		'y = .@xy[7];
		'summon[4] = monster(.@map$,'x+5,'y+5,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		'summon[5] = monster(.@map$,'x+5,'y-5,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		'summon[6] = monster(.@map$,'x-5,'y+5,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		'summon[7] = monster(.@map$,'x-5,'y-5,"",20351,1,instance_npcname(strnpcinfo(0))+"::OnEnd");
		setunitdata 'summon[4],UMOB_DMGIMMUNE,1;
		setunitdata 'summon[5],UMOB_DMGIMMUNE,1;
		setunitdata 'summon[6],UMOB_DMGIMMUNE,1;
		setunitdata 'summon[7],UMOB_DMGIMMUNE,1;
		sleep 150;
		if(unitexists('summon[4]))
			unitkill 'summon[4];
		if(unitexists('summon[5]))
			unitkill 'summon[5];
		if(unitexists('summon[6]))
			unitkill 'summon[6];
		if(unitexists('summon[7]))
			unitkill 'summon[7];
	}
	freeloop(false);
	end;
	
OnEnd:end;
	
OnBossKill:
	.@map$ = 'os_a_map$;
	'boss_alive = 0;
	'os_mission_daily = 3;
	killmonsterall 'os_a_map$;
	F_InstanceHide("Operation Officer#os_mission_daily",4);
	mapannounce .@map$,"เจ้าหน้าที่ปฏิบัติการ:ฉันคิดว่าฉันมาช้า!.",bc_blue|bc_map;
	navigateto(.@map$,187,195);
	end;
}

1@os_a,187,195,5	script	Operation Officer#os_mission_daily	4_M_ILYA,{
	if('os_mission_daily == 3 && checkquest(12454,HUNTING) == 2){
		mes "[ Operation Officer ]";
		mes "คุณจัดการกับ CP6 Mutant ทั้งหมดด้วยตัวเอง มันเป็นการต่อสู้ที่ดีหรือไม่?";
		next;
		mes "[ Operation Officer ]";
		mes "สิ่งเหล่านี้คือเสบียงและประสบการณ์ที่มอบให้กับกองกำลังที่เข้าร่วมปฏิบัติการ ไปเลย";
		erasequest 12454;
		getexp 150000,150000;
		getitem 25669,5;
		getitem 25723,1;
		next;
		mes "[ Operation Officer ]";
		mes "เป็นคะแนน CP6 ขออภัยฉันไม่สามารถให้ประสบการณ์ได้มากกว่านี้";
		end;
	} else if('os_mission_daily == 3 && checkquest(12454,HUNTING) == -1){
		mes "[ Operation Officer ]";
		mes "ออกไปกันเถอะ คุณจะต้องกลับไปในไม่ช้า";
		next;
		if(select("อยู่ที่นี่อีกสักพัก:ฉันจะไปแล้ว") == 1){
			mes "[ Operation Officer ]";
			mes "เร็วเข้า เราไม่สามารถอยู่ที่นี่ได้นานกว่านี้";
			end;
		}
		@os_reward = 0;
		warp "sp_cor",162,56;
		end;
	}
	end;
	
OnInstanceInit:
	F_InstanceHide(strnpcinfo(0),3);
	end;
}

1@os_a,252,109,0	script	#os_poison_daily_1	HIDDEN_WARP_NPC,1,1,{
	end;
	
OnTouch:
	.@DAMAGE = (MaxHp/10) * 5;
	heal -.@DAMAGE,0;
	end;
	
OnDisable:
	F_InstanceHide(strnpcinfo(0),5);
	end;

OnInstanceInit:
	while(!'os_mission_daily){
		sleep 1000;
		specialeffect 1020;
	}
	end;
}


1@os_a,253,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_2	HIDDEN_WARP_NPC,1,1
1@os_a,254,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_3	HIDDEN_WARP_NPC,1,1
1@os_a,255,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_4	HIDDEN_WARP_NPC,1,1
1@os_a,252,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_5	HIDDEN_WARP_NPC,1,1
1@os_a,253,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_6	HIDDEN_WARP_NPC,1,1
1@os_a,254,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_7	HIDDEN_WARP_NPC,1,1
1@os_a,255,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_8	HIDDEN_WARP_NPC,1,1
1@os_a,252,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_9	HIDDEN_WARP_NPC,1,1
1@os_a,253,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_10	HIDDEN_WARP_NPC,1,1
1@os_a,254,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_11	HIDDEN_WARP_NPC,1,1
1@os_a,255,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_12	HIDDEN_WARP_NPC,1,1
1@os_a,252,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_13	HIDDEN_WARP_NPC,1,1
1@os_a,253,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_14	HIDDEN_WARP_NPC,1,1
1@os_a,254,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_15	HIDDEN_WARP_NPC,1,1
1@os_a,255,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_16	HIDDEN_WARP_NPC,1,1

1@os_a,198,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_17	HIDDEN_WARP_NPC,1,1
1@os_a,199,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_18	HIDDEN_WARP_NPC,1,1
1@os_a,200,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_19	HIDDEN_WARP_NPC,1,1
1@os_a,201,109,0	duplicate(#os_poison_daily_1)	#os_poison_daily_20	HIDDEN_WARP_NPC,1,1
1@os_a,198,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_21	HIDDEN_WARP_NPC,1,1
1@os_a,199,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_22	HIDDEN_WARP_NPC,1,1
1@os_a,200,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_23	HIDDEN_WARP_NPC,1,1
1@os_a,201,108,0	duplicate(#os_poison_daily_1)	#os_poison_daily_24	HIDDEN_WARP_NPC,1,1
1@os_a,198,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_25	HIDDEN_WARP_NPC,1,1
1@os_a,199,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_26	HIDDEN_WARP_NPC,1,1
1@os_a,200,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_27	HIDDEN_WARP_NPC,1,1
1@os_a,201,107,0	duplicate(#os_poison_daily_1)	#os_poison_daily_28	HIDDEN_WARP_NPC,1,1
1@os_a,198,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_29	HIDDEN_WARP_NPC,1,1
1@os_a,199,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_30	HIDDEN_WARP_NPC,1,1
1@os_a,200,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_31	HIDDEN_WARP_NPC,1,1
1@os_a,201,106,0	duplicate(#os_poison_daily_1)	#os_poison_daily_32	HIDDEN_WARP_NPC,1,1
