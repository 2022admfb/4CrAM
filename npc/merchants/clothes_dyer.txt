//===== rAthena Script ======================================= 
//= Clothes Dyer
//===== By:================================================== 
//= Usnul
//===== Current Version:===================================== 
//= 2.5
//===== Compatible With:===================================== 
//= rAthena Project
//===== Description:========================================= 
//= Clothes dyer with standard palletes
//===== Additional Comments:================================= 
//= Fully working
//= 2.1 Shortened some labels to make this script loading,
//= Added Black+White Colors desc [Lupus]
//= 2.1a added adv classes + baby class support [Lupus]
//= 2.2 Spell checked. [Nexon]
//= 2.3 Fixed dis $hit! [Poki#3]
//= 2.4 Disabled cloth dyeing since it's not implemented
//	   on official servers and cause your client to crash
//	   [Playtester]
//= 2.5 Optimized. [Euphy]
//============================================================

prt_in,284,168,2	script	Dyer Ginedin Rephere	55,{
function Dyes;

	set .@EnableDyes,1;	// Disable (set to 0) if palettes cause errors [see note v2.4]

	set .n$,"[Dyer Ginedin Rephere]";
	mes .n$;
	mes "11 ... 12 ... อืม ... ดี ฉันคิดว่าฉันจะเสร็จได้ก่อนงานเลี้ยงคืนนี้ โอ้! ฉันไม่ได้สังเกตว่าคุณอยู่ที่นี่!";
	while(1) {
		next;
		mes .n$;
		mes "ฉันจะช่วยเหลือคุณได้อย่างไร?";
		next;
		switch(select("- พูดคุย:- เสื้อผ้าสีย้อม:- ราคาตลาด:- ^777777 ยกเลิก ^000000")) {
		case 1:
			mes .n$;
			mes "ชีวิตอาจจะดีขึ้นบ้าง แต่เมื่อดูคำสั่งซื้อทั้งหมดที่ได้รับ ... ต๊าย!";
			mes "ดูเหมือนว่าหญิงสาวแห่ง Rune-Midgarts จะต้องมีความสุขมากในวันนี้";
			next;
			mes .n$;
			mes "ไม่ใช่ว่าฉันคิดว่าพวกเขากำลังหลงระเริงกับอะไรที่หรูหรา";
			mes "ฉันไม่คิดว่าจะมีอะไรผิดปกติกับการแสวงหาความงามและความเป็นแฟชั่น";
			next;
			mes .n$;
			mes "สำหรับมนุษย์เราที่ไม่มีขนหลากสีหรือเกล็ดประดับเสื้อผ้าเป็นหนึ่งในไม่กี่วิธีที่เรามีเพื่อแสดงบุคลิกสไตล์และความงามของเรา";
			next;
			mes .n$;
			mes "ฮ่าฮ่า ... พอคิดดูก็รู้สึกว่างานคุ้มค่าจริงๆ ฉันเชื่อว่าฉันให้บริการที่ผู้คนปรารถนา";
			next;
			mes .n$;
			mes "รู้สึกได้ด้วยเหรอ? พลังงานที่ถูกปลดปล่อยออกมาเมื่อผ้าและหนังหยาบถูกทำให้มีชีวิตด้วยสี? .....";
			next;
			mes .n$;
			mes "แม้ว่าขั้นตอนจะน่าเบื่อและใช้เวลานาน แต่ความสุขและความสุขที่ฉันรู้สึกได้เมื่อแต่งตัวเสร็จมากกว่าที่จะชดเชยกับการทำงานหนักทั้งหมด !!";
			break;
		case 2:
			mes .n$;
			if (!.@EnableDyes) {
				mes "ขอโทษค่ะฉันยุ่งกับงานอื่น ๆ ที่จะย้อมเสื้อผ้าของคุณ";
				break;
			}
			mes "โอ้ ... คุณต้องการงานของฉันเหรอ? อืม ... โอเคฟังดูดี";
			next;
			mes .n$;
			mes "กรุณาเลือกสี";
			mes "ที่เหมาะกับคุณ";
			next;

			// Dyes(<color value>,<dye number>{,...});
			// Colors:[1]Orange, [2]Violet, [3]Red, [4]Black, [5]Green, [6]Blue, [7]White, [8]Yellow
			switch(BaseJob) {
			case Job_Novice:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(6,1,3,2,5,3,4,4);
			case Job_Swordman:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(3,1,5,3,4,4);
			case Job_Mage:
				if (Sex == SEX_MALE) Dyes(3,1,2,2,1,3,7,4); else Dyes(3,1,2,2,7,3,4,4);
			case Job_Archer:
				if (Sex == SEX_MALE) Dyes(4,1,2,2,5,3); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Acolyte:
				if (Sex == SEX_MALE) Dyes(6,1,3,2,7,3,4,4); else Dyes(3,1,5,3,4,4);
			case Job_Merchant:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(2,1,5,3,4,4);
			case Job_Thief:
				if (Sex == SEX_MALE) Dyes(5,1,3,2,7,4); else Dyes(3,1,7,3,4,4);
			case Job_Knight:
			case Job_Crusader:
				if (Sex == SEX_MALE) Dyes(2,1,3,3,4,4); else Dyes(6,1,7,3,4,4);
			case Job_Priest:
			case Job_Monk:
				if (Sex == SEX_MALE) Dyes(6,1,5,2,3,3,7,4); else Dyes(3,1,5,2,7,3,4,4);
			case Job_Wizard:
			case Job_Sage:
				if (Sex == SEX_MALE) Dyes(6,1,5,3,4,4); else Dyes(3,1,6,2,7,3,5,4);
			case Job_Blacksmith:
			case Job_Alchemist:
				if (Sex == SEX_MALE) Dyes(4,4,5,2,7,3); else Dyes(3,1,5,2,2,3,4,4);
			case Job_Hunter:
			case Job_Bard:
			case Job_Dancer:
				if (Sex == SEX_MALE) Dyes(4,1,6,2,5,3); else Dyes(6,1,5,3,2,4);
			case Job_Assassin:
			case Job_Rogue:
				if (Sex == SEX_MALE) Dyes(6,1); else Dyes(4,1,8,2,7,3);
			case Job_Super_Novice:
				if (Sex == SEX_MALE) Dyes(2,1,6,2,5,3,4,4); else Dyes(6,1,3,2,5,3,4,4);
			default:
				mes "ว้าวฉันไม่เคยเห็นเสื้อผ้าแบบนี้มาก่อน! ฉันขอโทษ แต่ฉันคิดว่าฉันไม่สามารถวาดมันได้";
				close;
			}
		case 3:
			mes .n$;
			mes "แต่ละสีราคา 10,000z และ 1 สี:";
			mes " ";
			mes " - ^FF4422Red ^000000:Scarlet Dyestuffs";
			mes " - ^D5A500Yellow ^000000:Lemon Dyestuffs";
			mes " - ^AA00AAViolet ^000000:Violet Dyestuffs";
			mes " - ^FF8800Orange ^000000:Orange Dyestuffs";
			mes " - ^4422FFBlue ^000000:CobaltBlue Dyestuffs";
			mes " - ^009500Green ^000000:DarkGreen Dyestuffs";
			mes " - Black: Black Dyestuffs";
			mes " - White: White Dyestuffs";
			break;
		case 4:
			goto L_End;
		}
	}

L_End:
	mes .n$;
	mes "ทำตัวตามสบายเหมือนเป็นบ้านคุณเอง. แม้ว่าฉันจะไม่มีเวลาให้เธอ.";
	close;

function Dyes {
	setarray .@DyeName$[0],"สีเริ่มต้น","ส้ม","ไวโอเล็ต","แดง","ดำ","เขียว","สีน้ำเงิน","สีขาว","สีเหลือง";
	setarray .@DyeItems[0],0,980,981,975,983,979,978,982,976;
	set .@menu$,"- ค่าเริ่มต้น:";
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+2)
		set .@menu$, .@menu$+"- "+.@DyeName$[getarg(.@i)]+":";
	set .@menu$, .@menu$+"- ^777777 ยกเลิก ^000000";
	set .@s, select(.@menu$)-1;
	if (.@s == (getargcount()/2)+1) goto L_End;
	mes .n$;
	if (.@s == 0) {
		mes "ฉันสามารถเปลี่ยนสีย้อมของคุณเป็น";
		mes "ค่าเริ่มต้นฟรี";
		mes "คุณแน่ใจไหม?";
		next;
		switch(select("ตกลง:ไม่")) {
		case 1:
			mes .n$;
			setlook 7,0;
			mes "เสื้อผ้าของคุณถูกย้อม "+.@DyeName$[0]+".";
			close;
		case 2:
			goto L_End;
		}
	}
	if (!countitem(.@DyeItems[getarg((.@s-1)*2)])) {
		mes "สำหรับฉันที่จะย้อมเสื้อผ้าของคุณฉันต้องใช้สีย้อมที่เหมาะสม โปรดตรวจสอบรายการราคาของฉันสำหรับข้อมูล";
		close;
	}
	if (Zeny < 10000) {
		mes "ฉันขอโทษ แต่คุณมีเงินไม่พอ";
		close;
	}
	delitem .@DyeItems[getarg((.@s-1)*2)],1;
	set Zeny, Zeny-10000;
	setlook 7, ((.@s)?(getarg((.@s-1)*2+1)):0);
	mes "เสื้อผ้าของคุณถูกย้อม "+((.@s)?.@DyeName$[getarg((.@s-1)*2)]:.@DyeName$[0])+".";
	close;
}
}

