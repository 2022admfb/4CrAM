//===== rAthena Script =======================================
//= Buying Shop Scripts
//===== By:==================================================
//= Kisuka
//===== Current Version:=====================================
//= 1.1b
//===== Compatible With:=====================================
//= rAthena Project
//===== Description:=========================================
//= Buying Shop licenses.
//===== Additional Comments:=================================
//= 1.0 First version. [Kisuka]
//= 1.1 Added Catalog Magician. [Euphy]
//= 1.1a Moved Catalog Magician to Renewal path. [Euphy]
//= 1.1b Added missing weight checks. [Euphy]
//============================================================ 

// Black Marketeer (Buy Licenses - Non-Merchant Classes) ::qskill_buy_store
//============================================================ 
que_job01,68,84,1	script	Black Marketeer#Buying	881,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "[Mr. Jass]";
		mes "คุณโลภเกินไปแม้เทียบกับคนอย่างฉัน!";
		mes "ทำไมคุณไม่ไปแบ่งเบากระเป๋าของคุณก่อน";
		close;
	}
	if (getskilllv("ALL_BUYING_STORE") == 1) {
		mes "[Mr. Jass]";
		mes "เฮ้คุณได้ทำสัญญากับฮิวจ์แล้ว";
		mes "ฉันไม่ได้มีธุระอะไรกับคุณ";
		close;
	}
	mes "[Mr. Jass]";
	mes "เธอต้องมีอะไรไม่ดีมาหาฉัน";
	mes "คุณต้องการอะไร?";
	next;
	switch(select("Bulk Buyer Shop สิทธิ์การใช้งาน:คุณคือใคร:ไม่มีอะไร!")) {
	case 1:
		mes "[Mr. Jass]";
		mes "รู้แล้ว!";
		mes "แน่นอนว่าฉันสามารถทำให้คุณได้";
		mes "ของฉันดูเหมือนของแท้ที่พ่อค้าได้รับจากไอ้นายฮิวจ์คนนั้น!";
		next;
		mes "[Mr. Jass]";
		mes "และใบอนุญาตของฉันดีกว่า 'เพราะคุณไม่จำเป็นต้องเรียนรู้ทักษะใด ๆ";
		mes "คุณต้องการกี่คน?";
		next;
		mes "[Mr. Jass]";
		mes "ขอแจ้งให้ทราบว่าฉันสามารถทำในปริมาณเล็กน้อยได้ครั้งละไม่เกิน 10 ครั้ง";
		mes "จะมีค่าใช้จ่าย 500 zeny สำหรับแต่ละคน";
		next;
		while(1) {
			input .@input;
			mes "[Mr. Jass]";
			if (.@input == 0) {
				mes "คุณไม่ต้องการใบอนุญาตเหล่านั้นหรือ?";
				close;
			} else if (.@input > 10) {
				mes "ฉันสามารถทำได้ครั้งละไม่เกิน 10 ครั้งเท่านั้นคุณก็รู้";
				next;
			} else {
				mes "มันจะมีค่าใช้จ่าย "+(.@input*500)+" Zeny";
				if (Zeny < (.@input*500)) {
					mes "แต่คุณไม่มีเงินเพียงพอ";
					next;
					mes "[Mr. Jass]";
					mes "คุณไม่รู้พื้นฐานของธุรกิจหรือไม่? ทุกอย่างมีราคา";
					mes "ถ้าคุณต้องการบางอย่างคุณต้องจ่ายสำหรับมัน";
				} else {
					mes "ฮ่า ๆ ๆ ๆ!";
					mes "คุณฮิวจ์ฉันจะดูแลธุรกิจใบอนุญาตของคุณ คุณจะเห็น!";
					mes "* หัวเราะคิกคัก *";
					getitem 12548,.@input; //Buy_Market_Permit2
					set Zeny,Zeny-(.@input*500);
				}
				close;
			}
		}
	case 2:
		mes "[Mr. Jass]";
		mes "ฉันจากบ้านเกิดไปนานแล้ว";
		mes "มันไม่มีความหมายที่จะถามว่าฉันเป็นใครเพราะทั้งหมดที่ฉันเหลืออยู่ตอนนี้คือความเกลียดชังของฉัน";
		next;
		mes "[Mr. Jass]";
		mes "...";
		mes "ฮิวจ์เป็นพ่อค้าที่ทุจริตและไม่มีจริยธรรมทางธุรกิจ";
		mes "จุดประสงค์เดียวในชีวิตของฉันคือทำลายฮิวจ์";
		next;
		mes "[Mr. Jass]";
		mes "แย่จังดื่มมากไป ... (* Hic *)";
		mes "นั่นเป็นเพียงการพูดของ alohol";
		mes "โปรดลืมสิ่งที่ฉันพูด";
		close;
	case 3:
		mes "[Mr. Jass]";
		mes "เอาล่ะได้เลย! คุณไม่จำเป็นต้องตะโกน";
		mes "ปล่อยฉันไว้คนเดียวถ้าคุณไม่มีธุระกับฉัน";
		close;
	}
}

// Purchasing Team (Learn Skill - Merchant Classes)
//============================================================ 
alberta_in,58,52,4	script	Purchasing Team#Buying	59,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 2400) {
		mes "- คุณไม่สามารถสนทนาหรือทำภารกิจได้เนื่องจากคุณบรรทุกสิ่งของมากเกินไป -";
		close;
	}
	mes "[Mr. Hugh]";
	if (BaseClass == Job_Merchant && getskilllv("MC_VENDING") >= 1) {
		if (getskilllv("ALL_BUYING_STORE") == 1) {
			mes "ฉันฮิวจ์จากทีมจัดซื้อ";
			mes "ฉันจะช่วยคุณได้อย่างไรในวันนี้";
			next;
			if(select("ซื้อใบอนุญาตร้านค้าผู้ซื้อจำนวนมาก:ออก") == 2) {
				mes "[Mr. Hugh]";
				mes "โปรดอย่าลังเลที่จะถามฉันว่าคุณต้องการใบอนุญาตร้านค้าผู้ซื้อจำนวนมากหรือไม่";
				mes "มาอีกแล้ว ~";
				close;
			}
			mes "[Mr. Hugh]";
			mes "200 zeny สำหรับสิทธิ์การใช้งานร้านค้า Bulk Buyer Shop แต่ละใบและคุณสามารถซื้อได้ครั้งละไม่เกิน 50 ใบ";
			mes "คุณต้องการใบอนุญาตกี่ใบ?";
			next;
			while(1) {
				input .@input;
				mes "[Mr. Hugh]";
				if (.@input == 0) {
					mes "คุณได้ยกเลิกการซื้อขาย";
					mes "มีวันที่ดี.";
					close;
				} else if (.@input > 50) {
					mes "โปรดป้อนค่าไม่เกิน 50";
					next;
				} else {
					mes "มันจะมีค่าใช้จ่าย "+(.@input*200)+" zeny สำหรับ "+.@input+" ใบอนุญาต";
					if (Zeny < (.@input*200)) {
						mes "แต่ดูเหมือนคุณจะไม่มีเงินเพียงพอ";
					} else {
						mes "ขอบคุณที่มาอุดหนุน.";
						getitem 6377,.@input; //Buy_Market_Permit
						set Zeny,Zeny-(.@input*200);
					}
					close;
				}
			}
		} else {
			mes "ฉันฮิวจ์จากทีมจัดซื้อที่ Alberta Merchant Guild";
			mes "คุณ ...";
			next;
			mes "[Mr. Hugh]";
			mes "เธอรู้รึเปล่า? กิลด์ของเราได้ออกใบอนุญาตเพื่อให้บุคคลทั่วไปสามารถซื้อสินค้าจากผู้อื่นได้";
			next;
			select("ฉันไม่เคยมีปัญหาในการซื้อของ ...");
			mes "[Mr. Hugh]";
			mes "คุณพูดถูก แต่ลองคิดดูสิ:";
			mes "คุณไม่เคยลำบากในการซื้อจำนวนมากใช่หรือไม่?";
			mes "คุณต้องค้นหาและพูดคุยกับทุกคนที่มีรายการที่คุณต้องการ";
			mes "ไม่สะดวกเลยใช่ไหม";
			next;
			mes "[Mr. Hugh]";
			mes "เนื่องจากการซื้อจำนวนมากเป็นปัญหาสำคัญสำหรับเราพ่อค้า";
			mes "ฉันได้เสนอแผนใหม่ให้กับกิลด์ของเราโดยอาศัยประสบการณ์ 10 ปีในการซื้อสินค้า";
			next;
			mes "[Mr. Hugh]";
			mes "'ให้แต่ละคนเปิด";
			mes "ร้านค้าผู้ซื้อจำนวนมาก! '";
			mes "นั่นคือชื่อข้อเสนอของฉัน";
			mes "คุณจะเห็นเมื่อคุณอ่าน ...";
			next;
			mes "[Mr. Hugh]";
			mes "...";
			mes "... (Mr. Hugh yammers และรายละเอียดทั้งหมด)";
			next;
			select("เอาล่ะประเด็นของคุณคืออะไร?");
			mes "[Mr. Hugh]";
			mes "โอ้ใช่. สรุป,";
			mes "คุณสามารถซื้อสินค้าบางรายการในจำนวนมากผ่านการจำหน่ายสินค้าอัตโนมัติ";
			next;
			mes "[Mr. Hugh]";
			mes "ในการเปิด Bulk Buyer Shop คุณต้องมีใบอนุญาตที่ออกจาก Merchant Guild";
			next;
			mes "[Mr. Hugh]";
			mes "คุณจำเป็นทุกครั้งที่เปิดร้าน เราคาดหวังว่าผลกำไรจะเพิ่มขึ้นอย่างมากจากการออกใบอนุญาตรูปแบบใหม่นี้";
			next;
			mes "[Mr. Hugh]";
			mes "นั่นคือประเด็นของข้อเสนอของฉัน!";
			mes "ประธานของเราดีใจมากที่ได้ทราบว่าเราจะทำเงินได้มาก!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "กรุณาเข้าประเด็นแล้ว!";
			next;
			mes "[Mr. Hugh]";
			mes "อย่าเพิ่งใจร้อนได้ไหม?";
			mes "ประเด็นของฉันคือเราสามารถให้คุณเปิด Bulk Buyer Shop ได้หากคุณได้เรียนรู้ Vending";
			next;
			mes "[Mr. Hugh]";
			mes "แน่นอนว่าเราเรียกเก็บเงิน 10,000 zeny เป็นค่าลงทะเบียนเพียงครั้งเดียว";
			mes "คุณจะต้องมีสิทธิ์ใช้งาน ^4A4AFFBulk Buyer Shop ^000000 ทุกครั้งที่คุณเปิดร้าน";
			next;
			mes "[Mr. Hugh]";
			mes "บอกว่าคุณต้องการลงทะเบียนเลยไหม";
			mes "ถ้าเป็นเช่นนั้นฉันจะสอนวิธีเปิด Bulk Buyer Shop ให้";
			next;
			if(select("เรียนรู้วิธีเปิด Bulk Buyer Shop:ยกเลิก") == 2) {
				mes "[Mr. Hugh]";
				mes "ผู้ชายที่น่าผิดหวัง!";
				mes "การใช้ตัวเลือกผู้ซื้อจำนวนมากนี้จะเป็นประโยชน์ต่อธุรกิจของคุณในหลาย ๆ ด้าน แต่เป็นการเรียกร้องของคุณ";
				mes "ยินดีให้คำปรึกษาเสมอ!";
				close;
			}
			mes "[Mr. Hugh]";
			if (Zeny < 10000) {
				mes "ค่าลงทะเบียน 10,000 zeny";
				mes "กรุณาเตรียมค่าธรรมเนียมให้พร้อมก่อน";
				close;
			} else {
				mes "คุณได้ตัดสินใจดีแล้ว";
				mes "กรุณาแจ้งค่าลงทะเบียนและลงชื่อตรงนี้ ....";
				next;
				input .@name$;
				mes "[Mr. Hugh]";
				mes .@name$+"....";
				mes "ฉันชอบลายมือของคุณ";
				mes "โอเคคุณได้รับอนุมัติให้เปิด Bulk Buyer Shop แล้ว";
				set Zeny,Zeny-10000;
				getitem 6377,5; //Buy_Stall_Permit
				skill "ALL_BUYING_STORE",1,SKILL_PERM_GRANT;
				next;
				mes "[Mr. Hugh]";
				mes "ขณะนี้มีเพียงไอเท็มปกติ ^8C2121EXCEPT ^000000 อุปกรณ์ยาบางอย่างและไอเท็มที่สร้างขึ้นด้วยมือสามารถซื้อได้จำนวนมาก แต่ก็ยังมีประโยชน์ต่อคุณมากขึ้นอยู่กับวิธีการใช้";
				next;
				mes "[Mr. Hugh]";
				mes "อ้อคุณต้องมีสินค้าอย่างน้อยหนึ่งรายการที่คุณต้องการซื้อในคลังของคุณเพราะคุณต้องแสดงให้คนอื่นเห็นผ่านทางร้านค้าของคุณ";
				next;
				mes "[Mr. Hugh]";
				mes "ตอนนี้ทักษะของคุณควรได้รับการลงทะเบียนในหน้าต่างทักษะของคุณ หากคุณมองไม่เห็นคุณอาจต้องย่อขนาดรายการทักษะของคุณและตรวจสอบแท็บงานที่ 3";
				next;
				mes "[Mr. Hugh]";
				mes "ฉันให้ใบอนุญาตร้านค้าผู้ซื้อจำนวนมาก 5 ใบสำหรับการทดลองใช้ของคุณ";
				mes "ช้อปเพลิน!";
				close;
			}
		}
	}
	mes "ฉันฮิวจ์จากทีมจัดซื้อที่ Alberta Merchant Guild";
	mes "ฉันชอบแชท แต่ตอนนี้ฉันยุ่งเกินไป";
	next;
	mes "[Mr. Hugh]";
	mes "เวลาของฉันทุ่มเทให้กับลูกค้าในอุตสาหกรรมการค้า แต่เพียงผู้เดียว";
	close;
}
