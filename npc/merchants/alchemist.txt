//===== rAthena Script ======================================= 
//= Alchemist Shop
//===== By:================================================== 
//= rAthena Dev Team
//===== Current Version:===================================== 
//= 1.8
//===== Compatible With:===================================== 
//= rAthena Project
//===== Description:========================================= 
//= Merchants for Alchemist Materials and Manuals.
//===== Additional Comments:================================= 
//= 1.1 fixed Medicine Bowl issue, thanx 2 MasterOfMuppets
//= 1.2 Reddozen's fixes of typos. added optional Elemental
//=     Potion Guide. [Lupus]
//= 1.3 Deleted Elemental Potions Guide due to original quest [Lupus]
//= 1.4 Alchemists can now purchase 2000 Medicine Bowls at once. [SinSloth]
//= 1.5 Updated to Aegis 10.3 Standards. [L0ne_W0lf]
//= 1.6 Corrected canceling purchase. [L0ne_W0lf]
//= 1.7 Updated to match AEGIS script. [Masao]
//= 1.8 Moved Craft Book Merchant to Renewal path. [Euphy]
//============================================================ 

alde_alche,24,188,3	script	Guild Dealer	740,{
	if (checkweight(1201,1) == 0) {
		mes "- เดี๋ยวก่อน! -";
		mes "- ขณะนี้คุณกำลังดำเนินการ -";
		mes "- มีสิ่งของติดตัวคุณมากเกินไป -";
		mes "- โปรดกลับมาอีกครั้ง -";
		mes "- หลังจากที่คุณเก็บสิ่งของบางอย่างลงในที่เก็บคาฟรา -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "ยินดีต้อนรับสู่";
	mes "Alchemist Union";
	mes "ฉันจะช่วยเหลือคุณในวันนี้ได้อย่างไร";
	next;
	switch(select("ซื้อวัสดุ:ซื้อคู่มือการผลิต:เลิก")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "คุณต้องการอะไร?";
		next;
		if (select("Medicine Bowl - 8 Zeny:ยกเลิก") == 1) {
			mes "[Gever Al Sharp]";
			mes "คุณต้องการกี่ชิ้น?";
			mes "ป้อน 0 หากคุณต้องการออก";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "ข้อตกลงถูกยกเลิก";
					mes "มาใหม่ครั้งหน้า.";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "หมายเลขต้อง";
					mes "น้อยกว่า 2000";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				//mes "คุณไม่ทำ";
				mes "มี zeny เพียงพอ";
				mes "ตรวจสอบจำนวน zeny";
				//mes "คุณมีครั้งแรก";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "ดูเหมือนจะไม่";
				mes "คุณสามารถพกพาได้ทุกอย่าง";
				mes "โปรดตรวจสอบช่องว่าง";
				mes "ในสินค้าคงคลังของคุณ";
				close;
			}
			set Zeny, Zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "ขอบคุณ.";
			mes "แล้วมาอีกครั้ง.";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "ดีละถ้าอย่างนั้น,";
		mes "มาอีกเมื่อ";
		mes "คุณต้องซื้อ";
		mes "วัสดุที่เกี่ยวข้องกับ";
		mes "เล่นแร่แปรธาตุได้ไหม?";
		close;
	case 2:
		mes "[Gever Al Sharp]";
		mes "อะไรที่คุณต้องการ?";
		mes "คู่มือโดยทั่วไป 100,000 zeny แต่มีคู่มือพิเศษสองสามเล่มที่มีค่าใช้จ่ายสูงกว่า";
		next;
		switch(select("คู่มือการสร้างยา:คู่มือการสร้างแอลกอฮอล์:คู่มือการสร้างระเบิดขวด:คู่มือการสร้างขวดกรด:คู่มือการสร้างขวดจากพืช:คู่มือการสร้างขวดทรงกลมทางทะเล:คู่มือการสร้างเสื้อเคลือบแวววาว:คู่มือการสร้างยาควบแน่น:ยกเลิกข้อตกลง")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "ดีละถ้าอย่างนั้น...";
			mes "กลับมาถ้าคุณ";
			mes "ต้องซื้อ";
			mes "คู่มือการผลิต";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "เอาล่ะ";
		mes "มีวันที่ดี.";
		close;
	}

S_SellManual:
	.@item_id = getarg(0);
	.@zeny_req = getarg(1);

	if (Zeny < .@zeny_req) {
		mes "[Gever Al Sharp]";
		//mes "คุณไม่ทำ";
		mes "มี zeny เพียงพอ";
		mes "ตรวจสอบจำนวน zeny";
		//mes "คุณมีครั้งแรก";
		close;
	}
	mes "[Gever Al Sharp]";
	mes getitemname(.@item_id) +"เหรอ?";
	mes "นั่นจะเป็น "+ .@zeny_req +" Zeny";
	next;
	if (select("ซื้อ:ยกเลิก") == 1) {
		Zeny = Zeny - .@zeny_req;
		getitem .@item_id,1;
		mes "[Gever Al Sharp]";
		mes "ขอบคุณสำหรับ";
		mes "การอุปถัมภ์ของคุณ";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "มาอีกครั้ง";
	mes "ครั้งหน้า.";
	close;
}

