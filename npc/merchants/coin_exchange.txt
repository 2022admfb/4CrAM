//===== rAthena Script =======================================
//= Coin Merchants
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Coin redemption NPCs.
//===== Additional Comments: =================================
//= 1.0 First version 
//= 1.1 Fixed checkweight. [Gepard]
//= 1.2 Optimized and merged into one file. [Euphy]
//============================================================

// Manuk & Splendide
//============================================================
-	script	::merchant_13_2	-1,{
	set .@n$, "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "ดูเหมือนว่าคุณจะแบกของเยอะเกินไป";
			mes "ทำไมไม่เก็บของบางส่วนแล้วกลับมาล่ะ";
			close;
		}
		mes "สวัสดี";
		mes "คุณกำลังมองหาอะไร";
		next;
		if (strnpcinfo(1) == "Merchant of Manuk") {
			setarray .@Items[2],12342,12343,12348;
			set .@Book,11019; //Manuk_Selling_Item
			set .@Coin,6080; //Manuk_Coin
			set .@i, select("ดูคำอธิบายรายการ:Manuk's Opportunity:Manuk's Courage:Manuk's Faith:ยกเลิก");
		}
		else {
			setarray .@Items[2],12344,12345,12349;
			set .@Book,11018; //Splendide_Selling_Item
			set .@Coin,6081; //Splendide_Coin
			set .@i, select("ดูคำอธิบายรายการ::Buy Pinguicula's Fruit Jam:Buy Luciola's Honey Jam:Buy Cornus' Tears:ยกเลิก");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "นี่คือคำอธิบายรายการ";
			close2;
			readbook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "ฉันสามารถขายได้ 3 "+getitemname(.@Items[.@i])+" สำหรับ ^3131FF1 coin^000000.";
			next;
			switch(select("ซื้อ:ยกเลิก")) {
			case 1:
				if (countitem(.@Coin)) {
					mes .@n$;
					mes "ขอบคุณที่มาเยี่ยมชม";
					delitem .@Coin,1; 
					getitem .@Items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "คุณมีเหรียญไม่พอ";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "ถ้าคุณเปลี่ยนใจกลับมาอีกครั้ง";
		close;
	}
	else {
		if (strnpcinfo(1) == "Merchant of Manuk") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	Merchant of Manuk	454
spl_in01,110,326,5	duplicate(merchant_13_2)	Merchant of Splendide	439
