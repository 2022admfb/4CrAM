//===== rAthena Script =======================================
//= Episode 11.1 Socket Enchant NPC
//===== By:==================================================
//= rAthena Dev Team
//===== Current Version:=====================================
//= 2.0a
//===== Compatible With:=====================================
//= rAthena Project
//===== Description:=========================================
//= [Official Conversion]
//= Adds slots to selected weapons and armor.
//===== Additional Comments:=================================
//= 0.1a added missing L_No:to 2 funcs [Lupus]
//= 0.1b Removed duplicates [Toms]
//= 0.2 Added missing next;'s [Evera]
//= 0.3 Fixed Zweihander[+] id [Lupus]
//= 1.0 Fixed ingredients check [Lupus]
//= 1.1 Updated to official. [SinSloth]
//= 1.1b Optimized/cleaned [ultramage]
//= 1.2 Fixed some typos and a nasty bug. [SinSloth]
//= 1.3 Fixed a wrong item ID with Sphinx Hat. [SinSloth]
//= 1.4 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.5 Removed Ontouch trigger area from NPCs. [L0ne_W0lf]
//= 1.6 Replaced effect numerics with constants. [Samuray22]
//= 1.7 Adjusted prices on some armor and weapons. [L0ne_W0lf]
//= 1.8 Added missing Gae Bolg and Dragon Slayer. [Kisuka]
//= 1.9 Moved Episode 12 items to separate NPC.
//=     Some cleanup & optimization. [Gepard]
//= 2.0 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//= 2.0a Added 'disable_items' command. [Euphy]
//============================================================

-	script	Seiyablem#dummy::SocketEnchant	-1,{
	if (checkweight(1201,1) == 0)
	{
		mes "- เดี๋ยวก่อน !! -";
		mes "- ขณะนี้คุณกำลังดำเนินการ -";
		mes "- มีสิ่งของติดตัวคุณมากเกินไป -";
		mes "- กรุณาลองอีกครั้ง -";
		mes "- หลังจากที่คุณลดน้ำหนัก -";
		close;
	}
	mes "[Seiyablem]";
	mes "ฉันเป็นวิศวกรที่เชี่ยวชาญในการเพิ่มช่องใส่อาวุธและชุดเกราะปกติ";
	mes "ค่าบริการของฉันวัสดุที่จำเป็นและโอกาสสำเร็จทั้งหมดขึ้นอยู่กับรายการเฉพาะที่ฉันกำลังทำอยู่";
	next;
	mes "[Seiyablem]";
	mes "การเพิ่มช่องอาจดูเหมือนง่าย แต่ซับซ้อนกว่าที่คิด";
	mes "หากคุณสนใจในบริการของฉันโปรดแจ้งให้เราทราบ";
	next;
	switch(select("เพิ่มช่องให้อาวุธ:เพิ่มช่องในเกราะ:ข้อมูล:ยกเลิก"))
	{
		case 1:
			mes "[Seiyablem]";
			mes "คุณต้องการเพิ่มช่องให้กับอาวุธหรือไม่?";
			mes "ฉันแบ่งประเภทอาวุธตามความยากของพวกเขาในการเพิ่ม Slot, C Class ง่ายที่สุดและ S Class ยากที่สุด";
			mes "อยากลองคลาสไหน";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "C Class ใช่มั้ย?";
					mes "ตอนนี้ฉันสามารถอัพเกรดอุปกรณ์บางอย่างในคลาสนั้นได้เท่านั้นดังนั้นโปรดเลือกหนึ่งในไอเท็มต่อไปนี้ เพื่อที่เราจะได้ลองเพิ่มสล็อตเข้าไป";
					next;
					switch(select("Trident:Rope:Violin"))
					{
						case 1:callfunc "Func_Socket",1460,1461,40,66,200,1010,10;
						case 2:callfunc "Func_Socket",1950,1951,40,66,200,1010,10;
						case 3:callfunc "Func_Socket",1901,1902,40,66,200,1010,10;
					}
				case 2:
					mes "[Seiyablem]";
					mes "B Class?";
					mes "ได้เลยนี่คือหมวดหมู่อุปกรณ์โดยเฉลี่ย";
					mes "โปรดเลือกอาวุธที่คุณต้องการให้ฉันลองเพิ่มช่อง";
					next;
					switch(select("Chain:Gladius:Gakkung Bow:Pike:Haedonggum:Lute:Wire:Waghnakh:Arbalest Bow"))
					{
						case 1:callfunc "Func_Socket",1519,1520,40,61,300,1010,10;
						case 2:callfunc "Func_Socket",1219,1220,40,61,300, 984, 1,999,5;
						case 3:callfunc "Func_Socket",1714,1716,40,61,300, 984, 2,999,5;
						case 4:callfunc "Func_Socket",1407,1408,40,61,200,1010,10;
						case 5:callfunc "Func_Socket",1123,1128,40,61,300, 984, 2,999,5;
						case 6:callfunc "Func_Socket",1905,1906,40,61,300,1011,10;
						case 7:callfunc "Func_Socket",1954,1955,40,61,300,1011,10;
						case 8:callfunc "Func_Socket",1801,1802,40,61,300,1010,10;
						case 9:callfunc "Func_Socket",1713,1715,40,61,300, 984, 2,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "A Class";
					mes "นี่คือดินแดนที่มีความเสี่ยงสูง!";
					mes "ได้เลยอาวุธอะไรที่คุณอยากให้ลองเพิ่มช่อง?";
					next;
					switch(select("Hunter Bow:Survivor's Rod (INT):Zweihander:Flamberge:Infiltrator:Ballista:Stunner:Berserk:Claymore"))
					{
						case 1:callfunc "Func_Socket",1718,1726,40,61,500,984,2,999,10;
						case 2:callfunc "Func_Socket",1619,1620,40,61,500,984,5,999,10;
						case 3:callfunc "Func_Socket",1168,1171,40,61,800,984,5,999,10;
						case 4:callfunc "Func_Socket",1129,1149,40,61,500,984,2,999,10;
						case 5:callfunc "Func_Socket",1261,1266,40,61,700,984,5,999,10;
						case 6:callfunc "Func_Socket",1722,1727,40,61,500,984,5,999,10;
						case 7:callfunc "Func_Socket",1522,1532,40,61,500,984,2,999,10;
						case 8:callfunc "Func_Socket",1814,1816,40,61,500,984,5,999,10;
						case 9:callfunc "Func_Socket",1163,1172,40,61,500,984,2,999,10; // NPC says "5 Oridecon" (Aegis) but takes only 2
					}
				case 4:
					mes "[Seiyablem]";
					//mes "โอ้โฮว้าว ...";
					mes "S Class? ได้เลย ...";
					mes "มันจะเป็นเรื่องมหัศจรรย์มากถ้าฉันสามารถดึงสิ่งนี้ออกมาได้";
					mes "เอาล่ะ ... เราจะลองเพิ่มช่องใส่อาวุธไหนดี?";
					next;
					switch(select("Gungnir:มีดพิษ:Ice Pick:Sucsamad:Ginnungagap:Cutlas:Crescent Scythe:Survivor's Rod (DEX)"))
					{
						case 1:callfunc "Func_Socket", 1413, 1418,40,51,1000,984,5,999,10;
						case 2:callfunc "Func_Socket", 1239,13016,40,51,1000,984,5,999,10;
						case 3:callfunc "Func_Socket", 1230,13017,40,51,2000,984,5,999,10;
						case 4:callfunc "Func_Socket", 1236,13018,40,51,1000,984,5,999,10;
						case 5:callfunc "Func_Socket",13002,13019,40,51,1000,984,5,999,10;
						case 6:callfunc "Func_Socket", 1135,13400,40,51,1000,984,5,999,10;
						case 7:callfunc "Func_Socket", 1466, 1476,40,51,1000,984,5,999,10;
						case 8:callfunc "Func_Socket", 1617, 1618,40,51,2000,984,5,999,10;
					}
			}
		case 2:
			mes "[Seiyablem]";
			mes "ชุดเกราะคุณพูด?";
			mes "แน่นอนไม่มีปัญหา.";
			mes "ชุดเกราะแบ่งออกเป็น 4 คลาสที่แตกต่างกันขึ้นอยู่กับความยากในการทำงาน";
			mes "C Class เป็นคลาสที่ง่ายที่สุดและ S Class เป็นคลาสที่ยากที่สุด";
			mes "อยากลองคลาสไหน";
			next;
			switch(select("C Class:B Class:A Class:S Class"))
			{
				case 1:
					mes "[Seiyablem]";
					mes "C Class Armor ใช่มั้ย?";
					mes "ฟังดูสมเหตุสมผล";
					mes "คุณอยากจะลองเพิ่มสล็อตชุดเกราะแบบไหน?";
					next;
					switch(select("Mantle:Coat:Circlet:Biretta"))
					{
						case 1:callfunc "Func_Socket",2307,2308,40,66,200,999,3;
						case 2:callfunc "Func_Socket",2309,2310,40,66,200,999,3;
						case 3:callfunc "Func_Socket",2232,2233,40,66,200,999,3;
						case 4:callfunc "Func_Socket",2216,2217,40,66,200,999,3;
					}
				case 2:
					mes "[Seiyablem]";
					mes "คุณได้เลือกB Class โดยเฉลี่ย";
					mes "แล้วคุณล่ะมีชุดเกราะแบบไหน?";
					next;
					switch(select("Mirror Shield:Chain Mail:Saint Robe:Silk Robe:Boots:Shoes:Muffler:Guard:Buckler:Shield:Bongun Hat"))
					{
						case 1: callfunc "Func_Socket",2107,2108,40,61,250,999,5;
						case 2: callfunc "Func_Socket",2314,2315,40,61,250,999,5;
						case 3: callfunc "Func_Socket",2325,2326,40,61,300,999,5;
						case 4: callfunc "Func_Socket",2321,2322,40,61,300,999,5;
						case 5: callfunc "Func_Socket",2405,2406,40,61,300,999,5;
						case 6: callfunc "Func_Socket",2403,2404,40,61,300,999,5;
						case 7: callfunc "Func_Socket",2503,2504,40,61,300,999,5;
						case 8: callfunc "Func_Socket",2101,2102,40,61,300,999,5;
						case 9: callfunc "Func_Socket",2103,2104,40,61,300,999,5;
						case 10:callfunc "Func_Socket",2105,2106,40,61,250,999,5;
						case 11:callfunc "Func_Socket",5046,5168,40,61,250,999,5;
					}
				case 3:
					mes "[Seiyablem]";
					mes "A Class";
					mes "นี่คือดินแดนที่มีความเสี่ยงสูง!";
					mes "เอาล่ะคุณอยากให้ฉันลองเพิ่ม Slot ไหน?";
					next;
					switch(select("Gemmed Sallet:Bucket Hat:Memory Book:Tights:Legion Plate Armor:Full Plate:Thief Clothes:Greaves:Coif:Manteau:Helm:Ninja Suit:Orc Helm:Ancient Cape:Monk Hat:Golden Gear:Brooch:Munak Hat"))
					{
						case 1: callfunc "Func_Socket",2230,2231,40,61,400,985,1;
						case 2: callfunc "Func_Socket",5114,5120,40,61,400,985,1;
						case 3: callfunc "Func_Socket",2109,2121,40,61,400,985,1;
						case 4: callfunc "Func_Socket",2330,2331,40,61,400,985,1;
						case 5: callfunc "Func_Socket",2341,2342,40,61,400,985,1;
						case 6: callfunc "Func_Socket",2316,2317,40,61,400,985,1;
						case 7: callfunc "Func_Socket",2335,2336,40,61,400,985,1;
						case 8: callfunc "Func_Socket",2411,2412,40,61,400,985,1;
						case 9: callfunc "Func_Socket",5092,5093,40,61,400,985,1;
						case 10:callfunc "Func_Socket",2505,2506,40,61,400,985,1;
						case 11:callfunc "Func_Socket",2228,2229,40,61,400,985,1;
						case 12:callfunc "Func_Socket",2337,2359,40,61,400,985,1;
						case 13:callfunc "Func_Socket",2299,5157,40,61,400,985,1;
						case 14:callfunc "Func_Socket",2507,2525,40,61,400,985,1;
						case 15:callfunc "Func_Socket",2251,5158,40,61,400,985,1;
						case 16:callfunc "Func_Socket",2246,5159,40,61,400,985,1;
						case 17:callfunc "Func_Socket",2605,2625,40,61,400,985,1;
						case 18:callfunc "Func_Socket",2264,5167,40,61,300,985,1;
					}
				case 4:
					mes "[Seiyablem]";
					mes "อืม ... S Class?";
					mes "คุณแน่ใจเกี่ยวกับเรื่องนี้หรือไม่?";
					mes "เอาล่ะ ... แจ้งให้ฉันทราบชุดเกราะที่คุณต้องการเพิ่มช่องและฉันจะบอกสิ่งที่ฉันต้องลอง";
					next;
					switch(select("Majestic Goat:Spiky Band:Bone Helm:Corsair:Crown:Tiara:Sphinx Hat:Robe of Cast:Earring:Ring:Bow Thimble"))
					{
						case 1: callfunc "Func_Socket",2256,5160,40,51,2000,985,2;
						case 2: callfunc "Func_Socket",2258,5161,40,51,2000,985,2;
						case 3: callfunc "Func_Socket",5017,5162,40,51,2000,985,2;
						case 4: callfunc "Func_Socket",5019,5163,40,51,2000,985,2;
						case 5: callfunc "Func_Socket",2235,5165,40,51,2000,985,2;
						case 6: callfunc "Func_Socket",2234,5164,40,51,2000,985,2;
						case 7: callfunc "Func_Socket",5053,5166,40,51,1000,985,2;
						case 8: callfunc "Func_Socket",2343,2360,40,51,1000,985,2;
						case 9: callfunc "Func_Socket",2602,2622,40,51,1000,985,2;
						case 10:callfunc "Func_Socket",2601,2621,40,51,1000,985,2;
						case 11:callfunc "Func_Socket",2619,2671,40,51,1000,985,2;
					}
			}
		case 3:
			mes "[Seiyablem]";
			mes "ฉันยังไม่ได้ปรับแต่งศิลปะของการเพิ่มสล็อตจริงๆ";
			mes "มันซับซ้อนมากจนฉันจะโกหก ถ้าฉันบอกคุณว่าฉันรู้ทุกปัจจัยที่ส่งผลต่อกระบวนการนี้";
			mes "ถึงกระนั้นฉันสังเกตเห็นแนวโน้มบางประการ ...";
			next;
			mes "[Seiyablem]";
			mes "ในบางเมืองการพยายามเพิ่มช่องจะประสบความสำเร็จมากกว่าสำหรับอุปกรณ์ที่มีการอัพเกรดน้อยลง";
			mes "ในเมืองอื่น ๆ ตรงกันข้าม";
			mes "มันไม่แปลกจริงๆเหรอ?";
			next;
			mes "[Seiyablem]";
			mes "นอกจากนี้ยังมีบางสิ่งที่สำคัญมากที่คุณควรรู้";
			mes "หากคุณขอให้ฉันเพิ่มช่องให้กับบางสิ่งตรวจสอบให้แน่ใจว่าคุณไม่มีช่องหลายรายการในคลังของคุณ";
			next;
			mes "[Seiyablem]";
			mes "นี่คือตัวอย่าง:หากคุณมี Manteau +7 และ Manteau ปกติในคลังของคุณฉันจะสุ่มเลือกหนึ่งในนั้นสำหรับการพยายามเพิ่มช่องของฉัน";
			mes "เพียงจำไว้ว่าต้องระวัง";
			next;
			mes "[Seiyablem]";
			mes "อีกครั้ง ^FF0000 พกอุปกรณ์ที่คุณต้องการให้ฉันเพิ่มช่องเท่านั้น ^000000";
			mes "อุปกรณ์อื่น ๆ ทั้งหมดที่มีชื่อเดียวกันควรใส่ไว้ในที่เก็บ Kafra ของคุณใช่หรือไม่?";
			close;
		case 4:
			mes "[Seiyablem]";
			mes "ง่ายนักผจญภัย";
			mes "หากคุณต้องการลองเพิ่มช่องให้กับอุปกรณ์ของคุณโปรดกลับมาและแจ้งให้เราทราบ";
			//mes "เซย่า ~";
			close;
	}
}

function	script	Func_Socket	{
	.@item_id = getarg(0);
	.@zeny_req = getarg(4);
	setarray .@item_req[0], getarg(5), getarg(7,0);
	setarray .@req_amount[0], getarg(6), getarg(8,0);

	disable_items;
	mes "[Seiyablem]";
	mes "คุณต้องการเพิ่มช่องในไฟล์ "+ getitemname(.@item_id) +"เหรอ?";
	mes "ได้โปรดนำฉันมาด้วย ^FF0000"+ .@req_amount[0] +" "+ getitemname(.@item_req[0]) + (.@req_amount[0] > 1 && .@item_req[0] != 999 ? "s":"") +"^000000, "+ (.@item_req[1] != 0 && .@req_amount[1] != 0 ? ("^FF0000"+ .@req_amount[1] +" "+ getitemname(.@item_req[1]) + (.@req_amount[1] > 1 && .@item_req[1] != 999 ? "s":"") + "^000000 ") :"") + "and my ^FF0000" + (.@zeny_req >= 1000 ? (.@zeny_req /1000 +", 000") :.@zeny_req) +", 000 zeny^000000 service fee.";
	mes "อ๊ะและอย่าลืมนำมาด้วย "+ getitemname(.@item_id) +"!";
	next;
	mes "[Seiyablem]";
	if (getiteminfo(.@item_id,5) & EQP_HAND_R) // weapon
	{
		mes "ฉันสามารถลองเพิ่มช่องได้ทันทีหากคุณมีไอเท็มและ zeny ที่ต้องการ";
		mes "อย่างไรก็ตามคุณควรรู้ว่ามีโอกาสที่ฉันจะล้มเหลว";
		mes "ดังนั้นฉันต้องเตือนคุณอย่างเป็นธรรม ...";
		next;
		mes "[Seiyablem]";
		mes "หากการพยายามเพิ่มช่องให้อาวุธของคุณล้มเหลว ^FF0000Weapon ^000000, ^FF0000 และการ์ดใด ๆ ที่รวมอยู่ในนั้นจะถูกทำลาย ^000000";
	}
	else // armor
	{
		mes "หากคุณมีวัสดุที่จำเป็นทั้งหมดค่าบริการ zeny และชุดเกราะของฉันเราสามารถดำเนินการเพิ่มช่องได้";
		mes "แต่ก่อนหน้านั้นฉันต้องเตือนคุณถึงความเสี่ยง";
		next;
		mes "[Seiyablem]";
		mes "หากความพยายามในการเพิ่มช่องในชุดเกราะของคุณล้มเหลวการอัพเกรด ^FF0000Armor ^000000, ^FF0000it ^000000 ^FF0000 และการ์ดใด ๆ ที่รวมอยู่ในนั้นจะถูกทำลาย ^000000";
	}
	mes "คุณยังต้องการลองเพิ่มช่องหรือไม่?";
	next;
	switch(select("พยายามเพิ่มช่อง:ยกเลิก"))
	{
		case 1:
			if ((Zeny >= .@zeny_req*1000) && countitem(.@item_req[0]) >= .@req_amount[0] && countitem(getarg(7,512)) >= .@req_amount[1] && countitem(.@item_id) > 0)// if there's no getarg(7) and getarg(8) provided, check itemcount against 0 (we still need a valid item ID though)
			{
				mes "[Seiyablem]";
				mes "เอาล่ะเริ่มงานกันเลย!";
				mes "คุณควรอธิษฐานขอให้ประสบความสำเร็จ";
				next;
				set .@a, rand(1,100);
				if ((.@a > getarg(2)) && (.@a < getarg(3)))
				{
					specialeffect getarg(3) == 51 ? EF_LORD :EF_SANCTUARY; // only S Class items have Lord of Vermillion effect
					mes "[Seiyablem]";
					mes "เยี่ยมมากดูเหมือนว่าจะประสบความสำเร็จ";
					mes "ดูเหมือนทำได้ดีทีเดียว ยินดีด้วย!";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					getitem getarg(1),1;
					next;
					mes "[Seiyablem]";
					mes "เจอกันใหม่บัดดี้!";
					close;
				}
				else
				{
					specialeffect EF_SUI_EXPLOSION;
					mes "[Seiyablem]";
					mes "ว้า! ... ฉันขอโทษที่ล้มเหลว";
					mes "อย่างไรก็ตามฉันเป็นผู้บริสุทธิ์อย่างสมบูรณ์";
					mes "นี่เป็นโชคดีของคุณและถูกลิขิตโดยพระเจ้าโอเค?";
					mes "อย่าเพิ่งผิดหวังและลองครั้งต่อไป";
					delitem .@item_id,1;
					delitem .@item_req[0],.@req_amount[0];
					if (.@item_req[1] != 0 && .@req_amount[1] != 0)
						delitem .@item_req[1],.@req_amount[1];
					Zeny = Zeny - .@zeny_req *1000;
					next;
					mes "[Seiyablem]";
					mes "ฉันขอให้คุณโชคดีในครั้งต่อไป!";
					close;
				}
			}
			else
			{
				mes "[Seiyablem]";
				mes "ฉันต้องการดำเนินการต่อด้วยการเพิ่มช่องนี้ แต่คุณพลาดบางอย่างไป";
				mes "คุณแน่ใจว่าคุณมีอุปกรณ์วัสดุที่จำเป็นและ zeny?";
				close;
			}
		case 2:
			mes "[Seiyablem]";
			mes "ต้องใช้เวลาคิดบ้างเหรอ?";
			mes "เอาล่ะฉันเข้าใจ";
			mes "แค่จำไว้ว่าชีวิตจะไม่สนุกถ้าคุณเล่นอย่างปลอดภัยอยู่เสมอ ~";
			close;
	}
}

payon,140,151,5	duplicate(SocketEnchant)	Seiyablem#pay	84
lhz_in02,281,35,5	duplicate(SocketEnchant)	Seiyablem#lhz	84
prt_in,33,70,5	duplicate(SocketEnchant)	Seiyablem#prt	84
morocc,51,41,4	duplicate(SocketEnchant)	Seiyablem#moc	84

-	script	Young Man#dummy::SocketMan	-1,{
	mes "[Young Man]";
	mes "ฉันกำลังพิจารณาที่จะเป็นวิศวกร Slotting";
	mes "เป็นสาขาใหม่ที่คุณสามารถเพิ่มสล็อตลงในอุปกรณ์ได้และมันจะเจ๋งมาก ถ้าฉันสามารถทำงานในอาชีพที่ร่ำรวยเช่นนี้ได้";
	next;
	mes "[Young Man]";
	mes "มีโชคมากมายที่เกี่ยวข้องกับการเพิ่มช่อง จากสิ่งที่ฉันเข้าใจได้สำเร็จ";
	mes "คุณล้มเหลวในบางครั้ง แต่ถ้าคุณโชคดีสุด ๆ คุณสามารถเพิ่มสองสล็อตได้";
	//mes "บ้า. ฮะ?";
	next;
	mes "[Young Man]";
	mes "แต่อย่าตื่นเต้นเกินไป";
	mes "มีเพียงอุปกรณ์บางอย่าง เท่านั้น ที่สามารถรองรับการเพิ่มช่องพิเศษสองช่องได้";
	mes "ยังมีอะไรอีกมากมายที่ฉันต้องเรียนรู้ก่อนที่จะเป็นเด็กฝึกงาน ...";
	close;
}

prt_in,31,57,1	duplicate(SocketMan)	Young Man#prt	97
payon,143,143,7	duplicate(SocketMan)	Young Man#pay	97
morocc,60,42,3	duplicate(SocketMan)	Young Man#moc	97
lhz_in02,269,33,5	duplicate(SocketMan)	Young Man#lhz	97
