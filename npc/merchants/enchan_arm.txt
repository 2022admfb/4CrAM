//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By:================================================== 
//= L0ne_W0lf
//===== Current Version:===================================== 
//= 1.3a
//===== Compatible With:===================================== 
//= rAthena Project
//===== Description:========================================= 
//= [Official Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments:================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//= 1.3 Fixed some typos and optimized a little more. [Daegaladh]
//= 1.3a Added 'disable_items' command. [Euphy]
//============================================================ 

prontera,165,60,2	script	Apprentice Craftsman	73,{
	disable_items;
	mes "[Apprentice Craftsman]";
	if (Zeny >= 400000) {
		mes "ฉันได้ศึกษาวิธีการเสริมเกราะเพื่อเพิ่มขีดความสามารถ";
		next;
		mes "[Apprentice Craftsman]";
		mes "Enchanting เป็นทักษะที่ยอดเยี่ยมที่หลอมรวมพลังสถานะลึกลับเข้าไปในซ็อกเก็ตที่ซ่อนอยู่ของชุดเกราะ";
		next;
		mes "[Apprentice Craftsman]";
		mes "อย่างไรก็ตามคุณต้องจำไว้ว่าหากมีชุดเกราะประเภทเดียวกันสองชุดอยู่ในความครอบครองของคุณ Enchantment จะถูกนำไปใช้ตามลำดับที่วางไว้ในคลังของคุณ";
		next;
		mes "[Apprentice Craftsman]";
		mes "ในกรณีนี้อาจใช้ Enchantment กับไอเท็มที่คุณไม่ได้หมายถึง Enchant ดังนั้นเพียงแค่นำ ^5555ff1 Armor ^000000 ที่คุณต้องการเพื่อความปลอดภัย ...";
		next;
		mes "[Apprentice Craftsman]";
		mes "ฉันไม่รับผิดชอบต่อสิ่งที่จะเกิดขึ้นหากคุณมีสินค้าประเภทเดียวกันมากกว่าหนึ่งรายการในสินค้าคงคลังของคุณ";
		next;
		switch(select("Non Slotted Armor.:Slotted Armor.:High Grade Armor.:บางทีคราวหน้า")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			mes "[Apprentice Craftsman]";
			mes "โปรดกลับมาเมื่อคุณมีความสนใจในการร่ายมนตร์ชุดเกราะของคุณ";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	mes "ฉันรับผิดชอบชุดเกราะที่น่าหลงใหล พูดง่ายๆคือฉันได้ศึกษาวิธีเพิ่มพลังเกราะ";
	next;
	mes "[Apprentice Craftsman]";
	mes "หากมีโอกาสคุณอยากจะร่ายมนตร์ชุดเกราะของคุณนำ 400,000 zeny มาให้ฉันและชุดเกราะที่คุณต้องการจะร่ายมนต์และคุณก็พร้อมที่จะไป";
	close;

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[Apprentice Craftsman]";
	if (countitem(.@itemid) == 1) {
		mes "Socket Enchant จะเสียเงิน 400,000 zeny และจะมีตัวเลือกสุ่มหลงเสน่ห์ แน่นอนว่ามีโอกาสทำลายเกราะของคุณ";
		next;
		mes "[Apprentice Craftsman]";
		mes "อันดับแรกและที่สำคัญที่สุด";
		mes "^ff5555 ระดับการปรับแต่งที่มีอยู่ของชุดเกราะ";
		mes "และการ์ดจะหายไป ^000000";
		mes "คุณยังต้องการลอง Enchant หรือไม่?";
		next;
		if(select("อืม ... ลองคิดดูนะ:เอาเลย") == 1) {
			mes "[Apprentice Craftsman]";
			mes "ฉันไม่สามารถตำหนิคุณได้ ปลอดภัยไว้ก่อนใช่ไหม";
			mes "ตอนนี้คุณมีวันที่ดี";
			close;
		}
		mes "[Apprentice Craftsman]";
		mes "ค่อนข้างเป็นนักผจญภัยเหรอ? เราจะ?";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[Apprentice Craftsman]";
			mes "ขออภัยคุณมี zeny ไม่เพียงพอ";
			close;
		}
		progressbar "ffff00",7;
		set Zeny, Zeny-400000;
		delitem .@itemid,1;
		switch (rand(1,.@failrate)) {
			case 1:set .@addpart,4702;break;
			case 2:set .@addpart,4712;break;
			case 3:set .@addpart,4722;break;
			case 4:set .@addpart,4732;break;
			case 5:set .@addpart,4742;break;
			case 6:set .@addpart,4752;break;
			case 7:
			case 8:set .@addpart,4701;break;
			case 9:
			case 10:set .@addpart,4711;break;
			case 11:
			case 12:set .@addpart,4721;break;
			case 13:
			case 14:set .@addpart,4731;break;
			case 15:
			case 16:set .@addpart,4741;break;
			case 17:
			case 18:set .@addpart,4751;break;
			case 19:
			case 20:
			case 21:set .@addpart,4700;break;
			case 22:
			case 23:
			case 24:set .@addpart,4710;break;
			case 25:
			case 26:
			case 27:set .@addpart,4720;break;
			case 28:
			case 29:
			case 30:set .@addpart,4730;break;
			case 31:
			case 32:
			case 33:set .@addpart,4740;break;
			case 34:
			case 35:
			case 36:set .@addpart,4750;break;
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[Apprentice Craftsman]";
				mes "มันแย่เกินไป";
				mes "อุปกรณ์ที่ร้องขอล้มเหลวในการร่ายมนต์";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "หืม? ไม่มีอะไรน่าหลงใหล!";
		mes "โปรดกลับมาพร้อมกับอุปกรณ์เพียงชิ้นเดียวที่จะหลงเสน่ห์";
		close;
	}
}
