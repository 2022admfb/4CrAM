//===== rAthena Script =======================================
//= Kunai Merchant Kashin
//===== By:==================================================
//= rAthena Dev Team
//===== Current Version:=====================================
//= 1.3a
//===== Compatible With:=====================================
//= rAthena Project
//===== Description:=========================================
//= Trades a few shurikens + ninja stones for elemental kunai.
//===== Additional Comments:=================================
//= 1.0 Added the npc. It uses a function that sends the item 
//=     id of the 2 required items plus the amount. Can trade
//=     up to 500 units (5,000 kunais) at once. [erKURITA]
//= 1.1 Officialized script [Playtester]
//= 1.2a Optimized/cleaned up a bit [ultramage]
//= 1.3 Updated to match AEGIS script. [Kisuka]
//= 1.3a Kagerou/Oboro support (BaseJob -> BaseClass) [Euphy]
//============================================================

que_ng,72,29,3	script	Kunai Merchant Kashin	83,{
	if(BaseClass == Job_Ninja) {
		mes "[Kashin]";
		mes "ฉัน Kashin ผู้จัดจำหน่าย";
		mes "ของ Kunai สำหรับ Ninjas ใช้";
		mes "มองไปรอบ ๆ และให้ฉัน";
		mes "รู้ว่าคุณสนใจ";
		mes "ในสินค้าของฉัน";
		next;
		switch(select("10 Fell Poison Kunai:10 Icicle Kunai:10 High Wind Kunai:10 Black Earth Kunai:10 Heat Wave Kunai:ยกเลิก")) {
		case 1:callfunc "Kunai_Trade",13250,20,7524,1,13259; break;
		case 2:callfunc "Kunai_Trade",13251,8,7522,2,13255; break;
		case 3:callfunc "Kunai_Trade",13252,4,7523,2,13257; break;
		case 4:callfunc "Kunai_Trade",13253,2,7524,1,13256; break;
		case 5:callfunc "Kunai_Trade",13254,1,7521,2,13258; break;
		case 6:
			mes "[Kashin]";
			mes "ถ้าอย่างนั้นขอบคุณ";
			mes "สำหรับการเยี่ยมชมร้านค้าของฉัน";
			mes "กรุณามาหาฉันเมื่อ";
			mes "คุณต้องซื้อบ้าง";
			mes "คูนาอิส. ลาก่อน ~";
			close;
		}
	}
	mes "[Kashin]";
	mes "ฉัน Kashin ผู้จัดจำหน่าย";
	mes "ของ Kunai สำหรับ Ninjas ถ้าคุณ";
	mes "มีเพื่อนที่เป็น";
	mes "นินจาแล้วคุณอาจจะ";
	mes "ต้องการบอกพวกเขาเกี่ยวกับฉัน";
	close;
}

function	script	Kunai_Trade	{
	setarray .@item_req[0], getarg(0), getarg(2);
	setarray .@req_amount[0], getarg(1), getarg(3);
	.@item_id = getarg(4);

	mes "[Kashin]";
	mes "คุณสามารถแลกเปลี่ยน";
	mes .@req_amount[0] +" "+ getitemname(.@item_req[0]) +" และ";
	mes .@req_amount[1] +" "+ getitemname(.@item_req[1]) +" สำหรับทุกๆ";
	mes "ชุด 10 "+ getitemname(.@item_id) +".";
	next;
	mes "[Kashin]";
	mes "ฉันให้คูนาอิได้สูงสุดครั้งละ 500 ชุดเท่านั้น";
	mes "หากคุณต้องการยกเลิกการซื้อขายกรุณากรอก '' 0. '' จำนวนเท่าใด";
	mes "คุณต้องการชุด Kunai ไหม";
	next;
	input .@amount;
	if(.@amount < 1 || .@amount > 500) {
		mes "[Kashin]";
		mes "เอ๊ะ? ฉันขอโทษ แต่";
		mes "ฉันให้คุณไม่ได้";
		mes "Kunai หลายชุด กรุณา";
		mes "ป้อนค่าน้อยกว่า 500";
		close;
	}
	if (countitem(.@item_req[0]) < .@amount*.@req_amount[0] || countitem(.@item_req[1]) < .@amount*.@req_amount[1]) {
		mes "[Kashin]";
		mes "อืมคุณไม่มี";
		mes "เพียงพอสำหรับสิ่งนี้";
		mes "แลกเปลี่ยน Kunai กรุณา";
		mes "ตรวจสอบรายการของคุณอีกครั้ง";
		close;
 	}
	if (checkweight(.@item_id, .@amount*10) == 0) {
		mes "[Kashin]";
		mes "อืมดูเหมือนว่าของคุณ";
		mes "ไม่มีสินค้าคงคลัง";
		mes "มีพื้นที่เพียงพอในการจัดเก็บ";
		mes "รายการอื่น ๆ คุณดีกว่า";
		mes "เพิ่มพื้นที่ว่างก่อน";
		close;
	}
	mes "[Kashin]";
	mes "เยี่ยมมากทุกอย่างเข้าแล้ว";
	mes "สั่งได้เลยครับ";
	mes "และทำการซื้อขายนี้ให้เสร็จสิ้น";
	mes "ฉันแน่ใจว่าคุณจะเป็น";
	mes "ค่อนข้างพอใจกับ";
	mes "Kunais เหล่านี้";
	delitem .@item_req[0],.@req_amount[0]*.@amount;
	delitem .@item_req[1],.@req_amount[1]*.@amount;
	getitem .@item_id,10*.@amount;
	close;
}

