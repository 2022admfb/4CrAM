//===== rAthena Script ======================================= 
//= Headgear Dyer (Cash)
//===== By:==================================================
//= Xantara
//= Maud_Dib
//===== Current Version:=====================================
//= 1.0
//===== Compatible With:=====================================
//= rAthena Project
//===== Description:=========================================
//= [Official Conversion]
//= Change headgear's color. Cost is a Mysterious Dyestuff
//===== Additional Comments:=================================
//= 1.0 First / Optimized Version
//============================================================

prt_in,253,168,4	script	Alora	862,{

// Mysterious Dyestuff
	set .@dyeID, 6220;
// Headgear Choices
	setarray .@hgn$[1], "Mage Hat","Beanie","Drooping Cat","Deviruchi Hat","Wig","Ribbon","Magestic Goat";
// Mage Hats - Normal, Yellow, Blue, Brown, Gray, Red
	setarray .@hg_1[1], 5027, 5242, 5241, 5240, 5239, 5238;
// Beanies - Normal, Pink, Blue, Brown
	setarray .@hg_2[1], 5076, 5237, 5236, 5235;
// Drooping Cats - Normal, Blue, Brown, Gray, Pink, Yellow
	setarray .@hg_3[1], 5058, 5233, 5231, 5230, 5232, 5234;
// Deviruchi Hats - Normal, Red, Gray, Brown
	setarray .@hg_4[1], 5038, 5227, 5228, 5229;
// Wigs - Happy, Shiny, Marvelous, Fantastic
	setarray .@hg_5[1], 5273, 5274, 5275, 5276;
// Ribbons - Normal, Black, Yellow, Green, Pink, Red, Orange, White
	setarray .@hg_6[1], 2208, 5191, 5192, 5193, 5194, 5195, 5196, 5197;
// Magestic Goats - Normal, Evolved
	setarray .@hg_7[1], 2256, 5217;

	mes "[Alora]";
	mes "สวัสดีฉันสามารถเปลี่ยนสีหมวกของคุณได้หากคุณนำสีย้อมลึกลับมาให้ฉัน";
	next;
	mes "[Alora]";
	mes "คุณมีหมวกที่อยากย้อมไหม?";
	next;
	for(set .@i,1; .@i < getarraysize(.@hgn$); set .@i,.@i+1) {
		if(.@i == 1)    set .@menu$, .@hgn$[.@i];
		else            set .@menu$, .@menu$ +":"+ .@hgn$[.@i];
	}
	set .@menu$, .@menu$ +":ยกเลิก";
	set .@t, select(.@menu$);
	if(.@t == getarraysize(.@hgn$)) {
		mes "[Alora]";
		mes "ขอให้เป็นนักผจญภัยที่ดี!";
		mes "หากคุณอยากลองสีใหม่บนหมวก Kafra ของคุณหรือสีที่คุณพบจากการผจญภัยของคุณโปรดมาหาฉัน!";
		close;
	}
	mes "[Alora]";
	mes "โอเคคุณอยากเปลี่ยนเป็นสีอะไร";
	next;
	for(set .@i,1; .@i < getarraysize(getd(".@hg_"+.@t+"")); set .@i,.@i+1) {
		if(.@i == 1 && .@t != 5)
			set .@menu$, "Normal";
		else if(.@i == 1 && .@t == 5)
			set .@menu$, getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
		else {
			sscanf(getitemname(getd(".@hg_"+.@t+"["+.@i+"]")),"@s "+ .@hgn$[.@t],.@colour$);
			set .@menu$, .@menu$ +":"+ .@colour$;
		}
	}
	set .@menu$, .@menu$ +":ยกเลิก";
	set .@c, select(.@menu$);
	if(.@c == getarraysize(getd(".@hg_"+.@t+""))) {
		mes "[Alora]";
		mes "โอเคไม่มีปัญหา!";
		close;
	}
	set .@dyHG, getd(".@hg_"+.@t+"["+.@c+"]");
	mes "[Alora]";
	mes "โอ้ฉันตื่นเต้นมากใช่ไหม";
	mes "และเพื่อให้แน่ใจว่าสีคืออะไร "+ .@hgn$[.@t] +" คุณต้องการให้ฉันใช้?";
	next;
	mes "[Alora]";
	mes "โปรดเข้าใจว่าฉันจะใช้ ^FF00001st "+ .@hgn$[.@t] +" ของสีนั้นในสินค้าคงคลังของคุณ! ^000000";
	next;
	mes "[Alora]";
	mes "การอัพเกรดและการ์ดใด ๆ จะหายไปด้วยสีย้อมลึกลับดังนั้นอย่าลืมมี ^0000FF + 0 "+ getitemname(.@dyHG) +" ไม่มีบัตรใด ๆ ^000000";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "โอเคขอบคุณสำหรับคำเตือนฉันคิดว่าฉันจะให้";
	next;
	set .@menu$, "ไม่เป็นไร";
	for(set .@i,1; .@i < getarraysize(getd(".@hg_"+.@t+"")); set .@i,.@i+1) {
		if(.@i == 1)    set .@menu$, .@menu$ +":Normal "+ getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
		else            set .@menu$, .@menu$ +":"+ getitemname(getd(".@hg_"+.@t+"["+.@i+"]"));
	}
	set .@mine, select(.@menu$) - 1;
	if(!.@mine) {
		mes "[Alora]";
		mes "ดีที่สุดคือมั่นใจมากเดินทางปลอดภัย";
		close;
	}
	set .@myHG, getd(".@hg_"+.@t+"["+.@mine+"]");
	if(.@dyHG == .@myHG) {
		mes "[Alora]";
		mes "ว้าวเกิดอะไรขึ้น?";
		close;
	}
	if(!countitem(.@dyeID) || !countitem(.@myHG)) {
		mes "[Alora]";
		mes "โอ้ฉันดูเหมือนคุณจะขาดอะไรไป";
		close;
	}
	mes "[Alora]";
	mes "ดูดีใช่ไหม!";
	mes "หวังว่าคุณจะกลับมาย้อมหมวกที่สวยกว่านี้นะ!";
	delitem .@dyeID,1; // Dyestuff
	delitem .@myHG,1;  // Your headgear
	getitem .@dyHG,1;  // Dyed headgear
	close;
}
