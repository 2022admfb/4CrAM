//===== rAthena Script ======================================= 
//= Quest NPCs related to Niflheim
//===== By:================================================== 
//= The rAthena Dev Team
//===== Current Version:===================================== 
//= 1.6
//===== Compatible With:===================================== 
//= rAthena Project; 7.15 +
//===== Description:========================================= 
//= [Official Conversion]
//= Piano Key Ouest
//= - Escape from Niflheim
//= - Variables in use:nif_esc, MISC_QUEST (Bit 32)
//= Message Delivery Quest
//= - Deliver a message to Erious' departed wife.
//= - Variables in use:nif_revive, MISC_QUEST (Bit 16384)
//===== Additional Comments:================================= 
//= 1.0 Initial release, moved Book of Devil and Piano quest
//=     from npc/cities/niflheim.txt [Evera]
//= 1.1 Optimized Book of Devil quest. used bit-wise var,
//=     Optimized Piano Quest - now uses 1 variable [Lupus]
//= 1.2 Fixed bad NPC header data to comply with rev. 11603. [L0ne_W0lf]
//= 1.3 Moved the Book of Devil quest to custom folder. [L0ne_W0lf]
//=     Updated to Aegis 10.3 Standed, added Message Delivery Quest.
//=     Rescripted Piano Quest. Prior quest progress will be lost.
//= 1.4 Fixed NPCs using ID 111 when it shoudl have been 139. [L0ne_W0lf]
//= 1.5 Swapped NPC IDs back from last change (139 back to 111). [L0ne_W0lf]
//=     Clicking will do nothing, you have to still "สัมผัส" the npc. 
//= 1.6 Added questlog support. [Euphy]
//============================================================ 

// Piano Quest::nif_ryu
//============================================================
niflheim,224,243,3	script	Alreg#nif	795,{
	if (nif_piano) set nif_piano,0;
	mes "[Alreg]";
	mes "ฉันชอบเนื้อ muheh";
	mes "คุณ! ดูเย้ายวน.";
	next;
	mes "[Alreg]";
	mes "ฉันต้องการลิ้มรสคุณ";
	next;
	mes "^FF3355 * กระทืบ * กระทืบ * ^000000";
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		percentheal -60,0;
		next;
		mes "["+ strcharinfo(0) +"]";
		mes "นู๋อู้วววววว ~!";
		next;
		mes "[Alreg]";
		mes "เป็นเวลานานตั้งแต่ฉันมีเนื้อสัตว์";
		mes "ดีจัง ... คุณลิ้มรส";
		mes "ดี. Alreg ให้สิ่งนี้แก่คุณ";
		mes "สำหรับการคืนทุน ปัจจุบัน!";
		mes "* เสี้ยน *";
		set nif_esc,rand(1,2);
		getitem 7184,1; //Piano_Keyboard
		close;
	}
	percentheal -30,0;
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "โอ๊ย ~! คุณคืออะไร";
	mes "พยายามจะฆ่าฉัน!";
	next;
	mes "[Alreg]";
	mes "หืม? โอ้ไม่ไม่ไม่";
	mes "ฉันพยายามจะกินคุณ";
	close;
}

nif_in,105,81,3	script	Crayu#nif	794,{
	mes "[Crayu]";
	mes "ใช้ชีวิตอย่างหนึ่ง ...";
	mes "ฟังเพลงนี้...";
	mes "หลายคนร้องเพราะเพลง";
	mes "เป็นเวลานาน แต่ไม่มีใครรู้";
	mes "เมื่อมันถูกสร้างขึ้นหรือใครเป็นผู้เขียน";
	next;
	mes "[Crayu]";
	mes "^FF00001 พระอาทิตย์ตกทางทิศตะวันตกของภูเขา";
	mes "^FF00002 ที่ซึ่งพลบค่ำสีม่วงตกลงมา";
	mes "^FF00003 ล้อมรอบด้วยเมโลดี้ที่สวยงาม";
	mes "^FF00004 คุณกลายเป็นกุญแจที่เพิกเฉยต่อหลักของมัน ^000000";
	next;
	if (nif_esc == 1 || nif_esc == 4) {
		mes "[Crayu]";
		mes "โอเครอ! นี่คือการทดสอบสำหรับคุณ!";
		switch(rand(1,4)) {
		case 1:
			set .@songline$,"พระอาทิตย์ตกทางทิศตะวันตกของภูเขา";
			mes "เขียนบรรทัดแรกของเพลง";
			break;
		case 2:
			set .@songline$,"ที่ซึ่งพลบค่ำสีม่วงตกลงมา";
			mes "เขียนบรรทัดที่สองของเพลง";
			break;
		case 3:
			set .@songline$,"ล้อมรอบด้วยเมโลดี้ที่สวยงาม";
			mes "เขียนบรรทัดที่สามของเพลง";
			break;
		case 4:
			set .@songline$,"คุณกลายเป็นกุญแจสำคัญที่เพิกเฉยต่อเจ้านายของมัน";
			mes "เขียนบรรทัดที่สี่ของเพลง";
			break;
		}
		next;
		input .@input$;
		if (.@input$ == .@songline$) {
			mes "[Crayu]";
			mes "ยอดเยี่ยม ... !";
			mes "คุณได้รับฟังแล้ว ~!";
			mes "นั่นทำให้คุณเป็นหนึ่งในไฟล์";
			mes "แขกที่ชื่นชอบ นี่คือไฟล์";
			mes "รางวัลเล็ก ๆ น้อย ๆ สำหรับคุณ";
			next;
			if (nif_esc == 1) set nif_esc,3;
			else set nif_esc,6;
			getitem 7184,1; //Piano_Keyboard
			mes "[Crayu]";
			mes "สุดท้ายนี้ฉันหวังว่าคุณจะแสดงไฟล์";
			mes "เคารพกวีอื่น ๆ เช่นเดียวกับ";
			mes "คุณทำเพื่อฉัน";
			close;
		}
		else {
			mes "[Crayu]";
			mes "* ถอนหายใจ * ... ถ้าคุณมีเพียง";
			mes "ฟังฉันมันจะ";
			mes "เป็นเรื่องง่ายที่จะตอบ";
			mes "พยายามจำเส้น";
			mes "แล้วลองอีกครั้ง!";
			close;
		}
	}
	next;
	mes "[Crayu]";
	mes "ฉันสงสัยว่าเนื้อเพลงแปลว่าอะไร ...";
	mes "พวกเขาดูค่อนข้างลึกซึ้ง ...";
	mes "ฉันมีความรู้สึกว่าเพลงนี้";
	mes "มีความลับ";
	close;
}

nif_in,31,20,3	script	Kuzkahina#nif	794,{
	if (nif_esc == 2 || nif_esc == 3) {
		mes "[Kuzkahina]";
		mes "ฉันไม่เข้าใจ";
		mes "ทำไมฉันทำไม่ได้";
		mes "money กับร้านนี้ ...";
		mes "แม้ตอนที่ฉันยังมีชีวิตอยู่";
		mes "ฉันไม่สามารถทำอะไรได้เลย";
		mes "เงินกับธุรกิจของฉัน ...";
		next;
		mes "[Kuzkahina]";
		mes "เฮ้คุณ! ทำตัวให้เป็นประโยชน์";
		mes "และทิ้งสิ่งนี้ไปเมื่อคุณจากไป";
		set nif_esc,nif_esc+2;
		getitem 7184,1; //Piano_Keyboard
		next;
		mes "[Kuzkahina]";
		mes "เอ้ยร้านนี้มีอะไร";
		mes "ฉันลงเอยด้วยการจ้างพนักงานที่ไร้ประโยชน์เช่นนี้ได้อย่างไร ... ?";
		mes "* พึมพำพึมพำ *";
		mes "* พึมพำพึมพำ *";
		close;
	}
	mes "[Kuzkahina]";
	mes "ฉันไม่เข้าใจ";
	mes "ทำไมฉันทำไม่ได้";
	mes "money กับร้านนี้ ...";
	mes "แม้ตอนที่ฉันยังมีชีวิตอยู่";
	mes "ฉันไม่สามารถทำอะไรได้เลย";
	mes "เงินกับธุรกิจของฉัน ...";
	close;
}

niflheim,208,103,1	script	#Graveyard1	111,1,1,{
	end;

OnTouch_:
	if (nif_esc == 5 || nif_esc == 7 || nif_esc == 8 || nif_esc == 10) {
		mes "^3355FF คุณพบบางสิ่ง";
		mes "ครึ่งฝังใกล้หลุมศพ";
		mes "มันดูดีมาก ^000000";
		next;
		mes "^3355FF คุณต้องการรับไหม ^000000";
		if (select("ใช่ไม่ใช่") == 1) {
			if (nif_esc < 10)
				set nif_esc,nif_esc+2;
			else if (nif_esc == 10)
				set nif_esc,11;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

niflheim,169,71,1	script	#Graveyard2	111,1,1,{
	end;

OnTouch_:
	if (nif_esc == 6 || nif_esc == 9) {
		mes "^3355FF คุณพบบางสิ่ง";
		mes "ครึ่งหนึ่งถูกฝังไว้ใกล้หลุมศพ";
		mes "มันดูดีมาก ^000000";
		next;
		mes "^3355FF คุณต้องการรับไหม ^000000";
		if (select("ใช่ไม่ใช่") == 1) {
			set nif_esc,nif_esc+2;
			getitem 7184,1; //Piano_Keyboard
		}
		close;
	}
	end;
}

nif_in,115,181,0	script	#Piano	111,1,1,{
	end;

OnTouch_:
	if (nif_esc == 0 && (MISC_QUEST & 32) == 0) {
		mes "^3355FF คุณเห็นเปียโนตัวใหญ่และหนัก";
		mes "คุณจะได้รับความรู้สึกนั้น";
		mes "เพลงของมันจะฟังดูไพเราะ ^000000";
		next;
		mes "^3355FF ในขณะที่คุณตรวจสอบเปียโน";
		mes "คุณจะพบว่ามี 7 ปุ่ม";
		mes "ขาดหายไปในไฟล์";
		mes "ตรงกลางของแป้นพิมพ์";
		mes "หากคุณมีกุญแจทั้งหมด";
		mes "คุณสามารถเล่นเพลง ... ^000000";
		close;
	}
	if (nif_esc == 11 && countitem(7184) > 5) {
		set nif_esc,12;
		delitem 7184,6; //Piano_Keyboard
		mes "^3355FF คุณเอากุญแจที่คุณพบ";
		mes "และใส่เข้าไป";
		mes "แต่ละพื้นที่ว่าง ^000000";
		next;
		mes "^3355FF แต่แล้วคุณก็รู้";
		mes "ว่าคุณพลาดคนสุดท้าย";
		mes "กุญแจสำคัญสำหรับพื้นที่ห่างไกล";
		mes "ด้านซ้ายของแป้นพิมพ์ ... ^000000";
		close;
	}
	end;
}

-	script	Piano3	HIDDEN_NPC,{
	end;

OnTouch_:
	if (nif_esc == 12 || nif_esc == 13 || (MISC_QUEST & 32)) {
		mes "^3355FF ในขณะที่คุณโค้งเข้าหา";
		mes "ช่องว่างคีย์สุดท้าย";
		mes "ไปดูกันเลยดีกว่า ... ^000000";
		next;
		soundeffect "complete.wav",0;
		next;
		if (nif_esc == 12) {
			set MISC_QUEST,MISC_QUEST | 32;
			set nif_piano, 0;
		}
		mes "^3355FF คุณเริ่มรู้สึกวิงเวียนและ";
		mes "ร่างกายของคุณรู้สึกราวกับว่ามันเป็น";
		mes "ลอยอยู่ในอากาศ จากนั้นไฟล์";
		mes "วิสัยทัศน์เริ่มเบลอ ... ^000000";
		close2;
		warp "nif_in",179,163;
		end;
	}
	end;
}

nif_in,188,168,3	script	Witch#nif	792,{
	mes "[Kirkena]";
	mes "ฮะ? ... คุณยังมีชีวิตอยู่ใช่ไหม";
	mes "ฉันไม่แน่ใจว่าคุณเป็นอย่างไร";
	mes "สามารถไปที่นี่ได้ แต่ฉันทำไม่ได้";
	mes "รู้เหตุผลของคุณที่มา";
	mes "แต่สถานที่นี้อันตรายสำหรับ";
	mes "ความเป็นอยู่.";
	next;
	mes "[Kirkena]";
	mes "ฉันกำลังส่งคุณกลับ";
	mes "นอกเมืองนี้";
	mes "ถ้าคุณสามารถช่วยได้คุณ";
	mes "ดีกว่าไม่กลับมา";
	mes "สถานที่นี้.";
	close2;
	warp "umbala",138,208;
	end;
}

// Message Delivery Quest::nif_npc_lackee
//============================================================
prt_in,161,20,3	script	Erious#nif	92,{
	if (MISC_QUEST & 16384) {
		mes "[Erious]";
		mes "ขอให้ปลอดภัยจากอันตราย";
		mes "ในการเดินทางของคุณนักผจญภัย";
		close;
	}
	if (nif_revive == 0) {
		mes "[Erious]";
		mes "* สะอื้น * สะอื้น *";
		mes "* ร้อง * ว้าาาา ... !";
		mes "Elly คุณทำได้อย่างไร";
		mes "ปล่อยฉันไว้ทางนี้ ...";
		emotion ET_CRY;
		next;
		if (select("ปลอบโยนเขา:ไม่สนใจเขา") == 1) {
			mes "[Erious]";
			mes "อ่าขอบคุณสำหรับความกรุณา";
			mes "* สะอื้น * ภรรยาของฉันเสียชีวิตในขณะที่ฉันอยู่";
			mes "การเดินทาง. ฉันขอโทษที่ฉัน";
			mes "ไม่สามารถอยู่กับเธอเมื่อ";
			mes "จุดจบมา ...";
			next;
			mes "[Erious]";
			mes "ฉันหวังว่าฉันจะได้คุยกับแอลลี่ของฉัน";
			mes "แม้ว่าฉันจะรู้ว่ามันสายเกินไปแล้ว";
			mes "* ร้องไห้ * แต่ ... มีครั้งสุดท้าย";
			mes "หวังว่าฉันจะมี แน่นอนของฉัน";
			mes "โอกาสสุดท้าย ...";
			next;
			mes "[Erious]";
			mes "มีคนบอกว่าที่ไหนสักแห่ง";
			mes "ในโลกนี้มีเมืองอยู่";
			mes "ที่ที่คนตายอาศัยอยู่ ฉันอาจจะ";
			mes "สามารถพบภรรยาของฉันที่นั่น แต่";
			mes "ฉันทิ้งลูกไว้ที่นี่ไม่ได้";
			mes "โดยไม่มีฉัน. * ร้องไห้ *";
			next;
			if (select("ฉันสามารถส่งข้อความของคุณถึงเธอได้:ฉันเสียใจมากที่ได้ทราบเช่นนั้น") == 1) {
				mes "[Erious]";
				mes "โอ้ได้มั้ย?";
				mes "ขอบคุณมากสำหรับ";
				mes "ความเอื้ออาทร. ถ้าคุณสามารถทำได้";
				mes "สำหรับฉันฉันจะเป็น";
				mes "เป็นหนี้บุญคุณอย่างมาก";
				next;
				mes "[Erious]";
				mes "คุณเป็นนักผจญภัยใช่ไหม";
				mes "หากการเดินทางของคุณนำคุณไปสู่จุดนั้น";
				mes "เมืองแห่งความตายและถ้าคุณ";
				mes "บังเอิญเจอเธอโดยบังเอิญ ...";
				next;
				mes "[Erious]";
				mes "กรุณาส่งข้อความของ Elly มาหาฉัน";
				mes "บอกให้เธอรู้ว่าเกิดอะไรขึ้น";
				mes "เดือนที่แล้วเป็นความผิดของฉันทั้งหมด";
				mes "ขอขอบคุณสำหรับความช่วยเหลือของคุณ,";
				mes "และโปรดดูแล";
				set nif_revive,1;
				setquest 11038;
				close;
			}
			mes "[Erious]";
			mes "* ร้องไห้ * ไม่มีทางที่ฉัน";
			mes "จะได้พบเธออีกครั้ง? ถ้าผม";
			mes "คุยกับเธออย่างน้อยอีกครั้ง";
			mes "แม้ว่าจะเป็นครั้งสุดท้ายก็ตาม";
			mes "ฉันจะทำทุกอย่าง ...";
			close;
		}
		mes "[Erious]";
		mes "* ร้อง * Eeeeelllllyyyy ~~~~!";
		mes "ฉันไม่เคยคิดว่าจะเป็นเช่นนั้น";
		mes "ครั้งสุดท้ายที่เราจะได้เห็น";
		mes "กันและกัน ...";
		mes "*ร้องไห้*...";
		close;
	}
	else if (nif_revive == 1) {
		mes "[Erious]";
		mes "สถานที่แห่งนี้ฉันได้รับการบอกเล่าเกี่ยวกับ ...";
		mes "เมืองแห่งความตาย ...";
		mes "จากที่ฉันจำได้มันคือ";
		mes "ตั้งอยู่ใกล้กับพื้นที่พักผ่อน";
		next;
		mes "[Erious]";
		mes "ทั้งวันทั้งคืนมันเต็มไปหมด";
		mes "ด้วยเสียงกรีดร้องที่ไม่สิ้นสุดของ";
		mes "ความเจ็บปวดและความสิ้นหวัง ฉันหวังว่า";
		mes "สิ่งนี้จะเป็นประโยชน์ในการค้นหาของคุณ";
		close;
	}
	else if (nif_revive == 5 || nif_revive == 6) {
		mes "[Erious]";
		mes "พุทโธ่! คุณพบเมืองนี้จริงๆ";
		mes "มีไหม ฉันซาบซึ้งจริงๆ";
		mes "คุณได้ทนกับความยากลำบากทั้งหมดนี้";
		mes "สำหรับฉัน ... อืม ... ในที่สุด ...";
		mes "คุณสามารถพบกับแอลลี่ของฉันได้หรือไม่?";
		next;
		if (select("ใช่:ขอโทษฉันทำไม่ได้") == 1) {
			if (nif_revive == 6 && countitem(934) > 0) {
				mes "[Erious]";
				mes "จริงจังมั้ย? เธอพูดอะไร?";
				mes "เธอบอกว่าเธอจะยกโทษให้ฉันไหม?";
				next;
				select("ใช่. และเธอให้ฉัน ...");
				delitem 934,1; //Mementos
				mes "[Erious]";
				mes "อา ... ในกล่องนี้เป็นของขวัญ";
				mes "ฉันให้เธอเมื่อเราเริ่มต้น";
				mes "เจอกัน. เธอมี";
				mes "เจ็บป่วยร้ายแรงก่อนหน้าเรา";
				mes "ได้พบฉันจึงสัญญากับเธอว่าฉัน";
				mes "จะรักษาอาการป่วยของเธอ ...";
				next;
				mes "[Erious]";
				mes "นั่นเป็นเหตุผลว่าทำไมฉันถึงไปเที่ยวครั้งนั้น ...";
				mes "เพื่อหาวิธีรักษาหรือยา";
				mes "สำหรับเธอ ... แต่ความพยายามของฉันเปลี่ยนไป";
				mes "ออกจะดีสำหรับอะไร";
				mes "...................";
				mes ".........";
				next;
				mes "[Erious]";
				mes "อย่างไรก็ตามฉันขอขอบคุณ";
				mes "ความใจกว้าง. ฉันต้องการที่จะจ่าย";
				mes "คุณกลับมาสำหรับปัญหาทั้งหมด";
				mes "ฉันก่อให้เกิดคุณ แต่ฉันมีเพียง";
				mes "ยาที่ฉันพบสำหรับเธอ";
				mes "เจ็บป่วยระหว่างการเดินทางครั้งนี้";
				next;
				mes "[Erious]";
				mes "ฉันหวังว่าคุณจะกินยานี้";
				mes "เป็นสัญลักษณ์ของความพึงพอใจของฉัน";
				next;
				if (checkweight(610,26)) {
					set MISC_QUEST,MISC_QUEST | 16384;
					set nif_revive,0;
					completequest 11043;
					getitem 607,2; //Yggdrasilberry
					getitem 608,2; //Seed_Of_Yggdrasil
					getitem 610,10; //Leaf_Of_Yggdrasil
					mes "[Erious]";
					mes "ขอบคุณสำหรับทุกสิ่ง.";
					mes "ขอให้คุณโชคดีและปลอดภัย";
					mes "ในการผจญภัยของคุณ";
					close;
				}
				mes "[Erious]";
				mes "ฉันมีบางอย่างจะให้คุณ";
				mes "แต่คุณแบกรับมากเกินไป";
				mes "สิ่งที่ฉันจะมอบให้";
				mes "ตอนนี้. ทำไมคุณไม่";
				mes "เก็บของไว้ที่ไหนสักแห่ง?";
				close;
			}
			else if (nif_revive == 6 && countitem(934) < 1) {
				mes "[Erious]";
				mes "อืม ... นี่มันแปลก ฉันแน่ใจว่าเธอจะให้อะไรคุณเป็นสัญลักษณ์ในการพบเธอ บังเอิญมีอะไรที่คุณลืมนำมาให้ฉันบ้างไหม?";
				close;
			}
			mes "[Erious]";
			mes "ฉันเดาว่าคุณเข้าใจผิด";
			mes "บางอย่างหรือคุณกำลังพยายาม";
			mes "เพื่อหลอกลวงชายคนหนึ่งด้วยความเศร้าโศก";
			close;
		}
		if (nif_revive == 5) {
			mes "[Erious]";
			mes "ฉันเห็น ... คุณหาไม่เจอ";
			mes "เธอ ... อย่างไรก็ตามฉันก็เข้าใจเช่นกัน";
			mes "ที่ตอบสนองคำขอของฉันอาจ";
			mes "เป็นไปไม่ได้ ...";
			next;
			mes "[Erious]";
			mes "คุณออกนอกเส้นทางไปแล้วจริงๆ";
			mes "เพื่อช่วยฉันฉันอยากจะให้คุณ";
			mes "สิ่งที่เป็นสัญลักษณ์ของไฟล์";
			mes "บำเรอ.";
			next;
			if (checkweight(610,10)) {
				set MISC_QUEST,MISC_QUEST | 16384;
				set nif_revive,0;
				completequest 11042;
				getitem 610,10; //Leaf_Of_Yggdrasil
				mes "[Erious]";
				mes "ขอบคุณสำหรับทุกสิ่ง.";
				mes "ขอให้คุณโชคดีและปลอดภัย";
				mes "จากอันตรายในการเดินทางของคุณ";
				close;
			}
			mes "[Erious]";
			mes "ฉันมีบางอย่างจะให้คุณ";
			mes "แต่คุณแบกรับมากเกินไป";
			mes "สิ่งที่ฉันจะมอบให้";
			mes "ตอนนี้. ทำไมคุณไม่";
			mes "เก็บของไว้ที่ไหนสักแห่ง?";
			close;
		}
		mes "[Erious]";
		mes "ฉันเห็น...";
		mes "...รอ! ไม่ได้เป็นของ";
		mes "กับภรรยาของฉันแอลลี่? ทำไมคุณ";
		mes "พยายามโกงผู้ชายที่เสียใจ ... ?";
		close;
	}
	end;
}

umbala,215,181,3	script	Billik	733,{
	if (nif_revive > 1 || (MISC_QUEST & 16384)) {
		mes "[Billik]";
		mes "ไม่ได้เจอกันนานเลย";
		mes "หากคุณมีโอกาสใน";
		mes "ในอนาคตพบกันใน";
		mes "เมืองแห่งความตายในเวลาต่อมา";
		mes "ฮ่า ๆ ๆ ๆ ~";
		emotion ET_BEST;
		close;
	}
	if (nif_revive == 1) {
		mes "[Billik]";
		mes "เมืองแห่งความตาย ...";
		mes "มันเป็นสถานที่ที่อันตรายมาก";
		mes "ทำไมคุณถึงอยากไปที่นั่น?";
		next;
		if (select("ฉันต้องเจอใครสักคนที่นั่น:มันเป็นส่วนหนึ่งของการผจญภัยของฉัน") == 1) {
			mes "[Billik]";
			mes "ใครบางคนที่คุณต้องพบที่นั่น ...";
			mes "อืม ... ฉันคิดว่าคุณต้องการ";
			mes "ติดต่อคนตาย ...";
			mes "ฉันยังเห็นว่าคุณ";
			mes "มุ่งมั่นที่จะไป";
			next;
			mes "[Billik]";
			mes "* ถอนหายใจ * เอาล่ะถ้าคุณต้องการจริงๆ";
			mes "ไปคุณต้องหาผู้ชายที่ชื่อ";
			mes "'เฟย์ลิน' มันสำคัญมากที่จะ";
			mes "บอกให้เขารู้ว่าฉันแนะนำ";
			mes "เขากับคุณ";
			next;
			mes "[Billik]";
			mes "เขาไม่ค่อยไว้ใจ แต่เขา";
			mes "ชอบดอกกุหลาบมากดังนั้นจึงควรนำดอกกุหลาบมาด้วย";
			mes "สำหรับเขา. ด้วยวิธีนี้เขาจะรู้ว่า";
			mes "แน่ใจว่าฉันได้ส่งคุณไปแล้ว";
			set nif_revive,2;
			changequest 11038,11039;
			next;
			mes "[Billik]";
			mes "เมืองของคนตายตั้งอยู่";
			mes "ทางเหนือของสถานที่แห่งนี้ จำไว้";
			mes "อันตรายมากที่จะไปที่นั่น";
			mes "แม้จะมีประสบการณ์ดีก็ตาม";
			mes "นักผจญภัยดังนั้น ...";
			mes "ระวัง.";
			close;
		}
		mes "[Billik]";
		mes "ฮ่าฮ่าฮ่า ~ ฉันเดาว่าคุณชอบการผจญภัย";
		mes "เท่าที่ฉันทำ แม้ว่าจะเป็นไฟล์";
		mes "สถานที่ที่อันตรายมากคุณจะอยู่";
		mes "จ่ายคืนสำหรับความพยายามของคุณหลังจากนั้น";
		mes "คุณไปถึงที่นั่น แต่ระวัง.";
		close;
	}
	else if (nif_revive == 0) {
		mes "[Billik]";
		mes "อะไรทำให้คุณมาที่นี่? ผมยุ่งอยู่,";
		mes "ออกไปถ้าคุณไม่มีธุระอะไร";
		close;
	}
}

niflheim,153,215,3	script	Feylin	794,{
	if (nif_revive == 2) {
		mes "[เฟย์ลิน]";
		mes "แย่"+((Sex == SEX_MALE)?"ผู้ชาย":"สาว")+"...";
		mes "ตายตั้งแต่อายุยังน้อย ...";
		mes "ฉันเสียใจเสมอที่เห็นไฟล์";
		mes "หนุ่มสาวจากไป";
		next;
		mes "[เฟย์ลิน]";
		mes "............ ??";
		mes "ฉันเห็นว่าคุณยังไม่ตาย";
		mes "เหรอ? มันไม่น่าเชื่อเลย";
		mes "ดูคนที่มีชีวิตอีกคน";
		mes "ในเมืองนี้";
		next;
		mes "[เฟย์ลิน]";
		mes "คุณมาเพื่อช่วยเหลือใครบางคน";
		mes "หรือคุณมีจุดประสงค์อื่น?";
		mes "หากคุณมาที่นี่เพื่อพบกับ";
		mes "เพื่อนที่เสียชีวิตของคุณคุณ";
		mes "มาหาคนผิด";
		next;
		mes "[เฟย์ลิน]";
		mes "ไม่มีใครใน Niflheim ทำได้";
		mes "แสดงปาฏิหาริย์แบบนั้น ...";
		next;
		mes "[เฟย์ลิน]";
		mes "ฉันบอกได้เลยว่าคุณเคยผ่านมาแล้ว";
		mes "ความยากลำบากมาก แต่น่าเสียดาย";
		mes "คุณมาที่นี่เพื่ออะไร ฉันหวังว่า";
		mes "คุณกลับไปที่ที่คุณมา";
		mes "ก่อนที่จะสายเกินไป";
		set nif_revive,3;
		changequest 11039,11040;
		close;
	}
	else if (nif_revive == 3) {
		mes "[เฟย์ลิน]";
		mes "ฉันขอให้คุณออกจากสิ่งนี้";
		mes "เมืองทันที. ฉันขอโทษ";
		mes "แต่ฉันไม่รู้อะไรเลย";
		mes "คุณกำลังถาม";
		emotion ET_FRET;
		next;
		if (select("แต่ ...:ฉันขอโทษ") == 1) {
			mes "[เฟย์ลิน]";
			mes "คุณต้องรู้อะไรบางอย่าง ...";
			mes "มิฉะนั้นคุณจะไม่เป็น";
			mes "ดื้อมาก ฉันไม่แน่ใจ";
			mes "ใครบอกให้คุณติดต่อ";
			mes "ตายก็เป็นไปได้ ...";
			next;
			mes "[เฟย์ลิน]";
			mes "แต่ฉันคงไม่ได้บอกแน่นอน";
			mes "คุณ. นอกจากนี้การพูดเกี่ยวกับ";
			mes "สิ่งนั้นเป็นสิ่งต้องห้าม";
			set nif_revive,4;
			changequest 11040,11041;
			close;
		}
		mes "[เฟย์ลิน]";
		mes "โปรดพิจารณาทิ้งสิ่งนี้";
		mes "วางโดยเร็วที่สุด";
		mes "ไม่ควรมีใครอยู่ใน";
		mes "Niflheim นานเกินไป";
		close;
	}
	else if (nif_revive == 4) {
		if (countitem(748) > 0) {
			mes "[เฟย์ลิน]";
			mes "หึบิลลิกต้องส่งคุณไปแล้ว";
			mes "เนื่องจากฉันเป็นหนี้บุญคุณเขาฉัน";
			mes "สมมติว่าฉันจะช่วยคุณ";
			mes "แต่ผมขอเตือนคุณ";
			mes "ราคาที่ฉันเรียกร้องอาจจะ";
			mes "มากกว่าที่คุณคาดหวัง ...";
			next;
			if (select("ฉันเต็มใจที่จะทำสิ่งนี้:ฉันไม่คิดว่าจะทำได้") == 1) {
				if (BaseLevel > 79) {
					mes "[เฟย์ลิน]";
					mes "เอาล่ะ แต่คุณต้องสัญญา";
					mes "ฉันว่าคุณจะไม่ปล่อยให้คนอื่น";
					mes "ผู้คนรู้ว่าฉันเกี่ยวกับอะไร";
					mes "จะบอกคุณ.";
					next;
					mes "[เฟย์ลิน]";
					mes "คาถาอาคมนี้ต้องใช้มาก";
					mes "เสียสละ. ไม่จำเป็นต้องพูดตรงนั้น";
					mes "เป็นโอกาสที่คาถานี้อาจ";
					mes "ล้มเหลว. ฉันก็ต้องมี";
					mes "10 Yggdrasil Leaf ที่จะร่ายสิ่งนี้";
					mes "สะกด. คุณต้องการดำเนินการต่อหรือไม่";
					next;
					if (select("ใช่ไม่ใช่") == 1) {
						if (countitem(610) > 9) {
							mes "[เฟย์ลิน]";
							mes "ฉันเห็น. จากนั้นเราจะเริ่ม";
							mes "วุมบาห์โซกาสะบาหฺ!";
							mes "วูมบาห์วูมบาห์ฮอมบาห์!";
							mes "เย้!";
							next;
							set .@nif_revive1,rand(1,100);
							if (.@nif_revive1 > 0) && (.@nif_revive1 < 88) {
								mes "[เฟย์ลิน]";
								mes "...ฉันขอโทษ.";
								mes "ฉันไม่สามารถเรียก";
								mes "ใครที่คุณต้องการเห็น ฉัน";
								mes "ขออภัยจริงๆสำหรับผลลัพธ์นี้";
								emotion ET_HUK;
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								set nif_revive,5;
								changequest 11041,11042;
								percentheal -99,0;
								next;
								mes "[เฟย์ลิน]";
								mes "แต่น่าเสียดายที่มันอาจจะ";
								mes "ไม่สามารถเรียกไฟล์";
								mes "จิตวิญญาณของบุคคลที่มี";
								mes "ที่คุณอยากจะพูด ...";
								close;
							}
							else if (.@nif_revive1 > 87) && (.@nif_revive1 < 101) {
								delitem 748,1; //Witherless_Rose
								delitem 610,10; //Leaf_Of_Yggdrasil
								percentheal -50,0;
								mes "[เฟย์ลิน]";
								mes "..........";
								mes "..........";
								next;
								mes "[Elly]";
								mes "..........";
								mes "..... อืม ...";
								mes "... ฉันเพิ่งตายไม่ใช่เหรอ?";
								mes "อืมฉันรู้จักคุณไหม";
								next;
								select("ฉันนำข้อความจากสามีของคุณ");
								mes "[Elly]";
								mes "โอ้ ... ฉันเข้าใจแล้ว ... ฉันขอโทษที่ไม่ได้พบเขาก่อนที่ฉันจะเสียชีวิต ...";
								mes "แล้วข้อความของเขาสำหรับฉันคืออะไร?";
								next;
								select("ยกโทษให้เขาในสิ่งที่เกิดขึ้นเมื่อหนึ่งเดือนที่แล้ว");
								mes "[Elly]";
								mes "* เฮ้อ * เขาเป็นสามีที่ดีมาก ...";
								mes "เขาให้ความสนใจเช่นนี้แม้กระทั่ง";
								mes "สิ่งเล็ก ๆ น้อย ๆ แม้ว่าฉัน";
								mes "ควรเป็นคนที่ขอโทษ";
								mes "ให้เขา...";
								next;
								mes "[Elly]";
								mes "โปรดให้กล่องนี้แก่เขาและบอก";
								mes "เขาจะลืมฉัน เขาควร";
								mes "ใช้ชีวิตที่เหลืออย่างมีความสุข";
								mes "กับคนอื่น โอ้ยังให้";
								mes "เขารู้ว่าฉันให้อภัยเขา";
								set nif_revive,6;
								changequest 11040,11043;
								getitem 934,1; //Mementos
								next;
								mes "[Elly]";
								mes "ฉันดีใจที่ได้ยินจาก";
								mes "สามีอีกครั้ง แต่ฉัน";
								mes "คิดว่าฉันต้องไปแล้ว";
								mes "ขอบคุณสำหรับปัญหาของคุณ";
								mes "อำลา ...";
								next;
								mes "[Elly]";
								mes ".....................";
								mes "....................";
								next;
								mes "[เฟย์ลิน]";
								mes "ฉันเดาว่าคุณทำอะไรสำเร็จแล้ว";
								mes "คุณต้องการ. ถ้าอย่างนั้นระวัง";
								mes "เมื่อคุณกลับไป ...";
								close;
							}
						}
						else {
							mes "[เฟย์ลิน]";
							mes "หากคุณไม่มีไฟล์";
							mes "รายการเราไม่สามารถดำเนินการต่อได้";
							mes "จำไว้ว่าฉันต้องการ 10 Yggdrasil";
							mes "ออกจากการร่ายมนตร์นี้";
							mes "กรุณานำมาโดยเร็ว";
							mes "เป็นไปได้.";
							close;
						}
					}
					mes "[เฟย์ลิน]";
					mes "ฉันหวังว่าคุณจะนำสิ่งของต่างๆ";
					mes "อันดับแรก. แต่ ... ฉันไม่มี";
					mes "มั่นใจที่จะสัญญาว่า";
					mes "คาถานี้จะร่ายสำเร็จ";
					close;
				}
				mes "[เฟย์ลิน]";
				mes "อืม ... ดูเหมือนจะไม่มี";
				mes "ความแข็งแกร่งที่จะอดทน";
				mes "การร่ายคาถานี้ ที่";
				mes "จุดแข็งของคุณในปัจจุบันนี้";
				mes "คาถาจะฆ่าคุณ";
				mes "ฉันไม่สามารถรับความเสี่ยงนั้นได้";
				close;
			}
			mes "[เฟย์ลิน]";
			mes "คุณตัดสินใจดีแล้ว";
			mes "ไม่ใช่ความคิดที่ดีที่จะแสดง";
			mes "เวทมนตร์ชนิดนี้ใน";
			mes "ที่หนึ่งเพราะ";
			mes "ความเสี่ยงที่เกี่ยวข้อง ...";
			close;
		}
		mes "[เฟย์ลิน]";
		mes "ไม่ว่าจะถามกี่ครั้ง";
		mes "ฉันฉันไม่สามารถช่วยคุณได้ กรุณา";
		mes "ออกจากสถานที่นี้โดยเร็วที่สุด";
		close;
	}
	mes "[เฟย์ลิน]";
	mes "อะไรที่ทำให้คุณต้องมาเช่นนั้น";
	mes "เมืองอันตราย? กรุณาออกไป";
	mes "สถานที่นี้โดยเร็วที่สุด";
	close;
}

