//===== rAthena Script ======================================= 
//= Quest NPCs related to Geffen
//===== By:================================================== 
//= kobra_k88 & Samuray22
//===== Current Version:===================================== 
//= 1.5
//===== Compatible With:===================================== 
//= rAthena Project; 7.15 +
//===== Description:========================================= 
//= Welding Mask and Headset Quests.
//===== Additional Comments:================================= 
//= 1.1 Fixed exploits [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated "หน้ากากเชื่อม" Quest to the Official one. [Samuray22]
//= 1.3a replaced item "ชื่อ" with item id. Added missing [Lupus]
//= 1.4 Updated welding mask and headset quests. [L0ne_W0lf]
//= 1.5 Added Number Match Game. [Euphy]
//============================================================ 

// Blacksmith:'Welding Mask' Quest
//============================================================
geffen_in,144,166,4	script	Blacksmith	63,{
	mes "[ช่างตีเหล็ก]";
	mes "คุณไม่เคยได้ยินเกี่ยวกับฉันเหรอ? ทำไมฉันถึงเป็นช่างตีเหล็กรุ่นเก๋าของดินแดนนี้ ฉันใช้เวลา 30 ปีในไฟนรกที่ร้อนแรงและกระหายเลือดดัดเหล็กตามความต้องการของฉัน!";
	next;
	mes "[ช่างตีเหล็ก]";
	mes "แต่...";
	mes "ฉันอายุมากขึ้นและความกล้าหาญของฉันเริ่มจางหายไปตามอายุ ดังนั้นฉันจึงได้คิดค้นบางสิ่งบางอย่างสำหรับช่างตีเหล็กที่กำลังมาแรงและกำลังมาแรงหวังว่าพวกเขาจะเชี่ยวชาญงานฝีมือของฉันด้วย ...";
	next;
	switch(select("มันคืออะไร?:...")) {
	case 1:
		mes "[ช่างตีเหล็ก]";
		mes "พวกเราช่างตีเหล็กต้องจัดการเหล็กภายใต้ความร้อนสูงอยู่เสมออากาศร้อนและโลหะที่หลอมละลายนั้นเกินกว่าที่คนส่วนใหญ่จะทนได้";
		next;
		mes "[ช่างตีเหล็ก]";
		mes "เพื่อประโยชน์ในงานฝีมือของเราใบหน้าที่สวยงาม แต่เป็นผู้ชายของเราจึงตกอยู่ในความเสี่ยง ...";
		next;
		mes "[ช่างตีเหล็ก]";
		mes "แต่ด้วยความมหัศจรรย์ของเทคโนโลยีนี้จึงไม่ต้องกังวล! นี่แน่ะ ^3355FF หน้ากากเชื่อม ^000000 !!";
		next;
		mes "[ช่างตีเหล็ก]";
		mes "มันทำงานโดยปิดหน้าของคุณด้วยแผ่นโลหะไขมัน เป็นไอเท็มที่ใคร ๆ ก็อยากได้! ถ้าไม่ใช่ตัวคุณเองก็ควรมอบสิ่งนี้ให้กับเพื่อนที่เรียนตีเหล็ก";
		next;
		mes "[ช่างตีเหล็ก]";
		mes "แล้วเป็นไงล่ะ? ถ้าคุณให้ ^2244FF50 Steel ^000000 และ ^4422FF เพียง 2,000 Zeny ^000000 ฉันจะทำให้คุณทันที ~ !!";
		next;
		switch(select("หืม ... ไม่เลว เอาล่ะ:มันทำงานอีกครั้งได้อย่างไร?")) {
		case 1:
			if ((countitem(999) > 49) && (Zeny > 1999)){
				mes "[ช่างตีเหล็ก]";
				mes "อาฉันเห็นว่าคุณชื่นชมผลงานของช่างตีเหล็ก! ได้เลยฉันจะทำทันที !!";
				next;
				delitem 999,50;  //Steel
				set Zeny,Zeny-2000;
				getitem 2292,1; // Welding_Mask
				mes "[ช่างตีเหล็ก]";
				mes "ฮ่าฮ่าฮ่า ~! ใช้ประโยชน์จากสิ่งนั้น!";
				close;
			}
			else {
				mes "[ช่างตีเหล็ก]";
				mes "อืม ...";
				mes "คุณไม่มีสิ่งของที่ฉันต้องการ";
				next;
				mes "[ช่างตีเหล็ก]";
				mes "ช่างตีเหล็กทั่วไปจะตีความว่านี่เป็นสัญญาณของการไม่เคารพ แต่ฉันจะยกโทษให้คุณ อย่าลืมว่าฉันต้องการอะไรถ้าคุณต้องการให้ฉันทำหน้ากากนี้ให้คุณ";
				close;
			}
		case 2:
			mes "[ช่างตีเหล็ก]";
			mes "^3355FF หน้ากากเชื่อม ^000000 ปกป้องใบหน้าของคุณด้วยการปิดทับด้วยแผ่นโลหะไขมัน สิ่งประดิษฐ์นี้เป็นผลมาจากการใช้ค้อนและทั่งค้อนและทั่งที่ทำงานหนักตลอด 30 ปี";
			close;
		}
	case 2:
		mes "[ช่างตีเหล็ก]";
		mes "อา...!";
		mes "ฉันเดาว่านี่เป็นครั้งแรกที่คุณได้พบกับช่างตีเหล็กที่เก่งกาจเท่าฉันและคุณก็พูดไม่ออกด้วยความกลัว กรุณาสละเวลาและพูดเมื่อคุณพร้อม";
		next;
		mes "^3355FF ดูเหมือนผู้ชายคนนี้";
		mes "อิ่มนิดหน่อย";
		mes "ของตัวเอง ^000000";
		close;
	}
}

// Eric:'Headset' Quest
//============================================================
geffen_in,30,71,4	script	Eric	83,{
	mes "[เอริค]";
	mes "โปรดฟัง";
	mes "กับเรื่องราวของฉัน";
	mes "จากความสุขของฉัน";
	mes "ปู่.";
	next;
	switch(select("พูดคุย:ถามเกี่ยวกับรายการที่ต้องการ:ผลิต:เลิก")) {
	case 1:
		mes "[เอริค]";
		mes "ปู่ของฉันที่จากไปเมื่อไม่กี่ปีก่อนเป็นคนพิเศษสำหรับฉัน เขามักจะอยู่กับฉันและดูแลฉัน เขาดีกว่าคนเก่าของฉันมากที่ทำงานยุ่งอยู่ตลอดเวลา";
		next;
		mes "[เอริค]";
		mes "สำหรับฉันห้องสมุดของปู่เป็นเหมือนดินแดนในจินตนาการที่ฉันสามารถหลบหนีได้ ฉันใช้เวลาส่วนใหญ่อยู่ที่นั่น";
		next;
		mes "[เอริค]";
		mes "ฉันได้เรียนรู้สิ่งต่างๆมากมายจากหนังสือของเขาในห้องสมุด ปู่สนใจเรื่องการเล่นแร่แปรธาตุและดนตรีและห้องสมุดของเขาเต็มไปด้วยหนังสือเก่าลึกลับมากมาย";
		next;
		mes "[เอริค]";
		mes "วันหนึ่ง...";
		mes "ตามปกติฉันไปที่ห้องสมุดและค้นพบแผนผังที่น่าสนใจ";
		next;
		mes "[เอริค]";
		mes "มันแสดงให้เห็นเครื่องดนตรีบางอย่างที่ดูเหมือน Ear Muffs ที่เมื่อสวมบนศีรษะแล้วสามารถส่งเสียงเพลงหรือกระซิบข้างหูผ่านลวดเส้นเล็ก ๆ";
		next;
		mes "[เอริค]";
		mes "ฉันคิดว่า...";
		mes "มันเป็นงานที่ยังทำไม่เสร็จของพ่อของฉัน ฉันได้พบกับนักเล่นแร่แปรธาตุสองสามคนและขอให้พวกเขาสร้างมันขึ้นมา แต่พวกเขาปฏิเสธข้อเสนอของฉัน ...";
		next;
		mes "[เอริค]";
		mes "ในที่สุดฉันก็คิดว่า 'ฉันต้องทำมันด้วยตัวเอง' ดังนั้นฉันจึงค้นคว้าอยู่สองสามปีและตอนนี้ฉันเข้าใจวิธีการสร้างสิ่งนั้นแล้ว แต่ฉันไม่สามารถรวบรวมวัสดุเพื่อสร้างมันได้!";
		next;
		mes "[เอริค]";
		mes "ถ้าฉันสามารถหาบทความเหล่านั้นได้ฉันจะสร้างมันทันที หากคุณสนใจไอเดียของฉันคุณจะนำมาให้ฉันไหม";
		next;
		mes "[เอริค]";
		mes "สิ่งที่ฉันอยากทำคือสร้างสิ่งนี้เพื่อเป็นเกียรติแก่ปู่ของฉันและทำงานที่เขาเริ่มให้เสร็จ ถ้าคุณขอให้ฉันเก็บไว้ฉันก็ไม่รังเกียจ";
		close;
	case 2:
		mes "[เอริค]";
		mes "40 ^3355FFSteel ^000000";
		mes "1 ^3355FFOridecon ^000000";
		mes "1 ^3355FF แอลกอฮอล์ ^000000";
		mes "1 ^3355FFCoal ^000000";
		mes "นี่คือสิ่งที่ฉันต้องการสำหรับผลงานชิ้นเอกของปู่";
		close;
	case 3:
		if ((countitem(999) > 39) && (countitem(984) > 0) && (countitem(970) > 0) && (countitem(1003) > 0)) {
			delitem 999,40; //Steel
			delitem 984,1; //Oridecon
			delitem 970,1; //Alchol
			delitem 1003,1; //Coal
			getitem 5001,1; //Headset
			mes "* แตะ! แตะ! แตะ! *";
			next;
			mes "[เอริค]";
			mes "ขอบคุณ.";
			mes "เพราะคุณฉันสามารถทำตามความปรารถนาของคุณปู่ของฉันได้ เขาจะมีความสุขถ้าเขาดูถูกฉันจากสวรรค์";
			next;
			mes "[เอริค]";
			mes "ก็อย่างที่บอกว่าเอาอย่างนี้เถอะ ฉันแค่ภูมิใจที่ทำสิ่งนี้ได้ด้วยตัวเอง";
			close;
		}
		else {
			mes "[เอริค]";
			mes "ขอบคุณที่พยายามช่วยฉัน แต่คุณไม่ได้นำสิ่งของทั้งหมดที่ฉันต้องการมาด้วย ฉันคิดว่าคุณจะพบพวกเขาทั้งหมดในไม่ช้า";
			close;
		}
	case 4:
		mes "[เอริค]";
		mes "น - ไม่?";
		mes "ดี...";
		mes "คุณมีของคุณเอง";
		mes "กังวลฉันคิดว่า";
		mes "อำลา.";
		close;
	}
}

// Number Match Game::gef_yagu
//============================================================
geffen,122,77,4	script	Nia#yagu	700,{
	mes "[Nia]";
	mes "สวัสดีฉันชื่อนีอาคนที่ห้า";
	mes "ศิษย์ของ Mawong, the";
	mes "ผู้บงการเกม ตอนนี้";
	mes "คุณต้องการเล่นไฟล์";
	mes "เกมจับคู่หมายเลขกับฉัน?";
	next;
	switch(select("เกมจับคู่หมายเลข:ฝึกจับคู่หมายเลข:เล่นจับคู่หมายเลข:เจ้าของบันทึก:ยกเลิก")) {
	case 1:
		mes "[Nia]";
		mes "อ่าวัตถุประสงค์ของ";
		mes "เกมจับคู่หมายเลขคือการ";
		mes "เดาหมายเลขที่ฉันคิด";
		mes "ของ. แน่นอนว่ามีไม่กี่อย่าง";
		mes "กฎที่ จำกัด จำนวน";
		mes "ที่ฉันสามารถเลือกได้";
		next;
		mes "[Nia]";
		mes "ตัวเลขทั้งหมดที่ฉันทำ";
		mes "ต้องเป็น 3 หลักฉันทำไม่ได้";
		mes "ใช้ 0 สำหรับตัวเลขใด ๆ";
		mes "และฉันไม่สามารถใช้สิ่งเดียวกันได้";
		mes "หมายเลขสำหรับหลักใด ๆ";
		mes "มากกว่าหนึ่งครั้ง.";
		next;
		mes "[Nia]";
		mes "ตัวอย่างเช่นฉันทำไม่ได้";
		mes "ใช้ตัวเลข 103, 112,";
		mes "252 หรือ 701 ตัวเลขเหล่านั้น";
		mes "มี 0 หรือใช้";
		mes "ตัวเลขเดียวกันในหลัก";
		mes "มากกว่าหนึ่งครั้ง. เข้าใจแล้วใช่ไหม";
		next;
		mes "[Nia]";
		mes "ตอนนี้คุณเดาได้เพียง 5 ครั้ง";
		mes "และหลังจากทายแต่ละครั้งฉันจะบอก";
		mes "คุณเดาได้กี่หลัก";
		mes "ตามด้วยจำนวนตัวเลขที่เดาถูกต้องใน";
		mes "ตำแหน่งหลักที่ถูกต้อง";
		next;
		mes "[Nia]";
		mes "ตัวอย่างเช่นสมมติว่าไฟล์";
		mes "เลขที่ฉันคิดขึ้นมาคือ 168";
		mes "หากการเดาครั้งแรกของคุณคือ 678 แสดงว่าคุณเดาได้สองหลัก";
		mes "6 และ 8 อย่างถูกต้อง อย่างไรก็ตาม";
		mes "มีเพียง 8 เท่านั้นที่อยู่ในตำแหน่งที่ถูกต้อง";
		next;
		mes "[Nia]";
		mes "คุณก็รู้ว่ามันเป็นความคิดที่ดี";
		mes "หากคุณลองโหมดฝึกฝน";
		mes "อันดับแรก. จากนั้นเมื่อคุณมากขึ้น";
		mes "สะดวกสบายเล่นจริง";
		mes "เกมจับคู่หมายเลข อย่าลืมว่าความเร็วนั้นมีผลกับคะแนนของคุณ!";
		next;
		mes "[Nia]";
		mes "อ่าถ้าคุณบังเอิญ";
		mes "สร้างสถิติใหม่ในไฟล์";
		mes "เกมจับคู่หมายเลขคุณจะ";
		mes "มีการบันทึกชื่อของคุณ";
		mes "อย่าพลาดโอกาสนี้สำหรับ";
		mes "ชื่อเสียงและอาจจะโชคลาภ ~";
		close;
	case 2:
		mes "[Nia]";
		mes "เยี่ยมมาฝึกกันเถอะ";
		mes "เกมจับคู่หมายเลข กรุณา";
		mes "ลองเดาหมายเลขของฉันโอเคไหม";
		mes "หากคุณต้องการยกเลิกโปรดป้อนตัวเลขที่มากกว่า 1,000";
		set .@practice,1;
		break;
	case 3:
		mes "[Nia]";
		mes "เยี่ยมมากมาเล่น Number Match กันเถอะ!";
		mes "คุณจะมีโอกาส 5 ครั้งในการเดาหมายเลขที่ฉันคิด";
		mes "หากคุณต้องการยกเลิกโปรดป้อนตัวเลขที่มากกว่า 1,000";
		set .@practice,0;
		break;
	case 4:
		set .@name_record$, $050908_minus1_yagu$;
		if (.@name_record$ == "") //custom
			set .@name_record$,"(ว่าง)";
		set .@score_record, $050908_minus1_yagu;
		set .@score_min, .@score_record / 60;
		set .@score_sec, .@score_record % 60;
		mes "[Nia]";
		mes "^ff0000" + .@name_record$ + "^000000 คือ";
		mes "เจ้าของบันทึกหมายเลข";
		mes "จับคู่เกมกับเวลา";
		mes "^ff0000" + .@score_min + " นาที " + .@score_sec + " วินาที ^000000";
		close;
	case 5:
		mes "[Nia]";
		mes "ดูแลตัวเอง";
		mes "นักผจญภัย. ถ้าคุณเคย";
		mes "รู้สึกเหมือนเล่นตัวเลข";
		mes "เกมจับคู่มาคุยกัน";
		mes "สำหรับฉันโอเค? แล้วเจอกันนะ ~";
		close;
	}
	next;

	// Generate 3 digits of a number.
	while(1) {
		set .@yagu100, rand(1,9);
		set .@yagu10, rand(1,9);
		set .@yagu1, rand(1,9);
		if ((.@yagu100 != .@yagu10) && (.@yagu100 != .@yagu1) && (.@yagu10 != .@yagu1)) {
			// Store the actual number (unofficial).
			set .@yagu, (100*.@yagu100) + (10*.@yagu10) + .@yagu1;
			break;
		}
	}

	// The official time calculation is based on ServerTime,
	// an int HHMMSS equivalent to atoi(gettimestr("% H% M% S",7)).
	// UNIX time is really a better way to do this.
	set .@start_time, gettimetick(2);

	while(1) {

		// Guess a number that meets all conditions.
		while(1) {
			if (input(.@input,100,999) != 0) {
				mes "[Nia]";
				mes "คุณได้ยกเลิกไฟล์";
				mes "เกมจับคู่หมายเลข";
				close;
			}
			set .@input100, .@input / 100;
			set .@input10, (.@input % 100) / 10;
			set .@input1, .@input % 10;
			if ((.@input100 > 0) && (.@input10 > 0) && (.@input1 > 0)) {
				if ((.@input100 != .@input10) && (.@input100 != .@input1) && (.@input10 != .@input1))
					break;
			}
			mes "[Nia]";
			mes "คุณได้ป้อนไฟล์";
			mes "ตัวเลขเป็น 0 หรือป้อนไฟล์";
			mes "ค่าตัวเลขเดียวกันใน";
			mes "ตัวเลขมากกว่าหนึ่งครั้ง";
			mes "โปรดป้อนการคาดเดาอื่น";
			next;
		}

		set .@retry, .@retry+1;
		mes "[Nia]";
		mes "^ff0000"+strcharinfo(0)+"^000000,";
		mes "การคาดเดาของคุณคือ ^0000ff"+.@input+"^000000";
		mes "ให้เวลาฉันสักครู่";
		mes "มาพร้อมกับผลลัพธ์ของคุณ";
		next;

		// Correct digits in correct locations.
		set .@strike,0;
		if (.@yagu100 == .@input100)
			set .@strike, .@strike+1;
		if (.@yagu10 == .@input10)
			set .@strike, .@strike+1;
		if (.@yagu1 == .@input1)
			set .@strike, .@strike+1;

		// Correct digits in incorrect locations.
		set .@ball,0;
		if ((.@yagu100 == .@input10) || (.@yagu100 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu10 == .@input100) || (.@yagu10 == .@input1))
			set .@ball, .@ball+1;
		if ((.@yagu1 == .@input100) || (.@yagu1 == .@input10))
			set .@ball, .@ball+1;

		if (.@strike == 3) {
			if (.@practice) {
				mes "[Nia]";
				mes "ใช่ตัวเลขที่";
				mes "ฉันเดาว่าเป็น ^ff0000"+.@yagu+"^000000!";
				mes "ขอแสดงความยินดีคุณ";
				mes "เพิ่งชนะการฝึกนี้";
				mes "เกม! ตอนนี้คุณควรจะเป็น";
				mes "พร้อมของจริง ~";
				close;
			} else {

				// Calculate final time and check against the previous record.
				set .@end_time, gettimetick(2);
				if (.@end_time < .@start_time)
					set .@end_time, .@end_time + 3600;
				set .@total_time, .@end_time - .@start_time;
				set .@total_min, .@total_time / 60;
				set .@total_sec, .@total_time % 60;
				set .@name_record$, $050908_minus1_yagu$;
				if (.@name_record$ == "") //custom
					set .@name_record$,"(ว่าง)";
				set .@score_record, $050908_minus1_yagu;
				set .@score_min, .@score_record / 60;
				set .@score_sec, .@score_record % 60;

				if (.@score_record >= .@total_time || !.@score_record) { //custom (second condition)
					set $050908_minus1_yagu$, strcharinfo(0);
					set $050908_minus1_yagu, .@total_time;
					mes "[Nia]";
					mes "เยี่ยมมาก! คุณเดา";
					mes "หมายเลขของฉันซึ่งก็คือ";
					mes "^ff0000" + .@yagu + "^000000 คุณทำเสร็จแล้ว";
					mes "^ff0000" + .@total_min + " นาที " + .@total_sec + " วินาที ^000000,";
					mes "ทำลายสถิติก่อนหน้านี้";
					mes "ของ ^ff0000" + .@score_min + " นาที " + .@score_sec + " วินาที ^000000";
					next;
					mes "[Nia]";
					mes "^ff0000" + .@name_record$ + "^000000";
					mes "สร้างสถิติเก่านั้น";
					mes "แต่ตอนนี้คุณคือ";
					mes "คนใหม่ที่จะเอาชนะใน";
					mes "เกมจับคู่ ยินดีด้วย";
					mes "สำหรับการสร้างสถิติใหม่!";
					close;
				}
				mes "[Nia]";
				mes "ทำได้ดีมากคุณเดาได้";
				mes "เบอร์ฉัน! คำตอบของ";
				mes "แน่นอนคือ ^ff0000" + .@yagu + "^000000 คุณจัดการได้";
				mes "เพื่อเดาหมายเลขนี้ให้ถูกต้อง";
				mes "ใน ^ff0000" + .@total_min + " นาที " + .@total_sec + " วินาที ^000000";
				next;
				mes "[Nia]";
				mes "^ff0000" + .@name_record$ + "^000000 คือ";
				mes "การจับคู่หมายเลขปัจจุบัน";
				mes "เจ้าของบันทึกพร้อมเวลา";
				mes "ของ ^ff0000" + .@score_min + " นาที " + .@score_sec + " วินาที ^000000";
				mes "ขอให้โชคดีและหวังว่าคุณ";
				mes "สามารถทำลายสถิตินี้ได้ในสักวัน ~";
				close;
			}
		} else {
			if (.@practice) {
				mes "[Nia]";
				mes "การคาดเดาของคุณมี ^ff0000" + .@ball + "^000000 ของ";
				mes "ตัวเลขที่ถูกต้อง ^ff0000" + .@strike + "^000000 ของ";
				mes "ตัวเลขในการเดาของคุณมี";
				mes "หมายเลขที่ถูกต้องใน";
				mes "ตำแหน่งหลักที่ถูกต้อง";
				mes "ได้เลยลองอีกครั้ง ~";
				next;
			} else {
				mes "[Nia]";
				mes "การคาดเดาล่าสุดของคุณมี ^ff0000" + .@ball + "^000000";
				mes "ของตัวเลขที่ถูกต้อง";
				mes "และ ^ff0000" + .@strike + "^000000 หลักในการเดาของคุณ";
				mes "มีหมายเลขที่ถูกต้องใน";
				mes "ตำแหน่งหลักที่ถูกต้อง";
				switch(.@retry) {
					case 1:mes "คุณมีการคาดเดาสี่ครั้งที่เหลือ"; break;
					case 2:mes "คุณมีสามการคาดเดาที่เหลือ"; break;
					case 3:mes "คุณมีสองการคาดเดาที่เหลือ"; break;
					case 4:mes "คุณเหลือเพียงการเดาเดียวเท่านั้น ..."; break;
					case 5:mes "คำตอบคือ ^ff0000" + .@yagu + "^000000"; close;
				}
				next;
			}
		}
	}
}