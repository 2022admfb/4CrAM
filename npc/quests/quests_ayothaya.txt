//===== rAthena Script =======================================
//= Quest NPCs related to Ayothaya
//===== By ===================================================
//= Fredzilla, MasterOfMuppets
//===== Translate By: ==================================================
//= ZelLuRose
//===== Version ==============================================
//= 2.6
//===== Compatible With ======================================
//= rAthena Project
//===== Description ==========================================
//= [Official Conversion]
//= Ayothaya Ring Quest
//= Ayothaya Dungeon Quest
//= Tom Yum Goong Quest
//===== Comments =============================================
//= 1.0 First Release, made with infomation from RagnaInfo
//= 1.1 Added anti-novice protection [Lupus]
//= 1.3 Fixed exploit [Lupus]
//= 1.4 Updated, more close to the official. Thanks to birkiczd
//=     Also optimized it again, remove unused global variable [Lupus]
//= 1.5 Removed Duplicates [Silent]
//= 1.6 Moved quests from cities/ to here:
//=     Shrimp, Dungeon, Tom Yum Goong [Evera]
//= 1.7 Updated the Ayothaya Ring quest to the official one [MasterOfMuppets]
//= 1.8 Fixed experience gains to match upcoming rate adjustments. [SinSloth]
//= 1.9 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.0 Rescripted Tom Yum Goong and Dungeon quest to Aegis [L0ne_W0lf]
//=     10.3 standards.
//= 2.0a Corrected a Typo error ";;". [Samuray22]
//= 2.1 Added missing checkweights. [L0ne_W0lf]
//= 2.2 Replaced effect numerics with constants. [L0ne_W0lf]
//= 2.3 Replaced effect numerics with constants. [Samuray22]
//= 2.4 Updated RE/Pre-RE EXP. [Euphy]
//= 2.5 Added questlog support. [Euphy]
//= 2.6 Added Renewal quest items. [Euphy]
//============================================================

// Ayothaya Ring Quest :: ayo_ring
//============================================================
ayothaya,233,105,3	script	หญิงดูท่าทางเเข็งเเกร่ง	838,2,2,{
	if (thai_find == 14) {
		mes "[สุดา]";
		mes "ท่านได้เจอ";
		mes "กับอานนท์ไหม?";
		next;
		mes "[สุดา]";
		mes "ท่านยังไม่";
		mes "เจอเขาอีกเหรอ?";
		mes "ถ้าอย่างนั้น";
		mes "เขาอยู่ไหนหล่ะ?!";
		next;
		mes "[สุดา]";
		mes "โอ้...";
		mes "ฉันก็ไม่ค่อยอยากจะเชื่อสิ่งที่ท่านพูดเท่าไหร่หรอกนะ แต่ท่านเจอแหวนของฉัน แค่นี้ก็พอแล้ว ขอบคุณที่ช่วยนะ";
		next;
		mes "[สุดา]";
		mes "หากท่านเจอกับอานนท์ ช่วยบอกเขาให้กลับมาหาฉันด้วย ตอนนี้ท่านไปผจญภัยต่อเถอะ~";
		next;
		mes "[สุดา]";
		mes "ถ้าฉันเจอ";
		mes "เขาอีกครั้งนะ...";
		mes "เขาจะต้อง ^660000โดนดี^000000 แน่ๆ...";
		mes "^666666*บดฟันไปมา*^000000";
		close;
	} else if (thai_find > 2 && thai_find < 14) {
		mes "[สุดา]";
		mes "^666666*เห้อ...*^000000";
		mes "ท่านยังไม่";
		mes "เจอเขาอีกเหรอ?";
		mes "ไม่เป็นไรๆ";
		mes "เวลาไม่ใช่อุปสรรคของเรา";
		next;
		mes "[สุดา]";
		mes "และ 'เรา' นั้นหมายถึง '^660000ท่าน^000000' ฉันไม่สนแผนของท่าน หรืออะไรก็ตามที่ท่านอาจจะกำหนดไว้ เพราะท่านคือคนที่ใช้เวลา";
		mes "ให้ฉัน! โฮะ โฮะ โฮะ โฮะ!";
		close;
	} else if (thai_find == 2) {
		if (countitem(7288)) {
			mes "[สุดา]";
			mes "อ่า... นั้นมัน...?!!";
			mes "ใช่เลย นั้นแหละ!";
			mes "แหวนหมั้นของฉัน";
			mes "ที่ถูกเจ้าอานนท์บ้านั้นโยนลงน้ำไป!";
			next;
			mes "[สุดา]";
			mes "ขอบคุณมากๆ! เพื่อเป็นเกียรติแก่ท่าน ลูกเจ็ดคนแรกของเราจะตั้งชื่อตามท่าน! โอ้โหโฮ่โฮ่โฮ้โฮ้โฮ่ ~";
			next;
			mes "[สุดา]";
			mes "ตอนนี้...";
			mes "สิ่งที่ต้องทำก็คือ";
			mes "ตามหาอานนท์";
			mes "เจ้าบ้านั้น";
			mes "หายไปไหนกันแน่นะ?!";
			next;
			mes "[สุดา]";
			mes "^666666*อะแฮ่ม*^000000";
			mes "ฉะ ฉัน ไม่ได้อยาก";
			mes "ทำให้เขากลัวนะ ฉันต้องการเเค่";
			mes "แหวนหมั้นของฉันคืน";
			next;
			mes "[สุดา]";
			mes "^666666*เห้อ*^000000";
			mes "ฉันว่า";
			mes "วันที่ฉันจะได้เป็นเจ้าสาว";
			mes "มันยังห่างไกลอยู่เลย...";
			next;
			mes "^3355FFสุดาจ้องมาที่คุณ";
			mes "พร้อมกับทำสายตาอ้อนวอน^000000";
			next;
			switch(select("ยะ อย่ามองข้าแบบนั้นนะ!:เอาหล่ะ เอาหล่ะ")) {
			case 1:
				mes "[สุดา]";
				mes "เถอะหน่า! ท่านรู้ทุกสิ่งที่เกิดขึ้นเเล้ว และฉันรู้ว่าท่าน";
				mes "จะเดินดูรอบๆแถวนี้";
				next;
				mes "[สุดา]";
				mes "ฉันขอแค่ให้ท่านหาอานนท์ไปด้วย ตอนนี้ท่านไม่ได้ชอบแล้วเหรอ? ขอบคุณท่านที่ช่วยฉัน~";
				next;
				mes "[สุดา]";
				mes "ในระหว่างนี้ ฉันจะคิดว่าจะเอาคืนอานนท์อย่างไรดี ที่ทิ้งฉันไว้ให้ลำบากแบบนี้! เร็วเข้า รีบไปหาเขาซะสิ! โฮะ โฮะ โฮะ โฮะ!";
				emotion ET_SMILE;
				next;
				emotion ET_THINK, playerattached();
				mes "["+strcharinfo(0)+"]";
				mes "(เธอดูน่ากลัวจัง!";
				mes "เราคงต้อง";
				mes "ทำให้เธออีกสินะ...)";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "(ทำไมเราต้อง";
				mes "ทำเรื่องต่างๆ";
				mes "เพื่อคนอื่นอยู่ตลอดเลยเนี่ย?!)";
				break;
			case 2:
				mes "[สุดา]";
				mes "ใช่ ฉันรู้ว่าฉันไว้ใจท่านได้! เพราะฉะนั้นไปหาอานนท์ซะ! มันเป็นโชคชะตาที่เขาต้องเป็น ^660000ของฉัน^000000  โฮะ โฮะ โฮะ โฮะ!";
				next;
				mes "[สุดา]";
				mes "ฉันไม่ได้เห็นเขาในหมู่บ้านมาสักพักแล้ว และฉันก็ไม่คิดว่าเขาจะกระโดดลงไปในน้ำ เพื่อหาแหวนของฉันหรอก เพราะเขาเป็นคนที่ขี้เกียจมาก";
				next;
				mes "[สุดา]";
				mes "ท่านช่วยไปบอกอานนท์ได้ไหมว่า";
				mes "ฉันได้แหวนกลับมาเเล้ว กลับมาหาฉันได้ไหม? หวังว่าท่านจะจำได้นะท่านนักผจญภัย";
				break;
			}
			delitem 7288,1; //Thai_Ring
			changequest 12030,12031;
			set thai_find,3;
			close;
		} else {
			mes "[สุดา]";
			mes "หึ...?";
			mes "ท่านเจอแหวนไหม?";
			mes "ขอดูหน่อย เอามาให้ฉันดู!";
			next;
			mes "[สุดา]";
			mes "อะไรเนี่ย...";
			mes "ไม่ได้อะไรเลยงั้นเหรอ!";
			mes "อย่าโกหกฉันนะ";
			mes "เอาแหวนของฉันมา!";
			next;
			mes "[สุดา]";
			mes "เร็วๆเข้าแล้วรีบไป";
			mes "หาแหวนของฉัน ฉันจะรอ";
			mes "ฟังข่าวดีที่นี่แหละ";
			mes "โฮะ โฮะ โฮะ โฮะ~!";
			if (checkquest(12029) == -1)
				setquest 12029;
			close;
		}
	} else if (thai_find == 1) {
		mes "[สุดา]";
		mes "ฉันรู้สึกแย่นิดๆนะ ที่ขอให้ท่านช่วย เพราะท่าน";
		mes "ก็เป็นคนนอก";
		next;
		mes "[สุดา]";
		mes "แต่แหวนนั่นสำคัญสำหรับฉันจริงๆ หากท่านสามารถหามาให้ฉันได้ ฉันก็จะได้เป็นผู้หญิงที่มีความสุขที่สุดในโลก";
		next;
		mes "[สุดา]";
		mes "ขอร้องหล่ะ";
		mes "เอามันกลับมาให้ฉันเถอะนะ";
		mes "ฉันอยากอยู่กับอานนท์ในฐานะสามีภรรยา ที่มีความสุขตลอดไป ท่านไม่เข้าใจเหรอ?!";
		close;
	} else {
		mes "[หญิงดูท่าทางแข็งแกร่ง]";
		mes "อืมม ว่าไง...?";
		mes "ท่านต้องเป็นหนึ่งในนักท่องเที่ยวแน่ๆ ฉันอยากจะพาท่านเดินเที่ยวไปรอบๆ และฟังเรื่องราวของท่านกับโลกภายนอกจริงๆ แต่...";
		next;
		if (rand(0,1)) {
			mes "[หญิงดูท่าทางแข็งแกร่ง]";
			mes "ตอนนี้";
			mes "ฉันยุ่งมากกกกก";
			mes "ยุ่งมากจริงๆ";
			next;
			mes "[หญิงดูท่าทางแข็งแกร่ง]";
			mes "หึ...?";
			mes "ท่านไม่คิดว่าฉันยุ่ง เพราะฉันยืนอยู่กับที่และไม่ขยับเลยใช่ไหมหล่ะ?! แหงสิ ท่านนี่ช่างหยาบคายจริงๆ!";
			next;
			mes "[หญิงดูท่าทางแข็งแกร่ง]";
			mes "ตอนนี้ฉันต้องหา";
			mes "แหวนหมั้นของฉันก่อน";
			mes "ถ้าคนรักของฉัน";
			mes "ไม่ได้โยนมันลงไปหน่ะนะ...";
			next;
			switch(select("อะไรนะ...?:โกหกทั้งนั้น~!")) {
			case 1:
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "อะไร 'อะไร?'";
				mes "ท่านคิดว่าที่ฉัน";
				mes "ยืนอยู่แบบนี้";
				mes "มันสนุกมากเหรอ?!";
				next;
				emotion ET_KIK, playerattached();
				mes "["+strcharinfo(0)+"]";
				mes "ไม่ ไม่ ไม่ ไม่...";
				mes "ข้าหมายถึง ข้าไม่อยาก";
				mes "จะเชื่อว่าเจ้าหน่ะ";
				mes "มีคู่หมั้นเเล้ว!";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "หะ ว่าไงนะ!";
				mes "ท่านเป็นใคร";
				mes "มาพูดจาหยาบคายแบบนี้กับฉัน ไปซะฉันไม่มีอะไรจะคุยด้วยเเล้ว!";
				close;
			case 2:
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "โกหกอะไร?";
				mes "ฉันจะไม่ทนฟัง";
				mes "กับสิ่งที่ท่านพูด อย่าง 'ข้าไม่อยากจะเชื่อว่าเจ้าหน่ะมีคู่หมั้นเเล้ว!' อีก";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "โว้ โว้ โว้~";
				mes "ใจเย็น พี่สาว";
				mes "ฟังข้าก่อน";
				mes "สิ่งที่ข้าสงสัยคือ...";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "ฉันรู้ ฉันรู้";
				mes "ท่านคงสงสัยว่า";
				mes "ทำไมฉันถึงยืนอยู่ใกล้น้ำใช่ไหมหล่ะ นั่นก็เพราะว่า แหวนของฉันถูกโยนทิ้งมา";
				mes "และฉันต้องเอามันกลับคืนมา";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "นี่ฉันกำลังทำอะไรเนี่ย คุยกับนักท่องเที่ยวงั้นเหรอ? ^666666*เห้ออ...*^000000 ก็ดี ท่านได้รู้ในสิ่งที่อยากรู้แล้ว หวังว่าจะพอใจนะ";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "...";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "...";
				mes "......";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "...";
				mes "......";
				mes "........นี่";
				next;
				mes "[หญิงดูท่าทางแข็งแกร่ง]";
				mes "ทำไมถึงยังจ้องฉันอยู่คะ?";
				mes "ท่านไม่มีอย่างอื่นให้ทำหรือยังไง?";
				next;
				switch(select("ไม่:ม่าย~ เธอเป็นผู้หญิงที่น่าสนใจนะ")) {
				case 1:
					mes "[หญิงดูท่าทางแข็งแกร่ง]";
					mes "ฉันรู้ ฮืมมม...";
					mes "ท่านไม่ใช่สเป็คฉัน";
					if (Sex == SEX_MALE) {
						mes "ไม่ ไม่ ไม่ได้หมายความว่าฉันสนใจท่าน";
						mes "เพราะข้าหมั้นเเล้ว";
					} else
						mes "ก่อนจะคิดอะไรก็เเล้วแต่ ฉันคิดว่าท่านกับฉัน เราเหมือนกันนะ";
					next;
					mes "[หญิงดูท่าทางแข็งแกร่ง]";
					mes "ฉันขอแนะนำตัวก่อนนะ";
					mes "ฉันชื่อสุดา ฉันอยู่อโยธยามาทั้งชีวิต ถ้าท่านไม่มีอะไรทำ";
					mes "ฉันก็อยากจะถามท่าน";
					next;
					mes "[สุดา]";
					mes "คู่หมั้นของฉันชื่ออานนท์ เขามักจะสนใจอย่างอื่นนอกที่ไม่ใช่ฉันเสมอ ฉันไม่เข้าใจว่าทำไม และฉันคิดว่าฉันไม่สมควรถูกเมินแบบนี้!";
					next;
					mes "[สุดา]";
					mes "^666666*เห้อ...*^000000";
					mes "ยังไงก็ตาม เราก็หมั้นกันแล้ว";
					mes "และเราก็มีแหวนของกันและกัน...";
					next;
					mes "[สุดา]";
					mes "ฉันคิดว่าการหมั้นของเรา จะช่วยปรับความสัมพันธ์ของเราได้ แต่เขาเพิ่งเอาแหวนของฉันโยนลงไปในน้ำ";
					next;
					mes "[สุดา]";
					mes "ฉันอารมณ์เสีย ก็เลยตะหวาดใส่เขาไปเพราะอยากได้แหวนคืน";
					mes "จากนั้นฉันก็ไม่ได้เจอเขาอีกเลย";
					mes "นี่แหละคือสิ่งที่ฉันอยากจะถาม...";
					next;
					mes "[" +strcharinfo(0)+ "]";
					mes "เธอยากให้ฉัน";
					mes "หาแหวนของเธอ";
					mes "ใช่ไหม?";
					next;
					mes "[สุดา]";
					mes "อ่า...";
					mes "ท่านก็รู้นิ";
					mes "ว่าฉันอยากให้ทำอะไร";
					mes "ดีเลย งานจะได้ง่ายขึ้น";
					next;
					mes "[สุดา]";
					mes "และถ้าฉันได้แหวนคืนมานะ";
					mes "ฉันจะให้เขาชดใช้";
					mes "กับสิ่งที่เขาทำกับฉัน";
					mes "โดยการพาฉันไปเที่ยวพักร้อนที่ Jawaii...";
					next;
					mes "[สุดา]";
					mes "...เขาจะต้อง";
					mes "^660000ชดใช้^000000";
					mes "^666666*บดฟันไปมา*^000000";
					next;
					emotion ET_SURPRISE, playerattached();
					mes "[" +strcharinfo(0)+ "]";
					mes "(...น-น่ากลัว!)";
					next;
					mes "[สุดา]";
					mes "เพราะงั้น ฉันอยากให้ท่านไปหาแหวนหมั้นของฉัน เพราะตอนนี้น้ำกำลังนิ่ง มันคงยังไม่ถูกพัดไปไหน";
					next;
					mes "[สุดา]";
					mes "และนี่ก็คือสิ่งที่ฉันอยากจะขอท่าน งั้นท่านรีบลงไปในน้ำเลย เดี๋ยวฉันจะเฝ้าตรงนี้เอง";
					mes "โฮะ โฮะ โฮะ โฮะ~!";
					emotion ET_SMILE;
					set thai_find,1;
					close;
				case 2:
					mes "[หญิงดูท่าทางแข็งแกร่ง]";
					mes "^666666*เห้ออ...*^000000";
					mes "ท่านจะเอาแต่มองแล้วก็ไม่ช่วยอะไรเลยงั้นเหรอ?'";
					mes "ฉันคิดว่านักผจญภัยมีความหมายเหมือน";
					mes "กับ 'ฮีโร่' ไม่ใช่ 'ขี้เกียจ'";
					next;
					mes "[หญิงดูท่าทางแข็งแกร่ง]";
					mes "อ่อ เข้าใจเเล้ว...";
					mes "จะไม่ช่วยคนแปลกหน้าสินะ?";
					mes "เอาหล่ะ ฉันชื่อ สุดา สาวสวยที่รอเอาแหวนหมั้นที่ตกลงไปในน้ำคืน";
					next;
					mes "[หญิงดูท่าทางแข็งแกร่ง]";
					mes "นั้นแหละ...!";
					mes "พวกเราก็ไม่ใช่";
					mes "คนแปลกหน้าอีกต่อไปแล้วนะ!";
					mes "โฮะ โฮะ โฮะ โฮะ~!";
					next;
					mes "[สุดา]";
					mes "ท่านไม่สงสารฉันกับสถานการณ์ที่เกิดขึ้นกับฉันเลยเหรอ? ถ้ามีใครสักคนช่วยหญิงสาวคนนี้ให้ได้แหวนคืน เธอคนนั้นคงจะเป็นเจ้าสาวที่มีความสุขที่สุดในโลกแน่เลย";
					next;
					mes "[สุดา]";
					mes "และผู้ที่ช่วยเธอ";
					mes "ก็จะเป็นที่รู้จัก";
					mes "ในฐานะ ฮีโร่แห่งอโยธยา";
					next;
					mes "[" +strcharinfo(0)+ "]";
					mes " ...ว่าไงนะ?!";
					next;
					mes "[สุดา]";
					mes "ง่ายๆเลยก็ ท่านก็ไปหาแหวนหมั้นของฉัน เเล้วฉันก็จะเป็นเจ้าสาวที่มีความุขที่สุดในโลก";
					mes "มีอะไรที่เข้าใจยากเหรอ?";
					next;
					mes "[สุดา]";
					mes "อานนท์เขาเป็นคู่หมั้นของฉัน เขาใจดีมาก แต่เขาก็หายตัวไป";
					mes "ตั้งแต่ที่ฉันตะหวาดใส่เขาไป แล้วยังไงต่อกับสาวตัวเล็กอย่างฉันหล่ะ! เขาไม่เห็นความพยายามของฉันที่จะทำให้เขาเป็นคนดีเลยเหรอ?!";
					next;
					mes "[" +strcharinfo(0)+ "]";
					mes "เขาวิ่งหนีเธอไป";
					mes "อย่างงั้นเหรอ?";
					next;
					mes "[สุดา]";
					mes "เอ๊ะ...?";
					mes "อะไรนะ?";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "^666666*อะแฮ่ม*^000000";
					mes "เธอหน่ะ";
					mes "ดีเกินไปสำหรับเขา";
					mes "(นี่ฉันจะวิ่งหนีเองบ้างเเล้วนะเนี่ย!)";
					next;
					mes "[สุดา]";
					mes "ใช่ ใช่ ฉันรู้";
					mes "แต่สิ่งที่สำคัญก็คือ";
					mes "ฉันต้องได้แหวนคืน แล้วจากนั้นอานนท์ ก็จะเป็นของฉัน ไปจนกว่า ^660000จะตายไปด้วยกัน^000000";
					next;
					mes "[สุดา]";
					mes "แล้วสรุป";
					mes "ท่านจะช่วยฉันใช่ไหม?!";
					mes "ถ้าท่านช่วยฉันกับอานนท์ เราจะตั้งชื่อลูกเจ็ดคนแรกให้เกี่ยวกับคุณ";
					next;
					mes "[สุดา]";
					mes "งั้นลงไปดูในน้ำ! กระแสน้ำไม่แรงมาก เพราะฉะนั้นฉันมั่นใจว่ามันยังอยู่ที่นี่! แล้วท่านจะรออะไรหล่ะ ท่านก็ว่ายลงไปสิ! โอ้โหโฮ่โฮ้โฮ่!";
					if (checkquest(12029) == -1)
						setquest 12029;
					set thai_find,1;
					close;
				}
			break;
			}
		} else {
			mes "[หญิงดูท่าทางแข็งแกร่ง]";
			mes "^666666*เห้อ...*^000000";
			mes "ทำไมมันไม่ลอยขึ้นมาสักที?บางทีมันอาจจะไม่ขึ้นมาก็ได้ เอ หรือจะลองกวนน้ำดู หาแท่งไม้มาสักแท่งดีมั้ยนะ...?";
			next;
			mes "[หญิงดูท่าทางแข็งแกร่ง]";
			mes "...?!";
			mes "มองอะไร";
			mes "เหรอคะ?!";
			mes "ฉันกำลังยุ่ง ถ้า";
			mes "อยากบอกหรือให้ช่วยอะไร";
			mes "ฉันทำให้ไม่ได้หรอกนะคะ!";
			next;
			mes "[หญิงดูท่าทางแข็งแกร่ง]";
			mes "^666666เดี๋ยวนี้นักท่องเที่ยว";
			mes "เยอะจริงๆ ฉันไม่อยากให้";
			mes "พวกเขามารบกวน";
			mes "พวกเราเลยจริงๆ...^000000";
			close;
		}
	}

OnTouch_:
	if (thai_find == 0) {
		mes "[หญิงดูท่าทางแข็งแกร่ง]";
		mes "^666666*เห้ออ...*^000000";
		mes "มันอยู่ที่ไหนนะ!?";
		next;
		mes "[หญิงดูท่าทางแข็งแกร่ง]";
		mes "ท่านมองอะไรหรือ?";
		mes "ท่านไม่เคยเห็น";
		mes "หญิงงามถอนหายใจหรือ? ปล่อยฉันไว้ที่นี่แหละ!";
		close;
	}
}

ayo_dun02,91,263,0	script	#Annonblood	111,2,3,{
if (!$@annonactive) {
	if (thai_find == 13) {
		mes "["+strcharinfo(0)+"]";
		mes "คราบเลือดเหรอ?";
		mes "เกิดอะไรขึ้น?";
		mes "ฉันควรไปดู...";
		next;
		mes "^3355FFใต้ต้นไม้ใหญ่";
		mes "คุณพบพุ่มไม้";
		mes "ที่มีเลือด";
		mes "เปื้อนอยู่บนใบไม้^000000";
		next;
		mes "^3355FFภายในพุ่มไม้คุณจะเห็นว่ารอยเลือด";
		mes "ไหลยาวไปเป็นทาง เหมือนกับสัตว์ที่บาดเจ็บ";
		mes "ดิ้นรนเพื่อหาที่หลบภัยในพุ่มไม้^000000";
		next;
		mes "[" +strcharinfo(0)+ "]";
		mes "ฮืมม....";
		next;
		switch(select("ค้นพุ่มไม้:ขุดพุ่มไม้")) {
		case 1:
			if (!rand(0,2)) {
				mes "[" +strcharinfo(0)+ "]";
				mes "^666666*แค่ก แค่ก*^000000";
				mes "ฝุ่นเยอะมาก!";
				next;
				mes "[?]";
				mes "...หยี๋";
				mes "หะ... หนะ นี่มัน...!";
				next;
				emotion ET_SURPRISE, playerattached();
				mes "[" +strcharinfo(0)+ "]";
				mes "หะ...?";
				mes "ฉันได้ยินอะไร";
				mes "บางอย่างจากด้านล่าง!";
				mes "มาขุดกันเถอะ!";
				next;
				mes "^3355FFคุณดึงพุ่มไม้ขึ้น";
				mes "^3355FFและภายใต้ฝุ่น";
				mes "^3355FFกับพุ่มไม้";
				mes "คุณพบโพรงเล็กๆ^000000";
				next;
				set $@annonactive,1;
				enablenpc "Haggard Man";
				mes "[ชายที่ซูบผอม]";
				mes "^666666*แค่ก แค่ก!*^000000";
				mes "ขะ..ใครน่ะ?";
				mes "สุดาเหรอ?! ถ้านั่นคือเธอ";
				mes "อยู่ตรงนั้นซะ ฉันจะฆ่าตัวตาย!";
				emotion ET_HUK, playerattached();
				next;
				switch(select("มาทำอะไรที่นี่?:ฉันไม่ใช่สุดา!")) {
				case 1:
					mes "["+strcharinfo(0)+"]";
					mes "เฮ้...";
					mes "นั่น จะทำอะไร...หน่ะ";
					next;
					set $@annonactive,0;
					disablenpc "Haggard Man";
					mes "^3355FFเขาหายไป!^000000";
					close;
				case 2:
					mes "[ชายที่ซูบผอม]";
					mes "ฮืมม...?";
					mes "ข้ารู้สึกถึงความมีน้ำใจ ความจริงใจ และความรักที่มีต่อเพื่อนมนุษย์ แน่นอนท่านไม่ใช่สุดา";
					next;
					mes "[ชายที่ซูบผอม]";
					mes "ท่านเป็นคนนอก ไม่ได้คุ้นเคยอะไรกับข้า ข้าไม่รู้ว่าท่านพบที่ซ่อนข้าได้อย่างไร  แต่ถ้าท่านมีอะไรจะพูด ก็รีบๆพูดซะ!";
					close;
				}
			} else {
				mes "^3355FFคุณขุดพุ่มไม้ไปมา";
				mes "จนเกิดฝุ่นก้อนโต^000000";
				next;
				mes "["+strcharinfo(0)+"]";
				mes "^666666*แค่ก แค่ก!*^000000";
				mes "ทำไมฝุ่น";
				mes "มันเยอะขนาดนี้เนี่ย!";
				close;
			}
		case 2:
			mes "^3355FFในขณะที่คุณขุดพุ่มไม้ พื้นดินใต้พุ่มไม้ก็ถล่มลงไป";
			mes "และคุณได้ยินเสียงกรีดร้องมาจากข้างล่าง^000000";
			next;
			if (!rand(0,2)) {
				set $@annonactive,1;
				enablenpc "Haggard Man";
				mes "[?]";
				mes "^666666*แค่ก แค่ก*^000000";
				mes "...ขะ ขา ขาของฉัน!";
				close;
			} else {
				mes "[" +strcharinfo(0)+ "]";
				mes "ฮืมมม...?";
				mes "นั่นเสียงอะไรหน่ะ?";
				mes "อืมหืม ฝุ่นเยอะมาก";
				mes "ฉันหยุดขุดน่าจะดีกว่า";
				mes "^666666*แค่ก แค่ก แค่ก*^000000";
				close;
			}
		}
	} else if (thai_find == 14) {
		mes "^3355FFคุณพบว่าโพรงนั้นว่างเปล่า ดูเหมือนว่าอานนท์ จะพบที่ซ่อนใหม่^000000";
		close;
	} else {
		mes "^3355FFคุณพบกองพุ่มไม้ที่ตายแล้วใต้ต้นไม้ใหญ่ พื้นที่รอบๆ พุ่มไม้ดูเหมือนจะมีฝุ่นมาก^000000";
		close;
	}

OnTouch_:
	if (thai_find == 12) {
		if (!rand(0,2)) {
			emotion ET_SURPRISE, playerattached();
			mes "[" +strcharinfo(0)+ "]";
			mes "หะ...?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "นั่นมัน";
			mes "ดูเหมือนเลือด!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "ฮืมม...";
			mes "ใช่...";
			mes "นั่นมันเลือดแน่นอน";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "นี่มันแปลกๆนะ";
			mes "ที่มาเห็นเลือด";
			mes "ในสถานที่แบบนี้...";
			set thai_find,13;
			changequest 12033,12034;
			close;
		}
	}
}

}

ayo_dun02,91,264,3	script	Haggard Man	841,{
	//completequest 12034;
	if (thai_find == 13) {
		mes "[ชายที่ซูบผอม]";
		mes "^666666*...แค่ก แค่ก*^000000";
		mes "ไม่...! ที่ซ่อนตัวของข้า!";
		next;
		mes "[ชายที่ซูบผอม]";
		mes "ฮืมม...?";
		mes "ข้าเลือดออกงั้นเหรอ?!";
		mes "มันเป็นไปได้ยังไง?";
		mes "การฝึกจิตของข้า มันไม่ควรจะเป็นแบบนี้สิ...!";
		next;
		switch(select("รักษาแผลของเขาก่อน:นั่นท่านใช่ไหม อานนท์")) {
		case 1:
			mes "[" +strcharinfo(0)+ "]";
			mes "อย่างไรก็ตามข้าคิดว่าท่านต้องได้รับการรักษา ขอข้าดูก่อนว่ามียาอะไรบ้าง";
			next;
			if (countitem(504)) {
				mes "["+strcharinfo(0)+"]";
				mes "อ่า...";
				mes "เรามี";
				mes "White Potion";
				delitem 504,1; //White_Potion
				next;
				mes "[ชายที่ซูบผอม]";
				mes "เจ้าหมายถึง White Potion,";
				mes "ยาที่ดีที่สุดในการรักษางั้นเหรอ?";
				mes "ข้าขอขอบคุณมากที่เจ้าให้ข้า ^666666*ฟู้ว*^000000 เอาหล่ะ ข้ารู้สึกดีขึ้นเเล้ว";
				next;
				mes "[อานนท์]";
				mes "อ่า ขอโทษที่ไม่แนะนำตัวก่อนหน้านี้";
				mes "ข้าชื่ออานนท์ เจ้าคงสงสัยว่าข้า";
				mes "มาทำอะไรใต้ดินแบบนี้";
				next;
				mes "[อานนท์]";
				mes "ข้ากำลังฝึกร่างกายแบบพิเศษ เพื่อฝึกจิตวิญญาณของข้าเอง ถึงแม้ว่ามันอาจจะไม่ค่อยน่าเชื่อก็เถอะ";
				next;
				mes "[อานนท์]";
				mes "อะไรนะ...?";
				mes "สุดาส่งเจ้ามางั้นเหรอ?! ดีจัง...";
				mes "ยังไง ข้าก็ขอบคุณเจ้ามากนะที่ช่วยข้า ตอนนี้ได้เวลาที่ข้าจะฝึกต่อแล้ว";
				next;
				mes "[" +strcharinfo(0)+ "]";
				specialeffect EF_CLOAKING;
				mes "เดี๋ยวก่อนข้าให้ท่านไปไม่ได้! ท่านไม่รู้ว่ามันยากขนาดไหนที่ต้องตามหาท่าน! ท่านไปไหนไม่ได้!";
				next;
				mes "[อานนท์]";
				mes "หะ? ถ้าอย่างนั้นเจ้าต้องการอะไร? ข้าขอร้องหล่ะ อย่าไปบอกสุดานะ ว่าเจ้าเจอข้าแล้ว ไม่ว่ายังไง ข้าก็จะไม่กลับไปหาเธอ!";
				next;
				mes "[อานนท์]";
				mes "เอ่อ...?";
				mes "เจ้าเจอแหวน";
				mes "ของเธอแล้วเหรอ?";
				mes "โอ้ข้าคิดว่า";
				mes "จะไม่เจอมันอีกแล้วนะเนี่ย";
				next;
				mes "[อานนท์]";
				mes "เธออาจจะบอกเจ้าว่าข้าหนีออกมาเพราะข้าทำแหวนหาย และถ้าเจ้าได้แหวนกลับมา ข้าจะกลับไปใช่มั้ย?";
				next;
				mes "[อานนท์]";
				mes "ก็ดี...";
				mes "ที่จริง";
				mes "ข้าตั้งใจโยนแหวนนั่นทิ้งไปเอง!";
				mes "ข้าคิดแต่แรกแล้ว";
				mes "ว่าจะไม่กลับไปหาสุดา";
				next;
				mes "[อานนท์]";
				mes "ดูเหมือนว่าเจ้าอยากรู้อะไรเพิ่มสินะ แต่เจ้าต้องสัญญาก่อนว่า เจ้าจะไม่บอก";
				mes "ที่อยู่ของข้าให้สุดารู้ แล้วข้าจะบอกเจ้า";
				next;
				switch(select("ท่านคิดว่าโลกหมุนรอบตัวท่านเหรอ?:ท่านต้องการให้ข้าทำอะไร?")) {
				case 1:
					mes "[" +strcharinfo(0)+ "]";
					mes "ท่านคิดว่าโลก";
					mes "หมุนรอบตัวท่านงั้นเหรอ?";
					mes "ยังไงท่านก็หนีไม่พ้นหรอก!";
					next;
					mes "[อานนท์]";
					mes "ข้ารู้ ข้ารู้!";
					mes "แต่เจ้าไม่เข้าใจ สุดานั้นเป็นคนสวย";
					next;
					mes "[อานนท์]";
					mes "แต่พ่อแม่ของเราจัดงานสมรสให้เราเมื่อเรายังเป็นเด็ก";
					mes "การจัดงานแบบนั้น สำหรับข้ามันไม่สำคัญ แต่สุดาเหมือนจริงจังกับมันมาก";
					next;
					mes "[อานนท์]";
					mes "เธอทำให้ข้ากลัวจะตาย";
					mes "เมื่อใดก็ตามที่ข้าอยู่กับเธอ";
					mes "ข้าก็ทำอะไรไม่ถูก และเวลาเธออยู่ใกล้ข้า ข้าก็มักจะฝันร้ายเกี่ยวกับเธอ ^666666*เห่อออ..*^000000";
					next;
					mes "[อานนท์]";
					mes "เธอคิดว่ามันตลกที่ช๊อตข้าด้วยไฟฟ้า";
					mes "เธอมัดข้าไว้ในห้องมืด และเลี้ยงข้าด้วย Jellopy ตอนนี้เจ้าเห็นหรือยัง ว่าทำไมข้าถึงอยากได้อิสระ?";
					next;
					mes "[อานนท์]";
					mes "ถ้าเจ้าไม่บอกเธอว่าได้เจอข้า ข้าจะตอบแทนเจ้าแน่นอน ข้าขอร้องแค่แกล้งทำเป็นว่าเจ้าไม่ได้มาพบข้า";
					next;
					select("...ได้เลย");
					mes "[อานนท์]";
					mes "ขอบคุณมาก!";
					mes "งั้นข้าจะตอบแทนเจ้า ข้าจะใช้สกิลนึงของข้าจากการที่ข้าฝึกจิตมา";
					break;
				case 2:
					mes "[อานนท์]";
					mes "สิ่งที่ข้าต้องการให้เจ้าทำก็คือ เก็บความลับของข้าไว้  บอกสุดาว่าเจ้าไม่พบข้า และหาข้ายากมาก";
					next;
					mes "[อานนท์]";
					mes "และอีกอย่าง ที่เธอบอกว่าถ้าเจ้าช่วยเธอ เธอก็จะตั้งชื่อลูกให้เกี่ยวกับเจ้า ...เจ้าต้องการแบบนั้นจริงเหรอ ข้าบอกเลยว่าข้าจะไม่มีลูก โลกนี้มันมีเเต่ความชั่วร้ายมากพอเเล้ว";
					next;
					mes "[อานนท์]";
					mes "เอาหล่ะ ข้าคิดอยู่ว่าจะทำอย่างไรเพื่อตอบแทน";
					mes "ที่เจ้าจะไม่พูดเรื่องของข้า งั้นข้าจะแสดงทักษะในสิ่งที่ข้าฝึกจิตมาก็เเล้วกัน";
					break;
				}
				next;
				mes "[อานนท์]";
				mes "ไหนดูซิ...";
				mes "ข้าขอบิดแขน";
				mes "เจ้ามาทางนี้หน่อยนะ";
				mes "แล้วก็กดตรงนี้...";
				next;
				mes "[" +strcharinfo(0)+ "]";
				mes "เหว่อออ~!";
				mes "นี่จะทำอะไรหน่ะ";
				mes "นี่มันเจ็บนะ!";
				next;
				mes "[อานนท์]";
				mes "โอ้ นี่เป็นการนวดแบบพิเศษ";
				mes "ไม่ต้องห่วง มันจะช่วยให้เจ้าคลายความเหนื่อยล้า";
				mes "เเล้วเจ้าจะเห็นสวรรค์เลยหล่ะ~";
				next;
				mes "^3355FF*ตุบ ตุบ ตุบ*";
				mes "*ตุบ ตุบ ตุบ*";
				mes "*ตับ ตับ ตับ*";
				mes "*ตับ ตับ ตับ*^000000";
				next;
				mes "^3355FFคุณรู้สึกว่าอานนท์ ปรับกระดูกของคุณ";
				mes "ให้อยู่ในตำแหน่งที่ถูกต้อง คุณรู้สึกผ่อนคลายและสดชื่นอย่างมาก แทนที่จะเจ็บซะอีก";
				set thai_find,14;
				completequest 12034;
				if (checkre(3)) {
					if (BaseLevel < 56) getexp 900,0;
					else if (BaseLevel > 55 && BaseLevel < 61) getexp 1050,0;
					else if (BaseLevel > 60 && BaseLevel < 66) getexp 1868,0;
					else if (BaseLevel > 65 && BaseLevel < 71) getexp 2741,0;
					else if (BaseLevel > 70 && BaseLevel < 76) getexp 7075,0;
					else if (BaseLevel > 75 && BaseLevel < 81) getexp 13024,0;
					else if (BaseLevel > 80 && BaseLevel < 86) getexp 15034,0;
					else if (BaseLevel > 85 && BaseLevel < 91) getexp 18205,0;
					else getexp 40678,0;
				} else {
					if (BaseLevel < 56) getexp 9000,0;
					else if (BaseLevel > 55 && BaseLevel < 61) getexp 10500,0;
					else if (BaseLevel > 60 && BaseLevel < 66) getexp 18684,0;
					else if (BaseLevel > 65 && BaseLevel < 71) getexp 27411,0;
					else if (BaseLevel > 70 && BaseLevel < 76) getexp 70757,0;
					else if (BaseLevel > 75 && BaseLevel < 81) getexp 130246,0;
					else if (BaseLevel > 80 && BaseLevel < 86) getexp 150340,0;
					else if (BaseLevel > 85 && BaseLevel < 91) getexp 182052,0;
					else getexp 406786,0;
				}
				next;
				mes "[" +strcharinfo(0)+ "]";
				mes "โว้ว...?";
				mes "รู้สึกดีมากเลย~";
				mes "มันก็รู้สึกเหมือนจิตใจ";
				mes "ของฉันได้รับการชำระไปด้วย!";
				mes "โอเค ตกลงๆ!";
				next;
				mes "[อานนท์]";
				mes "ขอบคุณมาก ขอบคุณมากๆเลย";
				mes "เท่านี้ข้าก็ไม่ต้องไปติดกับสุดาแล้ว อย่างน้อยก็ช่วยให้นานขึ้นอีกนิด";
				next;
				mes "[อานนท์]";
				mes "ตอนนี้ ถ้าจะให้ดี";
				mes "ข้าต้องทำทางออก";
				mes "ก่อนที่คนอื่นจะมาเจอข้าอีก";
				mes "โชคดีนะ~!";
				close2;
				set $@annonactive,0;
				disablenpc "Haggard Man";
				end;
			} else {
				mes "[" +strcharinfo(0)+ "]";
				mes "ไม่ ไม่";
				mes "เขาบาดเจ็บเกินไป";
				mes "ยาของเราไม่ช่วยอะไรแน่ๆ";
				mes "เราต้องการยาที่แรงกว่านี้...";
				next;
				mes "[" +strcharinfo(0)+ "]";
				mes "เอาหล่ะ อยู่นี่นะ";
				mes "ข้าจะไปหายา";
				mes "มารักษาท่านเอง";
				next;
				mes "[ชายที่ซูบผอม]";
				mes "ไม่ เจ้าไม่ต้อง...";
				mes "^666666*แค่ก แค่ก แค่ก*^000000";
				mes "ดะ เดี๋ยว อย่าดันข้า";
				mes "กลับไปข้างใน!";
				mes "เหว่อออ~!";
				next;
				set $@annonactive,0;
				disablenpc "Haggard Man";
				mes "[" +strcharinfo(0)+ "]";
				mes "โอเค ถึงเวลา";
				mes "ต้องเป็นคุณหมอจำเป็นแล้ว!";
				mes "เราก็มีเวลาไม่มาก!";
				mes "ภารกิจของเรา: ไปเอายา";
				mes "ที่มีฤทธิ์สูง";
				mes "มาให้ชายคนนี้~";
				emotion ET_BEST, playerattached();
				close;
			}
		case 2:
			mes "[ชายที่ซูบผอม]";
			mes "อานนท์...?";
			mes "นั้นใคร?";
			mes "ข้าไม่เคยได้ยินชื่อเขาเลย!";
			mes "ข้าไม่รู้จักเขา โอเคนะ!";
			mes "อย่ามารบกวนการฝึกของข้า!";
			next;
			mes "[" +strcharinfo(0)+ "]";
			mes "เอ่อ...";
			mes "อืมม ท่านก็ไม่ควรตะโกนออกมาแบบนั้นนะ แน่ใจนะ ว่าท่านไม่ใช่อานนท์?";
			next;
			mes "[ชายที่ซูบผอม]";
			mes "ข้าบอกว่า!";
			mes "ข้าไม่เคย";
			mes "ได้ยินชื่อของเขา!";
			mes "ออกไปซะ!";
			next;
			mes "[ชายที่ซูบผอม]";
			mes "ข้าต้องซ่อนตัว";
			mes "ให้พ้นจากสุดา";
			mes "^666666*เขาร้องไห้*^000000";
			close2;
			set $@annonactive,0;
			disablenpc "Haggard Man";
			end;
		}
	} else {
		mes "[ชายที่ซูบผอม]";
		mes "หะ? เจ้าเป็นใคร?";
		mes "นี่ไม่ใช่ที่ที่ปลอดภัยของพวกนักท่องเที่ยว! เจ้าควรออกไปซะ!";
		close2;
		set $@annonactive,0;
		disablenpc "Haggard Man";
		end;
	}

OnInit:
	set $@annonactive,0;
	disablenpc "Haggard Man";
	end;
}
		
ayothaya,253,99,3	script	ชาวประมง	843,{
	if (thai_find == 1) {
		mes "[ดนัย]";
		mes "ที่นี่เป็นที่รู้จักกันดีในเรื่องของปลา";
		mes "ที่นี่นั้นปลาจะมารวมตัวกันเยอะมาก";
		mes "ทำให้ง่ายต่อการตกปลา";
		next;
		mes "[ดนัย]";
		mes "เราให้บริการเช่าเบ็ดตกปลา ทุกครั้งที่ท่านตกปลาท่านจะต้องใช้ ^3355FFMonster's Feed 1 ชิ้น^000000  เพื่อใช้เป็นเหยื่อล่อ";
		mes "และจ่ายค่าเช่าเบ็ดอีก ^3355FF50 Zeny^000000";
		next;
		mes "[ดนัย]";
		mes "อยากจะลองไหมหล่ะ?";
		next;
		switch(select("อยาก:ไม่หล่ะ ขอบคุณ")) {
		case 1:
			if (countitem(528) && Zeny > 49) {
				mes "^3355FFคุณเหวี่ยง";
				mes "เบ็ดลงไปในน้ำ^000000";
				next;
				mes "...";
				next;
				mes "...";
				mes "......";
				next;
				mes "^3355FFคุณตกได้ Phen!^000000";
				next;
				delitem 528,1; //Monster's_Feed
				set Zeny, Zeny-50;
				set .@randfish,rand(1,100);
				if (.@randfish > 0 && .@randfish < 40) {
					mes "^3355FFขณะที่กำลังปรุง Phen เป็นอาหาร";
					mes "คุณเจอ Stone ในท้องของมัน^000000";
					close2;
					getitem 7049,1; //Stone
					end;
				} else if (.@randfish > 39 && .@randfish < 60) {
					mes "^3355FFในตัวของ Phen";
					mes "คุณเจอ Red Potion^000000";
					close2;
					getitem 501,1; //Red_Potion
					end;
				} else if (.@randfish > 59 && .@randfish < 70) {
					mes "^3355FFในตัวของ Phen";
					mes "คุณเจอ Yellow Potion^000000";
					close2;
					getitem 503,1; //Yellow_Potion
					end;
				} else if (.@randfish > 69 && .@randfish < 80) {
					mes "^3355FFไม่มีอะไรเลย...";
					mes "แต่ตอนนี้คุณทาน ซูชิได้!^000000";
					close2;
					getitem 551,1; //Shusi
					end;
				} else if (.@randfish > 79 && .@randfish < 90) {
					mes "^3355FFคุณพบ";
					mes "Flower Ring";
					mes "ในตัวของ Phen^000000";
					close2;
					getitem 2612,1; //Flower_Ring
					end;
				} else if (.@randfish == 90) {
					mes "^3355FFคุณพบ";
					mes "Diamond Ring";
					mes "ในตัวของ Phen^000000";
					close2;
					getitem 2613,1; //Diamond_Ring
					end;
				} else if (.@randfish > 90 && .@randfish < 101) {
					mes "^3355FFคุณพบแหวน";
					mes "ที่มีชื่อสลักไว้";
					mes "อยู่ด้านในวง^000000";
					next;
					mes "^3355FFคุณได้รับ";
					mes "แหวนหมั้น^000000";
					close2;
					set thai_find,2;
					changequest 12029,12030;
					getitem 7288,1; //Thai_Ring
					end;
				}
			} else {
				mes "[ดนัย]";
				mes "จำไว้ว่าท่านต้องใช้ ^3355FFMonster's Feed 1 ชิ้น^000000 เพื่อใช้เป็นเหยื่อล่อ กับอีก ^3355FF50 zeny^000000 เพื่อเช่าเบ็ด";
				close;
			}
		case 2:
			mes "[ดนัย]";
			mes "ไม่เป็นไรๆ";
			mes "ไว้ค่อยกลับมาใหม่เมื่อไหร่ก็ได้";
			mes "การตกปลาจะทำให้ผ่อนคลาย";
			mes "และทำให้ท่านรู้สึกสงบ...";
			close;
		}
		close;
	} else if (thai_find < 1) {
		mes "[ดนัย]";
		mes "ปลาที่นี่";
		mes "มันกินทุกอย่างที่มันเจอ";
		mes "เมื่อท่านจับมันได้";
		mes "ท่านก็จะเจอสิ่งที่น่าสนใจในตัวมัน";
		next;
		mes "[ดนัย]";
		mes "ดังนั้น ถ้าท่านมีโอกาส";
		mes "ทำไมไม่ลองนั่งลง ผ่อนคลาย";
		mes "แล้วตกปลาดูหล่ะ?";
		close;
	} else {
		mes "[ดนัย]";
		mes "ยินดีต้อนรับ ไม่ได้เจอกันนานเลยนะ!";
		mes "ดูเหมือนคนจะรู้ว่าอาจจะโชคดีถ้ามาตกปลาที่นี่ หลังจากที่ข่าวกระจายออกไป";
		mes "ที่นี่ก็มีลูกค้าเพิ่มขึ้นมากเลย";
		next;
		mes "[ดนัย]";
		mes "ข้าเข้าใจนะว่าท่านต้องการปลา";
		mes "แต่ตอนนี้ที่ตกปลา";
		mes "เต็มทุกจุดเลย";
		mes "ต้องขอโทษด้วย";
		next;
		mes "[ดนัย]";
		mes "ให้คนอื่นได้ลอง";
		mes "ใช้โอกาสของตัวเอง";
		mes "ในการเสี่ยงโชคบ้าง";
		mes "ก็เเล้วกัน";
		close;
	}
}

ayo_dun01,255,62,0	script	AyoFootprint1	139,2,0,{
OnTouch_:
	if (thai_find == 4) {
		if (rand(1,3) < 2) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFคุณพบรอยเท้าที่มุ่งหน้าไปทางทิศ ^5C3317เหนือ^3355FF ดูเหมือนว่ามีบางคนกำลังรีบเหมือนที่ชาวบ้านพูดนะ!^000000";
			set thai_find,5;
			changequest 12032,12033;
			close;
		}
	}
	end;
}

ayo_dun01,74,142,0	script	AyoFootprint2	139,2,0,{
OnTouch_:
	if (thai_find == 5) {
		if (rand(1,3) < 2) {
			emotion ET_THINK, playerattached();
			mes "^3355FFคุณพบรอยเท้าอีกอัน ดูเหมือนว่ามีบางคนกำลังวิ่งหนีบางอย่าง รอยเท้านี้มุ่งหน้าไปทางทิศ ^5C3317ตะวันออก^3355FF คุณควรตามไป";
			mes "เผื่อคุณจะพบอะไร^000000";
			set thai_find,6;
			close;
		}
	}
	end;
}

ayo_dun01,244,256,2	script	AyoFootprint3	139,2,0,{
OnTouch_:
	if (thai_find == 6) {
		if (rand(1,3) < 2) {
			emotion ET_STARE_ABOUT, playerattached();
			mes "^3355FFรอยเท้าสิ้นสุดบริเวณนี้ ใครก็ตามที่วิ่งมาจะต้องเหนื่อยและสะดุดกับอะไรบางอย่าง";
			mes "บางทีเจ้าของรอยเท้าอาจอยู่แถวๆนี้...^000000";
			set thai_find,7;
			close;
		}
	}
	end;
}

ayo_dun01,17,257,0	script	AyoFootprint4	139,2,0,{
OnTouch_:
	if (thai_find == 7) {
		if (rand(1,3) < 2) {
			emotion ET_AHA, playerattached();
			mes "^3355FFเมื่อคุณตามรอยเท้า";
			mes "คุณจะมาถึง '^5C3317ประตูแห่งศาลเจ้า^3355FF' ไม่ว่าจะเป็นใครก็ตาม เขาต้องวิ่งลงไปชั้น 2 แน่นอน^000000";
			set thai_find,8;
			close;
		}
	}
	end;
}

ayo_dun02,145,120,0	script	AyoFootprint5	139,2,0,{
OnTouch_:
	if (thai_find == 8) {
		if (rand(1,3) < 2) {
			mes "^3355FFคุณพบรอยเท้าอันเดียวกันที่นี่";
			mes "ที่นี่ดูอันตรายมาก";
			mes "และคุณก็สงสัยว่า ทำไมถึงมีคน";
			mes "คิดจะมาที่นี่ด้วย";
			mes "รอยเท้ายังคงวิ่งต่อไป";
			mes "ยังทิศ ^5C3317เหนือ^3355FF^000000";
			set thai_find,9;
			close;
		}
	}
	end;
}

ayo_dun02,135,168,0	script	AyoFootprint6	139,0,2,{
OnTouch_:
	if (thai_find == 9) {
		if (rand(1,3) == 1) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFรอยเท้ายังคงดำเนินต่อไป";
			mes "รอยเท้ายังใหม่ๆ อยู่เลย^000000";
			next;
			mes "^3355FFหืมม...?";
			mes "มีบางอย่างที่นี่...^000000";
			next;
			switch(select("ไม่สนใจ:^5C3317ตรวจสอบ^000000")) {
			case 1:
				mes "["+strcharinfo(0)+"]";
				mes "ก็คงไม่มีอะไรมั้ง แต่ตอนนี้ฉันควรไปทางไหนดี?";
				set thai_find,10;
				close;
			case 2:
				set .@randayo,rand(1,10);
				if (.@randayo < 2) {
					mes "["+strcharinfo(0)+"]";
					mes "ทำไมกัน นี่เป็นแพ็คของ White Potion นิ คนที่เป็นเจ้าของ คงทำหล่นไว้เเน่ๆ อืม...เราอาจจะใช้มันทีหลังก็ได้..";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "เดี๋ยวนะ...";
					mes "มีรอยเท้า";
					mes "มุ่งไปทางทิศ";
					mes "^5C3317ตะวันออก^000000 อีกหนิ";
					mes "ไปดูหน่อย";
					mes "ดีกว่า";
					set thai_find,10;
					getitem 504,10; //White_Potion
					close;
				} else if (.@randayo > 2 && .@randayo < 5) {
					mes "["+strcharinfo(0)+"]";
					mes "ทำไมกัน นี่เป็นแพ็คของ Yellow Potion นิ คนที่เป็นเจ้าของ คงทำหล่นไว้เเน่ๆ อืม...เราอาจจะใช้มันทีหลังก็ได้..";
					next;
					mes "["+strcharinfo(0)+"]";
					mes "เดี๋ยวนะ...";
					mes "มีรอยเท้า";
					mes "มุ่งไปทางทิศ";
					mes "^5C3317ตะวันออก^000000 อีกหนิ";
					mes "ไปดูหน่อย";
					mes "ดีกว่า";
					set thai_find,10;
					getitem 503,10; //Yellow potion
					close;
				} else {
					mes "[หนู]";
					mes "^FF0000จี๊ด!";
					mes "^FF0000จี๊ด จี๊ด!^000000";
					emotion ET_HUK, playerattached();
					next;
					mes "["+strcharinfo(0)+"]";
					mes "^8C1717หะ^000000!";
					mes "นั่นมันอะไรหน่ะ?!";
					next;
					mes "^3355FFนั่นมันก็แค่หนูนี่นา";
					mes "บ้าจริงๆ กลัวหนูเนี่ยนะ?^000000";
					next;
					mes "^3355FFคุณพบรอยเท้า";
					mes "อีกรอยที่มุ่งตรงไป";
					mes "ยังทิศ ^5C3317ตะวันออก^3355FF^000000";
					set thai_find,10;
					close;
				}
			}
			close;
		}
	}
	end;
}

ayo_dun02,77,213,0	script	AyoFootprint7	139,1,1,{
OnTouch_:
	if (thai_find == 10) {
		if (rand(1,3) < 2) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFเขาอยู่ไหน...?";
			mes "เขาต้องเข้าไป";
			mes "ในดันเจี้ยนที่ลึกมากแน่ๆ";
			mes "แค่มองหาเขา";
			mes "ก็เหนื่อยจะเเย่เเล้ว...^000000";
			set thai_find,11;
			close;
		}
	}
	end;
}

ayo_dun02,86,254,0	script	AyoFootprint8	139,1,1,{
OnTouch_:
	if (thai_find == 11) {
		if (rand(1,3) < 2) {
			emotion ET_SURPRISE, playerattached();
			mes "^3355FFคุณพบรอยเลือด ใครก็ตามที่วิ่งไปในทิศทางนี้อาจได้ รับบาดเจ็บ รอยเลือดไหลไปทาง ^5C3317ทิศเหนือ^3355FF^000000";
			changequest 12032,12033;
			set thai_find,12;
			close;
		}
	}
	end;
}

ayothaya,193,171,3	script	ชายแก่#02	842,{
	if (thai_find == 3) {
		mes "[แต้ม]";
		mes "แปลก...";
		mes "ทำไมผู้ชายคนนั้น";
		mes "ถึงวิ่งเข้าไป";
		mes "ที่ศาลแบบนั้น?";
		mes "นั่นมันทำให้ข้า";
		mes "คิดไม่ตกเลยจริงๆ...";
		next;
		mes "[แต้ม]";
		emotion ET_SURPRISE;
		mes "เอ่อ เจ้าไม่ได้มาจาก";
		mes "รูน-มิการ์ดงั้นเหรอ?";
		mes "ไง สวัสดี~";
		next;
		mes "[แต้ม]";
		mes "อ่า...";
		mes "เจ้าได้ยินที่ข้าพูดกับตัวเองงั้นเหรอ? ข้าไม่ก็เห็นหรอกว่าเขาเป็นใคร แต่มีชายคนนึงวิ่งเข้าไปในป่า ที่เป็นทางไปศาลเจ้า ดูเหมือนว่าเขาจะกลัวอะไรสักอย่างด้วย";
		next;
		mes "[แต้ม]";
		mes "เหมือนเขาไม่ใช่คนที่นี่นะ เพราะงั้นเขาคงกลัวก็เลยหนีไป เจ้าคิดว่าเขาจะเป็นพวกอาชญากรไหม?";
		next;
		mes "[แต้ม]";
		emotion ET_AHA;
		mes "อ่า! ข้าว่า เหมือนเขาจะตะโกนออกมาประมาณว่า '^FF0000สุดา^000000' หรืออะไรสักอย่างเนี่ยแหละ แต่ก็ไม่รู้ว่าหมายถึงอะไร?";
		next;
		mes "[แต้ม]";
		mes "เอาหล่ะ ไม่ต้องกังวลไปหรอก ชาวบ้านของเราทุกคนไม่ได้เป็นอย่างนั้น อย่างไรก็ตามข้าหวังว่าเจ้าจะสนุกกับการเดินทางในอโยธยานะ";
		set thai_find,4;
		changequest 12031,12032;
		close;
	} else {
		set .@randayo,rand(1,7);
		if (.@randayo < 5) {
			mes "[แต้ม]";
			mes "สวัสดี~";
			mes "ข้าเดาได้เลยว่าเจ้าต้องมาจาก รูน-มิการ์ดแน่ ดูจากการแต่งตัวก็รู้";
			next;
			mes "[แต้ม]";
			mes "ช่วงที่ผ่านมานี้ ข้าสังเกตุว่า";
			mes "หมู่บ้านของเรามีนักท่องเที่ยว เพิ่มขึ้น";
			mes "และหมู่บ้านของเราก็";
			mes "มีแนวโน้มจะได้รับอิทธิพล";
			mes "ในทางบวกอีกด้วยนะ";
			next;
			mes "[แต้ม]";
			mes "อ่า...";
			mes "แล้วตอนนี้เจ้า";
			mes "ก็อยู่ที่นี่ ทำไมเจ้า";
			mes "ไม่ลองไปชิมอาหารแท้ๆ";
			mes "จากอโยธยาดูหล่ะ?";
			next;
			mes "[แต้ม]";
			mes "พวกเราขึ้นชื่อในเรื่องของ";
			mes "อาหารทะเล อาหารรสจัด รสหวาน";
			mes "และพวกเราก็มีร้านอาหาร";
			mes "อยู่ทั่วไปอีกด้วย";
			next;
			mes "[แต้ม]";
			mes "ยังไงซะ ข้าว่าเจ้าจะต้องชอบ แล้วก็หวังว่าจะสนุกกับเมืองอโยธยานะ มาอโยธยาบ่อยๆหล่ะ~";
			close;
		} else if (.@randayo > 4 && .@randayo < 7) {
			mes "[แต้ม]";
			mes "อ่า~";
			mes "ข้านี่อยากทานอาหารของแม่มะลิอีกจัง โดยเฉพาะ 'ต้มยำกุ้ง' เนี่ย สุดยอดเลย";
			next;
			mes "[แต้ม]";
			mes "ข้าว่ามัน";
			mes "เป็นอาหาร";
			mes "ที่อร่อยที่สุดในโลกเลย!";
			close;
		} else {
			mes "[แต้ม]";
			mes "ไง สวัสดี~";
			mes "ข้าเดาได้เลยว่าเจ้าต้องมาจาก รูน-มิการ์ดแน่ ดูจากการแต่งตัวก็รู้";
			next;
			mes "[แต้ม]";
			mes "หวังว่าจะสนุกกับเมืองอโยธยานะ มาอโยธยาบ่อยๆหล่ะ~";
			close;
		}
	}
}

// Ayothaya Dungeon Quest :: ayo_tiger
//============================================================
ayothaya,83,132,0	script	ดุสิต#thai	843,{
	if (ayodunquest == 0) {
		mes "[ดุสิต]";
		mes "โอ้...!";
		mes "เจ้าต้องเป็นนักท่องเที่ยวเเน่เลย";
		mes "ข้ามีเรื่องที่เจ้าอาจอยากฟัง เจ้าอยากฟังไหมหละ?";
		next;
		if (select("เรื่องอะไรเหรอ?:ข้ารู้แล้วว่าเรื่องอะไร") == 1) {
			mes "[ดุสิต]";
			mes "อโยธยามีประวัติศาสตร์อันยาวนาน มันอาจดูสงบสุขในตอนนี้ แต่ในอดีตหมู่บ้านของเราเคยถูกคุกคามจากสัตว์ร้าย";
			next;
			mes "[ดุสิต]";
			mes "สัตว์ร้ายนี้ พวกเรารู้จักมัน";
			mes "ในชื่อว่า ^660000เสือสมิง^000000";
			mes "ซึ่งมันเคยอาศัยอยู่ในซากปรักหักพังโบราณ";
			next;
			mes "[ดุสิต]";
			mes "เสือสมิงชอบรสชาติเนื้อของมนุษย์";
			mes "และทุกคนก็รู้ดีว่า มันชอบกลายร่างเป็นมนุษย์ที่กินเข้าไป มันก็เลยหลอกเพื่อน ครอบครัวหรือมนุษย์คนอื่ๆนมากินได้อีก";
			next;
			mes "[ดุสิต]";
			mes "คนในท้องถิ่นยังกลัวอยู่ว่า เสือสมิงอาจเปลี่ยนเป็นคนที่พวกเขารู้จักและแอบเข้าไปในบ้านเพื่อกินพวกเขาก็ได้";
			next;
			mes "[ดุสิต]";
			mes "แต่ถึงยังไง จริงๆแล้วมันเกิดมาเป็นเสือ";
			mes "มันก็เลยมีบางอย่างที่มันทำเหมือนมนุษย์ไม่ได้";
			mes "ง่ายๆเลย อย่างเช่น การจุดไม้ขีดไฟ";
			next;
			mes "[ดุสิต]";
			mes "ดังนั้น ถ้าเจ้าเห็นเพื่อนของเจ้า";
			mes "ทำตัวแปลกๆ";
			mes "ลองบอกให้เขาจุดไม้ขีดไฟดู";
			mes "เพื่อยืนยันว่าเขา";
			mes "ไม่ใช่เสือสมิง";
			next;
			mes "[ดุสิต]";
			mes "ยังไงก็ตาม...";
			mes "ข้าก็ไม่ได้แน่ใจอะไรหรอกนะ ว่าจริงๆแล้วมันจะได้ผลหรือเปล่า ทางที่ดีถ้าคิดว่าเจอมัน";
			mes "ก็วิ่งหนีเอาตัวรอดไว้ก่อนดีกว่า";
			next;
			mes "[ดุสิต]";
			mes "แล้วเจ้าเชื่อ";
			mes "ในเรื่องของการที่มี";
			mes "'เสือสมิง' อยู่ไหมหล่ะ?";
			next;
			if (select("ข้าไม่เชื่อหรอก:เชื่อสิ ข้าว่ามันดูน่ากลัวจัง!") == 1) {
				mes "[ดุสิต]";
				mes "อืมม...";
				mes "ก็ดีปู่ของข้าเคยเล่าเรื่องเกี่ยวกับเสือสมิงให้ฟัง";
				mes "ตอนที่ข้ายังเด็กๆ";
				mes "แต่ข้าก็ไม่เคยเห็นมัน";
				next;
				mes "[ดุสิต]";
				mes "สิ่งที่ใกล้เคียงที่สุดที่ข้าเคยเห็น ที่เหมือนกับเสือสมิงคือแมวทองคำห้อยกระดิ่ง ยังไงข้าก็ไม่คิดว่ามันจะกินคนหรอกนะ";
				close;
			}
			mes "[ดุสิต]";
			mes "อย่างที่บอก ว่าถ้าเจอเสือสมิงหละก็ ให้วิ่งหนีสุดชีวิตเลยนะ!";
			mes "มันจะกินเจ้า!";
			next;
			mes "[ดุสิต]";
			mes "และถ้าเจ้าสนใจอยากจะรู้";
			mes "เรื่องราวเพิ่มเติม ข้าแนะนำคนๆนึงให้รู้จักได้นะ";
			mes "เขาคนนี้รู้เรื่องเสือสมิงนี่";
			mes "ดีกว่าใครๆเลย";
			next;
			mes "[ดุสิต]";
			mes "ทำไมเจ้าไม่ลองไปคุยกับหมอผีบุญถมดูหล่ะ? เขาเป็นคนที่ค่อนข้างแปลก แต่เขาก็มีบางอย่างที่น่าสนใจ...";
			next;
			mes "[ดุสิต]";
			mes "...";
			mes "......";
			next;
			mes "[ดุสิต]";
			mes "เอาเถอะ แล้วเจ้าก็จะรู้เองว่าข้าพูดถึงอะไร ข้าหวังว่าเขาจะบอกอะไรมากกว่านี้นะ เกี่ยวกับเจ้าเสือนั่น";
			set ayodunquest,1;
			setquest 12035;
			close;
		}
		mes "[ดุสิต]";
		mes "อ่าว งั้นเหรอ?";
		mes "งั้นก็ดูแลตัวเองดีหล่ะ!";
		mes "เพราะมันจะคอยกัดกินชีวิตเจ้า!";
		close;
	} else if (ayodunquest > 0) {
		mes "[ดุสิต]";
		mes "เป็นยังไงบ้าง?";
		mes "ถ้าเจ้าสนใจเรื่อง";
		mes "เสือสมิง";
		mes "ลองไปคุยกับหมอผีดูสิ";
		next;
		mes "[ดุสิต]";
		mes "แต่ก็ระวังตัวไว้ด้วยหล่ะ!";
		mes "ไม่เเน่ว่า เสือสมิงอาจจะ";
		mes "อยู่ใกล้ตัวมากกว่าที่เจ้าคิด ก็ได้!";
		close;
	}
}

ayo_in01,181,193,4	script	หมอผี#thai	840,{

	// สำหรับภารกิจทั้งสองส่วน:
	// - Pre-Renewal ต้องใช้ Needle Packet และ Spool
	// - Renewal ต้องใช้ Fabric และ Nine Tails
	if (checkre(0))
		setarray .@items[0],7213,7217; //Needle_Packet, Spool
	else
		setarray .@items[0],1059,1022; //Transparent_Cloth, Fox_Tail

	if (ayodunquest == 1) {
		mes "[บุญถม]";
		mes "เจ้า...!";
		mes "ใช่ไหม....!";
		mes "โอ้วววววว!";
		next;
		mes "[บุญถม]";
		mes "โอ้...?";
		next;
		mes "[บุญถม]";
		mes "...";
		next;
		mes "[บุญถม]";
		mes "...";
		mes "......";
		next;
		mes "[บุญถม]";
		mes "...";
		mes "......";
		mes ".........";
		next;
		mes "[บุญถม]";
		mes "ไม่เป็นไร";
		mes "ฮะ! ข้าคงหลง";
		mes "กับตรรกะของข้าเอง";
		next;
		mes "[บุญถม]";
		mes "เจ้ามาหาข้าเพื่อขอความช่วยเหลือ? ข้าเห็นความกลัวในตาของเจ้า ข้าจะมองลงไปในจิตวิญญาณของเจ้า และดูว่าเจ้ากลัวอะไร อืมมม...";
		next;
		mes "[บุญถม]";
		mes "...";
		next;
		mes "[บุญถม]";
		mes "...";
		mes "......";
		next;
		mes "[บุญถม]";
		mes "...";
		mes "......";
		mes ".........";
		next;
		mes "[บุญถม]";
		mes "อะไร...!";
		mes "ข้าเห็นเเล้ว เจ้ากลัว 'เสือสมิง'! นี่ข้าไม่ได้ยินข่าวเกี่ยวกับมันมานานมากเเล้วนะ...";
		next;
		mes "[บุญถม]";
		mes "เมื่อนานมาแล้ว";
		mes "เสือสมิง";
		mes "ได้มายังหมู่บ้านนี้";
		mes "และกินผู้คนบริสุทธิ์ไปจำนวนมาก";
		next;
		mes "[บุญถม]";
		mes "ยังไงก็ตาม ความน่ากลัวของมัน";
		mes "ก็มาถึงจุดจบ เมื่อมัน";
		mes "ถูกจับได้ และข้าก็เป็นหนึ่งในกลุ่มคนที่จับมันด้วย";
		next;
		mes "[บุญถม]";
		mes "ข้ายืนยันว่าจะฆ่ามัน แต่คนอื่นกลัวเกินไปที่จะทำแบบนั้น";
		mes "สุดท้ายเราถึงพามันไปขังไว้ที่ซากปรักหักพังนั้น";
		next;
		mes "[บุญถม]";
		mes "ข้าเชื่อว่าอย่างน้อยก็น่าจะสัก 10 ปีแล้ว บางทีตอนนี้มันอาจจะตายไปแล้วก็ได้";
		mes "แต่ก็ไม่มีใครรู้หรอก ว่าเสือมันแข็งแกร่งขนาดไหน";
		mes "หรือมันอาจจะยังมีชีวิตอยู่ก็ได้นะ";
		next;
		mes "[บุญถม]";
		mes "ข้าว่าอย่าไปคิดมากกับมันเลย มันก็ยังคงถูกขังไว้นั้นแหละ";
		next;
		switch(select("ข้าต้องการสำรวจซากปรักหักพังโบราณนั้น:ข้าก็ว่างั้น ข้าไม่กลัวมันเเล้ว")) {
		case 1:
			mes "[บุญถม]";
			mes "เจ้า...";
			mes "เจ้านี่แปลก";
			mes "เจ้าต้องการสำรวจซากปรักหักพังโบราณนั่น นักผจญภัยคนอื่นๆสนใจเรื่องความปลอดภัยก่อนตลอด";
			next;
			mes "[บุญถม]";
			mes "แต่มันก็เป็นทางเลือกของเจ้า และจำไว้ด้วยว่ามันอันตรายมากที่จะต้องผ่านเส้นทางกว่าจะไปถึงศูนย์กลางของซากโบราณ ปกติแล้วข้าจะห้ามไว้นะ";
			next;
			mes "[บุญถม]";
			mes "แต่ถ้าเจ้าอยากจะไป";
			mes "ข้าสามารถให้คำแนะนำที่เป็นประโยชน์กับเจ้าได้";
			mes "เจ้าอยากฟังไหมหล่ะ?";
			next;
			if (select("ถ้ามันอันตรายขนาดนั้น ข้าไม่เอาก็ได้:แน่นอน!") == 1) {
				mes "[บุญถม]";
				mes "ตัดสินใจได้ดี";
				mes "เจ้าควรระวังตัวไว้";
				mes "ถ้าเจ้ายังอยากมีชีวิตอยู่ต่อ";
				close;
			}
			mes "[บุญถม]";
			mes "อืม โอเค";
			mes "ในเมื่อเจ้าสนใจ...";
			next;
			mes "[บุญถม]";
			mes "ถ้าเจ้าต้องการเข้าไปในซากโบราณ เจ้าต้องมีการป้องกันตัวจากวิญญาณชั่วร้าย";
			next;
			if (Class == Job_Priest || Class == Job_Acolyte || Class == Job_Crusader) {
				mes "[บุญถม]";
				mes "แม้ว่าเจ้าจะสามารถ";
				mes "ใช้พลังแห่งความบริสุทธิ์ได้";
				mes "เจ้าก็ยังต้องการความมั่นคงทางวิญญาณจากแผ่นดินของเราอยู่ดี";
			} else {
				mes "[บุญถม]";
				mes "มีผีและปีศาจมากมายในถ้ำ";
				mes "และพวกมันจะโจมตีผู้ที่ไม่มีพลังศักดิ์สิทธิ์ ... เช่นเดียวกับเจ้า";
			}
			next;
			mes "[บุญถม]";
			mes "ข้าสามารถทำให้เจ้า";
			mes "เป็นคนที่มีพลังวิเศษได้ เพราะฉะนั้น ข้าจึงต้องการของบางอย่าง คือ";
			next;
			mes "[บุญถม]";
			mes "^336699๑ "+getitemname(.@items[0])+"^000000";
			mes "^336699๑ "+getitemname(.@items[1])+"^000000";
			mes "^336699๑ Solid Husk^000000";
			mes "^336699๑ Holy Water^000000...";
			mes "นั้นคือทั้งหมดที่ข้าต้องการ";
			next;
			mes "[บุญถม]";
			mes "ไว้ค่อยกลับมา";
			mes "เมื่อเจ้าเตรียม";
			mes "ของทั้งหมดพร้อมเเล้ว";
			set ayodunquest,2;
			changequest 12035,12036;
			close;
		}
		mes "[บุญถม]";
		mes "หืมมม...";
		mes "ไม่กลัวเลยเหรอ?";
		next;
		mes "[บุญถม]";
		mes "............";
		mes "............";
		close;
	} else if (ayodunquest == 2) {
		if (countitem(.@items[0]) > 0 && countitem(.@items[1]) > 0 && countitem(7190) > 0 && countitem(523) > 0) {
			mes "[บุญถม]";
			mes "สุดยอด!";
			mes "ตอนนี้เจ้าได้นำทุกสิ่ง";
			mes "ที่ข้าต้องการมาให้ข้าเเล้ว";
			mes "ข้าจะทำของให้ตามที่ข้าสัญญาก็แล้วกัน...";
			next;
			mes "[บุญถม]";
			mes "โอมมมมม...";
			next;
			mes "[บุญถม]";
			mes "อืมมมม...";
			mes "อืมมมมมมมม";
			next;
			mes "[บุญถม]";
			mes "ฟู้ววว ฟู้ววว...";
			next;
			mes "[บุญถม]";
			mes "เอาหล่ะ";
			mes "ไปได้แล้ว ถ้าเจ้ามีสายสิญจน์นี้";
			mes "เจ้าก็สามารถเข้าไปสู่ซากปรกหักพังได้โดยไม่ต้องกังวลสิ่งใดแล้ว";
			delitem .@items[0],1;
			delitem .@items[1],1;
			delitem 7190,1; //Solid_Peeling
			delitem 523,1; //Holy_Water
			set ayodunquest,3;
			changequest 12036,12037;
			getitem 7285,1; //Thread_Skein
			next;
			mes "[บุญถม]";
			mes "สุดท้าย ถ้าเจ้าทำสายสิญจน์หาย ก็ไม่ต้องกังวล เเค่เจ้านำของที่เป็นวัตถุดิบมาให้ข้า";
			mes "ข้าก็จะทำสายสิญจน์อันใหม่ให้";
			next;
			mes "[บุญถม]";
			mes "ซากโบราณประกอบด้วย 2 ชั้น";
			mes "ก่อนที่เจ้าจะเข้าสู่ชั้น 2 (ใต้ดิน) ให้กลับมาหาข้าเพื่อที่ข้าจะได้บอกวิธีเข้าสู่สถานที่นั้น";
			close;
		}
		mes "[บุญถม]";
		mes "เจ้าลืมว่าต้องใช้อะไรเหรอ";
		mes "นี่เจ้าอยากได้ของขลังจากข้าจริงหรือเปล่าเนี่ย";
		mes "ตั้งใจฟังให้ดีๆด้วยสิ ข้าต้องการ";
		next;
		mes "[บุญถม]";
		mes "^336699๑ "+getitemname(.@items[0])+"^000000";
		mes "^336699๑ "+getitemname(.@items[1])+"^000000";
		mes "^336699๑ Solid Husk^000000";
		mes "^336699๑ Holy Water^000000";
		next;
		mes "[บุญถม]";
		mes "ไว้เจอกันใหม่";
		close;
	} else if (ayodunquest == 3) {
		if (countitem(.@items[0]) > 0 && countitem(.@items[1]) > 0 && countitem(7190) > 0 && countitem(523) > 0) {
			mes "[บุญถม]";
			mes "สุดยอด ตอนนี้เจ้าได้นำทุกสิ่งที่ข้าต้องการมาให้ข้าเเล้ว ข้าจะทำของให้ตามที่ข้าสัญญาก็แล้วกัน ...";
			next;
			mes "[บุญถม]";
			mes "โอมมมมม...";
			next;
			mes "[บุญถม]";
			mes "อืมมมม...";
			mes "อืมมมมมมมม";
			next;
			mes "[บุญถม]";
			mes "ฟู้ววว ฟู้ววว...";
			next;
			mes "[บุญถม]";
			mes "เอาหล่ะ";
			mes "ไปได้แล้ว ถ้าเจ้ามีสายสิญจน์นี้";
			mes "เจ้าก็สามารถเข้าไปสู่ซากปรกหักพังได้โดยไม่ต้องกังวลสิ่งใดแล้ว";
			delitem .@items[0],1;
			delitem .@items[1],1;
			delitem 7190,1; //Solid_Peeling
			delitem 523,1; //Holy_Water
			getitem 7285,1; //Thread_Skein
			next;
			mes "[บุญถม]";
			mes "สุดท้าย ถ้าเจ้าทำสายสิญจน์หาย ก็ไม่ต้องกังวล เเค่เจ้านำของที่เป็นวัตถุดิบมาให้ข้า ข้าก็จะทำสายสิญจน์อันใหม่ให้";
			close;
		}
		mes "[บุญถม]";
		mes "หากเจ้าไม่มีสายสิญจน์ก็ไม่ต้องกังวลไป ข้าสามารถทำเพิ่มให้เจ้าได้นะ แค่ไปเอาของเหล่านี้มาให้ข้า";
		next;
		mes "[บุญถม]";
		mes "^336699๑ "+getitemname(.@items[0])+"^000000";
		mes "^336699๑ "+getitemname(.@items[1])+"^000000";
		mes "^336699๑ Solid Husk^000000";
		mes "^336699๑ Holy Water^000000";
		next;
		mes "[บุญถม]";
		mes "ไว้เจอกันใหม่นะ";
		close;
	} else if (ayodunquest > 3 && ayodunquest < 9) {
		mes "[บุญถม]";
		mes "ในที่สุดเจ้าก็เริ่มมองหาเสือแล้ว ข้าหวังว่าเจ้าจะปลอดภัยจากอันตรายนะ";
		next;
		mes "[บุญถม]";
		mes "ทำตัวสบายๆ";
		mes "จะถามอะไรฉันก็ได้ทุกเมื่อ";
		close;
	} else if (ayodunquest == 9) {
		mes "["+strcharinfo(0)+"]";
		mes "ข้าได้คัดลอกข้อความ";
		mes "จากตอนที่ไปสำรวจดันเจี้ยนมา";
		mes "เจ้าจะดูนี่ไหมหละ?";
		next;
		mes "[หินชนวน]";
		mes "^333333อย่าเข้าไป";
		mes "ที่  ชั้น 2 (ชั้นใต้ดิน)";
		mes "...มันอันตราย... เจ้าจะ...";
		mes "...เจอ... เสือ...^000000";
		next;
		mes "[หินชนวน]";
		mes "^333333เจ้าต้องฆ่า...เพื่อที่... จะต้อง... ไปยัง... ชั้น 2 (ชั้นใต้ดิน)...";
		mes "เจ้าต้องการอีกเหรอ... พลังศักดิ์สิทธิ์... เพื่อ ... ไม่อย่างนั้น มันเป็นไปไม่ได้ ...^000000";
		next;
		mes "[บุญถม]";
		mes "มันเขียนโดยเพื่อนของข้าคนหนึ่งที่เข้ามาในซากปรักหักพัง";
		mes "เขารู้สึกเสียใจที่ปล่อยให้เจ้าเสือนั้นยังมีชีวิตอยู่ ไม่ว่ามันจะกลับมาหรือไม่ก็ตาม";
		next;
		mes "[บุญถม]";
		mes "เจ้าพบบันทึกนี้ แสดงว่าเจ้านี่ต้องมีความมุ่งมั่นมากในการลุยซากปรักหักพังนั้นนะเนี่ย";
		next;
		mes "[บุญถม]";
		mes "อย่างที่เจ้าเดาได้จากบันทึกนี้นี่แหละ";
		mes "ชั้น 2 (ชั้นใต้ดิน) นั้นอันตรายมาก เจ้าจะต้องมีสายสิญจน์ที่ผ่านการร่ายมนต์ศักดิ์สิทธิ์มาแล้ว";
		next;
		mes "[บุญถม]";
		mes "ตอนนี้ เจ้ากลับไปรวบรวมวัตถุดิบ มาให้ข้าเพื่อข้าจะได้ทำสายสิญจน์ที่มีพลังมากกว่านี้ให้เจ้า";
		next;
		mes "[บุญถม]";
		mes "^336699๒ Holy Water^000000";
		mes "^336699๑ Yggdrasil Leaf^000000";
		mes "^336699๒ "+getitemname(.@items[0])+"^000000";
		mes "^336699๒ "+getitemname(.@items[1])+"^000000";
		set ayodunquest,10;
		changequest 12037,12038;
		next;
		mes "[บุญถม]";
		mes "กลับมาหาข้า";
		mes "เมื่อเจ้าหาของมาครบแล้ว";
		close;
	} else if (ayodunquest == 10) {
		if (countitem(523) > 1 && countitem(610) > 0 && countitem(.@items[0]) > 1 && countitem(.@items[1]) > 1) {
			mes "[บุญถม]";
			mes "ฮืมม~";
			mes "เจ้านำของทุกอย่าง";
			mes "มาครบแล้วหนิ";
			mes "ช่างกระตือรือร้นจริงๆ";
			next;
			mes "[บุญถม]";
			mes "โอมมมมม...";
			next;
			mes "[บุญถม]";
			mes "งืมมมมม...";
			mes "งัมมมมม...";
			next;
			mes "[บุญถม]";
			mes "ฟู้วว ฟู้วว...";
			next;
			mes "[บุญถม]";
			mes "อ่ะ...เอานี่";
			mes "พกสายสิญจน์นี่ไปด้วย";
			mes "เเล้วเจ้าจะเข้าไปสู่ซากปรักหักพังนั้นได้โดยไม่ต้องกังวล";
			delitem .@items[0],2;
			delitem .@items[1],2;
			delitem 610,1; //Leaf_Of_Yggdrasil
			delitem 523,2; //Holy_Water
			set ayodunquest,11;
			getitem 7287,1; //Thread_Skein_
			changequest 12038,12039;
			next;
			mes "[บุญถม]";
			mes "สุดท้าย ถ้าเจ้าทำสายสิญจน์หาย ก็ไม่ต้องกังวล";
			mes "เเค่เจ้านำของที่เป็นวัตถุดิบมาให้ข้า ข้าก็จะทำสายสิญจน์อันใหม่ให้";
			close;
		}
		mes "[บุญถม]";
		mes "อืมมม...?";
		mes "ข้ายังไม่ได้บอกเจ้าหรือว่า";
		mes "ข้าอยากจะทำ";
		mes "สายสิญจน์ที่มี";
		mes "พลังมากขึ้นกว่านี้?";
		next;
		mes "[บุญถม]";
		mes "^336699๒ Holy Water^000000";
		mes "^336699๑ Yggdrasil Leaf^000000";
		mes "^336699๒ "+getitemname(.@items[0])+"^000000";
		mes "^336699๒ "+getitemname(.@items[1])+"^000000";
		next;
		mes "[บุญถม]";
		mes "กลับมาหาข้า";
		mes "พร้อมกับของทั้งหมด";
		mes "และข้าจะทำ";
		mes "'สายสิญจน์'";
		mes "ที่มีพลังกว่านี้ให้เจ้า";
		close;
	} else if (ayodunquest == 11) {
		if (countitem(7287) > 0) {
			mes "[บุญถม]";
			mes "สิ่งนี้";
			mes "จะปกป้องเจ้า";
			mes "จากพวกวิญญาณที่ชั่วร้าย";
			mes "แต่สุดท้าย มันก็อยู่ที่เจ้าด้วย";
			mes "ว่าจะปกป้องตัวเอง";
			mes "จากการโจมตีของพวกมันด้วยหรือเปล่า?";
			close;
		} else if (countitem(523) > 1 && countitem(610) > 0 && countitem(.@items[0]) > 1 && countitem(.@items[1]) > 1 && countitem(7287) == 0) {
			mes "[บุญถม]";
			mes "โอมมมมม...";
			next;
			mes "[บุญถม]";
			mes "งืมมมมม...";
			mes "งัมมมมม...";
			next;
			mes "[บุญถม]";
			mes "ฟู้วว ฟู้วว...";
			next;
			mes "[บุญถม]";
			mes "อ่ะ...เอานี่";
			mes "พกสายสิญจน์นี่ไปด้วย";
			mes "เเล้วเจ้าจะเข้าไปสู่ซากปรักหักพังนั้นได้โดยไม่ต้องกังวล";
			delitem .@items[0],2;
			delitem .@items[1],2;
			delitem 610,1; //Leaf_Of_Yggdrasil
			delitem 523,2; //Holy_Water
			getitem 7287,1; //Thread_Skein_
			close;
		}
		mes "[บุญถม]";
		mes "อืมมม...?";
		mes "ข้ายังไม่ได้บอกเจ้าหรือว่า";
		mes "ข้าอยากจะทำ";
		mes "สายสิญจน์ที่มี";
		mes "พลังมากขึ้นกว่านี้?";
		next;
		mes "[บุญถม]";
		mes "^336699๒ Holy Water^000000";
		mes "^336699๑ Yggdrasil Leaf^000000";
		mes "^336699๒ "+getitemname(.@items[0])+"^000000";
		mes "^336699๒ "+getitemname(.@items[1])+"^000000";
		close;
	} else if (ayodunquest == 12) {
		mes "[บุญถม]";
		mes "หึ...!";
		mes "เจ้าต้องพบอะไรมาบางอย่างแน่";
		mes "ข้าเห็นใบหน้าของเจ้าดูมืดมนและมีเงาปกคลุมจิตใจเจ้า";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "ข้าพบ";
		mes "จดหมายเก่า";
		mes "ในซากปรักหักพัง";
		next;
		mes "[Old Letter]";
		mes "^333333ถึง หมอผี...ฉันเป็นคนโง่...ที่นั่นมี เสือสมิง'...ใคร ....ซากของฉัน... นำไป...ให้ หมอผี...";
		next;
		mes "[บุญถม]";
		mes "อ่อ...";
		mes "โอ้ พุทโธ";
		mes "นั่น เพื่อนเก่าข้า...";
		next;
		mes "[บุญถม]";
		mes "ขอบใจที่มาบอกข้านะ";
		mes "เพื่อนของข้าทิ้งกล่องนั้นไว้ให้ข้า";
		mes "แต่ข้าคิดว่าเขาต้องการที่จะให้กล่องนั้นกับคนที่พบซากของเขา";
		next;
		mes "[บุญถม]";
		mes "หากเจ้าต้องการสำรวจซากโบราณนั้นอีกครั้ง แต่เจ้าไม่มีสายสิญจน์ ก็มาหาข้าได้เสมอ";
		set ayodunquest,13;
		completequest 12039;
		close;
	} else if (ayodunquest > 12) {
		if (countitem(7287) > 0) {
			mes "[บุญถม]";
			mes "ขอให้โชคดีกับการเดินทาง";
			mes "ของเจ้าก็แล้วกัน";
			close;
		} else if (countitem(523) > 1 && countitem(610) > 0 && countitem(.@items[0]) > 1 && countitem(.@items[1]) > 1 && countitem(7287) == 0) {
			mes "[บุญถม]";
			mes "ทำสายสิญจน์";
			mes "หายไปใช่ไหม?";
			mes "แต่จากวัตถุดิบที่เจ้ามี";
			mes "ข้าจะทำสายสิญจน์ใหม่";
			mes "ให้เจ้าอีกชิ้น";
			mes "เจ้าจะทำไหม";
			next;
			menu "ทำ",make,"ไม่ทำ",dont;
		make:
			mes "[บุญถม]";
			mes "โอมมมมม...";
			next;
			mes "[บุญถม]";
			mes "งืมมมมม...";
			mes "งัมมมมม...";
			next;
			mes "[บุญถม]";
			mes "ฟู้วว ฟู้วว...";
			next;
			mes "[บุญถม]";
			mes "อ่ะ...เอานี่";
			mes "พกสายสิญจน์นี่ไปด้วย";
			mes "เเล้วเจ้าจะเข้าไปสู่ซากปรักหักพังนั้นได้โดยไม่ต้องกังวล";
			delitem .@items[0],2;
			delitem .@items[1],2;
			delitem 610,1; //Leaf_Of_Yggdrasil
			delitem 523,2; //Holy_Water
			getitem 7287,1; //Thread_Skein_
			close;
			
		dont:
			mes "[บุญถม]";
			mes "ไม่เป็นไร ไว้ค่อยกลับมาใหม่ก็ได้";
			close;
		}
		mes "[บุญถม]";
		mes "อืมมม...?";
		mes "ข้ายังไม่ได้บอกเจ้าหรือว่า";
		mes "ข้าอยากจะทำ";
		mes "สายสิญจน์ที่มี";
		mes "พลังมากขึ้นกว่านี้?";
		next;
		mes "[บุญถม]";
		mes "^336699๒ Holy Water^000000";
		mes "^336699๑ Yggdrasil Leaf^000000";
		mes "^336699๒ "+getitemname(.@items[0])+"^000000";
		mes "^336699๒ "+getitemname(.@items[1])+"^000000";
		close;
	}
	mes "[บุญถม]";
	mes "โอมมมมม...";
	close;
}

ayo_fild01,129,197,0	script	ปู่เรียม#thai1	842,{
	if ((ayodunquest > 2 && countitem(7285) > 0) || (ayodunquest > 2 && countitem(7287) > 0)) {
		mes "[ปู่เรียม]";
		mes "หึ...?";
		mes "ข้าสัมผัสได้!";
		mes "ข้าสัมผัสได้ถึง";
		mes "พลังที่บริสุทธิ์!";
		mes "มันมาจากเจ้าหนิ!";
		next;
		mes "[ปู่เรียม]";
		mes "ข้าเดาว่าเจ้า";
		mes "จะเข้าไปยังซากโบราณ";
		mes "นั่น คือสิ่งที่เจ้าต้องการ?";
		mes "ในการมาที่นี่ใช่ไหม?";
		next;
		mes "[ปู่เรียม]";
		mes "ข้าไม่แนะนำ";
		mes "ในการเข้าไปข้างในหรอกนะ";
		mes "ขนาดคนในท้องถิ่นเอง";
		mes "ยังกลัวสถานที่นี้เลย";
		next;
		mes "[ปู่เรียม]";
		mes "ถึงเสือสมิงจะไม่ได้อยู่เเถวๆนี้ สถานที่นี้ก็จะมีวิญญาณชั่วร้ายอยู่อยู่ดี แม้จะมีสายสิญจน์สถานที่นี้ก็ยังคงอันตราย";
		next;
		mes "[ปู่เรียม]";
		mes "แล้วเจ้าต้องการอะไร";
		mes "เจ้ายังต้องการ";
		mes "ที่จะสำรวจพื้นที่";
		mes "ต่ออยู่ไหม?";
		next;
		if (select("แน่นอน:ไม่หล่ะ! ข้าเริ่มกลัวเเล้ว") == 1) {
			mes "[ปู่เรียม]";
			mes "ฮืมม...";
			mes "ดูเหมือนเจ้าชอบความตื่นเต้นท้าทายสินะ";
			mes "หรือเจ้าอยากตายกันเเน่";
			next;
			mes "[ปู่เรียม]";
			mes "แต่ยังไงมันก็ไม่ใช่เรื่องของข้า";
			mes "ยังไงเจ้าก็มีวิญญาณศักดิ์สิทธิ์คุ้มครองอยู่ แต่เจ้าอาจจะไม่อยากลงไป ชั้นใต้ดิน ชั้นที่ 2 ของซากนั้นหรอกนะ";
			next;
			mes "[ปู่เรียม]";
			mes "พวกชาวรูน-มิการ์ด";
			mes "พูดไว้ว่า 'ความอยากรู้อยากเห็นจะเป็นอันตราย'";
			mes "เอาหล่ะไปได้เเล้ว ไป!";
			next;
			mes "^3355FFเขาผลักคุณออกจากเนินเขา";
			mes "แล้วคุณก็ร่วงลงมาเหมือนก้อนหิน^000000";
			close2;
			percentheal -10,0;
			warp "ayo_fild02",30,135;
			end;
		}
		mes "[ปู่เรียม]";
		mes "ตัดสินใจได้ดี!";
		mes "อย่างที่ข้าคาดไว้ เจ้าฉลาดพอ";
		mes "ที่จะไม่เอาชีวิตตัวเอง";
		mes "ไปเสี่ยงซะเปล่าๆ";
		close;
	}
	mes "[ปู่เรียม]";
	mes "ถ้าข้าเป็นเจ้า";
	mes "ข้าจะไม่กล้า แม้เเต่จะเข้า";
	mes "ไปใกล้ซากปรักหักพังนั่น";
	next;
	mes "[ปู่เรียม]";
	mes "ทุกคนที่ไม่ได้รับการคุ้มครองจาก วิญญาณที่ศักดิ์สิทธิ์ จะง่ายต่อการจัดการของสัตว์ร้ายที่ อยู่ที่นั่น";
	mes "และก็จะถูกกำจัดทิ้งในระยะเวลา อันสั้น!";
	next;
	mes "[ปู่เรียม]";
	mes "กลับไปซะเจ้านักผจญภัย";
	mes "ถ้าเจ้าอยากจะสำรวจ";
	mes "พื้นที่นี้จริงๆ";
	mes "เจ้าต้องแสดงพลังวิญญาณศักดิ์สิทธิ์ ให้ข้าดู";
	close;
}

ayo_fild02,25,154,0	script	เอก#thai	843,{
	mes "[เอก]";
	mes "ไง เจ้าเป็นยังไงบ้าง?";
	mes "ข้าหวังว่าพวกสิ่งชั่วร้าย จะไม่ตามเจ้าออกมาจากซากปรักหักพัง นั้น แล้วมาหลอกหลอนเจ้าในฝันต่อหรอกนะ";
	next;
	mes "[เอก]";
	mes "เมื่อเจ้ากลับไป";
	mes "อย่าลืมที่จะไล่";
	mes "และทำการปัดเป่าสิ่งชั่วร้ายออกไป ด้วยหล่ะ";
	mes "ไม่อย่างนั้น";
	mes "เจ้าจะพบปัญหาภายหลังเเน่นอน...";
	next;
	mes "[เอก]";
	mes "แล้วเจ้าอยาก";
	mes "จะกลับไปหรือยังหล่ะ?";
	next;
	if (select("อยาก:ไม่ ข้าอยากดูรอบๆอีกหน่อย...") == 1) {
		mes "[เอก]";
		mes "เอาหล่ะ ถ้างั้น...";
		mes "ไปกันเลย...";
		next;
		mes "^3355FFทันใดนั้นเขาก็จับคุณ";
		mes "แล้วเหวี่ยงคุณขึ้นไปในอากาศ!^000000";
		close2;
		warp "ayo_fild01",115,200;
		end;
	}
	mes "[เอก]";
	mes "โอเค";
	mes "ดูเเลตัวเองด้วย";
	close;
}

// Water Traps
ayo_dun01,273,26,0	script	 #th_dun1_1_1::AyoTrap1	111,0,4,{
	mes "^3355FFคุณมองลงไปในสระที่มีน้ำ";
	mes "อยู่ด้านล่าง มันดูลึกมากๆ^000000";
	next;
	mes "^3355FFคุณกลัวมากๆ";
	mes "แต่ถ้าตกลงไป^000000";
	mes "^2F2F4Fคุณอาจจะตายได้^000000";
	close;

OnTouch_:
	mes "^3355FFคุณมองลงไปในสระที่มีน้ำ";
	mes "อยู่ด้านล่าง มันดูลึกมากๆ^000000";
	next;
	mes "^3355FFคุณกลัวมากๆ";
	mes "แต่ถ้าตกลงไป^000000";
	mes "^2F2F4Fคุณอาจจะตายได้^000000";
	close;
}

ayo_dun01,273,27,0	duplicate(AyoTrap1)	 #th_dun1_1_2	111
ayo_dun01,272,27,0	duplicate(AyoTrap1)	 #th_dun1_1_3	111,0,4
ayo_dun01,272,26,0	duplicate(AyoTrap1)	 #th_dun1_1_4	111

ayo_dun01,26,27,0	script	 #th_dun1_1::AyoTrap2	-1,1,1,{
OnTouch_:
	percentheal -100,0;
	end;
}

ayo_dun01,28,27,0	duplicate(AyoTrap2)	 #th_dun1_2	-1,0,1
ayo_dun01,27,25,0	duplicate(AyoTrap2)	 #th_dun1_3	-1,1,0
ayo_dun01,25,25,0	duplicate(AyoTrap2)	 #th_dun1_4	-1
ayo_dun01,272,273,0	duplicate(AyoTrap2)	 #th_dun1_5	-1,1,1
ayo_dun01,274,273,0	duplicate(AyoTrap2)	 #th_dun1_6	-1,0,1
ayo_dun01,273,271,0	duplicate(AyoTrap2)	 #th_dun1_7	-1,1,0
ayo_dun01,271,271,0	duplicate(AyoTrap2)	 #th_dun1_8	-1
ayo_dun01,26,273,0	duplicate(AyoTrap2)	 #th_dun1_9	-1,1,1
ayo_dun01,28,273,0	duplicate(AyoTrap2)	 #th_dun1_10	-1,0,1
ayo_dun01,27,271,0	duplicate(AyoTrap2)	 #th_dun1_11	-1,1,0
ayo_dun01,25,271,0	duplicate(AyoTrap2)	 #th_dun1_12	-1

// Hints
ayo_dun01,156,148,0	script	 #hint01::AyoHint	-1,0,4,{
OnTouch_:
	if (rand(1,100) > 60 && ayodunquest < 11) {
		mes "^3355FFสายสิญจน์ในกระเป๋าของคุณก็เริ่ม เปล่งประกาย คุณรู้สึกว่ามีบางสิ่งอยู่ใกล้คุณ...^000000";
		close2;
		viewpoint 1,198,164,0,0xFF0000;
		viewpoint 1,87,16,1,0xFF0000;
		viewpoint 1,268,214,2,0xFF0000;
		viewpoint 1,147,274,3,0xFF0000;
		viewpoint 1,99,118,4,0xFF0000;
		viewpoint 1,16,188,5,0xFF0000;
	}
	end;
}

ayo_dun01,259,42,0	duplicate(AyoHint)	 #hint02	-1,2,0
ayo_dun01,259,44,0	duplicate(AyoHint)	 #hint02_2	-1,2,0
ayo_dun01,259,46,0	duplicate(AyoHint)	 #hint02_3	-1,2,0

// Puzzle pieces
ayo_dun01,198,164,0	script	 #hun_thai_1	111,{
	if (ayodunquest == 3) {
		set ayodunquest,4;
		mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 1 ของกระดานชนวน^000000";
		next;
		mes "^3355FFมันอ่านว่า '^0000FFอย่าเข้าไปชั้นใต้ดินชั้นที่ 2^3355FF' คุณไม่สามารถอ่านข้อความที่เหลือได้^000000";
		close;
	}
	mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 1 ของกระดานชนวน^000000";
	close;
}

ayo_dun01,87,16,0	script	 #hun_thai_2	111,{
	if (ayodunquest == 4) {
		set ayodunquest,5;
		mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 2 ของกระดานชนวน^000000";
		next;
		mes "^3355FFมันอ่านว่า '^0000FF...มัน อันตราย... เจ้า จะ...^3355FF' คุณไม่สามารถอ่านข้อความที่เหลือได้^000000";
		close;
	}
	mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 2 ของกระดานชนวน^000000";
	close;
}

ayo_dun01,268,214,0	script	 #hun_thai_3	111,{
	if (ayodunquest == 5) {
		set ayodunquest,6;
		mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 3 ของกระดานชนวน^000000";
		next;
		mes "^3355FFมันอ่านว่า '^0000FF...พบ... เสือ...^3355FF' คุณไม่สามารถอ่านข้อความที่เหลือได้^000000";
		close;
	}
	mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 3 ของกระดานชนวน^000000";
	close;
}

ayo_dun01,147,274,0	script	 #hun_thai_4	111,{
	if (ayodunquest == 6) {
		set ayodunquest,7;
		mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 4 ของกระดานชนวน^000000";
		next;
		mes "^3355FFมันอ่านว่า '^0000FFเจ้าต้องฆ่า... เพื่อที่จะ... ต้อง... ไปอย่างไร... ชั้นที่ 2...^3355FF' คุณไม่สามารถอ่านข้อความที่เหลือได้^000000";
		close;
	}
	mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 4 ของกระดานชนวน^000000";
	close;
}

ayo_dun01,99,118,0	script	 #hun_thai_5	111,{
	if (ayodunquest == 7) {
		set ayodunquest,8;
		mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 5 ของกระดานชนวน^000000";
		next;
		mes "^3355FFมันอ่านว่า '^0000FFเจ้าต้องการเพิ่มอีก... พลังศักดิ์สิทธิ์... เพื่อที่จะ...^3355FF' คุณไม่สามารถอ่านข้อความที่เหลือได้^000000";
		close;
	}
	mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนที่ 5 ของกระดานชนวน^000000";
	close;
}

ayo_dun01,16,188,0	script	 #hun_thai_6	111,{
	if (ayodunquest == 8) {
		set ayodunquest,9;
		setquest 12036;
		mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนสุดท้ายของกระดานชนวน^000000";
		next;
		mes "^3355FFมันอ่านว่า '^0000FFไม่อย่างนั้นมันเป็นไปไม่ได้...^3355FF' คุณไม่สามารถอ่านข้อความที่เหลือได้^000000";
		close;
	}
	mes "^3355FFคุณพบชิ้นส่วนของหินชนวนที่มีตัว อักษรฝังอยู่ ดูเหมือนว่าจะเป็นส่วนสุดท้ายของกระดานชนวน^000000";
	close;
}

ayo_dun01,24,283,0	script	 #2_dun_in	111,{
	if (ayodunquest > 10) {
		if (countitem(7287) > 0) {
			mes "^3355FFสายสิญจน์";
			mes "จะเพิ่มพลัง";
			mes "ความเเข็งแกร่ง และมั่นคง";
			mes "ด้วยการปกป้อง";
			mes "ตัวของเจ้า";
			mes "จนกว่าเจ้าจะไปถึงชั้น 2^000000.";
			close2;
			if (rand(1,10) > 5) {
				warp "ayo_dun02",23,26;
			} else {
				warp "ayo_dun02",275,26;
			}
			end;
		}
		mes "^3355FFคุณพบสิ่งที่ดูเหมือนจะเป็นทางเข้า สู่ชั้น 2 แต่ตอนนี้คุณเต็มไปด้วยความหวาดกลัวอย่างมาก สุดท้ายคุณก็ไม่สามารถพาตัวเองลงไป ได้...^000000";
		close;
	}
	mes "^3355FFคุณพบสิ่งที่ดูเหมือนจะเป็นทางเข้า สู่ชั้น 2 แต่ตอนนี้คุณเต็มไปด้วยความหวาดกลัวอย่างมาก สุดท้ายคุณก็ไม่สามารถพาตัวเองลงไป ได้...^000000";
	close;
}

ayo_dun02,268,108,0	script	 #reward_tiger	111,{
	if (ayodunquest == 11 && countitem(7287) > 0) {
		mes "^3355FFคุณพบซากครึ่งหนึ่งของโครงกระดูกของคน ที่ยื่นออกมาจากพื้นดิน ดูเหมือนจะเป็นซากศพของสหายของหมอผี^000000";
		next;
		mes "^3355FFคุณพบกระดาษชิ้นหนึ่งข้างโครงกระดูก และมีกล่องด้วย^000000";
		next;
		mes "^3355FFคุณอ่านสิ่งที่เขียนบนกระดาษแผ่นเก่าๆ ตัวอักษรเริ่มจางไปตามอายุ และอ่านยากมาก^000000";
		next;
		mes "[Old Letter]";
		mes "^333333ถึง หมอผี,";
		mes "ฉันมันโง่ มันมี 'เสือสมิง'...ใครที่ .... ซากของฉัน ... ได้โปรด  นำไปให้...กับ หมอผี...";
		next;
		mes "^3355FFคุณเก็บ";
		mes "กล่อง^000000";
		set ayodunquest,12;
		completequest 12039;
		getitem 603,1; //Old_Blue_Box
		close;
	}
}

// Tom Tung Goong Quest :: ayo_tom
//============================================================
ayothaya,196,265,3	script	อนันต์#ayo	842,{
	if (tomyumgoong == 0) {
		mes "[อนันต์]";
		mes "เจ้ารู้ไหม";
		mes "อาหารอะไรที่ขึ้นชื่อ";
		mes "ที่สุดในหมู่บ้านของเรา?";
		next;
		mes "[อนันต์]";
		mes "หมู่บ้านนี้ขึ้นชื่อในเรื่องของ";
		mes "อาหารที่มีเอกลักษณ์เฉพาะตัว";
		mes "และหนึ่งในเมนูที่ขึ้นชื่อที่สุดคือ";
		mes "'^3131FFต้มยำกุ้ง^000000'";
		next;
		mes "[อนันต์]";
		mes "ต้มยำกุ้งเป็นซุปประเภทหนึ่งที่เผ็ด มาก";
		mes "ถ้าได้ลองซักครั้งแล้วเจ้าจะติดใจกับรสชาติที่ลึกซึ้ง ลองสองครั้งแล้วเจ้าจะรู้สึกสดชื่น ด้วยกลิ่นที่ยั่วเย้า";
		next;
		mes "[อนันต์]";
		mes "มันเป็นหนึ่งในอาหารที่อร่อยที่สุดในโลก!";
		mes "ทุกคนในโลกอยากจะลอง";
		mes "สักครั้งในช่วงชีวิตของเขา";
		mes "เจ้าอยากจะลองไหมหล่ะ?";
		next;
		if (select("แน่นอน:ไม่หล่ะ ขอบคุณ ข้าไม่ชอบรสเผ็ด") == 1) {
			mes "[อนันต์]";
			mes "เอาหล่ะ...!";
			mes "ตอนนี้";
			mes "ใครเหมาะจะเตรียม";
			mes "ต้มยำกุ้งให้เจ้าดีนะ?";
			mes "อืมมม...";
			next;
			mes "[อนันต์]";
			mes "ทุกๆคนในหมู่บ้านนี้";
			mes "รู้วิธีทำหมดแหละ";
			mes "แต่ข้าเเนะนำคนนึงให้";
			mes "เจ้าลองไปหา ^3131FFแม่มะลิ^000000 ดูก็เเล้วกัน";
			next;
			emotion ET_BEST;
			set tomyumgoong,1;
			setquest 8123;
			mes "[อนันต์]";
			mes "เธอเป็นแม่ครัวที่เก่งที่สุด";
			mes "ถ้าพูดถึงในเรื่องต้มยำกุ้ง!";
			mes "เจ้าลองไปขอให้เขาทำให้กินดูสิ";
			close;
		}
		mes "[อนันต์]";
		mes "ถ้างั้น...";
		mes "หากเจ้าเปลี่ยนใจ";
		mes "จะกลับมาเมื่อไหร่";
		mes "ก็ได้นะ";
		close;
	} else if (tomyumgoong == 1) {
		mes "[อนันต์]";
		mes "อ่า เจ้าอยากจะรู้";
		mes "ว่า ^3131FFแม่มะลิ^000000 อยู่ไหนงั้นเหรอ?";
		mes "มีสถานที่ปรุงอาหารไม่มากนัก";
		mes "ที่จะอยู่นอกเหนือจากร้านอาหารใช่ไหม?";
		mes "ฮ่าๆๆ~";
		close;
	}
	mes "[อนันต์]";
	mes "อโยธยา เมืองทอง...";
	mes "ถึงแม้ว่าข้าจะโตขึ้นมาในหมู่บ้านนี้";
	mes "ข้าก็ยังคิดว่าหมู่บ้านของข้า";
	mes "ช่างงดงามซะเหลือเกิน";
	next;
	mes "[อนันต์]";
	mes "เจ้าคิดว่ายังไง?";
	mes "ไม่เห็นด้วยกับข้าเหรอ?";
	close;
}

ayo_in01,145,163,3	script	แม่ครัว#ayo	839,{
	if (tomyumgoong < 2) {
		mes "[แม่มะลิ]";
		mes "สวัสดีจ้า!";
		mes "เรียกฉัน แม่มะลิ นะจ๊ะ";
		mes "อาหารทุกจานของฉัน";
		mes "มักจะทำให้ลูกค้าประหลาดใจ กับรสชาติที่ดึงดูด และล้ำลึก";
		next;
		mes "[แม่มะลิ]";
		mes "และฉันภูมิใจเสนอ";
		mes "^3131FFต้มยำกุ้ง^000000 ให้เป็นอาหารขึ้นชื่อ";
		mes "ของหมู่บ้านนี้เลย";
		mes "ชื่อเสียงของฉันเลื่องลือไปเพราะ ต้มยำกุ้งนี่แหละ";
		if (tomyumgoong == 0) {
			close;
		} else {
			next;
			mes "[แม่มะลิ]";
			mes "อ่อ เข้าใจเเล้ว...";
			mes "อยากให้ฉันทำ";
			mes "ต้มยำกุ้ง ให้หล่ะสิ";
			mes "ใช่ไหม?";
			next;
			mes "[แม่มะลิ]";
			mes "จริงๆแล้วฉันยุ่งมากเลยตั้งแต่เปิดร้าน";
			mes "แต่หากลูกต้องการลิ้มรสต้มยำกุ้งของฉัน";
			mes "ฉันจะทำให้ เอาไหม?";
			next;
			if (select("แน่นอน ทำเลยๆ!:อ่า...ไม่ดีกว่า") == 1) {
				mes "[แม่มะลิ]";
				mes "อย่างแรก";
				mes "ลูกต้องหาวัตถุดิบในการทำ";
				mes "ต้มยำกุ้งให้ฉัน เข้าใจนะ? ฉันต้องการ ^CE0000กุ้ง^000000, ^CE0000พริก^000000 และ ^CE0000มะนาว^000000";
				next;
				mes "[แม่มะลิ]";
				mes "จำไว้ว่ากุ้งเป็นส่วนประกอบหลักของอาหารชามนี้ ดังนั้นกุ้งที่สดใหม่จะทำให้อาหารชามนี้อร่อยได้";
				next;
				select("หาจากที่ไหนได้บ้าง?");
				mes "[แม่มะลิ]";
				mes "ฉันรอให้ถาม";
				mes "อยู่พอดีเลย";
				mes "สำหรับกุ้ง ก็ลองไปหาดู";
				mes "แถวๆชายหาดหน่ะ";
				mes "น่าจะมีเยอะ";
				next;
				mes "[แม่มะลิ]";
				mes "แต่มันก็ดูยุ่งยาก ถ้าอย่างงั้น ลูกแค่ไปซื้อกุ้ง";
				mes "จากคนขายกุ้งสดแถวนี้มาก็แล้วกัน";
				next;
				mes "[แม่มะลิ]";
				mes "อืมม...";
				mes "ถ้าลูกบอกชื่อเขา";
				mes "เขาอาจจะให้";
				mes "ส่วนลดก็ได้นะ";
				set tomyumgoong,2;
				changequest 8123,8124;
				next;
				mes "[แม่มะลิ]";
				mes "เขาชื่อ ^3131FFทองพูน^000000.";
				mes "เขาขายกุ้งอยู่แถวๆหมู่บ้านนี่แหละ ไปหาเขาและขอซื้อกุ้งจากเขามาสัก ๒๐ ตัวดูนะ";
				close;
			}
			mes "[แม่มะลิ]";
			mes "ฉันเข้าใจว่านี่อาจเป็นเรื่องยาก";
			mes "แต่ก็น่าลองถ้าลูกต้องการลิ้มรสต้มยำกุ้งที่ดีที่สุด";
			close;
		}
	} else if (tomyumgoong > 1 && tomyumgoong < 4) {
		if (tomyumgoong == 2 && countitem(567) > 19) {
			mes "[แม่มะลิ]";
			mes "เอ่ออ...";
			mes "เกิดอะไรขึ้นกับกุ้ง?";
			mes "ทำไมกุ้งเละ มีรอยฉีกขนาดนี้! ฉันไม่เชื่อว่า ทองพูน จะขายกุ้ง";
			mes "ที่ไม่ดีแบบนี้กับลูกค้า!";
			next;
			mes "[แม่มะลิ]";
			mes "ฉันต้องขอโทษด้วย";
			mes "แต่ฉันไม่สามารถทำ";
			mes "ต้มยำกุ้ง ที่ดีที่สุด";
			mes "ได้ด้วยกุ้งพวกนี้";
			next;
			mes "[แม่มะลิ]";
			mes "ฉันรู้ว่ามันลำบาก แต่ช่วยกลับไปหาทองพูน";
			mes "และไปขอกุ้งที่ดีที่สุดจากเขามาให้หน่อย";
			mes "เพื่อที่จะเอามาทำ ต้มยำกุ้ง ได้ไหม?";
			close;
		} else if (tomyumgoong == 3) {
			if (countitem(567) > 19) {
				mes "[แม่มะลิ]";
				mes "อ่า ลูกกลับมาแล้ว!";
				mes "ไหน ดูหน่อยซิ ว่าเอาอะไร";
				mes "กลับมาบ้าง ฮืมม...";
				mes "นี่คือ...";
				next;
				emotion ET_BEST;
				mes "[แม่มะลิ]";
				mes "กุ้งสดใหม่ที่สุดที่ฉันตามหา!";
				mes "ทำได้ดีมาก ~";
				mes "ด้วยกุ้งตัวใหญ่ๆ และสดใหม่เหล่านี้";
				mes "ฉันสามารถปรุงต้มยำกุ้งที่ดีที่สุดให้ลูกได้แน่นอน! ขอบคุณมากนะจ๊ะ!";
				delitem 567,20; //Prawn
				next;
				mes "[แม่มะลิ]";
				mes "มันจะเป็นอาหารที่สุดยอดมาก ด้วยความหวานธรรมชาติของกุ้ง ความเปรี้ยวของมะนาว และน้ำปลานิดหน่อย!";
				next;
				mes "[แม่มะลิ]";
				mes "โอ้... ใช่";
				mes "แต่เรามีแค่กุ้ง";
				mes "ตอนนี้ฉันอยากให้";
				mes "ลูกหา ^CE0000มะนาว^000000 มาให้ด้วย";
				next;
				if (select("หามะนาวจากที่ไหนได้บ้าง?:ฉันเกลียดอาหารรสเปรี้ยว!") == 1) {
					set tomyumgoong,4;
					changequest 8124,8125;
					mes "[แม่มะลิ]";
					mes "ไม่กี่วันที่ผ่านมา ฉันเห็นมะนาวต้นนึง";
					mes "ตอนที่เดินเล่นอยู่";
					mes "แต่มันเป็นต้นของชาวบ้านไม่ใช่พ่อค้าเเม่ค้า";
					next;
					mes "[แม่มะลิ]";
					mes "ฉันไม่แน่ใจว่าเขาจะขาย";
					mes "มะนาวให้ไหม";
					mes "แต่ลูกน่าจะลอง";
					mes "ขอเขาได้นะ";
					close;
				}
				emotion ET_ANGER;
				mes "[แม่มะลิ]";
				mes "โง่หรือเปล่า...!";
				mes "ถ้าคิดจะเลิกเพราะไม่ชอบรสเปรี้ยว งั้นก็ไม่ต้องมาสนใจ ต้มยำกุ้ง ของฉัน!";
				next;
				mes "[แม่มะลิ]";
				mes "ถ้าได้ลองต้มยำกุ้งของฉัน";
				mes "เธอตะเสียใจ";
				mes "กับสิ่งที่ได้พูดไป!";
				close;
			}
			mes "[แม่มะลิ]";
			mes "ฮะ...?";
			mes "นี่ลูกทำอะไรกับกุ้ง";
			mes "หรือเปล่า?";
			mes "ลูกไม่ได้กินมันหมด";
			mes "ระหว่างทางมาที่นี่ใช่ไหม?";
			close;
		}
		mes "[แม่มะลิ]";
		mes "เพื่อให้แน่ใจ ไปถาม";
		mes "ทองพูน เพื่อให้เขา";
		mes "ให้กุ้งมา 20 ตัว";
		mes "รีบเลย~";
		close;
	} else if (tomyumgoong > 3 && tomyumgoong < 6) {
		if (tomyumgoong == 4 && countitem(568) > 9) {
			mes "[แม่มะลิ]";
			mes "คิดว่านี่";
			mes "เป็นมะนาวที่ดีที่สุดเหรอ?";
			mes "ลองดูให้ดีๆซิ!";
			next;
			mes "[แม่มะลิ]";
			mes "ผิวของมันไม่มีความมันเงา";
			mes "ไม่มีกลิ่นหอม";
			mes "นี่มันก็แค่มะนาวธรรมดา!";
			close;
		} else if (tomyumgoong == 5) {
			if (countitem(568) > 9) {
				emotion ET_SURPRISE;
				mes "[แม่มะลิ]";
				mes "ยินดีต้อนรับกลับ!";
				mes "ว้าว กลับมาเร็วนะเนี่ย";
				mes "ไหน ขอตรวจดูมะนาว";
				mes "ที่เอามาหน่อย?";
				next;
				mes "[แม่มะลิ]";
				mes "ว้าว สุดยอด!";
				mes "เนี่ยแหละ มะนาวสดของจริง!";
				mes "แล้วลูกไปเอามาเองยังไงเนี่ย? แต่ยังไงก็ขอบคุณนะจ๊ะ~";
				next;
				mes "[แม่มะลิ]";
				mes "ต่อไปลูกก็ไปซื้อ";
				mes "^CE0000พริก^000000 แล้วก็";
				mes "วัคถุดิบอื่นๆในตลาดนิดหน่อย";
				next;
				mes "[แม่มะลิ]";
				mes "ลูกลองไปคุย";
				mes "กับพ่อค้าในตลาดดู";
				mes "พวกเขารู้";
				mes "ว่าลูกต้องการอะไร";
				next;
				if (select("โอเค ไว้จะกลับมาใหม่:เหนื่อยเเล้วอ่ะ ขอพักก่อน...") == 1) {
					delitem 568,10; //Lemon
					set tomyumgoong,6;
					changequest 8125,8126;
					mes "[แม่มะลิ]";
					mes "แล้วพบกันใหม่~";
					close;
				}
				mes "[แม่มะลิ]";
				mes "ก็แล้วเเต่";
				mes "แต่จำไว้นะ...";
				mes "กุ้งอาจจะเสีย";
				mes "ในตอนที่ลูกพักผ่อนก็ได้";
				close;
			}
			mes "[แม่มะลิ]";
			mes "ฮะ...?";
			mes "นี่ลูกทำอะไรกับมะนาว";
			mes "หรือเปล่าเนี่ย?";
			mes "ลูกไม่ได้กินมันหมด";
			mes "ระหว่างทางมาที่นี่ใช่ไหม?";
			close;
		}
		mes "[แม่มะลิ]";
		mes "เอาหล่ะ...";
		mes "ฉันหวังว่า";
		mes "ลูกจะได้มะนาว";
		mes "ที่ดีที่สุดจากเจ้าของต้นไม้นะ";
		next;
		mes "[แม่มะลิ]";
		mes "ยังไงซะ";
		mes "ลูกก็ต้องเอามะนาว";
		mes "ที่ดีที่สุดมาให้ฉัน";
		mes "เพื่อทำสุดยอด ต้มยำกุ้ง!";
		close;
	} else if (tomyumgoong == 6) {
		if (countitem(7286) > 29) {
			mes "[แม่มะลิ]";
			mes "ฉันไม่เข้าใจ...";
			mes "ลูกได้ส่วนผสมที่ไม่ดีเหล่านี้มาจากไหน!";
			mes "ฉันไม่ได้บอกเหรอว่า";
			mes "ต้มยำกุ้งของฉัน";
			mes "ต้องใช้ของที่ดีที่สุด?!";
			close;
		}
		mes "[แม่มะลิ]";
		mes "ฉันรู้ว่าลูกยุ่งมากในการทำสิ่งที่ฉันชอบ ฉันหวังว่าลูกจะพยายามอย่างเต็มที่ ที่จะทำสิ่งนี้ให้เสร็จนะ";
		next;
		mes "[แม่มะลิ]";
		mes "ลองไปดูที่ตลาด น่าจะดี หลังจากนั้น ต้มยำกุ้ง อันสุดวิเศษของฉัน จะรออยู่นะ!";
		close;
	} else if (tomyumgoong == 7) {
		if (MaxWeight - Weight > 1499) {
			if (countitem(7286) > 29) {
				delitem 7286,30; //Chilli
				emotion ET_SURPRISE;
				mes "[แม่มะลิ]";
				mes "ตอนนี้...";
				mes "ทุกอย่างก็พร้อมเเล้ว!";
				mes "ก่อนเริ่ม ฉันขอตรวจ";
				mes "วัตถุดิบก่อนนะ";
				next;
				mes "[แม่มะลิ]";
				mes "^CE0000กุ้ง^000000, ^CE0000พริก^000000, ^CE0000มะนาว^000000...";
				mes "น้ำปลา กับอะไรอีกนิดหน่อย...";
				next;
				mes "[แม่มะลิ]";
				mes "สุดยอด!";
				mes "เริ่มกันเลย";
				next;
				mes "^FF0000*ตุบ*^000000";
				mes "^9C009C*ตับ*^000000";
				mes "^009C00*ปัก*^000000";
				specialeffect EF_ENCHANTPOISON;
				next;
				mes "^FF0031*สับ สับ*^000000";
				mes "^FF9C00*ตับ ตับ ตับ*^000000";
				mes "^639CFF*ปุ ปุ*^000000 -";
				specialeffect EF_VENOMDUST;
				next;
				mes "[แม่มะลิ]";
				mes "- ^9C6300ตับ ตับ^000000 ^006363เฉาะ เฉาะ^000000";
				mes "^316363สับ สับ^000000";
				mes "^FF0000ตับ ตับ ตับ^000000 ^009C00ปุ ปุ^000000 -";
				specialeffect EF_STONECURSE;
				specialeffect EF_HASTEUP;
				specialeffect EF_BEGINSPELL;
				next;
				mes "[แม่มะลิ]";
				mes "ว้าาา^00009Cย้าา^0000FFย้าาาา^0063FFย้าาาาาาา^000000!";
				emotion ET_HUK;
				specialeffect EF_FLASHER;
				specialeffect EF_LORD;
				next;
				mes "[แม่มะลิ]";
				mes " ...";
				mes " ......";
				mes " .........";
				next;
				set tomyumgoong,8;
				completequest 8126;
				getitem 566,10; //Tomyumkung
				emotion ET_BEST;
				mes "[แม่มะลิ]";
				mes "นี่จ๊ะ";
				mes "ต้มยำกุ้ง!";
				mes "อ่า~ รสชาตินี้";
				mes "กลิ่นนี้... ใช่เลย";
				mes "ฝึมือฉันยังไม่ตกจริงๆ";
				next;
				mes "[แม่มะลิ]";
				mes "ลูกรู้หรือไม่ความลับของต้มยำกุ้งคืออะไร? มันไม่ใช่แค่อาหาร แต่มันมีพลังในการรักษา และช่วยให้ลูกมีรูปร่างที่ดีขึ้นด้วยนะ";
				next;
				mes "[แม่มะลิ]";
				mes "เพราะ...";
				mes "มันมีพลัง";
				mes "ของพระเจ้า";
				next;
				mes "[แม่มะลิ]";
				mes "เอาหล่ะ...!";
				mes "ฉันจะให้";
				mes "ต้มยำกุ้ง 10 ชาม";
				mes "ฟรีไปเลย~";
				next;
				mes "[แม่มะลิ]";
				mes "ขอบคุณที่ไปหาวัตถุดิบมาให้นะจ๊ะ ฉันจะมีความสุขมากเลยถ้าลูกแบ่ง ต้มยำกุ้ง ให้เพื่อนๆได้ลองชิม";
				next;
				mes "[แม่มะลิ]";
				mes "แบ่งปันรสชาติของอโยธยา และให้เขารู้ถึงความสวยงามของหมู่บ้านนี้ ดีใจที่ได้เจอกันนะลูก ดูแลตัวเองดีๆด้วยหล่ะ~";
				close;
			}
			mes "[แม่มะลิ]";
			mes "ฮะ? ลูกทำอะไรกับพริกเนี่ย ลูกไม่ได้กินมันหมดระหว่างทางมาที่นี่ใช่ไหม?";
			close;
		}
		mes "[แม่มะลิ]";
		mes "อืมม...";
		mes "ลูกดูเหมือนจะแบกของมากเกินไปนะ ทำไมไม่ลองไปเก็บของก่อนหล่ะ แล้วค่อยกลับมา?";
		close;
	}
	emotion ET_KEK;
	mes "[แม่มะลิ]";
	mes "อ่า...!";
	mes "ยุ่งมากเลย ยุ่งมากเลย!";
	mes "ลูกค้าของฉันไม่เคยเบื่อกับรสชาติ";
	mes "ต้มยำกุ้งของฉันเลย!";
	mes "อิ อิ อิ~!";
	close;
}

ayothaya,153,86,5	script	ทองพูน#ayo	843,{
	if ((MaxWeight-Weight) < 2000 || checkweight(1201,1) == 0) {
		mes "- สักครู่นะ! -";
		mes "- ท่านแบกของ -";
		mes "- ไว้กับตัวเองเยอะมาก -";
		mes "- ไปเก็บของเเล้ว -";
		mes "- มาลองใหม่อีกทีนะ -";
		close;
	}
	if (tomyumgoong == 2) {
		mes "[ทองพูน]";
		mes "เชิญเข้ามาท่านนักผจญภัย";
		mes "ดูนี่ซะ! ข้ามี";
		mes "กุ้งเยอะเเยะเลย!";
		next;
		mes "[ทองพูน]";
		mes "ท่านมองหากุ้งอยู่งั้นรึ";
		mes "เอาเท่าไหร่หล่ะ";
		mes "ยี่สิบ? สี่สิบ?";
		next;
		if (select("ข้าจะเอากุ้งไปทำต้มยำกุ้งหน่ะ:เอ่อ ทำไมมันดูแพงจัง") == 1) {
			mes "[ทองพูน]";
			mes "อ่า! ข้าเดาว่า แม่มะลิแนะนำข้าให้ท่านรู้จักสินะ";
			mes "งั้นข้าว่าท่านต้องซื้อเยอะแน่เลย?";
			next;
			mes "[ทองพูน]";
			mes "แต่สุดท้าย";
			mes "ก็ต้องใช้กุ้งเยอะมาก";
			mes "ในการที่จะทำ";
			mes "ต้มยำกุ้ง";
			next;
			mes "[ทองพูน]";
			mes "โอ้ ใช่ ท่านจะต้องใช้กุ้ง 20 ตัว ในการทำต้มยำกุ้งของแม่มะลิ";
			mes "ข้าให้ส่วนลดกับท่านด้วย:";
			mes "กุ้ง 20 ตัว ราคาแค่ 11,000 zeny";
			mes "ท่านว่ายังไง?";
			next;
			if (select("จัดไป:ข้าเอาไปซื้อ Jellopy ได้เยอะเลยนะนั้น") == 1) {
				if (Zeny > 10999) {
					if (MaxWeight - Weight > 3999) {
						set Zeny, Zeny-11000;
						set tomyumgoong,3;
						getitem 567,20; //Prawn
						emotion ET_BEST;
						mes "[ทองพูน]";
						mes "เยี่ยม ตอนนี้ท่านมีกุ้งที่สด";
						mes "และใหม่ที่สุดในหมู่บ้านเเล้ว! ถึงเวลาที่ท่านต้องกลับไปขอให้ แม่มะลิทำอาหารให้แล้ว";
						close;
					}
					mes "[ทองพูน]";
					mes "อ่ออว...";
					mes "ท่านถือของเยอะเกินไป ข้าคิดว่าท่านไม่น่าจะยกอะไรไหวอีกแล้วนะ";
					close;
				}
				mes "[ทองพูน]";
				mes "อ่ออว...";
				mes "ท่านมีเงินไม่พอ";
				close;
			}
			mes "[ทองพูน]";
			mes "เยี่ยม เยี่ยม เยี่ยม...";
			mes "เจ้าอาจจะอิ่มได้ด้วย Jellopy แต่มันไม่ได้อร่อยเท่ากุ้งของข้าหรอก!";
			close;
		}
		mes "[ทองพูน]";
		mes "อะไรนะ...?";
		mes "เจ้าบอกว่า";
		mes "มันแพงงั้นเหรอ?";
		mes "แต่แลกกับรสชาติที่เเสนหายากจาก";
		mes "ต้มยำกุ้ง";
		mes "มันก็คุ้มค่านะ!";
		close;
	} else if (tomyumgoong == 3) {
		mes "[ทองพูน]";
		mes "อืมม...?";
		mes "อยากได้กุ้งเพิ่มงั้นเหรอ?";
		mes "เเล้วจะซื้อเพิ่ม";
		mes "ไหมหล่ะ?";
		next;
		if (select("ซื้อ!:ไม่หล่ะ") == 1) {
			if (Zeny > 10999) {
				if (MaxWeight - Weight > 3999) {
					set Zeny, Zeny-11000;
					getitem 567,20; //Prawn
					mes "[ทองพูน]";
					mes "ไปเลย~";
					mes "กุ้งที่สดที่สุดในหมู่บ้านนี้ บางทีอาจสดที่สุดในโลกก็ได้!";
					close;
				}
				mes "[ทองพูน]";
				mes "อ่ออว...";
				mes "ท่านถือของเยอะเกินไป ข้าคิดว่าท่านไม่น่าจะยกอะไรไหวอีกแล้วนะ";
				close;
			}
			mes "[ทองพูน]";
			mes "อ่ออว...";
			mes "ท่านมีเงินไม่พอ";
			close;
		}
		mes "[ทองพูน]";
		mes "เยี่ยม ตอนนี้ท่านมีกุ้งที่สด";
		mes "และใหม่ที่สุดในหมู่บ้านเเล้ว! ถึงเวลาที่ท่านต้องกลับไปขอให้ แม่มะลิทำอาหารให้แล้ว";;
		close;
	}
	mes "[ทองพูน]";
	mes "ยินดีต้อนรับ ยินดีต้อนรับ!";
	mes "ข้ามีกุ้งที่ดีที่สุดที่มาจากน่านน้ำที่สะอาดที่สุดในโลก~!";
	close;
}

ayothaya,121,240,7	script	ตาจุ่น#ayo	842,1,1,{
	if (MaxWeight - Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- สักครู่นะ! -";
		mes "- เจ้าแบกของ -";
		mes "- ไว้กับตัวเองเยอะมาก -";
		mes "- ไปเก็บของเเล้ว -";
		mes "- มาลองใหม่อีกทีนะ -";
		close;
	}
	if (tomyumgoong == 5) {
		emotion ET_SWEAT;
		mes "[ตาจุ่น]";
		mes "อืมม...?";
		mes "เจ้าไม่อยาก";
		mes "ได้มะนาวเพิ่ม";
		mes "งั้นเหรอ?";
		next;
		switch(select("ไม่:เอามะนาวมาซะ ตาแก่!:ฉันขอมะนาวเพิ่มได้ไหม")) {
		case 1:
			emotion ET_DELIGHT;
			mes "[ตาจุ่น]";
			mes "ถ้าอย่างนั้นทำไมเจ้าไม่ลองชิมมะนาวที่ข้าให้เจ้ารอบล่าสุดดูล่ะ?";
			mes "มันเปรี้ยวจริง แต่ก็อร่อยมากเลยนะ!";
			close;
		case 2:
			emotion ET_FRET;
			mes "[ตาจุ่น]";
			mes "อะไร...!?";
			mes "เกิดอะไรขึ้นกับกับเจ้าหล่ะ เจ้าหนู?";
			mes "เจ้าทำอะไรกับมะนาว";
			mes "ที่ข้าให้ครั้งล่าสุดไปหล่ะ?";
			close;
		case 3:
			mes "[ตาจุ่น]";
			mes "เอ่อ ต้องขอโทษด้วย....คลังเก็บมะนาวของข้า";
			mes "ถูกพวก ^3131FF'Leaf Cats'^000000 ที่อยู่เเถวศาลเจ้าขโมยไป";
			next;
			mes "[ตาจุ่น]";
			mes "ข้าหวังว่าข้าจะช่วยเจ้าได้นะ";
			mes "แต่ข้าไม่ได้มีมะนาวมากพอที่จะเปิดธุรกิจของตัวเองหรอก";
			mes "ถ้าอยากได้มะนาว ทำไมไม่ลองไปจัดการพวกมันดูหล่ะ?";
			close;
		}
	}
	mes "[ตาจุ่น]";
	mes "...";
	close;

OnTouch_:
	if (MaxWeight - Weight < 2000 || checkweight(1201,1) == 0) {
		mes "- สักครู่นะ! -";
		mes "- เจ้าแบกของ -";
		mes "- ไว้กับตัวเองเยอะมาก -";
		mes "- ไปเก็บของเเล้ว -";
		mes "- มาลองใหม่อีกทีนะ -";
		close;
	}
	if (tomyumgoong == 4) {
		mes "[ตาจุ่น]";
		mes "สวัสดี";
		mes "มีอะไรให้ช่วย";
		mes "ไหม...?";
		emotion ET_SCRATCH;
		next;
		if (select("อยากได้มะนาว:ไม่หล่ะ ขอบคุณ") == 1) {
			mes "[ตาจุ่น]";
			mes "อืมม...";
			mes "มะนาวงั้นเหรอ?";
			mes "อย่างที่เห็น ข้ามีมะนาว";
			mes "มากมายอยู่บนต้น";
			mes "อยากได้จริงๆใช่ไหม?";
			next;
			if (select("ใช่ เดี๋ยวจ่ายเงินให้:ไม่หล่ะ ขอบคุณ") == 1) {
				mes "[ตาจุ่น]";
				mes "ข้าเริ่มปลูกต้นมะนาวเป็นงานอดิเรก";
				mes "แต่ข้าก็ไม่ได้หวังว่าจะได้มะนาวที่หวานอะไรขนาดนั้นหรอก";
				next;
				mes "[ตาจุ่น]";
				mes "แล้วเมื่อกี้ เจ้าบอกว่า";
				mes "จะซื้อมะนาวจากข้างั้นเหรอ?";
				mes "เจ้าเข้าใจผิดแล้วหล่ะข้าไม่ได้ปลูกไว้ขาย";
				next;
				mes "[ตาจุ่น]";
				mes "แต่...";
				mes "ข้าก็ยินดีที่จะ";
				mes "แบ่งมะนาวบางส่วน";
				mes "ให้ได้นะ ถ้าเจ้าสามารถทำได้...";
				next;
				mes "[ตาจุ่น]";
				mes "แค่เล่นเกมเล็กๆน้อยๆกับข้า!";
				mes "ทำไมหน่ะเหรอ? ก็ข้าเบื่อจะตาย ชายแก่อย่างข้าก็ต้องมีความสุขกับตัวเองบ้าง!";
				next;
				mes "[ตาจุ่น]";
				mes "ฮ่า ฮ่า ฮ่า~!";
				mes "เจ้าว่าไงนะ?";
				mes "อยากจะลองไหมหล่ะ?";
				next;
				if (select("แน่นอน!:ไม่หล่ะ ขอบคุณ") == 1) {
					mes "[ตาจุ่น]";
					mes "ดีมาก!";
					mes "เกมที่เรา";
					mes "จะเล่นกนนั้นง่ายมาก";
					mes "มันก็แค่เกมเด็กๆ";
					next;
					mes "[ตาจุ่น]";
					mes "^660000ค้อน";
					mes "กระดาษ กรรไกร!^000000";
					mes "บางทีเจ้าอาจเคยได้ยิน";
					mes "เรื่องนี้ในดินแดนของเจ้า";
					emotion ET_SCISSOR;
					next;
					mes "[ตาจุ่น]";
					mes "ไม่ต้องมองข้าอย่างนั้น ข้าเป็นชายแก่ และข้าก็แค่อยากเล่นเกมนี้";
					mes "เหมือนตอนเป็นเด็ก เจ้าจะเข้าใจเมื่อเจ้าอายุเท่าข้า";
					next;
					mes "[ตาจุ่น]";
					mes "เอาหล่ะ ถ้าเจ้าชนะ";
					mes "^3131FF3^000000 ใน ^3131FF5^000000 ตา";
					mes "เจ้าก็จะได้มะนาวไป";
					mes "พร้อมหรือยัง?";
					next;
					mes "[ตาจุ่น]";
					mes "โอเค...";
					mes "พูดเหมือนตอนเด็กๆด้วยนะ";
					mes "^990000เริ่มได้เล๊ยย!^000000";
					next;
					set @user_score,0;
					set @pc_score,0;
					mes "[ตาจุ่น]";
					mes "เป่า!";
					mes "ยิง!";
					mes "ฉุบ!";
					next;
					while(1) {
						if (@pc_score == 5 || @user_score == 3) {
							break;
						}
						else {
							set .@jun_mark,rand(1,3);
							switch(select("กรรไกร:ค้อน:กระดาษ")) {
							case 1:
								if (.@jun_mark == 1) {
									emotion ET_SCISSOR;
									emotion ET_SCISSOR, playerattached();
									mes "[ตาจุ่น]";
									mes "เห้อ";
									mes "เสมอกัน";
									mes "เอาใหม่ๆ!";
									mes "เป่า! ยิง!";
									mes "ฉุบ!";
									next;
								}
								else if (.@jun_mark == 2) {
									emotion ET_ROCK;
									emotion ET_SCISSOR, playerattached();
									set @pc_score,@pc_score + 1;
									mes "[ตาจุ่น]";
									mes "ใช่เลย...!";
									mes "มันต้องอย่างนี้เซ่!";
									next;
								}
								else {
									emotion ET_WRAP;
									emotion ET_SCISSOR, playerattached();
									set @pc_score,@pc_score + 1;
									set @user_score,@user_score + 1;
									mes "[ตาจุ่น]";
									mes "อะไรนะ...?";
									mes "ไม่อยากเชื่อเลย!";
									mes "โถ่.. !";
									next;
								}
								break;
							case 2:
								if (.@jun_mark == 1) {
									emotion ET_SCISSOR;
									emotion ET_ROCK, playerattached();
									set @pc_score,@pc_score + 1;
									set @user_score,@user_score + 1;
									mes "[ตาจุ่น]";
									mes "ไม่...!";
									mes "เกินไปแล้วนะๆ!";
									next;
								}
								else if (.@jun_mark == 2) {
									emotion ET_ROCK;
									emotion ET_ROCK, playerattached();
									mes "[ตาจุ่น]";
									mes "เห้อ";
									mes "เสมอกัน";
									mes "เอาใหม่ๆ!";
									mes "เป่า! ยิง!";
									mes "ฉุบ!";
									next;
								}
								else {
									emotion ET_WRAP;
									emotion ET_ROCK, playerattached();
									set @pc_score,@pc_score + 1;
									mes "[ตาจุ่น]";
									mes "ฮ่า ฮ่า ฮ่า!";
									mes "เจ้าแพ้ เด็กน้อย!";
									next;
								}
								break;
							case 3:
								if (.@jun_mark == 1) {
									emotion ET_SCISSOR;
									emotion ET_WRAP, playerattached();
									set @pc_score,@pc_score + 1;
									mes "[ตาจุ่น]";
									mes "ฮิ ฮิ~";
									mes "เด็กโง่!";
									next;
								}
								else if (.@jun_mark == 2) {
									emotion ET_ROCK;
									emotion ET_WRAP, playerattached();
									set @user_score,@user_score + 1;
									set @pc_score,@pc_score + 1;
									mes "[ตาจุ่น]";
									mes "ม่ายยยย...!";
									mes "เป็นไปไม่ได้!";
									next;
								}
								else {
									emotion ET_WRAP;
									emotion ET_WRAP, playerattached();
									mes "[ตาจุ่น]";
									mes "เห้อ";
									mes "เสมอกัน";
									mes "เอาใหม่ๆ!";
									mes "เป่า! ยิง!";
									mes "ฉุบ!";
									next;
								}
								break;
							}
						}
					}
					if (@user_score == 3) {
						emotion ET_HNG;
						mes "[ตาจุ่น]";
						mes "เจ้า... ชนะ";
						mes "ข้าเหนื่อยมาก";
						mes "ข้าว่า ข้าคงแข่งกับ";
						mes "พวกเด็กๆไม่ได้อีกต่อไปแน่";
						mes "ฮ่า ฮ่า ฮ่า!";
						next;
						mes "[ตาจุ่น]";
						mes "แต่ก็ตามสัญญา";
						mes "ข้าจะแบ่งมะนาว";
						mes "ส่วนหนึ่งของข้าให้";
						mes "อยากได้เท่าไหร่ก็หยิบไปเลย!";
						mes "^3355FFคุณเด็ดมะนาวมา";
						mes "10 ลูกจากต้นไม้^000000";
						set tomyumgoong,5;
						getitem 568,10; //Lemon
						next;
						emotion ET_THINK;
						mes "[ตาจุ่น]";
						mes "ฮืมม...";
						mes "ดูเหมือนจะใช้เยอะเหมือนกันนะเนี่ย แต่ดีเเล้ว ขอให้มีความสุขก็แล้วกัน";
						next;
						mes "[ตาจุ่น]";
						mes "แล้วก็อย่าลืม:";
						mes "อย่าเห็นแก่ตัว และแบ่งปันสิ่งที่เจ้ามีกับคนอื่นเสมอ นั่นเป็นหนึ่งในค่านิยมที่สำคัญที่สุดสำหรับมนุษย์";
						close;
					}
					emotion ET_SCISSOR;
					mes "[ตาจุ่น]";
					mes "ว่ะฮ่าฮ่าฮ่าฮ่า!";
					mes "ชัยชนะเป็นของข้า!";
					mes "เหมือนเจ้าจะไม่ได้มะนาวเลยหนิ";
					mes "แต่ถ้าเเน่จริงก็มาลองใหม่!";
					close;
				}
				mes "[ตาจุ่น]";
				mes "กลัวพวกเราไหม?";
				mes "ข้าหลอกเล่นๆ:";
				mes "ข้าเป็นหนึ่งในเกมที่ดีที่สุด!";
				close;
			}
			mes "[ตาจุ่น]";
			mes "ฮืมม...";
			mes "ตอนนี้ข้าสามารถทำอะไร";
			mes "กับมะนาวเหล่านี้";
			mes "ทั้งหมดได้บ้าง?";
			close;
		}
		mes "[ตาจุ่น]";
		mes "ทำตัวตามสบาย";
		mes "จะมาถามข้าเมื่อไหร่ก็ได้ โอเค๊?";
		close;
	}
	mes "[ตาจุ่น]";
	mes "...";
	close;
}

ayothaya,213,94,7	script	พ่อค้า#ayo	841,{
	if (tomyumgoong == 6) {
		mes "[พ่อค้าทองดำ]";
		mes "ว่าไง~";
		mes "เจ้ากำลังมองหา";
		mes "วัตถุดิบที่จะทำ";
		mes "ต้มยำกุ้งใช่ไหม?";
		next;
		mes "[พ่อค้าทองดำ]";
		mes "ไหนดูซิ...";
		mes "ข้าขายพริก";
		mes "น้ำปลา และพวกเครื่องปรุง";
		mes "กับพวกเครื่องเทศบางอย่าง";
		mes "อยากได้ไหมหล่ะ";
		mes "ข้าขายทั้งหมดแค่ 2,000 zeny เอง";
		next;
		if (select("ขอบคุณมาก ข้าเอาทั้งหมดเลย:นี่มันโกงกันหนิ!") == 1) {
			emotion ET_THANKS;
			if (Zeny > 1999) {
				if (MaxWeight - Weight > 1199) {
					set Zeny, Zeny-2000;
					set tomyumgoong,7;
					getitem 7286,30; //Chilli
					mes "[พ่อค้าทองดำ]";
					mes "ขอบคุณมาก";
					mes "หวังว่าเจ้าจะสนุกกับ";
					mes "ต้มยำกุ้งของเจ้านะ~";
					close;
				}
				mes "[พ่อค้าทองดำ]";
				mes "เอ่อ โอ้...";
				mes "ดูเหมือนว่า";
				mes "เจ้าจะแบกของ";
				mes "มากเกินไปนะ";
				next;
				mes "[พ่อค้าทองดำ]";
				mes "ข้าไม่สามารถ";
				mes "ให้อะไรเจ้าได้ ถ้าเจ้า";
				mes "ไม่มีช่องพอให้เก็บของ!";
				close;
			}
			mes "[พ่อค้าทองดำ]";
			mes "ฮืมม...?";
			mes "ตอนนี้เจ้าไม่มีเงิน";
			mes "พอที่จะซื้อของ";
			mes "จากข้าหนิ";
			close;
		}
		emotion ET_STARE_ABOUT;
		mes "[พ่อค้าทองดำ]";
		mes "อย่าพูดแบบนั้น";
		mes "ราคาของข้าสมเหตุสมผลเสมอ";
		mes "และรู้ไว้ด้วย ว่าข้าก็มีเกียรติ";
		mes "และก็มีชื่อเสียงด้วย";
		close;
	} else if (tomyumgoong == 7) {
		mes "[พ่อค้าทองดำ]";
		mes "ข้าว่าข้าให้วัตถุดิบเพียงพอแล้วนะ";
		mes "แต่ตอนนี้ข้าคิดว่า";
		mes "ข้าไม่รู้ว่าเจ้า";
		mes "จะทำต้มยำกุ้งเท่าไหร่";
		next;
		mes "[พ่อค้าทองดำ]";
		mes "เเล้วเจ้าสนใจ";
		mes "จะซื้อพริกอีกไหมหล่ะ?";
		next;
		if (select("ไม่หล่ะ ขอบคุณ:ได้เลย เอาอีก") == 1) {
			mes "[พ่อค้าทองดำ]";
			mes "ก็ดี๊~";
			close;
		}
		if (Zeny > 1999) {
			if (MaxWeight - Weight > 1199) {
				set Zeny, Zeny-2000;
				getitem 7286,30; //Chilli
				mes "[พ่อค้าทองดำ]";
				mes "ขอบใจมาก";
				mes "ไว้มาอีกนะ!";
				close;
			}
			mes "[พ่อค้าทองดำ]";
			mes "เอ่อ โอ้...";
			mes "ดูเหมือนว่า";
			mes "เจ้าจะแบกของ";
			mes "มากเกินไปนะ";
			next;
			mes "[พ่อค้าทองดำ]";
			mes "ข้าไม่สามารถ";
			mes "ให้อะไรเจ้าได้ ถ้าเจ้า";
			mes "ไม่มีช่องพอให้เก็บของ!";
			close;
		}
		mes "[พ่อค้าทองดำ]";
		mes "ฮืมม...?";
		mes "ตอนนี้เจ้าไม่มีเงิน";
		mes "พอที่จะซื้อของ";
		mes "จากข้าหนิ";
		close;
	}
	mes "[พ่อค้าทองดำ]";
	mes "ว่าไง!";
	mes "เคยลองกินพริกมาก่อนไหม?";
	next;
	mes "[พ่อค้าทองดำ]";
	mes "ถึงมันจะเล็ก แต่ก็มีรสชาติที่สุดยอดมาก";
	mes "นอกจากนี้ยังร้อนและเผ็ดมาก ดังนั้นเจ้าควรระวังถ้าจะกินมัน";
	next;
	mes "[พ่อค้าทองดำ]";
	mes "ผู้คนในอโยธยามีความสุขไปกับอาหารที่น่าลิ้มลอง ด้วยรสชาติที่แตกต่างอีกทั้งยังเผ็ด อาหารในอโยธยาเราใช้เครื่องเทศต่างๆ เช่น พริก กระเทียม ขิง และผักชี";
	next;
	mes "[พ่อค้าทองดำ]";
	mes "ข้าขายพริก";
	mes "ดังนั้นถ้าเจ้าต้องการพริก";
	mes "ก็มาหาข้านะ~";
	close;
}