//===== rAthena Script ======================================= 
//= Quest NPCs related to Payon
//===== By:================================================== 
//= kobra_k88
//===== Current Version:===================================== 
//= 1.7
//===== Compatible With:===================================== 
//= rAthena Project; 7.15 +
//===== Description:========================================= 
//= Pretend Murder, Ear Muffs, Oxygen, Derivuchi Cap, Helm of Angel Quests.
//===== Additional Comments:================================= 
//= 1.1 Helm of Angel bug fixed [Lupus]
//= 1.2 Fixed skirt of virgin [Lupus]
//= 1.3 Fixed exploits [Lupus]
//= 1.4 Removed Duplicates [Silent]
//= 1.5 Updated Dialogs from "แกล้งสังหารเควส" [Samuray22]
//= 1.5a replaced item "ชื่อ" with item id. [Lupus]
//= 1.6 Updated Dialogs from "เควสหน้ากากออกซิเจน" [Samuray22]
//= 1.7 Updated headgear quests. [L0ne_W0lf]
//= 1.8 Updating NPC Position. [JohnnyPlayy]
//============================================================ 

// 'Pretend Murdered' Quest
//============================================================
payon_in01,99,72,4	script	Granny	103,{
	if (checkweight(908,1) == 0) {
		mes "^3355FF รอสักครู่! ตอนนี้คุณถือสิ่งของจำนวนมากเกินไป โปรดกลับมาหลังจากใส่ของบางอย่างลงใน Kafra Storage แล้ว ^000000";
		close;
	}
	if (countitem(1049) > 3) {
		mes "[ยาย]";
		mes "ฉันหวังว่าฉันจะทำเสื้อผ้าให้หลาน ๆ สำหรับเทศกาล ...";
		next;
		mes "[ยาย]";
		mes "โอ้! คุณช่วยให้ ^3355FFSkirt of Virgin ^000000 ของคุณกับฉันได้ไหม ฉันต้องการสี่คน ได้โปรดฉันขอร้องคุณ";
		next;
		switch(select("เอาล่ะ:ไม่มีทาง!")) {
		case 1:
			delitem 1049,4; //Skirt_Of_Virgin
			mes "[ยาย]";
			mes "โอ้! ขอบคุณมาก ~ ตอนนี้ฉันสามารถทำเสื้อผ้าให้กับ Kitty Cutty ได้แล้ว ~";
			next;
			mes "[ยาย]";
			mes "อ๊ะเดี๋ยวก่อน! ฉันเกือบลืม. ฉันเคยเป็นนักแสดงหญิงยอดนิยมในวัยเยาว์ เพื่อนนักแสดงของฉันมอบของขวัญแต่งงานให้ฉันเมื่อฉันออกจาก Troupe ฉันเก็บมันไว้เพื่อเตือนฉันถึงช่วงเวลาอันมีค่าเหล่านั้น";
			next;
			mes "[ยาย]";
			mes "ถ้าเป็นไร ...";
			mes "ฉันต้องการให้สิ่งนี้แก่คุณ";
			next;
			getitem 2293,1; //Pretend_Murdered
			mes "[ยาย]";
			mes "ลาก่อนนักผจญภัย ~!";
			mes "ขอบคุณที่ทำให้หญิงชรามีความสุข ...";
			close;
		case 2:
			mes "[ยาย]";
			mes "* เฮ้อ * จะได้ ^3355FFSkirt of Virgin ^000000 ด้วยตัวเองได้ยังไง? ฉันอ่อนแอและอ่อนแอมากแม้แต่การกลิ้งตัวบนเก้าอี้โยกก็ทำให้ฉันหมดแรง * ดม * ทั้งหมดอยากให้หลานมีความสุข ...";
			close;
		}
	}
	else {
		mes "[ยาย]";
		mes "ฉันเก็บเห็ดบนภูเขา Payon ทุกวัน เวลาผ่านไปและก่อนที่ฉันจะรู้ว่ามันเป็นฤดูแห่งความสุขแล้ว ฉันหวังว่าฉันจะทำเสื้อผ้าสำหรับครอบครัวของฉันในช่วงเวลานี้ของปี";
		next;
		mes "[ยาย]";
		mes "...";
		mes "แต่สำหรับสิ่งนั้นฉันต้องการ ^3355FF4 Skirt of Virgin^000000 ...";
		next;
		mes "[ยาย]";
		mes "นักเดินทางหนุ่มโปรดช่วยฉันด้วย เมื่อคุณพบ ^3355FFSkirt of Virgin ^000000 โปรดนำมาให้ฉัน ฉันจะขอบคุณจริงๆ ...";
		close;
	}
}

// 'Ear Muffs' Quest
//============================================================
payon_in01,18,10,4	script	Mystic Lady	76,{
	mes "[มิสติกเลดี้]";
	mes "ครอบครัวของฉันผลิตและจำหน่ายผลิตภัณฑ์พิเศษสำหรับฤดูหนาว ^3355FFEar Muffs ^000000 เป็นเวลาหลายปี เราเพิ่งย้ายมาอยู่ที่นี่ แต่อากาศอบอุ่นตลอดเวลาทำมาหากินแทบไม่ได้";
	next;
	mes "[มิสติกเลดี้]";
	mes "หากคุณวางแผนที่จะเดินทางไปยังภูมิภาคที่หนาวเย็นกว่านี้ฉันขอแนะนำให้คุณนำ ^3355FFEar Muffs ^000000";
	emotion ET_THINK;
	next;
	mes "[มิสติกเลดี้]";
	mes "ที่ครอบหูเป็นสิ่งพิเศษสำหรับครอบครัวของฉันและเรามอบให้กับลูกค้าที่มี ^FF33551 Cursed Ruby ^000000, ^3311AA1 Headset, ^000000 ^3355FF 200 Feathers ^000000 และ ^DDDD005000 Zeny ^000000";
	emotion ET_THINK;
	next;
	switch(select("โอ้ใช่? ฟังดูดี:ไม่ขอบคุณแหม่ม")) {
	case 1:
		if ((countitem(724) > 0) && (countitem(949) > 199) && (countitem(5001) > 0) && (Zeny > 4999)) {
			mes "[มิสติกเลดี้]";
			delitem 724,1; //Cardinal_Jewel_
			delitem 949,200; //Feather
			delitem 5001,1; //Headset
			set Zeny,Zeny-5000;
			mes "ที่นี่ฉันจะให้คุณที่สร้างไว้ล่วงหน้า";
			getitem 2283,1; //Ear_Mufs
			next;
			mes "[มิสติกเลดี้]";
			mes "ขอขอบคุณที่ซื้อผลิตภัณฑ์ของเรา คุณจะไม่เสียใจที่ซื้อที่ครอบหูของเรา:รับประกันความพึงพอใจ ~";
			emotion ET_THINK;
			close;
		}
		else {
			mes "[มิสติกเลดี้]";
			mes "โอ้ที่รัก ...";
			mes "คุณมีเงินหรือสิ่งของไม่เพียงพอ ขออภัยเราไม่สามารถให้ส่วนลดใด ๆ โปรดเข้าใจว่าเราต้องทำมาหากิน ...";
			emotion ET_THINK;
			close;
		}
	case 2:
		mes "[มิสติกเลดี้]";
		mes "ไม่เป็นไร.";
		mes "ฉันเชื่อว่าเรา";
		mes "จะได้พบกันใหม่";
		mes "... หนึ่งในวันนี้";
		emotion ET_THINK;
		close;
	}
}

// 'Oxygen Mask' Quest
//============================================================
payon_in02,25,71,4	script	Boy	86,{
	if (countitem(701) > 4) {
		mes "[หนุ่มน้อย]";
		mes "อร๊ายยยยยยยยยย!";
		mes "อาไม่ฉันไม่สามารถ ...";
		next;
		mes "[หนุ่มน้อย]";
		mes "* ฮัก *";
		mes "ถึงกับตาแทบคลั่ง !! ทำไมแมลงเหล่านี้ดูน่ารักจัง?! นู๋อู้ ~ !!!";
		next;
		switch(select("แสดง Ora Ora:ให้ Ora Ora:ยกเลิก")) {
		case 1:
			mes "[หนุ่มน้อย]";
			mes "โอววววว !!!";
			mes "คือนี่คือ";
			mes "คนเดียว?!";
			mes "นี่คือ...";
			mes "อรอร !!";
			next;
			mes "[หนุ่มน้อย]";
			mes "ฉันจะทำให้สั้น! ให้ฉันแล้วฉันจะให้สมบัติของฉัน! แล้วความคิดของฉันเป็นอย่างไร? ต้องการจัดการ?";
			close;
		case 2:
			if (countitem(701) > 4) {
				delitem 701,5; //Ora_Ora
			}
			mes "[หนุ่มน้อย]";
			mes "มึง !!!";
			mes "ในที่สุดฉันก็มีสิ่งนี้!";
			mes "อรอร!";
			mes "โง่และน่าขยะแขยง";
			mes "โจรโรคจิต !!";
			mes "คุณจะจ่าย !!!";
			next;
			mes "[หนุ่มน้อย]";
			mes "ฮัก ... ฮัก ...";
			mes "ขออภัยฉันไม่สามารถควบคุมได้ด้วยความยินดีอยู่พักหนึ่ง ตอนนี้ฉันจะให้สมบัติของฉันตามสัญญา";
			next;
			getitem 5004,1; //Oxygen_Mask
			mes "[หนุ่มน้อย]";
			mes "เมื่อคุณสวมสิ่งนี้ลงบนปากของคุณและเริ่มหายใจมันจะกรองขยะออกก่อนที่จะไปที่ปอดของคุณ";
			next;
			mes "[หนุ่มน้อย]";
			mes "คักคักคักคัก!";
			mes "รอไม่ไหวแล้ว";
			mes "เพื่อใช้สิ่งนี้";
			mes "อรอร!";
			close;
		case 3:
			mes "[หนุ่มน้อย]";
			mes "หุหุหุหุหุ ...";
			mes "จะเกิดอะไรขึ้นถ้าฉันไม่สามารถกำจัด Thief Bugs ทั้งหมดนี้ได้? ฉันอาจจะรักพวกเขาด้วยซ้ำ ... * ฮัก *";
			close;
		}
	}
	else {
		mes "[หนุ่มน้อย]";
		mes "ว้ากกก! ฉันฉัน ...";
		mes "ฉันไม่สามารถรับมันได้อีกต่อไป!";
		mes "เจ้าไอ้ตัวเล็กเหม็นสกปรก!";
		next;
		switch(select("ดำเนินการต่อ:ยกเลิก")) {
		case 1:
			mes "[หนุ่มน้อย]";
			mes "โอ้มนุษย์ ...";
			mes "ทุกอย่างเริ่มต้นเมื่อพ่อแม่ของฉันเสียชีวิตเมื่อฉันยังเป็นเด็กตัวเล็ก ๆ ก่อนหน้านี้ฉันต้องทำงานเพื่อหาเลี้ยงชีพและมีช่วงเวลาที่ยากลำบากมาก";
			next;
			mes "[หนุ่มน้อย]";
			mes "ฉันทำงานมา 10 ปีด้วยความหวังที่จะซื้อบ้านในฝันที่ที่ฉันจะได้พบกับความสงบและความสบายใจอีกครั้ง ในที่สุดฉันก็คิดว่าฉันมี zeny มากพอที่จะซื้อบ้านแสนหวานของตัวเองได้";
			next;
			mes "[หนุ่มน้อย]";
			mes "น่าเสียดายที่ฉันไม่มีเงินมากพอที่จะซื้อบ้านหลังงามที่ฉันสนใจ แต่แล้วฉันก็พบบ้านหลังนี้ซึ่งใหญ่กว่าและถูกกว่าหลังแรก ซื้อมาแบบไม่คิด ...";
			next;
			mes "[หนุ่มน้อย]";
			mes "พระเยซูหวาน! ปรากฎว่าเป็นสวรรค์ของจอมโจรบัก !!!";
			next;
			mes "[หนุ่มน้อย]";
			mes "ฉันพยายามทำทุกอย่างที่ทำได้ ฉันเกลี้ยกล่อมพวกเขาฆ่าพวกเขา แต่มันทำให้เกิดความสงบได้เพียงชั่วครู่เท่านั้น! ฉันพยายามจะเผาบ้านหลังนี้ด้วยซ้ำ ... !";
			next;
			mes "[หนุ่มน้อย]";
			mes "* อ้าปากค้าง * ฉันหมายความว่าจริงๆฉันต้องการ ^3355FF Ora Ora ^000000 เพื่อกำจัดพวกมันหรือไม่? ผู้คนกล่าวว่ามันไม่ค่อยเห็นบ่อยนักในประเทศไกล ๆ";
			next;
			mes "[หนุ่มน้อย]";
			mes "ถ้าฉันสามารถจ่ายได้ 5 ตัวฉันจะไม่ต้องหงุดหงิดกับข้อบกพร่องที่น่าขยะแขยงเหล่านี้อีกต่อไป ...";
			close;
		case 2:
			mes "[หนุ่มน้อย]";
			mes "การต่อสู้กับแมลงที่ไม่มีวันสิ้นสุดนี้ทำให้ฉันหมดแรงจริงๆ ฉันยอมแพ้. ทำตามที่คุณต้องการคุณขี้!";
			close;
		}
	}
}

// 'Helm of Angel' and 'Deviruchi Cap' Quest
//============================================================
-	script	Young man#12	89,{
	mes "[หนุ่มน้อย]";
	mes "...มันคืออะไร?";
	next;
	switch(select("คุณทำให้ฉันเป็นไอเท็มพิเศษได้ไหม:เฮ้")) {
	case 1:
		mes "[หนุ่มน้อย]";
		mes "ฮะ? คุณรู้แล้วว่าฉันเชี่ยวชาญเรื่องอะไรใช่มั้ย? ฉันคิดว่าไม่จำเป็นต้องเป็นความลับตามปกติ บอกสิ่งที่คุณต้องการ";
		next;
		switch(select("Helm of Angel.:Deviruchi Cap.:ฉันจะมาในครั้งต่อไป")) {
		case 1:
			mes "[หนุ่มน้อย]";
			mes "1 Helm (with slot).";
			mes "1 Angel Wing.";
			mes "5 Fang of Garm.";
			mes "นั่นคือทั้งหมดที่ฉันต้องการ";
			next;
			mes "[หนุ่มน้อย]";
			mes "รอ! ขอแจ้งให้ทราบว่าฉันไม่สนใจว่าสินค้าในคลังของคุณได้รับการอัปเกรดหรือมีการ์ดติดอยู่";
			next;
			mes "[หนุ่มน้อย]";
			mes "ฉันหมายถึงการ์ดหรือการอัพเกรดใด ๆ ในไอเท็มที่ฉันจะใช้ทำบางอย่างให้คุณจะหายไปทันทีที่ฉันสร้างไอเท็ม ดังนั้นระวังเมื่อคุณส่งของมาให้ฉัน";
			next;
			mes "[หนุ่มน้อย]";
			mes "คุณรวบรวมรายการหรือไม่? จากนั้นมอบให้";
			next;
			switch(select("ให้สิ่งของแก่เขา:อย่าให้สิ่งของแก่เขา")) {
			case 1:
				if ((countitem(2229) > 0) && (countitem(2254) > 0) && (countitem(7036) > 4)) {
					mes "[หนุ่มน้อย]";
					mes "..... หืม.";
					mes "ได้เลยนี่คือ Helm of Angel ของคุณ เอาไป.";
					delitem 2229,1; //Helm_
					delitem 2254,1; //Angelic_Chain
					delitem 7036,5; //Fang_Of_Garm
					close2;
					getitem 5025,1; //Helm_Of_Angel
					end;
				}
				else {
					mes "[หนุ่มน้อย]";
					mes "... จำนวนรายการไม่ถูก คุณควรตรวจสอบอีกครั้ง";
					close;
				}
			case 2:
				mes "[หนุ่มน้อย]";
				mes "ทำตามใจ ...";
				close;
			}
		case 2:
			mes "[หนุ่มน้อย]";
			mes "600 Little Evil Horn";
			mes "40 Talon of Griffon.";
			mes "นั่นคือทั้งหมดที่ฉันต้องการ";
			next;
			mes "[หนุ่มน้อย]";
			mes "รอ! ขอแจ้งให้ทราบว่าฉันไม่สนใจว่าสินค้าในคลังของคุณได้รับการอัปเกรดหรือมีการ์ดติดอยู่";
			next;
			mes "[หนุ่มน้อย]";
			mes "ฉันหมายถึงการ์ดหรือการอัพเกรดใด ๆ ในไอเท็มที่ฉันจะใช้ทำบางอย่างให้คุณจะหายไปทันทีที่ฉันสร้างไอเท็ม ดังนั้นระวังเมื่อคุณส่งของมาให้ฉัน";
			next;
			mes "[หนุ่มน้อย]";
			mes "คุณรวบรวมรายการหรือไม่? จากนั้นมอบให้";
			next;
			switch(select("ให้สิ่งของแก่เขา:อย่าให้สิ่งของแก่เขา")) {
			case 1:
				if ((countitem(1038) > 599) && (countitem(7048) > 39)) {
					mes "[หนุ่มน้อย]";
					mes "..... หืม. นี่คือหมวก Deviruchi ของคุณ ช่วยรับไปเถอะ";
					delitem 1038,600; //Petite_DiablOfs_Horn
					delitem 7048,40; //Talon_Of_Griffin
					close2;
					getitem 5038,1; //Deviruchi_Cap
					end;
				}
				else {
					mes "[หนุ่มน้อย]";
					mes "... จำนวนรายการไม่ถูก คุณควรตรวจสอบอีกครั้ง";
					close;
				}
			case 2:
				mes "[หนุ่มน้อย]";
				mes "ทำตามใจ ...";
				close;
			}
		case 3:
			mes "[หนุ่มน้อย]";
			mes "หืม. ได้เลย.";
			mes "แล้วเราจะพบกันใหม่.";
			close;
		}
	case 2:
		mes "[หนุ่มน้อย]";
		mes "หืม.";
		mes "ดูแล.";
		close;
	}
}

