//===== rAthena Script ======================================= 
//= RWC 2012 Enchants
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds slots and enchantments to 2012 RWC Memory accessories.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//============================================================ 

morocc,130,62,3	script	RWC 2012 Enchants	878,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "เจ้าแบกของมาเยอะเกิน. กลับมาใหม่เมื่อเจ้าจัดการช่องในกระเป๋าแล้ว.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "ดำเนินการไม่ได้เพราะน้ำหนักเกิน.";
		close;
	}
	mes "[Goldberg]";
	mes "หวัดดี! ข้าสามารถออฟประดับ RWC Memorial ได้ด้วยหินพลังพิเศษ.";
	next;
	set .@part, EQI_ACC_L;
	if (!getequipisequiped(.@part)) {
		mes "[Goldberg]";
		mes "ต้องขอโทษด้วยแต่ดูเหมือนเจ้าไม่ได้ใส่ประดับมาในตำแหน่งข้างขวาของเจ้า.";
		close;
	}
	set .@equip_id, getequipid(.@part);
	if (.@equip_id < 2966 || .@equip_id > 2969) {
		mes "[Goldberg]";
		mes "อย่างไรก็ตาม, ข้าสามารถเห็นประดับที่เจ้าใส่มาได้. กรุณาไปใส่ประดับ RWC Memorial มา.";
		close;
	}
	set .@select, select("ขอโทษที, ข้าไม่สนใจ.:ได้โปรด, เพิ่มพลังให้ประดับข้าที.:ลบออฟออก.")-1;
	if (.@select == 0) {
		mes "[Goldberg]";
		mes "เข้าใจแล้ว, งั้นเจอกันใหม่ครั้งหน้า...";
		close;
	}
	setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
	if (.@select == 1) {
		switch(getequipid(.@part)) {
		case 2966:
			setarray .@option[0],2,2,4,4;
			break;
		case 2967:
			setarray .@option[0],0,2,4,4;
			break;
		case 2968:
			setarray .@option[0],1,1,3,3;
			break;
		case 2969:
			setarray .@option[0],0,1,3,3;
			break;
		default:
			mes "[Goldberg]";
			mes "ข้าขอโทษ, แต่ข้าไม่สามารถออฟประดับที่เจ้าใส่มาได้.";
			mes "ถ้าสวมใส่ประดับ RWC Memorial ที่ข้างซ้าย, ลองสลับมาใส่ที่ข้างขวาดู.";
			close;
		}
		for(set .@i,3; .@i>=0; set .@i,.@i-1) {
			if (.@equip_card[.@i] == 0) {
				set .@slot, .@i;
				set .@op_type, .@option[.@i];
				break;
			}
		}
		switch(.@op_type) {
		case 4:
			mes "[Goldberg]";
			mes "เจ้าแน่ใจที่จะออฟไหม ?";
			next;
			setarray .@enchant_select[0],1,2,3,4;
			set .@i, select("ยกเลิก:Fighting Spirit:ATK (%):Max HP:HP")-2;
			break;
		case 3:
			setarray .@enchant_select[0],5,6,7;
			set .@i, select("ยกเลิก:Spell:MATK (%):SP")-2;
			break;
		case 2:
			mes "[Goldberg]";
			mes "^ff0000ระวัง! มีโอกาส 25% ที่ออฟจะล้มเหลว. ถ้ามันเกิดขึ้น, ไอเท็มจะถูกทำลาย.^000000 เจ้าแน่ใจที่จะออฟไหม ?";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,14;
			set .@i, select("ยกเลิก:STR:AGI:VIT:INT:DEX:LUK:SP")-2;
			break;
		case 1:
			mes "[Goldberg]";
			mes "^ff0000ระวัง! มีโอกาส 25% ที่ออฟจะล้มเหลว. ถ้ามันเกิดขึ้น, ไอเท็มจะถูกทำลาย.^000000 เจ้าแน่ใจที่จะออฟไหม ?";
			next;
			setarray .@enchant_select[0],8,9,10,11,12,13,15,16;
			set .@i, select("ยกเลิก:STR:AGI:VIT:INT:DEX:LUK:MHP:HP")-2;
			break;
		case 0:
			mes "[Goldberg]";
			mes "ประดับของเจ้าได้ออฟหลายครั้งแล้ว นั่นเพราะข้าทำงานอย่างหนักไงล่ะ.";
			close;
		}
		if (.@i == -1) {
			mes "[Goldberg]";
			mes "เข้าใจแล้ว, งั้นเจอกันใหม่ครั้งหน้า.";
			close;
		}
		mes "[Goldberg]";
		mes "พลังที่ได้จะถูกสุ่มในออฟที่เจ้าเลือก. ^ff0000เมื่อออฟแล้วออฟจะไม่หายไป.^000000 ยังอยากที่จะลองต่อไหม ?";
		next;
		if(select("ไม่, หยุดเถอะ.:เอาเลย, ต่อเลย.") == 1) {
			mes "[Goldberg]";
			mes "เข้าใจแล้ว, งั้นเจอกันใหม่ครั้งหน้า...";
			close;
		}
		set .@enchant_type, .@enchant_select[.@i];
		if (!getequipisequiped(.@part)) {
			mes "[Goldberg]";
			mes "เจ้าจะถอดมันออกขณะที่ข้าออฟไม่ได้, เข้าใจ ?";
			close;
		}
		switch(.@enchant_type) {
		case 1:
			setarray .@enc[0],4811,4810,4809; //Fighting_Spirit1,Fighting_Spirit2,Fighting_Spirit3
			break;
		case 2:
			setarray .@enc[0],4819,4766,4767; //Atk1,Atk2,Atk3
			break;
		case 3:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 4:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		case 5:
			setarray .@enc[0],4760,4761,4806; //Matk1,Matk2,Matk3
			break;
		case 6:
			setarray .@enc[0],4815,4814,4813; //Spell1,Spell2,Spell3
			break;
		case 7:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 8:
			setarray .@enc[0],4700,4701,4702; //Strength1,Strength2,Strength3
			break;
		case 9:
			setarray .@enc[0],4730,4731,4732; //Agility1,Agility2,Agility3
			break;
		case 10:
			setarray .@enc[0],4740,4741,4742; //Vitality1,Vitality2,Vitality3
			break;
		case 11:
			setarray .@enc[0],4710,4711,4712; //Inteligence1,Inteligence2,Inteligence3
			break;
		case 12:
			setarray .@enc[0],4720,4721,4722; //Dexterity1,Dexterity2,Dexterity3
			break;
		case 13:
			setarray .@enc[0],4750,4751,4752; //Luck1,Luck2,Luck3
			break;
		case 14:
			setarray .@enc[0],4870,4800,4871; //SP25,SP50,SP75
			break;
		case 15:
			setarray .@enc[0],4861,4862,4867; //MHP1,MHP2,MHP3
			break;
		case 16:
			setarray .@enc[0],4795,4796,4797; //HP100,HP200,HP300
			break;
		default:
			mes "[Goldberg]";
			mes "ดูเหมือนว่าเราจะมีปัญหาแล้วล่ะ, ข้าขอตรวจดูก่อน.";
			close;
		}

		if (.@enchant_type < 8)
			set .@i, rand(1,300); // 0% break chance.
		else
			set .@i, rand(1,400); // 25% break chance.

		     if (.@i < 151) set .@enchant, .@enc[0];
		else if (.@i < 251) set .@enchant, .@enc[1];
		else if (.@i < 301) set .@enchant, .@enc[2];
		else set .@enchant,9;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@equip_id) ||
		    callfunc("F_IsEquipCardHack", .@part, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		set .@equip_card[.@slot], .@enchant;
		if (.@slot == 2 && .@enchant == 0) {
			set .@equip_card[3],0;
		} else if (.@slot == 1 && .@enchant == 0) {
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		} else if (.@slot == 0 && .@enchant == 0) {
			set .@equip_card[1],0;
			set .@equip_card[2],0;
			set .@equip_card[3],0;
		}

		set .@equip_refine, getequiprefinerycnt(.@part);
		delequip .@part;
		if (.@enchant == 9) {
			mes "[Goldberg]";
			mes "โอ้ ม่าย!";
 			mes "ไอเท็มมันไม่แข็งแกร่งพอมันถูกทำลายไปแล้ว. ข้าเสียใจด้วย.";
			specialeffect2 EF_LORD;
			close;
		}
		if (.@enchant == 0) { // Should never happen.
			mes "[Goldberg]";
			mes "โอ้... มันดูเหมือนว่าไม่มีอะไรเกิดขึ้นเลย. พลังที่ออฟมันหายไป. มันน่าอัปยศจริงๆ, แต่ได้โปรดลองมันอีกครั้ง!";
		} else {
			mes "[Goldberg]";
			mes "เยี่ยม!";
			mes "การออฟสำเร็จ! มันปรากฎขึ้นในช่องที่^990000"+(.@slot+1)+"^000000.";
			specialeffect2 EF_REPAIRWEAPON;
		}

//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[Goldberg]";
		mes "ข้าจะลบออฟออกให้โดยไม่เกี่ยวข้องกับช่องใส่การ์ด. เจ้าจะทำต่อไหม ?";
		next;
		if(select("หยุดก่อน.:แน่นอน, ทำเลย.") == 1) {
			mes "[Goldberg]";
			mes "กลับมาใหม่เมื่อเจ้าเปลี่ยนใจ.";
			close;
		}
		if (countitem(6665) == 0) {
			mes "[Goldberg]";
			mes "ข้าขอโทษ. แต่เจ้าไม่มี RWC Initialization coupon. ลองดูในกระเป๋าให้แน่ใจ ?";
			close;
		}
		if (.@equip_card[3] == 0) {
			mes "[Goldberg]";
			mes "หืม... เหมือนมันไม่ได้ผ่านการออฟมา. ข้าลบออฟไม่ได้หรอก! ลองตรวจดูดีๆ.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[Goldberg]";
		mes "ออฟได้ถูกลบออกจากไอเท็มแล้ว.";
		delitem 6665,1; //RWC_Inicializer
		delequip .@part;
	
//		GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@equip_card[.@i] >= 4700) // Armor Enchant System
				set .@equip_card[.@i],0;
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];
		
		close;
	}
}
